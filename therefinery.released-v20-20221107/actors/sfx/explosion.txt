///////////////////////////
// EXPLOSION for TSoZD ////
// by Tormentor667 ////////
///////////////////////////

Actor EmberParticle_Gravity : ParticleBase
{
	+THRUACTORS
	+MISSILE
	+LOWGRAVITY
	+FLOATBOB
	+FORCEXYBILLBOARD
	Radius 1
	Height 1
	BounceType Doom
	RenderStyle Add
	Alpha 1.0
	YScale 0.75
	XScale 0.75
	States
	{
	Spawn:
		EMB5 ABCDEFHIIIIIIII 5 Bright //A_Weave(1, 1, 1.0, 1.0)
		EMB5 A 0 A_Die
	Death:
		EMB5 IIIIII 3 Bright A_FadeOut(0.2)
		Loop
	}
}

Actor ExplosionSpawner : SwitchableDecoration
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOINTERACTION
	+NOCLIP
	-SOLID
	RenderStyle Add
	Alpha 1.0
	Scale 0.06
	States
	{
	Spawn:
	Active:
		EXN1 AAAAAA 1 BRIGHT A_SetScale(ScaleX + frandom(0.1,0.2), ScaleY + frandom(0.1,0.2))
		"####" A 0 BRIGHT A_PlaySoundEx("weapons/rocklx",0)
		"####" A 0 BRIGHT A_SpawnItemEx ("ExplosionLight")
		"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 BRIGHT
		{
		A_SpawnItemEx ("EmberParticle", 0, 0, 0, random(1,6), random(1,6), random(-5,5), random(0,359), SXF_CLIENTSIDE, 0);
		A_SpawnItemEx ("SmokeExplosion", 0, 0, 0, frandom(0.0, 2.0), frandom(0.0, 2.0), 0, 0, 0, 192);
		}
		"####" AAA 1 BRIGHT
		{
		A_FadeOut(0.3);
		A_SetScale(ScaleX + frandom(0.1,0.2), ScaleY + frandom(0.1,0.2));
		}
		Goto Inactive
	Inactive:
		TNT1 A 1
		Loop
	}
}

//////////////////////////
// Dynamic Light actors //
//////////////////////////

Actor ExplosionLight
{
	+CLIENTSIDEONLY
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 10
		Stop
	}
}

Actor SmokeExplosion : Flame1A
{
	Height 2
	Radius 1
	+NOGRAVITY
	+NOBLOCKMAP
	+MISSILE
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Translucent
	Alpha 0.0
	Scale 0.6
	States
	{
	Spawn:
		SMOC A 0 NoDelay
	Smoke:
		"####" A 0 ThrustThingZ(0, random(10, 40), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(1.0, 2.0), 0, 0)
		"####" AAAAAAA 1 A_FadeIn(0.05)
		"####" A 1
		{
		A_FadeOut(frandom(0.005,0.007));
		A_SetScale(ScaleX + frandom(0.01,0.015), ScaleY + frandom(0.01,0.015));
		A_ChangeVelocity(VelX/1.05, VelY/1.05, VelZ/1.05, CVF_REPLACE);
		}
		Goto Smoke+9
	Death:
		SMOC A 0
		Stop
	}
}