class TCOTDWallTorch : SwitchableDecoration replaces BlueTorch
{
	bool inRange;

	Default
	{
		Radius 16;
		Height 68;
		ProjectilePassHeight -16;
		-SOLID
	}

	States
	{
	Active:
		TBLX A 0
		{
			A_StartSound("Ignite");
			bDormant = false;
		}
	Spawn:
		TBLX A 0;
		TBLX A 3 Bright Light("WALLTORCH")
		{
			inRange = !CheckRange(1000.0, true);

			if (inRange)
			{
				A_SpawnItemEx("TCOTDWallTorchFire",
					frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(74.0, 76.0),
					0.001 * frandom(10.0, 200.0), 0.001 * frandom(10.0, 200.0), 0.001 * frandom(500.0, 1000.0));
			}
		}
		Loop;
	Inactive:
		TBLX A -1
		{
			bDormant = true;
		}
		Stop;
	}
}

class TCOTDWallTorchFire : TCOTDDietActorWithVel
{
	bool fadedIn;

	Default
	{
		RenderStyle "Add";
		Alpha 0;
		Scale 0.175;
		+ROLLSPRITE
		+ROLLCENTER
		+BRIGHT
		+FORCEXYBILLBOARD
		+NOINTERACTION
	}

	States
	{
	Spawn:
		FIR1 A 0 NoDelay
		{
			bSpriteFlip = RandomPick(false, true);
			A_SetRoll(frandom(0.0, 360.0));
		}
	SpawnLoop:
		"####" "#" 1
		{
			int rollDir = bSpriteFlip ? -1 : 1;
			double rollSpeed = 2.33 * rollDir;
			A_SetRoll(Roll + rollSpeed, SPF_INTERPOLATE);

			if (!fadedIn)
			{
				if (Alpha < 1.0)
				{
					A_FadeIn(0.1);
				}
				else
				{
					fadedIn = true;
				}
			}

			if (fadedIn)
			{
				A_SetScale(Scale.X * 0.98);
				A_FadeOut(0.025);
			}
		}
		Loop;
	}
}
