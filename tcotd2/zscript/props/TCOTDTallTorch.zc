/*
* TCOTD2 Game Code
* Copyright (C) 2020 - 2023  Nash Muhandes
*
* This program is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

//===========================================================================
//
// TCOTDTallTorch
//
//===========================================================================

class TCOTDTallTorch : ShortRedTorch replaces ShortRedTorch
{
	Default
	{
		Radius 10;
		Height 67;
	}

	States
	{
	Spawn:
		SMRT A 0 NoDelay
		{
			A_SpawnItemEx("TCOTDTallTorchLensFlare", 0, 0, 62.0);
		}
		SMRT A 1
		{
			if (!CheckRange(1000.0, true))
			{
				if (!IsActorPlayingSound(CHAN_VOICE, "sfx/fireloop2"))
					A_StartSound("sfx/fireloop2", CHAN_VOICE, CHANF_LOOP, volume: 0.333, startTime: frandom[rnd_TallTorchFireSpawn](0.f, S_GetLength("sfx/fireloop2")));

				A_SpawnItemEx("TCOTDTallTorchFlame", 0, 0, frandom[rnd_TallTorchFireSpawn](64.0, 66.0), 0, 0, frandom[rnd_TallTorchFireSpawn](1.0, 2.5));
			}
			else if (IsActorPlayingSound(CHAN_VOICE, "sfx/fireloop2"))
			{
				A_StopSound(CHAN_VOICE);
			}
		}
		Goto Spawn + 1;
	}
}

//===========================================================================
//
// TCOTDTallTorchFlame
//
//===========================================================================

class TCOTDTallTorchFlame : TCOTDFire
{
	Default
	{
		Scale 0.3;
	}

	States
	{
	Spawn:
		AFNO A 0;
		Goto SpawnLoop;
	}
}

//===========================================================================
//
// TCOTDTallTorchLensFlare
//
//===========================================================================

class TCOTDTallTorchLensFlare : TCOTDSimpleActor
{
	Default
	{
		RenderStyle "Add";
		Alpha 1.0;
		Scale 0.75;
		+NOINTERACTION
	}

	States
	{
	Spawn:
		FLRE A -1 Bright;
		Stop;
	}
}
