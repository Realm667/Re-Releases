/*
* TCOTD2 Game Code
* Copyright (C) 2020 - 2023  Nash Muhandes
*
* This program is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

//===========================================================================
//
// TCOTDFireSpawner
//
//===========================================================================

class TCOTDFireSpawner : TCOTDSwitchableDecoration
{
	Default
	{
		//$Category Special Effects (TCOTD2)
		//$Title Fire Spawner
		//$Color 12
		Height 40;
		Radius 30;
		+DONTSPLASH
		+NOCLIP
		+NOGRAVITY
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void SpawnFire(void)
	{
		let fire = Spawn("TCOTDFireSpawnerFlame", Pos, NO_REPLACE);
		if (fire)
		{
			double fireScale;
			double vx, vy, vz;

			fireScale = 0.35;
			vx = frandom[rnd_SpawnFire](-1.0, 1.0);
			vy = frandom[rnd_SpawnFire](-1.0, 1.0);
			vz = frandom[rnd_SpawnFire](2.0, 4.0);

			fire.A_SetScale(fireScale);
			fire.Vel = (vx, vy, vz);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	Spawn:
	Active:
		TNT1 A 4 NoDelay
		{
			A_StartSound("sfx/firestart", CHAN_7);
			A_StartSound("sfx/fireloop2", CHAN_6, CHANF_LOOP, 1.0, startTime: frandom[rnd_FireSpawn](0.f, S_GetLength("sfx/fireloop2")));
		}
		TNT1 A 1 Light("FireSpawnerLarge")
		{
			if (!CheckSightOrRange(1000.0, true))
			{
				SpawnFire();

				if (GetAge() % 5 == 0)
					TCOTDFire.SpawnEmbers(self);
			}
		}
		Goto Active + 1;
	Inactive:
		TNT1 A 0
		{
			A_StartSound("sfx/firedie", CHAN_7);
			A_StopSound(CHAN_6);
		}
		TNT1 A -1;
		Stop;
	}
}

//===========================================================================
//
// TCOTDFireSpawnerFlame
//
//===========================================================================

class TCOTDFireSpawnerFlame : TCOTDFire
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, "Flame1", "Flame2", "Flame3");
		Stop;
	Flame1:
		FLM1 A 0;
		Goto SpawnLoop;
	Flame2:
		FLM3 A 0;
		Goto SpawnLoop;
	Flame3:
		FLM5 A 0;
		Goto SpawnLoop;
	}
}
