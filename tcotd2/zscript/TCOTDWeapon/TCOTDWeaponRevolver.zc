/*
Credits:
-Revolver Sprites by Mike12
-Sounds from Clive Baker's Undying
-Bullet Puff sprites from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponRevolver : TCOTDWeapon replaces Pistol
{
	Default
	{
		Scale 0.6;
		AttackSound "Revolver/Fire";
		Tag "$TAG_PIST";
		Obituary "$OB_PIST";
		Inventory.PickupMessage "$IP_PIST";
		Weapon.Ammotype "Clip";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.SelectionOrder 190;
		+WEAPON.WIMPY_WEAPON
	}

	States
	{
	Select:
		REVL A 1 A_Raise();
		Loop;
	Deselect:
		REVL A 1 A_Lower();
		Loop;
	Ready:
		REVL A 1 A_WeaponReady();
		Loop;
	Fire:
		REVL A 2;
		TNT1 A 0 A_FireBullets(5.6, 0, 1, 9, "NewPuff");
		REVL B 2 A_Overlay(-2, "Flash");
		REVL C 3;
		REVL DEFG 1;
		REVL HAA 2;
		REVL A 1 A_ReFire();
		goto Ready;
	Flash:
		RVFL A 2 Bright A_Light2();
		RVFL B 2 Bright A_Light1();
		RVFL C 2 Bright A_Light0();
		Goto LightDone;
	Spawn:
		RPCK A -1;
		Stop;
	}
}

class NewSparks2 : Actor
{
	Default
	{
		Gravity 0.125;
		Radius 1;
		Height 1;
		Scale 0.60;
		Projectile;
		+BLOODLESSIMPACT
		+RIPPER
		-NOGRAVITY
	}

	States
	{
	Spawn:
		WRBL KLKL 2 Bright;
		TNT1 A 0 A_Jump(128, 1);
		Loop;
		WRBL MNOP 2 Bright;
		WRBL P 1 Bright A_FadeOut();
		Wait;
	}
}

class NewSparks : NewSparks2
{
	Default
	{
		Scale 0.4;
	}
}

class NewPuff : BulletPuff
{
	Default
	{
		Scale 0.5;
		VSpeed 0;
		RenderStyle "Normal";
		Alpha 1.0;
		+NOEXTREMEDEATH
	}

	States
	{
	Crash:
		RPUF A 1 Bright Light("PUFF") A_SpawnItemEx("NewSparks", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(1, 360));
		RPUF BCDEFG 1 Bright;
		Stop;
	Spawn:
	Melee:
		TNT1 A 0;
		Stop;
	}
}
