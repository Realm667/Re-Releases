/*
Credits:
-Revolver Sprites by Mike12
-Sounds from Clive Baker's Undying
-Bullet Puff sprites from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponRevolver : TCOTDWeapon replaces Pistol
{
	Default
	{
		//$Title Revolver
		Scale 0.6;
		AttackSound "Revolver/Fire";
		Tag "$TAG_PIST";
		Obituary "$OB_PIST";
		Inventory.PickupMessage "$IP_PIST";
		Weapon.Ammotype "TCOTDAmmoBullet";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.SelectionOrder 190;
		+WEAPON.WIMPY_WEAPON
	}

	States
	{
	Select:
		REVL A 1 A_Raise();
		Loop;
	Deselect:
		REVL A 1 A_Lower();
		Loop;
	Ready:
		REVL A 1 A_WeaponReady();
		Loop;
	Fire:
		REVL B 3 { A_Overlay(-2, "RevolverSmoke"); A_FireBullets(5.6, 1.7, 1, 9, "TCOTDBulletPuff"); }
		REVL CD 2;
		REVL E 1 A_WeaponOffset(4, 33);
		REVL F 1 A_WeaponOffset(5, 38);
		REVL G 1 A_WeaponOffset(6, 42);
		REVL H 1 A_WeaponOffset(6, 28);
		REVL H 1 A_WeaponOffset(2, 32);
		REVL H 1 A_WeaponOffset(-1, 36);
		REVL A 1 A_WeaponOffset(2, 30);
		REVL A 3 A_WeaponOffset(0, 32);
		REVL A 1 A_ReFire();
		goto Ready;
	RevolverSmoke:
		RVFL A 2 Bright A_Light2();
		RVFL B 2 Bright A_Light1();
		RVFL C 2 Bright A_Light0();
		Stop;
	Spawn:
		RPCK A -1;
		Stop;
	}
}
