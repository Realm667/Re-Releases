/*
Credits:
-Shotgun sprites & reload sound from Blood
-Firing sounds from Clive Baker's Undying

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponSawedOff : TCOTDWeapon replaces Shotgun
{
	Default
	{
		//$Title Sawed-Off
		Scale 0.6;
		Tag "$TAG_SAWA";
		Obituary "$OB_SAWA";
		Inventory.PickupMessage "$IP_SAWA";
		Weapon.AmmoType1 "TCOTDAmmoShell";
		Weapon.AmmoType2 "TCOTDAmmoShell";
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 2;
		Weapon.AmmoGive 12;
		Weapon.Kickback 100;
		Weapon.SelectionOrder 130;
	}

	States
	{
	Spawn:
		SPCK A -1;
		Loop;
	Ready:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, 2);
		SAWF A 1 A_WeaponReady();
		Loop;
		SAWF B 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, "Down1Shell");
		SAWF A 1 A_Lower();
		Loop;
	Down1Shell:
		SAWF B 1 A_Lower();
		Loop;
	Select:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, "Up1Shell");
		SAWF A 1 A_Raise();
		Loop;
	Up1Shell:
		SAWF B 1 A_Raise();
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, "Fire1Shell");
		SAWF B 2 { A_StartSound("SawedOff/Fire", CHAN_WEAPON); A_Overlay(-2, "GunSmoke"); A_GunFlash(); A_FireBullets(5.6, 3.5, 7, 5, "TCOTDBulletPuff", FBF_USEAMMO); }
		SAWF B 1 Offset(10, 52);
		SAWF B 1 Offset(14, 60);
		SAWF B 2 Offset(16, 64);
		SAWF B 1 Offset(14, 60);
		SAWF B 1 Offset(10, 52);
		SAWF B 1 Offset(3, 38);
		SAWF B 1 Offset(2, 36);
		SAWF B 1 Offset(1, 34) A_GiveInventory("SawedOffCheck", 1);
		SAWF B 2 A_WeaponOffset(0, 32);
		Goto Ready;
	Fire1Shell:
		SAWF C 2 { A_StartSound("SawedOff/Fire", CHAN_WEAPON); A_Overlay(-2, "GunSmoke"); A_GunFlash(); A_FireBullets(5.6, 3.5, 7, 5, "TCOTDBulletPuff", FBF_USEAMMO); }
		SAWF C 1 Offset(10, 52);
		SAWF C 1 Offset(14, 60);
		SAWF C 2 Offset(16, 64);
		SAWF C 1 Offset(14, 60);
		SAWF C 1 Offset(10, 52);
		SAWF C 1 Offset(3, 38);
		SAWF C 1 Offset(2, 36);
		SAWF C 1 Offset(1, 34);
		SAWF C 1 A_WeaponOffset(0, 32);
		Goto Load;
	AltFire:
		SAWF A 0 A_JumpIf(CountInv("SawedOffCheck"), "Fire");
		SAWF C 2 { A_StartSound("SawedOff/AltFire", CHAN_WEAPON); A_Overlay(-2, "BigGunSmoke"); A_GunFlash(); A_FireBullets(11.2, 7.1, 20, 5, "TCOTDBulletPuff", FBF_USEAMMO); }
		SAWF C 1 offset(10, 52);
		SAWF C 1 offset(28, 68);
		SAWF C 1 offset(32, 76);
		SAWF C 2 offset(38, 88);
		SAWF C 1 offset(36, 84);
		SAWF C 1 offset(32, 76);
		SAWF C 1 offset(28, 68);
		SAWF C 1 offset(14, 60);
		SAWF C 1 offset(10, 52);
		SAWF C 1 offset(3, 38);
		SAWF C 1 offset(2, 36);
		SAWF C 1 offset(1, 34);
		SAWF C 1 A_WeaponOffset(0, 32);
		Goto Load;
	Load:
		TNT1 A 0 A_TakeInventory("SawedOffCheck", 1);
		SAWF C 1 A_CheckReload();
		SAWF E 2;
		SAWF F 2 { A_StartSound("SawedOff/Reload", CHAN_WEAPON, CHANF_OVERLAP); A_SpawnWeaponFX("TCOTDEmptyCasingShell", 2); A_SpawnWeaponFX("TCOTDEmptyCasingShell", 4); }
		SAWF G 8 A_Overlay(2, "ElHando");
		SAWF JE 2;
		Goto Ready;
	ElHando:
		TNT1 A 0 A_OverlayFlags(2, PSPF_ADDWEAPON, false);
		SAWF H 1 A_OverlayOffset(2, 0, 32);
		SAWF H 1 A_OverlayOffset(2, -9, 26);
		SAWF H 1 A_OverlayOffset(2, -18, 20);
		SAWF H 1 A_OverlayOffset(2, -26, 15);
		SAWF H 1 A_OverlayOffset(2, -32, 10);
		SAWF I 1 A_OverlayOffset(2, 0, 32);
		SAWF I 1 A_OverlayOffset(2, -4, 40);
		SAWF I 1 A_OverlayOffset(2, -8, 48);
		Stop;
	Flash:
		SAWF D 2 A_Light2();
		SAWF D 2 A_Light1();
		Goto LightDone;
	GunSmoke:
		TNT1 A 0 A_OverlayFlags(-2, PSPF_ADDWEAPON, false);
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, 4);
		SFFL ABC 2 Bright A_OverlayOffset(-2, 0, 32);
		Stop;
		SFFL ABC 2 Bright A_OverlayOffset(-2, 16, 32);
		Stop;
	BigGunSmoke:
		TNT1 A 0 A_OverlayFlags(-2, PSPF_ADDWEAPON, false);
		SFFL DEF 2 Bright A_OverlayOffset(-2, 0, 32);
		Stop;
	}
}

class SawedOffCheck : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
		-INVENTORY.INVBAR;
	}
}
