/*
Credits:
-Shotgun sprites & reload sound from Blood
-Firing sounds from Clive Baker's Undying

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponSawedOff : TCOTDWeapon replaces Shotgun
{
	Default
	{
		Scale 0.6;
		Tag "$TAG_SAWA";
		Obituary "$OB_SAWA";
		Inventory.PickupMessage "$IP_SAWA";
		Weapon.AmmoType1 "Shell";
		Weapon.AmmoType2 "Shell";
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 2;
		Weapon.AmmoGive 12;
		Weapon.Kickback 100;
		Weapon.SelectionOrder 130;
	}

	States
	{
	Spawn:
		SPCK A -1;
		Loop;
	Ready:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, 2);
		SAWF A 1 A_WeaponReady();
		Loop;
		SAWF B 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, "Down1Shell");
		SAWF A 1 A_Lower();
		Loop;
	Down1Shell:
		SAWF B 1 A_Lower();
		Loop;
	Select:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, "Up1Shell");
		SAWF A 1 A_Raise();
		Loop;
	Up1Shell:
		SAWF B 1 A_Raise();
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, 15);
		SAWF A 1 A_PlayWeaponSound("SawedOff/Fire");
		TNT1 A 0 A_Overlay(-2, "GunSmoke");
		TNT1 A 0 A_GunFlash();
		SAWF B 1 A_FireBullets(5.6, 0, 7, 5, "NewPuff", FBF_USEAMMO);
		SAWF B 1 Offset(6, 44);
		SAWF B 1 Offset(10, 52);
		SAWF B 1 Offset(18, 68);
		SAWF B 1 Offset(14, 56);
		SAWF B 1 Offset(10, 52);
		SAWF B 1 Offset(8, 48);
		SAWF B 1 Offset(6, 44);
		SAWF B 1 Offset(4, 40);
		SAWF B 1 Offset(2, 36) A_GiveInventory("SawedOffCheck", 1);
		SAWF B 2;
		Goto Ready;
		SAWF B 1 A_PlayWeaponSound("SawedOff/Fire");
		TNT1 A 0 A_Overlay(-2, "GunSmoke");
		TNT1 A 0 A_GunFlash();
		SAWF C 1 A_FireBullets(5.6, 0, 7, 5, "NewPuff", FBF_USEAMMO);
		SAWF C 1 Offset(6, 44);
		SAWF C 1 Offset(10, 52);
		SAWF C 1 Offset(18, 68);
		SAWF C 1 Offset(14, 56);
		SAWF C 1 Offset(10, 52);
		SAWF C 1 Offset(8, 48);
		SAWF C 1 Offset(6, 44);
		SAWF C 1 Offset(4, 40);
		SAWF C 1 Offset(2, 36);
		SAWF C 1;
		Goto Load;
	AltFire:
		SAWF A 1 A_PlayWeaponSound("SawedOff/AltFire");
		TNT1 A 0 A_Overlay(-2, "BigGunSmoke");
		TNT1 A 0 A_GunFlash();
		SAWF C 1 A_FireBullets(11.2, 7.1, 20, 5, "NewPuff", FBF_USEAMMO);
		SAWF C 1 Offset(8, 48);
		SAWF C 1 Offset(15, 62);
		SAWF C 1 Offset(23, 78);
		SAWF C 1 Offset(27, 86);
		SAWF C 1 Offset(23, 78);
		SAWF C 1 Offset(19, 70);
		SAWF C 1 Offset(15, 62);
		SAWF C 1 Offset(11, 56);
		SAWF C 1 Offset(8, 48);
		SAWF C 1 Offset(6, 44);
		SAWF C 1 Offset(4, 40);
		SAWF C 1 Offset(2, 36);
		SAWF C 1;
		Goto Load;
	Load:
		TNT1 A 0 A_TakeInventory("SawedOffCheck", 1);
		SAWF C 1 A_CheckReload();
		SAWF E 2;
		SAWF F 2 A_PlaySound("SawedOff/Reload");
		SAWF G 8 A_Overlay(2, "ElHando");
		SAWF JE 2;
		Goto Ready;
	ElHando:
		TNT1 A 0 A_OverlayFlags(2, PSPF_ADDWEAPON, false);
		SAWF H 1 A_OverlayOffset(2, 0, 32);
		SAWF H 1 A_OverlayOffset(2, -9, 26);
		SAWF H 1 A_OverlayOffset(2, -18, 20);
		SAWF H 1 A_OverlayOffset(2, -26, 15);
		SAWF H 1 A_OverlayOffset(2, -32, 10);
		SAWF I 1 A_OverlayOffset(2, 0, 32);
		SAWF I 1 A_OverlayOffset(2, -4, 40);
		SAWF I 1 A_OverlayOffset(2, -8, 48);
		Stop;
	Flash:
		SAWF D 2 A_Light2();
		SAWF D 2 A_Light1();
		Goto LightDone;
	GunSmoke:
		TNT1 A 0 A_OverlayFlags(-2, PSPF_ADDWEAPON, false);
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck", 1, 4);
		SFFL ABC 2 Bright A_OverlayOffset(-2, 0, 32);
		Stop;
		SFFL ABC 2 Bright A_OverlayOffset(-2, 20, 32);
		Stop;
	BigGunSmoke:
		TNT1 A 0 A_OverlayFlags(-2, PSPF_ADDWEAPON, false);
		SFFL DEF 2 Bright A_OverlayOffset(-2, 0, 32);
		Stop;
	}
}

class SawedOffCheck : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
		-INVENTORY.INVBAR;
	}
}
