/*
Credits:
-Hand sprites from Fate (by Capstone);
-Pitchfork & Puff sprites from Blood
-Sounds from Thief
-Flying Pitchfork sprites by NeoWorm & Amuscaria acording to credits

Original code inherited from the original TCoTD 2, modified by Gothic.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponMelee : TCOTDWeapon
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.KickBack 100;
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOALERT
	}

	States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv", 1, "PitchforkReady");
		FIST A 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	PitchforkReady:
		FORK A 1 A_WeaponReady();
		Goto Ready;
	Deselect:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv", 1, "PitchforkDeselect");
		FIST A 1 A_Lower();
		Loop;
	PitchforkDeselect:
		FORK A 1 A_Lower();
		Loop;
	Select:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv", 1, "PitchforkSelect");
		FIST A 1 A_Raise();
		Loop;
	PitchforkSelect:
		FORK A 1 A_Raise();
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv", 1, "PitchforkFire");
		TNT1 A 0 A_Jump(160, 7);
		FIST B 2;
		FIST C 4 A_CustomPunch(random(3, 5), 0, 0, "BulletPuff", 0, 0, 0, "ArmorBonus", "Fist/Hit", 0);
		FIST CBAA 2;
		Goto Ready;
		FIST D 2;
		FIST E 4 A_CustomPunch(random(3, 5), 0, 0, "BulletPuff", 0, 0, 0, "ArmorBonus", "Fist/Hit", 0);
		FIST EDAA 2;
		Goto Ready;
	PitchforkFire:
		FORK A 2;
		TNT1 A 0;
		FORK A 1 Offset(0, 30);
		FORK B 1;
		FORK B 1 Offset(0, 30);
		FORK C 5 A_CustomPunch(random(5, 7), 0, 0, "HitPuffNew", 96, 0, 0, "ArmorBonus", 0, "Pitchfork/Miss");
		FORK C 1 Offset(0, 34);
		FORK B 1;
		FORK B 1 Offset(0, 34);
		FORK A 2 A_WeaponOffset(0, 32);
		FORK A 1 A_ReFire();
		Goto Ready;
	AltFire:
		FORK B 1 A_PlaySound("Pitchfork/Throw", CHAN_WEAPON);
		FORK C 1;
		FORK D 1 A_FireCustomMissile("Pitchfork_Thrown", 0, 1, 0, 5);
		FORK E 1 A_TakeInventory("PitchforkInv");
		FORK E 1 Offset(-18, 48);
		FORK E 1 Offset(-36, 64);
		FORK E 1 Offset(-54, 80);
		FORK E 1 Offset(-72, 92);
		TNT1 A 2;
		Goto Ready;
	Spawn:
		TNT1 A -1;
		Stop;
	}
}

class PitchforkSpark : Actor
{
	Default
	{
		Radius 3;
		Height 4;
		Speed 25;
		RenderStyle "Add";
		Scale 0.7;
		SeeSound "Fork/Stab";
		+CANNOTPUSH
		+DONTSPLASH
		+NOBLOCKMAP
		+NOGRAVITY
		+RANDOMIZE
		+THRUGHOST
	}

	States
	{
	Spawn:
		TNT1 A 0;
		BSPR A 0 A_Jump(128, 7);
		BSPR ABCDEFG 2 Bright;
		Stop;
		BSPR HIJKLMN 2 Bright;
		Stop;
	XDeath:
		TNT1 A 0;
		TNT1 A 0 A_PlaySound("Fork/Stab");
		BLD2 A 0 A_Jump(128,"XDeath2");
		BLD2 FGHI 4;
		Stop;
	XDeath2:
		BLD2 OPQR 4;
		Stop;
	}
}

class HitPuffNew : Actor
{
	Default
	{
		Scale 0.5;
		Activesound "";
		Attacksound "Pitchfork/Wall";	// what sound it makes when you hit a wall with it
		Seesound "Pitchfork/Flesh";		// what sound it makes when you beat somebody up with it
		+NOBLOCKMAP
		+NOGRAVITY
		+NOEXTREMEDEATH
		+PUFFONACTORS					// this is necessary for it to still make sound on enemies
	}

	States
	{
	Crash:
		PPUF A 0 A_SpawnItem("PitchforkSpark");
		PPUF ABCDE 2;
		Stop;
	Spawn:
	Melee:
		TNT1 A 1;
		Stop;
	}
}

class Pitchfork_Thrown : Actor
{
	Default
	{
		Radius 2;
		Height 1;
		Speed 20;
		Damage 5;
		Gravity 0.125;
		Projectile;
		+RIPPER
		-NOBLOCKMAP
		-NOGRAVITY
	}

	States
	{
	Spawn:
		XIPP A -1;
		Stop;
	Death:
		XIPP A 0 A_AlertMonsters();
		XIPP A 4 A_SpawnItemEx("PitchforkInv");
		Stop;
	}
}

class PitchforkInv : Inventory replaces Chainsaw
{
	Default
	{
		Scale 1.2;
		Inventory.PickupMessage "$IP_FORK";
		Inventory.PickupSound "ITEMUP";
		Inventory.MaxAmount 1;
		-INVENTORY.INVBAR
	}

	States
	{
	Spawn:
		XFOR A -1;
		Stop;
	}
}
