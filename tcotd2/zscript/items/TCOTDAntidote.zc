// -------------------------------------------------------------------------------------------
//
// -------------------------------------------------------------------------------------------

class TCOTDAntidote : Inventory
{
	mixin TCOTDCoopItemMixin;

	Default
	{
		//$Category New Health and Armor (TCOTD2)
		//$Title Antidote
		Scale 0.65;
		Tag "$TAG_ANTIDOTE";
		Inventory.Icon "graphics/hud/antidote.png";
		Inventory.MaxAmount 3;
		Inventory.PickupMessage "$IP_ANTIDOTE";
		Inventory.UseSound "misc/p_pkup";
		+FORCEXYBILLBOARD
		+COUNTITEM
		+INVENTORY.INVBAR
	}

	override bool Use (bool pickup)
	{
		let pmo = TCOTDPlayer(Owner);

		if (pmo)
		{
			let poison = pmo.FindInventory("TCOTDPoisonAilment");

			if (poison)
			{
				pmo.poisonBuildup = 0;
				poison.Destroy();
				return true;
			}
			else
			{
				pmo.A_Log(StringTable.Localize("$ANTIDOTE_NOTNEEDED"), true);
			}
		}

		return false;
	}

	States
	{
	Spawn:
		PSNH ABC 6;
		Loop;
	}
}
