// Copyright 2008 - 2020 Nash Muhandes
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
//
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
// 3. The name of the author may not be used to endorse or promote products
//    derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
// IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
// OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
// IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
// INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
// NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
// THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

//===========================================================================
//
// This is a stripped down version of the precipitation system from Bumi, an
// all-encompassing real-time weather and time cycle solution for GZDoom by
// Nash Muhandes.
// Bumi is (C) 2008 - 2020 Nash Muhandes
//
//===========================================================================

class TCOTDWeatherHandler : EventHandler
{
	const SNOW_SPAWN_RADIUS = 800.0;
	const SNOW_SPAWN_HEIGHT = 400.0;

	override void WorldTick(void)
	{
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (playeringame[i] && players[i].mo && players[i].mo.player)
			{
				if (gameaction == ga_savegame)
					continue;

				if (!CVar.GetCVar("tcotd2_weatherfx", players[consoleplayer]).GetBool())
					continue;

				Actor pmo = players[i].mo;
				Actor camera = players[i].camera;
				Actor origin = camera ? camera : pmo;
				int density = 6;
				while (density--)
				{
					double spx = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);
					double spy = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);
					Vector3 sPos = origin.Vec3Offset(spx, spy, SNOW_SPAWN_HEIGHT);
					double cz = origin.CurSector.HighestCeilingAt(sPos.XY);
					if (sPos.Z >= cz)
						sPos.Z = cz;
					if (!level.IsPointInLevel(sPos))
						continue;
					let snow = origin.Spawn("TCOTDSnowParticle", sPos, NO_REPLACE);
					if (snow)
					{
						snow.tracer = pmo;
						snow.Vel.X = frandom[rnd_SpawnPrecip](-0.8, 0.8);
						snow.Vel.Y = frandom[rnd_SpawnPrecip](-0.8, 0.8);
						snow.Vel.Z = frandom[rnd_SpawnPrecip](-4.0, -1.5);
					}
				}
			}
		}

		// don't serialize the snow particles
		if (gameaction == ga_savegame)
		{
			TCOTDGameplayStatics.DestroyActors("TCOTDSnowParticle");
		}
	}
}
