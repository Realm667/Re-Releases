/*
* TCOTD2 Game Code
* Copyright (C) 2020 - 2023  Nash Muhandes
*
* This program is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

//===========================================================================
//
// TCOTDWeatherHandler
// This is a stripped down version of the precipitation system from Bumi, an
// all-encompassing real-time weather and time cycle solution for GZDoom by
// Nash Muhandes.
// Bumi is (C) 2008 - 2023 Nash Muhandes
//
//===========================================================================

class TCOTDWeatherHandler : EventHandler
{
	const SNOW_SPAWN_RADIUS = 1000.0;
	const SNOW_SPAWN_HEIGHT = 400.0;

	FSpawnParticleParams snow;

	override void WorldTick(void)
	{
		//SpawnSnowParticles();
		SpawnSnowActors();
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void SpawnSnowParticles(void)
	{
		if (!CVar.GetCVar("tcotd2_weatherfx", players[consoleplayer]).GetBool())
			return;

		if (Level.IsFrozen())
			return;

		Actor cam = players[consoleplayer].camera;

		// cheap hack
		if (cam.WaterLevel >= 3)
			return;

		if (!snow.texture)
		{
			snow.color1 = "FFFFFF";
			snow.texture = TexMan.CheckForTexture("SNOWA0");
			snow.style = STYLE_Add;
			snow.flags = SPF_REPLACE;
			snow.lifetime = 400;
			snow.size = 6.;
			//snow.size = 0.;
			//snow.sizestep = 0.05;
			snow.startalpha = 1.;
			snow.fadestep = -0.01;
		}

		int density = 6;
		while (density--)
		{
			double spx = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);
			double spy = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);

			Vector3 sPos = cam.Vec3Offset(spx, spy, cam.Pos.Z);

			Sector sec = Level.PointInSector(sPos.XY);

			if (sec.GetTexture(Sector.ceiling) != skyflatnum)
				continue;

			double spz = sec.NextHighestCeilingAt(sPos.X, sPos.Y, cam.Pos.Z, cam.Pos.Z, 0) - 2.;
			sPos.Z = min(spz, SNOW_SPAWN_HEIGHT);

			snow.pos = sPos;
			snow.vel =
			(
				frandom[rnd_SpawnPrecip](-0.2, 0.2),
				frandom[rnd_SpawnPrecip](-0.2, 0.2),
				frandom[rnd_SpawnPrecip](-4.0, -2.0)
			);
			snow.accel.X = frandom[rnd_SpawnPrecip](-0.01, 0.01);
			snow.accel.Y = frandom[rnd_SpawnPrecip](-0.01, 0.01);

			Level.SpawnParticle(snow);
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void SpawnSnowActors(void)
	{
		if (Level.IsFrozen())
			return;

		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (playeringame[i] && players[i].mo && players[i].mo.player)
			{
				if (gameaction == ga_savegame)
					continue;

				if (!CVar.GetCVar("tcotd2_weatherfx", players[consoleplayer]).GetBool())
					continue;

				Actor cam = players[i].camera;

				// cheap hack
				if (cam.WaterLevel >= 3)
					continue;

				int density = 6;
				while (density--)
				{
					double spx = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);
					double spy = frandom[rnd_SpawnPrecip](-SNOW_SPAWN_RADIUS, SNOW_SPAWN_RADIUS);
					Vector3 sPos = cam.Vec3Offset(spx, spy, SNOW_SPAWN_HEIGHT);

					let snow = cam.Spawn("TCOTDSnowParticle", sPos, NO_REPLACE);
					if (snow)
					{
						snow.SetZ(min(snow.CeilingZ, sPos.Z));
						snow.Tracer = cam;
						snow.Vel.X = frandom[rnd_SpawnPrecip](-0.8, 0.8);
						snow.Vel.Y = frandom[rnd_SpawnPrecip](-0.8, 0.8);
						snow.Vel.Z = frandom[rnd_SpawnPrecip](-4.0, -1.5);
					}
				}
			}
		}

		// don't serialize the snow particles
		if (gameaction == ga_savegame)
		{
			TCOTDGameplayStatics.DestroyActors("TCOTDSnowParticle");
		}
	}
}
