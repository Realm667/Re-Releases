// Copyright 2020 Nash Muhandes
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions
// are met:
//
// 1. Redistributions of source code must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
// 2. Redistributions in binary form must reproduce the above copyright
//    notice, this list of conditions and the following disclaimer in the
//    documentation and/or other materials provided with the distribution.
// 3. The name of the author may not be used to endorse or promote products
//    derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
// IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
// OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
// IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
// INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
// NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
// THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

//===========================================================================
//
//
//
//===========================================================================

class TCOTDLODHandler : EventHandler
{
	const MAX_RANGE = 3000 * 3000.;
	const MAX_OBJECTS = 10000;
	const TICK_INTERVAL_MAX = Thinker.TICRATE / 2;

	struct TCOTDLODActorData
	{
		class<Actor> cls;
		Vector3 Pos;
		Actor mo;
	}

	private TCOTDLODActorData objects[MAX_OBJECTS];
	private uint index;
	private uint tickInterval;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void WorldTick(void)
	{
		if (Level.maptime == 1)
		{
			MakeLODObject("TCOTDGrass01");
			MakeLODObject("TCOTDGrass02");
			MakeLODObject("TCOTDGrass03");
			MakeLODObject("TCOTDBigBush");
			MakeLODObject("TCOTDMushroom01");
			MakeLODObject("TCOTDMushroom02");
			MakeLODObject("TCOTDMushroom03");
			MakeLODObject("TCOTDRock01");
			MakeLODObject("TCOTDRock02");
			MakeLODObject("TCOTDRock03");
			MakeLODObject("TCOTDRock04");
			//MakeLODObject("TCOTDTree01");
			//MakeLODObject("TCOTDTree02");
			//MakeLODObject("TCOTDTree03");
			//MakeLODObject("TCOTDTree04");
			//MakeLODObject("TCOTDTree05");
			//MakeLODObject("TCOTDTree06");
			//MakeLODObject("TCOTDTree07");
			//MakeLODObject("TCOTDTree08");
			//MakeLODObject("TCOTDFogCloud");
		}

		if (tickInterval-- > 0)
			return;

		Vector3 diff;
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (!playeringame[i])
				continue;

			for (uint j = 0; j < index; j++)
			{
				if (!objects[j].cls)
					continue;

				diff = level.Vec3Diff(objects[j].Pos, players[i].camera.Pos);
				if ((diff dot diff) <= MAX_RANGE)
				{
					if (!objects[j].mo)
					{
						objects[j].mo = Actor.Spawn(objects[j].cls, objects[j].Pos);
						objects[j].mo.ChangeStatNum(Thinker.STAT_INFO);
					}
				}
				else if (objects[j].mo)
					objects[j].mo.Destroy();
			}
		}

		tickInterval = TICK_INTERVAL_MAX;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	private void MakeLODObject(class<Actor> cls)
	{
		ThinkerIterator it = ThinkerIterator.Create(cls);
		Actor mo;
		while (mo = Actor(it.Next()))
		{
			objects[index].cls = mo.GetClass();
			objects[index++].Pos = mo.Pos;
			if (index >= MAX_OBJECTS)
				index = MAX_OBJECTS - 1;
			mo.Destroy();
		}
	}
}
