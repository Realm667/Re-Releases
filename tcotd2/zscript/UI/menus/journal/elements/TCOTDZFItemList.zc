/*
* TCOTD2 Game Code
* Copyright (C) 2020 - 2023  Nash Muhandes
*
* This program is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

//===========================================================================
//
// TCOTDZFItemList
//
//===========================================================================

class TCOTDZFItemList : TCOTDZFFrame
{
	// A reference to the journal menu must always exist
	TCOTDJournalMenu mJournalMenu;

	// A unique ID for this widget
	Name mID;

	// The inventory class that this widget will show
	String mFilterClassName;

	// The actual list of items
	Array<Inventory> mItems;
	Array<TCOTDZFClickableItem> mClickableItems;

	// The currently selected item
	int mSelected;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void Ticker(void)
	{
		Super.Ticker();

		let pmo = TCOTDPlayer(mJournalMenu.pmo);
		if (pmo.shouldRefreshJournalInCoop)
			RefreshJournalCoop();
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	static TCOTDZFItemList Create(Vector2 pos, Vector2 size, String className, TCOTDJournalMenu mnu)
	{
		let ret = new('TCOTDZFItemList');
		ret.SetBox(pos, size);
		ret.alpha = 1;

		ret.mFilterClassName = className;
		ret.mJournalMenu = mnu;

		return ret;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void UpdateHover(void)
	{
		// TO DO: clear the focus if mouse is outside of hover range
		if (isHovered)
			mJournalMenu.mCurrentlyFocused = self;
	}

	void UpdateHighlighted(void)
	{
		if (mClickableItems.Size() == 0)
			return;

		if (mSelected < 0)
			return;

		for (int i = 0; i < mClickableItems.Size(); i++)
		{
			mClickableItems[i].isHighlighted = false;
		}

		mClickableItems[mSelected].isHighlighted = true;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void RefreshJournalCoop(void)
	{
		// previous selection might be invalid, so just reset it
		mSelected = 0;

		// unpack everything first
		for (int i = 0; i < mClickableItems.Size(); i++)
		{
			// need to retrieve all the elements...
			let ci =  mClickableItems[i];
			let btn = mClickableItems[i].mButton;
			let shdw = mClickableItems[i].mShadow;
			shdw.Unpack();
			btn.Unpack();
			ci.Unpack();
		}

		// now rebuild the list
		Refresh();
	}

	void Refresh(void)
	{
		class<Inventory> cls = mFilterClassName;
		if (!cls)
			return;

		// clear the array
		if (mItems.Size() > 0)
			mItems.Clear();

		if (mClickableItems.Size() > 0)
		{
			for (int i = mClickableItems.Size(); i --> 0;)
			{
				mClickableItems[i].Unpack();
			}
			mClickableItems.Clear();
		}

		// find the items on the player
		Array<Inventory> tmpItems;
		tmpItems.Clear();
		//int count = GetItemCount();
		for (Inventory item = mJournalMenu.pmo.Inv; item != NULL; item = item.Inv)
		{
			if (item is cls)
			{
				tmpItems.Push(item);
			}
		}

		// sort by age
		for (int x = 0; x < tmpItems.Size(); x++)
		{
			for (int y = 0; y < tmpItems.Size() - 1; y++)
			{
				if (tmpItems[y + 1].GetAge() > tmpItems[y].GetAge())
				{
					let p = tmpItems[y + 1];
					tmpItems[y + 1] = tmpItems[y];
					tmpItems[y] = p;
				}
			}
		}

		// actually add the items to the member array
		for (int i = 0; i < tmpItems.Size(); i++)
		{
			if (tmpItems[i])
			{
				mItems.Push(tmpItems[i]);
			}
		}

		// rebuild the list
		if (mItems.Size() > 0)
		{
			double nextY = 0;
			for (int i = 0; i < mItems.Size(); i++)
			{
				if (mItems[i])
				{
					String txt = String.Format("- %s", StringTable.Localize(mItems[i].GetTag()));

					let newItemShadow = TCOTDZFLabel.Create
					(
						(0 + 1.5, nextY + 1.5),
						(self.box.size.X, 0),
						text: txt,
						fnt: SmallFont,
						wrap: true,
						autoSize: true,
						textScale: TCOTDJournalMenu.TXT_SCALE,
						textColor: SmallFont.FindFontColor('TCOTDBlack')
					);
					newItemShadow.alpha = 0.89;

					let newItem = TCOTDZFClickableItem.Create
					(
						(0, nextY),
						(self.box.size.X, 0),
						text: txt,
						fnt: SmallFont,
						wrap: true,
						autoSize: true,
						textScale: TCOTDJournalMenu.TXT_SCALE,
						textColor: SmallFont.FindFontColor('TCOTDJournalTextColor'),
						item: mItems[i],
						shadow: newItemShadow
					);

					newItem.mItem = mItems[i];

					newItemShadow.Pack(self);
					newItem.Pack(self);

					BrokenLines textLines = SmallFont.BreakLines(newItem.text, int(newItem.box.size.X / newItem.textScale));
					int prevLineCount = textLines.Count();
					nextY += (SmallFont.GetHeight() * newItem.textScale * prevLineCount);

					newItem.MakeButton(mJournalMenu.handler, prevLineCount, mFilterClassName, mItems[i].GetClassName());

					mClickableItems.Push(newItem);
				}
			}
		}
	}

	/*
	int GetItemCount(void)
	{
		class<Inventory> cls = mFilterClassName;
		if (!cls)
			return 0;

		int count = 0;
		for (Inventory item = mJournalMenu.pmo.Inv; item != NULL; item = item.Inv)
		{
			if (item is cls)
				count++;
		}
		return count;
	}
	*/

	int GetTotalItems(void)
	{
		return mItems.Size();
	}
}
