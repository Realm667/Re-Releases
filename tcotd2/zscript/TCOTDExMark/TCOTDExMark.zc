class TCOTDExMark : TCOTDDietActor
{
	bool touched;

	//===========================================================================
	//
	//
	//
	//===========================================================================

	Default
	{
		//$Title Exclamation Mark (greyed out on touching)
		Radius 64;
		Height 64;
		Scale 1.35;
		RenderStyle "Translucent";
		Alpha 0.75;
		FloatBobPhase 0;
		+FORCEXYBILLBOARD
		+SPECIAL
		+NOGRAVITY
	}

	// FloatBobPhase must be 0, otherwise multiplayer will desync.

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void Touch(Actor other)
	{
		if (touched) return;
		else if (other && other is "PlayerPawn")
		{
			// only the player who touched it sees it as inactive
			let pmo = TCOTDPlayer(other);
			let p = pmo.player;
			if (players[consoleplayer] == p)
			{
				SetStateLabel("Inactive");
				touched = true;
			}
		}
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	Spawn:
		EXCL A 1 Bright;
		Loop;
	Inactive:
		EXCL A 35 Bright
		{
			A_SpawnItemEx("TCOTDExMarkTouchedFX");
			A_StartSound("misc/p_pkup");
		}
		EXCL B -1;
		Stop;
	}
}


class TCOTDExMarkTouchedFX : TCOTDDietActor
{
	Default
	{
		RenderStyle "Shaded";
		StencilColor "FF FF FF";
		FloatBobPhase 0;
		+NOINTERACTION
		+FORCEXYBILLBOARD
	}

	States
	{
	Spawn:
		EXCL A 1 Bright
		{
			A_SetScale(Scale.X * 1.05, Scale.Y * 1.02);
			A_FadeOut(0.0225);
		}
		Loop;
	}
}
