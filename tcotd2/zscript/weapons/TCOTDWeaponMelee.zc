/*
Credits:
-Hand sprites from Fate (by Capstone);
-Pitchfork & Puff sprites from Blood
-Sounds from Thief
-Flying Pitchfork sprites by NeoWorm & Amuscaria acording to credits

Original code inherited from the original TCoTD 2, modified by Gothic.
*/

// ZScript rewrite by Nash Muhandes

class TCOTDWeaponMelee : TCOTDWeapon
{
	Default
	{
		//$Title Pitchfork
		Weapon.SelectionOrder 3700;
		Weapon.KickBack 100;
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOALERT
		+WEAPON.NOAUTOFIRE
		+THRUGHOST
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	override void DoEffect(void)
	{
		Super.DoEffect();
		UpdateTag();
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	void UpdateTag(void)
	{
		if (Owner)
		{
			if (Owner.CountInv("TCOTDPitchforkPickup") > 0)
			{
				SetTag("$TAG_MELEE_PITCHFORK");
			}
			else
			{
				SetTag("$TAG_MELEE_FIST");
			}
		}
	}

	action bool hasPitchfork(void)
	{
		if (!invoker.Owner)
			return false;
		return invoker.Owner.CountInv("TCOTDPitchforkPickup");
	}

	action State A_CheckPitchfork(StateLabel label)
	{
		State st = ResolveState(label);
		return hasPitchfork() ? st : NULL;
	}

	action void A_FistAttack(void)
	{
		A_CustomPunch(TCOTDGameplayStatics.GetAttackDamage(10.5), true, CPF_NOTURN, "TCOTDPunchPuff", meleesound: "fist/hit");
	}

	action void A_PitchforkAttack(void)
	{
		int angles = 4;
		double cone = 27.0;
		for (int i = 0; i < angles; i++)
		{
			int flags = FBF_EXPLICITANGLE | FBF_NORANDOMPUFFZ | FBF_NOFLASH;
			double ang = cone * (i / double(angles));
			Fire3DBullets(invoker.Angle - (cone / 2) + ang + 4.0, 0, 0, 8.5, "TCOTDPitchforkPuff", flags, DEFMELEERANGE * 4 / 3.);
		}
	}

	private action void A_ViewPunch(void)
	{
		if (!player)
			return;
		if (!CVar.GetCVar("tcotd2_viewkick", player).GetBool())
			return;

		A_SetViewPitch(0);
		A_Overlay(-PSP_FLASH - 4, "ViewPunch");
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	Spawn:
		TNT1 A -1;
		Stop;
	Ready:
		TNT1 A 0 A_CheckPitchfork("PitchforkReady");
		FIST A 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Select:
		TNT1 A 0 A_CheckPitchfork("PitchforkSelect");
		FIST A 1 A_Raise(RAISELOWERSPEED);
		Loop;
	Deselect:
		TNT1 A 0 A_CheckPitchfork("PitchforkDeselect");
		FIST A 1 A_Lower(RAISELOWERSPEED);
		Loop;
	Fire:
		TNT1 A 0 A_CheckPitchfork("PitchforkFire");
		TNT1 A 0 A_Jump(160, "PunchRightHand");
	PunchLeftHand:
		FIST B 2;
		FIST C 4
		{
			A_ViewPunch();
			A_FistAttack();
		}
		FIST CB 2;
		Goto Ready;
	PunchRightHand:
		FIST D 2;
		FIST E 4
		{
			A_ViewPunch();
			A_FistAttack();
		}
		FIST ED 2;
		Goto Ready;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	PitchforkReady:
		FORK A 1 A_WeaponReady();
		Goto Ready;
	PitchforkSelect:
		FORK A 1 A_Raise(RAISELOWERSPEED);
		Loop;
	PitchforkDeselect:
		FORK A 1 A_Lower(RAISELOWERSPEED);
		Loop;
	PitchforkFire:
		FORK A 1 A_WeaponOffset(0, 30, WOF_INTERPOLATE);
		FORK B 2;
		FORK C 9
		{
			A_ViewPunch();
			A_PitchforkAttack();
		}
		FORK C 1 A_WeaponOffset(0, 34, WOF_INTERPOLATE);
		FORK B 2;
		FORK A 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		Goto Ready;
	AltFire:
		FORK B 1 A_StartSound("pitchfork/throw", CHAN_WEAPON);
		FORK C 1;
		FORK D 1 A_FireProjectile("TCOTDPitchforkThrown", spawnheight: 5);
		FORK E 1 A_TakeInventory("TCOTDPitchforkPickup");
		FORK E 1 A_WeaponOffset(-18, 48, WOF_INTERPOLATE);
		FORK E 1 A_WeaponOffset(-36, 64, WOF_INTERPOLATE);
		FORK E 1 A_WeaponOffset(-54, 80, WOF_INTERPOLATE);
		FORK E 1 A_WeaponOffset(-72, 92, WOF_INTERPOLATE);
		TNT1 A 2;
		Goto Ready;
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	ViewPunch:
		TNT1 A 35
		{
			if (invoker.Owner == players[consoleplayer].camera)
			{
				let q = Spawn("TCOTDViewPunchQuake", invoker.Owner.Pos, NO_REPLACE);
				if (q)
				{
					q.A_QuakeEx(0.75, 0.75, 0.05,
						8, 0, 64, "", QF_RELATIVE | QF_SCALEDOWN);
				}
			}
		}
		Stop;
	}
}

class TCOTDPitchforkSpark : Actor
{
	Default
	{
		Radius 3;
		Height 4;
		Speed 25;
		RenderStyle "Add";
		Scale 0.7;
		SeeSound "Fork/Stab";
		+FORCEXYBILLBOARD
		+CANNOTPUSH
		+DONTSPLASH
		+NOBLOCKMAP
		+NOGRAVITY
		+RANDOMIZE
		+THRUGHOST
	}

	States
	{
	Spawn:
		TNT1 A 0;
		BSPR A 0 A_Jump(128, 7);
		BSPR ABCDEFG 2 Bright;
		Stop;
		BSPR HIJKLMN 2 Bright;
		Stop;
	XDeath:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("Fork/Stab");
		BLD2 A 0 A_Jump(128,"XDeath2");
		BLD2 FGHI 4;
		Stop;
	XDeath2:
		BLD2 OPQR 4;
		Stop;
	}
}

class TCOTDPitchforkPuff : Actor
{
	Default
	{
		DamageType "Stab";
		Scale 0.5;
		Activesound "";
		Attacksound "pitchfork/wall";	// what sound it makes when you hit a wall with it
		Seesound "pitchfork/flesh";		// what sound it makes when you beat somebody up with it
		+FORCEXYBILLBOARD
		+NOBLOCKMAP
		+NOGRAVITY
		+NOEXTREMEDEATH
		+PUFFONACTORS					// this is necessary for it to still make sound on enemies
	}

	States
	{
	Crash:
		//PPUF A 0 A_SpawnItem("TCOTDPitchforkSpark");
		PPUF ABCDE 2;
		Stop;
	Spawn:
	Melee:
		TNT1 A 1;
		Stop;
	}
}

class TCOTDPitchforkThrown : Actor
{
	protected Array<Actor> victims;

	Default
	{
		Radius 10;
		Height 5;
		Speed 20;
		DamageFunction GetDamage(61.5);
		Gravity 0.125;
		Projectile;
		+RIPPER
		+THRUGHOST
		+FORCEXYBILLBOARD
		+SKYEXPLODE
		+NOEXTREMEDEATH
		-NOBLOCKMAP
		-NOGRAVITY
	}

	private double GetDamage(double baseDamage)
	{
		if (victims.Size() > 1)
		{
			baseDamage = 52.5;
		}

		return TCOTDGameplayStatics.GetAttackDamage(baseDamage);
	}

	override int SpecialMissileHit(Actor victim)
	{
		if (victim && target && victim != target)
		{
			if (victims.Find(victim) == victims.Size())
			{
				victims.Push(victim);
			}
			else
			{
				return 1;
			}
		}

		return -1;
	}

	States
	{
	Spawn:
		XIPP A -1;
		Stop;
	Death:
		XIPP A 0 A_AlertMonsters();
		XIPP A 4 A_SpawnItemEx("TCOTDPitchforkPickup");
		Stop;
	}
}

class TCOTDPitchforkPickup : Inventory replaces Chainsaw
{
	Default
	{
		Scale 1.2;
		Inventory.PickupMessage "$IP_FORK";
		Inventory.PickupSound "items/pickup";
		Inventory.MaxAmount 1;
		-Inventory.INVBAR
		+FORCEXYBILLBOARD
	}

	States
	{
	Spawn:
		XFOR A -1;
		Stop;
	}
}

class TCOTDViewPunchQuake : Actor
{
	mixin TCOTDClientSideOnlyMixin;

	Default
	{
		+NOINTERACTION
		-SOLID
	}

	States
	{
	Spawn:
		TNT1 A 35;
		Stop;
	}
}
