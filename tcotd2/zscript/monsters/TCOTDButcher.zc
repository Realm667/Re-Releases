/////////////////
// FAT ASSHOLE //
/////////////////
class Fatty : TCOTDMonster
{
	Default
	{
		//$Category New Monsters (TCOTD2)
		//$Title Butcher
		//$Color 12
		Health 300;
		GibHealth 35;
		Radius 20;
		Height 77;
		Mass 500;
		Speed 1.75;
		Scale 0.8;
		MeleeThreshold 196;
		PainChance 100;
		SeeSound "fatty/sight";
		PainSound "fatty/pain";
		DeathSound "fatty/death";
		ActiveSound "fatty/active";
		Obituary "$OB_FATY";
		HitObituary "$HB_FATY";
		Tag "$FN_FATY";
		DropItem "TCOTDHealthPickupHeart", 128;
		Species "Fatty";
		+MISSILEMORE
	}

	States
	{
	Spawn:
		BFAT A 10 A_Look();
		Loop;
	See:
		BFAT A 1 A_Chase();
		BFAT AA 1 A_Chase2();
		BFAT A 1 A_Chase();
		BFAT AA 1 A_Chase2();
		BFAT B 1 A_Chase();
		BFAT BB 1 A_Chase2();
		BFAT B 1 A_Chase();
		BFAT BB 1 A_Chase2();
		BFAT C 1 A_Chase();
		BFAT CC 1 A_Chase2();
		BFAT C 1 A_Chase();
		BFAT CC 1 A_Chase2();
		BFAT D 1 A_Chase();
		BFAT DD 1 A_Chase2();
		BFAT D 1 A_Chase();
		BFAT DD 1 A_Chase2();
		BFAT E 1 A_Chase();
		BFAT EE 1 A_Chase2();
		BFAT E 1 A_Chase();
		BFAT EE 1 A_Chase2();
		BFAT F 1 A_Chase();
		BFAT FF 1 A_Chase2();
		BFAT F 1 A_Chase();
		BFAT FF 1 A_Chase2();
		Loop;
	Melee:
		BFAT H 4 A_FaceTarget();
		BFAT I 1 A_SkelWhoosh();
		BFAT I 3 A_FaceTarget();
		BFAT J 4 A_SkelFist();
		BFAT K 4;
		BFAT L 4;
		Goto See;
	Missile:
		BFAT M 4 A_FaceTarget();
		BFAT N 7 A_FaceTarget();
		BFAT O 5 A_FaceTarget();
		TNT1 A 0 A_Jump(128, 3);
		BFAT P 5 A_SpawnProjectile("ButcherVomit1", 56, flags: CMF_AIMDIRECTION);
		BFAT O 10 A_FaceTarget();
		Goto See;
		BFAT P 5 A_SpawnProjectile("ButcherVomit2", 56, flags: CMF_AIMDIRECTION);
		BFAT O 10 A_FaceTarget();
		Goto See;
	Pain:
		BFAT G 5;
		BFAT G 5 A_Pain();
		Goto See;
	Death:
		BFTD A 6 A_Scream();
		BFTD BC 6;
		BFTD D 6 A_NoBlocking();
		BFTD EF 6;
		BFTD G -1;
		Stop;
	XDeath:
		BFTD H 5 A_Scream();
		BFTD I 5;
		BFTD J 5
		{
			A_SpawnGibs();
			A_NoBlocking();
		}
		BFTD K 5;
		BFTD LMNOPQ 5;
		BFTD QRQRRRQRQQRRQQ 10;
		BFTD R -1;
		Stop;
	Raise:
		BFTD GFEDCBA 5;
		Goto See;
	}
}

class ButcherGoo : Actor
{
	Default
	{
		Height 2;
		Radius 2;
		Damage 0;
		Scale 0.2;
		Alpha 0.85;
		RenderStyle "Add";
		+CANNOTPUSH
		+LOWGRAVITY
		-SOLID
	}

	States
	{
	Spawn:
		BARF ABCDEFABCDEFABCDEF 3 Light("GGLOW2");
	Death:
		BARF AAAA 4 A_FadeOut(0.25);
		Stop;
	}
}

class ButcherVomit1 : Actor
{
	Default
	{
		Height 8;
		Radius 8;
		DamageFunction (random[rnd_ButcherDmg](10, 12));
		Speed 18;
		Scale 0.4;
		RenderStyle "Add";
		Alpha 0.85;
		ReactionTime 3;
		Projectile;
		SeeSound "fatty/attack";
		DeathSound "fatty/vomitsplat";
	}

	protected void A_SpawnGoo(void)
	{
		A_SpawnItemEx("ButcherGoo",
			frandom[rnd_bvomit1](-2, 2),
			frandom[rnd_bvomit1](-2, 2),
			frandom[rnd_bvomit1](-3, 3),
			frandom[rnd_bvomit1](-2, 2),
			frandom[rnd_bvomit1](-2, 2),
			frandom[rnd_bvomit1](-2, 2));
	}

	States
	{
	Spawn:
		BARF AB 3 Light("GGLOW") A_SetAngle(Angle + frandom[rnd_bvomit2](-5., 5.));
		TNT1 AA 0 A_SpawnGoo();
		BARF CDEF 3 Light("GGLOW") A_SetAngle(Angle + frandom[rnd_bvomit2](-5., 5.));
		TNT1 AA 0 A_SpawnGoo();
		BARF EF 3 Light("GGLOW") A_SetAngle(Angle + frandom[rnd_bvomit2](-5., 5.));
		TNT1 AA 0 A_SpawnGoo();
		Loop;
	Death:
		FBXX A 0 A_SetScale(1.0);
		FBXX A 8;
		FBXX B 6;
		FBXX C 4;
		Stop;
	Crash:
		TNT1 A 0 { A_StartSound("fatty/vomitsplat"); A_SetScale(1.0); }
		FBXX A 8;
		FBXX B 6;
		FBXX C 4;
		Stop;
	}
}

class ButcherVomit2 : ButcherVomit1
{
	States
	{
	Spawn:
		BARF AB 3 Light("GGLOW") { A_SetAngle(Angle + frandom[rnd_bvomit3](-5., 5.)); A_SeekerMissile(10, 20); }
		TNT1 AA 0 A_SpawnGoo();
		BARF CDEF 3 Light("GGLOW") A_SetAngle(Angle + frandom[rnd_bvomit3](-5., 5.));
		TNT1 AA 0 A_SpawnGoo();
		BARF EF 3 Light("GGLOW") { A_SetAngle(Angle + frandom[rnd_bvomit3](-5., 5.)); A_SeekerMissile(10, 20); }
		TNT1 AA 0 A_SpawnGoo();
		Loop;
	}
}
