////////////
// WRAITH //
////////////
class DEWraith : TCOTDMonster
{
	private bool bFlinched;

	Default
	{
		//$Title Wraith
		Health 50;
		Radius 16;
		Height 32;
		Mass 50;
		Speed 3.33333333;
		FloatSpeed 1.33333333;
		Damage 2;
		Scale 1.1;
		RenderStyle "Add";
		Alpha 0.67;
		PainChance int(256 * 0.33335);
		AttackSound "wraith/sight";
		PainSound "archvile/pain";
		DeathSound "wraith/death";
		Obituary "$OB_WRTH";
		HitObituary "$HB_WRTH";
		Tag "$FN_WRTH";
		+DONTFALL
		+DONTGIB
		+FLOAT
		+NOBLOOD
		+NOGRAVITY
		+NOTARGET
		+NOSPRITESHADOW
		+ONLYSLAMSOLID
	}

	void A_WraithFlinched ()
	{
		bFlinched = true;
	}

	override int DamageMobj (Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		bool bChargingWhenDamaged = bSkullFly;
		bFlinched = false;

		if (bChargingWhenDamaged)
		{
			// [Blue Shadow] Monster is charging. Don't let it be thrust by damage and clear the SKULLFLY flag
			// so that its velocity isn't zeroed and it can be subject to pain.
			bDontThrust = true;
			bSkullFly = false;
		}

		// [Blue Shadow] Inflict the damage.
		int dmg = Super.DamageMobj(inflictor, source, damage, mod, flags, angle);

		if (bChargingWhenDamaged)
		{
			// [Blue Shadow] The flag served its purpose now the damage has been inflicted, so clear it.
			bDontThrust = false;

			if (!bFlinched && health > 0)
			{
				// [Blue Shadow] Monster didn't enter the Pain state sequence and it's still alive. Let it continue
				// charging and set the SKULLFLY flag back so it can slam-damage whatever it hits.
				bSkullFly = true;
			}
			else
			{
				// [Blue Shadow] Monster either entered the Pain state sequence or it died from the damage. Stop it
				// dead in its tracks.
				Vel = (0, 0, 0);
			}
		}

		return dmg;
	}

	States
	{
	Spawn:
		WRAI AB 10 A_Look();
		Loop;
	See:
		WRAI A 1 A_VileChase();
		WRAI AA 1 A_Chase(NULL, NULL);
		WRAI A 1 A_VileChase();
		WRAI AA 1 A_Chase(NULL, NULL);
		WRAI B 1 A_VileChase();
		WRAI BB 1 A_Chase(NULL, NULL);
		WRAI B 1 A_VileChase();
		WRAI BB 1 A_Chase(NULL, NULL);
		Loop;
	Missile:
		WRAI A 10 A_FaceTarget();
		WRAI B 4 A_SkullAttack();
		WRAI AB 4;
		Goto Missile + 2;
	Melee:
		WRAI A 5 A_FaceTarget();
		WRAI B 5 A_CustomMeleeAttack(random[rnd_WraithDmg](1, 8), "wraith/attack");
		Goto See;
	Pain:
		WRAI A 3 A_WraithFlinched();
		WRAI A 3 A_Pain();
		Goto See;
	Heal:
		WRAI AB 3;
		TNT1 A 0 A_Die();
	Death:
		WRAI E 4 A_NoBlocking();
		WRAI F 5 A_Scream();
		WRAI GHIJ 4;
		Stop;
	}
}
