////////////
// THAMUZ //
////////////
class Thamuz : TCOTDMonster //kept actor original name for scripts purposes - ozy81
{
	int user_theta;
	int user_height;
	int user_offset;

	Default
	{
		//$Category New Monsters (TCOTD2)
		//$Title Tchernobog
		Health 3200;
		Radius 40;
		Height 110;
		Speed 4.33333333; //slower compared to Cyberbog but summon tentacles
		PainChance 35;
		Mass 2000;
		Scale 0.85;
		Bloodcolor "00 aa 00";
		MinMissileChance 5;
		SeeSound "Omni/Laugh";
		PainSound "Omni/Pain";
		DeathSound "Omni/Death";
		ActiveSound "Omni/Search";
		Obituary "$OB_THAM";
		Species "Horror";
		+BOSS
		+MISSILEMORE
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		//+E2M8BOSS
		//+E4M6BOSS
		+LOOKALLAROUND
		+MISSILEEVENMORE
	}

	States
	{
	Spawn:
		TCHR A 10 NoDelay A_Look();
		Loop;
	See:
		"####" A 1 A_Chase();
		"####" AA 1 A_Chase2();
		"####" A 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" A 1 A_Chase();
		"####" AA 1 A_Chase2();
		"####" A 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" B 1 { A_Chase(); A_SpawnItemEx("BeastStompND"); }
		"####" BB 1 A_Chase2();
		"####" B 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" B 1 A_Chase();
		"####" BB 1 A_Chase2();
		"####" B 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" C 1 A_Chase();
		"####" CC 1 A_Chase2();
		"####" C 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" C 1 A_Chase();
		"####" CC 1 A_Chase2();
		"####" C 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" D 1 { A_Chase(); A_SpawnItemEx("BeastStompND"); }
		"####" DD 1 A_Chase2();
		"####" D 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		"####" D 1 A_Chase();
		"####" DD 1 A_Chase2();
		"####" D 0 A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSPRITEFRAME);
		Loop;
	Missile:
		"####" A 0 { user_Height=96; }
		"####" A 0 { user_offset=-25; }
		"####" A 0 A_Jump(160,"Missile2");
		"####" AGG 5 A_FaceTarget();
		"####" H 10 A_SpawnItemEx("ThamuzCircle", flags: SXF_SETMASTER);
		"####" H 0 A_SpawnItemEx("ThamuzTentacle", 64., 64., flags: SXF_SETMASTER);
		"####" H 0 A_SpawnItemEx("ThamuzTentacle", 64., -64., flags: SXF_SETMASTER);
		"####" H 0 A_SpawnItemEx("ThamuzTentacle", -64., 64., flags: SXF_SETMASTER);
		"####" H 0 A_SpawnItemEx("ThamuzTentacle", -64., -64., flags: SXF_SETMASTER);
		"####" G 15 A_FaceTarget();
		Goto See;
	Missile2:
		"####" A 0 A_Jump(160, "Missile3");
		"####" A 0 A_SetInvulnerable();
		"####" G 20 { A_StartSound("Apocalypse/cast"); A_SpawnItemEx("ThamuzCircle"); }
	Shock:
		"####" G 0 A_SpawnProjectile("DCBMiss3", 50, 0, user_theta);
		"####" G 0 { user_theta = user_theta + 20; }
		"####" G 0 A_JumpIf(user_theta == 360, 1);
		Loop;
		"####" G 90 { user_theta = 0; }
		"####" G 10 A_UnsetInvulnerable();
		Goto See;
	Missile3:
		"####" AG 3 A_FaceTarget();
		"####" G 4 A_FaceTarget();
		"####" H 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" J 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" H 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" J 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" H 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" J 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" H 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" J 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" H 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		"####" I 4 A_FaceTarget();
		"####" J 4 A_SpawnProjectile("DCBMiss2", user_height, 0);
		Goto See;
	Pain:
		"####" L 2;
		"####" L 2 A_Pain();
		"####" L 0 A_Jump(256, "See");
	XDeath:
	Death:
		"####" L 0 A_StartSound("SFX\Explosion1");
		"####" LLLL 5  A_SpawnItemEx("Explosion_Green_A1A", frandom(-64, 64), frandom(-64, 64), frandom(0, 60));
		"####" L 0 A_StartSound("SFX\Explosion1");
		"####" MNO 5  A_SpawnItemEx("Explosion_Green_A1A", frandom(-64, 64), frandom(-64, 64), frandom(0, 60));
		"####" P 7 A_Scream();
		"####" Q 0 A_StartSound("Thamuz/death2", CHAN_5);
		"####" QQQQQQQRRRRRRR 1 A_SpawnProjectile("SFX_Wraith", frandom(0, 64), 0, frandom(0, 359), 2, frandom(-16, 16));
		"####" S 0  A_SpawnItemEx("Explosion_Green_A1A", frandom(-64, 64), frandom(-64, 64), frandom(0, 60));
		"####" SSSSSSSTTTTTTTUUUUUUU 1 A_SpawnProjectile("SFX_Wraith", frandom(0, 64), 0, frandom(0, 359), 2, frandom(-16, 16));
		"####" U 1 A_NoBlocking();
		"####" U 5 A_KillChildren; //remove summoned tentacles
		"####" U 3 A_Fadeout;
		Wait;
	}
}

class ThamuzGhost : Actor
{
	Default
	{
		Radius 2;
		Height 2;
		Scale 0.85;
		Projectile;
		Alpha 0;
	}

	States
	{
	Spawn:
		"####" "#" 3;
		"####" "#" 0 A_SetTranslucent(0.6);
		"####" "#" 3 A_FadeOut();
		Wait;
	}
}

//Circle and Tentacle comes from unreleased version of Hyperion mod by Drugod and Rolls3D, adapted for TCOTD2 by Ozy81
class ThamuzCircle : Actor
{
	Default
	{
		Radius 32;
		Height 0;
		Renderstyle "Add";
		Alpha 0.65;
		Species "Horror";
		+BRIGHT
		+NOBLOCKMAP
		+NOINTERACTION
		+THRUSPECIES
	}

	States
	{
	Spawn:
		UNKN ABCDEFGHIJ 4;
		UNKN JIHGFED 4;
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4;
	Summoned:
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4 A_Fadeout;
		Wait;
	}
}

class ThamuzTentacle : TCOTDMonster
{
	Default
	{
		//$Title Thamuz Tentacle
		Height 64;
		Radius 8;
		Speed 4;
		Health 25;
		Mass 3000;
		MeleeRange 80;
		PainChance 128;
		Scale 0.85;
		Alpha 0.25;
		Renderstyle "Add";
		ActiveSound "TentacleDown";
		DeathSound "TentacleExplode";
		SeeSound "TentacleUp";
		Bloodcolor "00 aa 00";
		Obituary "$OB_TENT";
		Species "Horror";
		+BRIGHT //until I will make bmaps - ozy81
		+DONTSPLASH
		+LOOKALLAROUND
		+NOTARGETSWITCH
		+NORADIUSDMG
		+STRIFEDAMAGE
		+THRUSPECIES
		+VISIBILITYPULSE
		-CANPUSHWALLS
		-COUNTKILL
	}

	States
	{
	Spawn:
		TENT A 1 A_Look();
		Loop;
	See:
		TENT AA 3 A_Chase();
		TENT B 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT BB 3 A_Chase();
		TENT C 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT CC 3 A_Chase();
		TENT D 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT DD 3 A_Chase();
		TENT E 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT EE 3 A_Chase();
		TENT F 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT FF 3 A_Chase();
		Loop;
	Melee:
		TENT GH 4;
		TENT HHHHHH 0 A_SpawnItemEx("Drt", 0, 0, 0, frandom(-3, 3), frandom(-3, 3), 3, 0, SXF_NOCHECKPOSITION);
		TENT I 4 A_CustomMeleeAttack(random(1, 2) * 4, "TentacleSwipe", "", "Fear", false);
		TENT HG 4;
		TENT G 0 A_CposRefire();
		Goto See;
	Pain:
		TENT BCDBABCFEFFEDC 3;
		TENT B 2 A_Pain();
		Goto See;
	Death:
		TEND A 4;
		TEND B 4 A_Scream();
		TEND C 4;
		TEND D 4 A_NoBlocking();
		TEND E 4;
		TEND F 4;
		TENE A random(35, 75);
		TENF B 4 A_StartSound("TentacleDown", CHAN_AUTO);
		TENF C 4;
		TENF D 4;
		TENF E 4;
		TENF F 4;
		TENF G 4;
		Stop;
	}
}

class Drt : Actor
{
	Default
	{
		Radius 2;
		Damage 0;
		Speed 5;
		Gravity 0.5;
		Alpha 0.25;
		Renderstyle "Add";
		Projectile;
		Species "Horror";
		+BRIGHT
		+RANDOMIZE
		+DONTSPLASH
		+THRUSPECIES
		-NOGRAVITY
		-NOBLOCKMAP
		-NOTELEPORT
	}

    States
    {
    Spawn:
        DIRT A 0;
        DIRT A 0 ThrustThingZ(0, 15, 0, 1);
		DIRT A 0 A_Jump(256, "A", "B", "C");
    A:
        DIRT ABC 5 A_FadeOut(0.03);
        Loop;
	B:
		DIRT A 0 A_StartSound("TentacleDown", CHAN_AUTO);
	B2:
        DIRT DEF 5 A_FadeOut(0.03);
        Loop;
	C:
        DIRT GHI 5 A_FadeOut(0.03);
        Loop;
    Death:
        DIRT JKL 3;
        Stop;
    }
}

class DCBMiss2 : Actor
{
	Default
	{
		Radius 12;
		Height 8;
		Health 5;
		Speed 10;
		FastSpeed 20;
		Damage 10;
		Alpha 0.65;
		RenderStyle "Add";
		Projectile;
		Species "Horror";
		SeeSound "q2weapon/rocketlauncherfire";
		DeathSound "q2weapon/rocketexplo";
		+THRUSPECIES
	}

	States
	{
	Spawn:
		CFX5 A 1 Bright Light("ThamuzMissile_2") A_SpawnItemEx("DCBMiss2Trail");
		Loop;
	Death:
		CFX3 A 2 Bright Light("ThamuzMissile_2X_1");
		CFX3 B 2 Bright Light("ThamuzMissile_2X_2");
		CFX3 C 2 Bright Light("ThamuzMissile_2X_3");
		CFX3 D 2 Bright Light("ThamuzMissile_2X_4");
		CFX3 E 2 Bright Light("ThamuzMissile_2X_5");
		CFX3 F 2 Bright Light("ThamuzMissile_2X_6");
		CFX3 G 2 Bright Light("ThamuzMissile_2X_7");
		CFX3 G 2 Bright Light("ThamuzMissile_2X_8");
		CFX3 G 2 Bright Light("ThamuzMissile_2X_9");
		CFX3 G 2 Bright Light("ThamuzMissile_2X_10");
		Stop;
	}
}

class DCBMiss1 : Actor
{
	Default
	{
		Radius 12;
		Height 12;
		Speed 6;
		Damage 30;
		Projectile;
		Species "Horror";
		RenderStyle "Add";
		SeeSound "q2weapon/bfgball";
		DeathSound "q2weapon/bfgballexplo";
		+EXTREMEDEATH
		+SEEKERMISSILE
		+THRUSPECIES
	}

	States
	{
	Spawn:
		CBTS A 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS B 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS C 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS D 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS E 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS F 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS G 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS H 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS I 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		CBTS J 2 Bright Light("ThamuzMissile_1") A_SeekerMissile(10, 90);
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail");
		Loop;
	Death:
		TNT1 A 0
		{
			for (int i = 0; i <= 76; i++)
			{
				A_SpawnItemEx("DCBMiss3Extra",
					0., 0., 5.,
					frandom(-5., 5.), frandom(-5., 5.), frandom(-5., 5.),
					frandom(0., 360.));
			}
		}
		CBTS K 2 Bright Light("ThamuzMissile_1X_1") A_Explode(140);
		CBTS L 2 Bright Light("ThamuzMissile_1X_2");
		CBTS M 2 Bright Light("ThamuzMissile_1X_3");
		CBTS N 2 Bright Light("ThamuzMissile_1X_4");
		CBTS O 2 Bright Light("ThamuzMissile_1X_5");
		CBTS P 2 Bright Light("ThamuzMissile_1X_6");
		CBTS Q 2 Bright Light("ThamuzMissile_1X_7");
		CBTS R 2 Bright Light("ThamuzMissile_1X_8");
		CBTS S 2 Bright Light("ThamuzMissile_1X_9");
		CBTS T 2 Bright Light("ThamuzMissile_1X_10");
		CBTS U 2 Bright Light("ThamuzMissile_1X_11");
		CBTS V 2 Bright Light("ThamuzMissile_1X_12");
		CBTS W 2 Bright Light("ThamuzMissile_1X_13");
		Stop;
	}
}

class DCBM1Trail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Scale 0.8;
		Projectile;
		RenderStyle "Add";
		Alpha 0.6;
		+NOCLIP
	}

	States
	{
	Spawn:
		TNT1 A 4;
		CBTS LMNOPQRSTUVW 2 Bright A_FadeOut();
		Stop;
	}
}

class DCBMiss2Trail : DCBM1Trail
{
	Default
	{
		Scale 0.25;
		Alpha 0.4;
	}

	States
	{
	Spawn:
		FSFX DEFGHIJKL 1 Bright;
		FSFX M 1 Bright A_FadeOut();
		Wait;
	}
}

class DCBMiss3 : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Health 5;
		Speed 8;
		Scale 2;
		RenderStyle "Add";
		SeeSound "h2necromancer/magiccast";
		Projectile;
		Species "Horror";
		+BLOODLESSIMPACT
		+DONTSPLASH
		+RIPPER
		+THRUSPECIES
	}

	States
	{
	Spawn:
		TNT1 A 2 A_Gravity();
		Loop;
	Death:
		FSFX NNNOOOPPPQQQRRRSSSTTTNNNOOOPPPQQQRRRSSSTTT 1 Bright Light("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra", 0, 0, 5, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(0, 360));
		TNT1 A 0 A_SpawnProjectile("DCBMiss3M", 0, 0, 180);
		Stop;
	}
}

class DCBMiss3M : Actor
{
	Default
	{
		Radius 10;
		Height 10;
		Speed 28;
		Damage 12;
		Scale 1.3;
		RenderStyle "Add";
		Projectile;
		Species "Horror";
		DeathSound "comet/explosion";
		+FLOORHUGGER
		+THRUSPECIES
	}

	States
	{
	Spawn:
		FSFX N 1 Bright Light("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3MG");
		FSFX NNOOOPPPQQQRRRSSSTTT 1 Bright Light("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra");
		Loop;
	Death:
		FSFX A 0 A_Explode(128, 96);
		FSFX AAAAA 0 A_SpawnItemEx("DCBMiss3Extra");
		FSFX U 3 Bright Light("ThamuzMissile_3X_1");
		FSFX V 3 Bright Light("ThamuzMissile_3X_2");
		FSFX W 3 Bright Light("ThamuzMissile_3X_3");
		Stop;
	}
}

class DCBMiss3Extra : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		+NOBLOCKMAP
		+NOGRAVITY
	}

	States
	{
	Spawn:
		TNT1 AA 0 NoDelay A_SpawnItemEx("DCBMiss3Extra2", 0,0, 5, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(0, 360));
		Stop;
	}
}

class DCBMiss3Extra2 : DCBMiss3Extra
{
	Default
	{
		Damage 2;
		Scale 0.18;
		RenderStyle "Add";
		Projectile;
		Translation "168:191=80:82";
		Species "Horror";
		+BLOODLESSIMPACT
		+RIPPER
		+THRUSPECIES
	}

	States
	{
	Spawn:
		CBTS SS 1 Bright A_Explode(12, 32);
		TNT1 A 0 A_Jump(120, 1);
		Loop;
		CBTS SSS 1 Bright A_Explode(12, 32);
		CBTS S 1 Bright A_FadeOut();
		Wait;
	}
}

class DCBMiss3MG : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Scale 0.8;
		Damage 5;
		RenderStyle "Add";
		Alpha 0.6;
		Projectile;
		Species "Horror";
		+RIPPER
		+THRUSPECIES
	}

	States
	{
	Spawn:
		TNT1 A 4;
		FSFX NOPQRST 3 Bright;
		Stop;
	}
}

class Explosion_Green_A1A : Actor
{
	Default
	{
		Radius 1;
		Height 2;
		+NOBLOCKMAP
		+NOGRAVITY
	}

	States
	{
	Spawn:
		TNT1 A 1 NoDelay Radius_Quake(8, 140, 0, 16, 0);
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GreenEmber_A1A", frandom(-16, 16), frandom(-16, 16), frandom(0, 76), frandom(-1, 1), frandom(-1, 1), frandom(1, 8));
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1A", frandom(-8, 8), frandom(-8, 8), frandom(0, 16), frandom(-1, 1), frandom(-1, 1), frandom(0, 8));
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1B", frandom(-8, 8), frandom(-8, 8), frandom(0, 16), frandom(-1, 1), frandom(-1, 1), frandom(0, 8));
		TNT1 AAAAA 0 A_SpawnProjectile("SFX_Wraith", frandom(0, 64), 0, frandom(0, 359), 2, frandom(-16, 16));
		Stop;
	}
}

class Fire_Green_A1A : Explosion_Green_A1A
{
	Default
	{
		RenderStyle "Add";
	}

	States
	{
	Spawn:
		AFGR KJIHGFEDCBA 1 Bright NoDelay A_FadeOut(0.02);
		Wait;
	}
}

class Fire_Green_A1B : Fire_Green_A1A
{
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(-Scale.X, Scale.Y);
		Goto Super::Spawn;
	}
}

class GreenEmber_A1A : Fire_Green_A1A
{
	Default
	{
		Scale 0.05;
	}

	States
	{
	Spawn:
		EMGR A 1 Bright A_FadeOut(0.005);
		Loop;
	}
}

class SFX_Wraith : Actor
{
	Default
	{
		Projectile;
		SeeSound "monster/SprSit";
		Speed 8;
		RenderStyle "Add";
		Alpha 0.6;
		Translation "80:111=112:127";
	}

	States
	{
	Spawn:
		WRAI AABB 5 A_FadeOut(0.05);
		Loop;
	}
}
