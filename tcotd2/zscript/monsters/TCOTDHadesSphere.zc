class HS : TCOTDMonster
{
	Default
	{
		//$Title Hades Sphere
		Health 50;
		Radius 16;
		Height 32;
		Speed 7;
		SeeSound "hades/sight";
		Obituary "$OB_HADE";
		Tag "$FN_HADE";
		+DONTFALL
		+DONTGIB
		+DONTTHRUST
		+DONTHARMCLASS
		+FLOAT
		+LOOKALLAROUND
		+NOGRAVITY
		+NOTARGET
		+NOBLOOD
	}

	States
	{
	Spawn:
		TNT1 A 1 A_Look();
		Loop;
	See:
		HADE EFGH 4 Bright;
		HADE A 2 Bright A_StartSound("hades/walk");
		HADE AABBBBCCCCDDDD 1 Bright A_Chase();
		//HADE D 0 A_Jump(32, 1); // [Blue Shadow] Disable the teleportation ability. We don't want it.
		Goto See + 4;
		HADE H 0 A_UnSetSolid();
		HADE H 0 A_UnSetShootable();
		HADE H 5 Bright A_StartSound("hades/teleport");
		HADE GFEIJKL 4 Bright;
	Teleporting:
		TNT1 AAAAAAAAAAAAAAAA 1 A_Chase();
		TNT1 A 0 A_Jump(96, 1);
		Loop;
		HADE L 3 Bright A_StartSound("hades/teleport");
		HADE KJIEFG 4 Bright;
		HADE H 0 A_SetShootable();
		HADE H 0 A_SetSolid;
		Goto See + 4;
	Melee:
		HADE H 4 Bright A_UnSetShootable();
		HADE GFE 4 Bright;
		HADE E 0 { DamageType = 'Boom'; Die(nullptr, nullptr); }
	Death.Boom:
		HADE M 0 A_NoBlocking();
		HADE M 0 A_SetTranslucent(0.9, 1);
		HADE M 0 A_StartSound("hades/explode");
		HADE M 4 Bright A_Explode(health, 176, XF_NOTMISSILE | XF_EXPLICITDAMAGETYPE, false, 96);
		HADE NOPQ 5 Bright;
		Stop;
	Death:
		HADE N 0 A_NoBlocking();
		HADE H 5 Bright A_StartSound("hades/death");
		HADE GFERSTUVWX 4 Bright;
		Stop;
	}
}
