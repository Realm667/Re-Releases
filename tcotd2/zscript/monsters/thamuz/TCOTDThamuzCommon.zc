// ------------------------------------------------------------------------------------------------
//
// ------------------------------------------------------------------------------------------------

class ThamuzBase : TCOTDMonster abstract
{
	Default
	{
		Health 6400;
		Radius 40;
		Height 110;
		Mass 2000;
		PainChance 35;
		PainChance "Stab", 35 / 3;
		MinMissileChance 5;
		Obituary "$OB_THAM";
		Tag "$FN_THAM";
		Species "Thamuz";
		+BOSS
		+MISSILEMORE
		+NORADIUSDMG
		+LOOKALLAROUND
		+MISSILEEVENMORE
		+CASTSPRITESHADOW
	}

	protected void A_ThamuzSpawnGhost(void)
	{
		A_SpawnItemEx("ThamuzGhost", flags: SXF_TRANSFERSCALE | SXF_TRANSFERSPRITEFRAME);
	}

	protected void A_ThamuzChase(bool stomp = false)
	{
		if (stomp) A_SpawnItemEx("BeastStompND");
		A_ThamuzSpawnGhost();
		A_Chase();
	}

	protected void A_ThamuzAttackDecide(void)
	{
		StateLabel attackSeq = nullptr;
		int hp = health * 100 / GetMaxHealth();
		int chance = random[rnd_ThamuzAttackDecide]() * 100 / 255;

		/*---------------------------------------------------------
		|                     |            Probability            |
		|     HP Thresold     |-----------------------------------|
		|                     |  Primary  | Secondary |   Spell   |
		|---------------------|-----------------------------------|
		| 75% or above        |   100%    |    0%     |    0%     |
		| Between 45% and 75% |    65%    |    35%    |    0%     |
		| Less than 45%       |    50%    |    20%    |    30%    |
		---------------------------------------------------------*/

		if (hp >= 75)
		{
			attackSeq = 'Missile.Primary';
		}
		else if (hp >= 45)
		{
			if (chance >= 35)
			{
				attackSeq = 'Missile.Primary';
			}
			else
			{
				attackSeq = 'Missile.Secondary';
			}
		}
		else if (chance >= 50)
		{
			attackSeq = 'Missile.Primary';
		}
		else if (chance >= 30)
		{
			attackSeq = 'Missile.Secondary';
		}
		else
		{
			attackSeq = 'Missile.Spell';
		}

		SetStateLabel(attackSeq);
	}

	protected void A_ThamuzCircle(bool playsnd = true)
	{
		A_FaceTarget();
		bNoPain = true;
		DamageFactor = 0.33335;
		if (playsnd) A_StartSound("thamuzdoom/circle", CHAN_6);
		A_SpawnItemEx("ThamuzCircle", zofs: 0.1);
	}

	protected void A_ThamuzSpellEnd(void)
	{
		bNoPain = false;
		DamageFactor = 1.0;
	}

	protected void A_ThamuzFlamesSpellFlames(void)
	{
		for (int i = 0; i < 360; i += 20)
		{
			A_SpawnItemEx("DCBMiss3", 72, angle: i);
		}
	}

	protected void A_ThamuzFlamesSpellAOE(void)
	{
		A_Explode(TCOTDGameplayStatics.GetAttackDamage(15), 96, XF_NOTMISSILE | XF_THRUSTLESS, fulldamagedistance: 96);
	}
}

class ThamuzGhost : Actor
{
	Default
	{
		Alpha 0;
		+FORCEXYBILLBOARD
		+NOINTERACTION
	}

	States
	{
	Spawn:
		"####" "#" 3;
		"####" "#" 0 A_SetRenderStyle(0.6, STYLE_Translucent);
		"####" "#" 3 A_FadeOut();
		Wait;
	}
}

// Comes from unreleased version of Hyperion mod by Drugod and Rolls3D, adapted for TCOTD2 by Ozy81
class ThamuzCircle : Actor
{
	Default
	{
		Renderstyle "Add";
		Alpha 0.65;
		+BRIGHT
		+NOINTERACTION
	}

	States
	{
	Spawn:
		UNKN ABCDEFGHIJ 4;
		UNKN JIHGFED 4;
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4;
	Summoned:
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4 A_Fadeout;
		Wait;
	}
}

class DCBMiss3 : Actor
{
	Default
	{
		Scale 2;
		RenderStyle "Add";
		+MISSILE // [Blue Shadow] CMF_TRACKOWNER won't work unless the caller is a missile.
		+BRIGHT
		+FORCEXYBILLBOARD
		+NOINTERACTION
	}

	States
	{
	Spawn:
		FSFX N 0 NoDelay A_StartSound("thamuzdoom/missile3");
		FSFX NNNOOOPPPQQQRRRSSSTTTNNNOOOPPPQQQRRRSSSTTT 1 Light("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra", 0, 0, 5, frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](0, 360));
		FSFX T 0 A_SpawnProjectile("DCBMiss3M", 0, angle: 180, CMF_TRACKOWNER);
		Stop;
	}
}

class DCBMiss3M : Actor
{
	Default
	{
		Radius 10;
		Height 20;
		Speed 28;
		DamageFunction 400;
		Scale 1.3;
		RenderStyle "Add";
		Projectile;
		Species "Thamuz";
		DeathSound "thamuzdoom/missile3mexplode";
		+NOEXTREMEDEATH
		+FORCEXYBILLBOARD
		+FLOORHUGGER
		+THRUSPECIES
		+DONTSPLASH
		+BRIGHT
	}

	protected void A_DCBMiss3MSpawnSFX(int count = 1)
	{
		for (int i = 0; i < count; i++)
		{
			A_SpawnItemEx("DCBMiss3Extra",
				0, 0, 5,
				frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5),
				frandom[rnd_DCDMissExtra](0, 360));
		}
	}

	States
	{
	Spawn:
		FSFX NNNOOOPPPQQQRRRSSSTTT 1 Light("ThamuzMissile_3") A_DCBMiss3MSpawnSFX();
		Loop;
	Death:
		FSFX U 3 Light("ThamuzMissile_3X_1") A_DCBMiss3MSpawnSFX(5);
		FSFX V 3 Light("ThamuzMissile_3X_2");
		FSFX W 3 Light("ThamuzMissile_3X_3");
		Stop;
	}
}

class DCBMiss3Extra : Actor
{
	Default
	{
		Scale 0.18;
		RenderStyle "Add";
		Translation "168:191=80:82";
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+BRIGHT
	}

	States
	{
	Spawn:
		CBTS SS 1;
		CBTS S 0 A_Jump(120, 1);
		Loop;
		CBTS SSS 1;
		CBTS S 1 A_FadeOut();
		Wait;
	}
}
