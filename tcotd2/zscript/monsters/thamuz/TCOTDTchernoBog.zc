////////////
// THAMUZ //
////////////
class Thamuz : ThamuzBase //kept actor original name for scripts purposes - ozy81
{
	Default
	{
		//$Category New Monsters (TCOTD2)
		//$Title Tchernobog
		Speed 4.33333333; //slower compared to Cyberbog but summon tentacles
		Scale 0.85;
		BloodColor "00 aa 00";
		SeeSound "thamuzblood/laugh";
		PainSound "thamuzblood/pain";
		DeathSound "thamuzblood/die";
		ActiveSound "thamuzblood/search";
		+DONTHARMSPECIES
	}

	protected void A_ThamuzSpawnTentacles(void)
	{
		A_SpawnItemEx("ThamuzTentacle", 64, 64, flags: SXF_SETMASTER);
		A_SpawnItemEx("ThamuzTentacle", 64, -64, flags: SXF_SETMASTER);
		A_SpawnItemEx("ThamuzTentacle", -64, 64, flags: SXF_SETMASTER);
		A_SpawnItemEx("ThamuzTentacle", -64, -64, flags: SXF_SETMASTER);
	}

	protected void A_ThamuzFireballAtk(void)
	{
		A_FaceTarget();

		let misl = A_SpawnProjectile("DCBMiss2", 96);

		if (misl)
		{
			misl.VelIntercept(target);
		}
	}

	States
	{
	Spawn:
		TCHR A 10 A_Look();
		Loop;
	See:
		TCHR A 1 A_ThamuzChase();
		TCHR AA 1 A_Chase2();
		TCHR A 1 A_ThamuzChase();
		TCHR AA 1 A_Chase2();
		TCHR B 1 A_ThamuzChase(true);
		TCHR BB 1 A_Chase2();
		TCHR B 1 A_ThamuzChase();
		TCHR BB 1 A_Chase2();
		TCHR C 1 A_ThamuzChase();
		TCHR CC 1 A_Chase2();
		TCHR C 1 A_ThamuzChase();
		TCHR CC 1 A_Chase2();
		TCHR D 1 A_ThamuzChase(true);
		TCHR DD 1 A_Chase2();
		TCHR D 1 A_ThamuzChase();
		TCHR DD 1 A_Chase2();
		Loop;
	Missile:
		TCHR A 0 A_Jump(160, "Missile.FlamesSpell");
	Missile.SummonTentacles:
		TCHR G 20 A_ThamuzCircle(false);
		TCHR H 15 A_ThamuzSpawnTentacles();
		TCHR G 10 A_ThamuzSpellEnd();
		Goto See;
	Missile.FlamesSpell:
		TCHR A 0 A_Jump(160, "Missile.Fireballs");
		TCHR G 20 A_ThamuzCircle();
		TCHR G 14 { A_ThamuzFlamesSpellFlames(); A_ThamuzFlamesSpellAOE(); }
		TCHR G 14 A_ThamuzFlamesSpellAOE();
		TCHR G 62 A_ThamuzFlamesSpellAOE();
		TCHR G 10 A_ThamuzSpellEnd();
		Goto See;
	Missile.Fireballs:
		TCHR GG 3 A_FaceTarget();
		TCHR G 4 A_FaceTarget();
		TCHR H 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR J 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR H 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR J 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR H 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR J 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR H 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR J 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR H 4 A_ThamuzFireballAtk();
		TCHR I 4 A_FaceTarget();
		TCHR J 4 A_ThamuzFireballAtk();
		Goto See;
	Pain:
		TCHR L 5;
		TCHR L 5 A_Pain();
		Goto See;
	Death:
		TCHR L 0 A_StartSound("sfx/explosion1");
		TCHR LLLL 5 A_SpawnItemEx("Explosion_Green_A1A", frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](0, 60));
		TCHR L 0 A_StartSound("sfx/explosion1");
		TCHR MNO 5 A_SpawnItemEx("Explosion_Green_A1A", frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](0, 60));
		TCHR P 7 A_Scream();
		TCHR Q 0 A_StartSound("thamuzdoom/death2", CHAN_5);
		TCHR QQQQQQQRRRRRRR 1 A_SpawnProjectile("SFX_Wraith", frandom[rnd_TchernoBall1](0, 64), 0, frandom[rnd_TchernoBall1](0, 359), 2, frandom[rnd_TchernoBall1](-16, 16));
		TCHR S 0  A_SpawnItemEx("Explosion_Green_A1A", frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](-64, 64), frandom[rnd_TchernoBall1](0, 60));
		TCHR SSSSSSSTTTTTTTUUUUUUU 1 A_SpawnProjectile("SFX_Wraith", frandom[rnd_TchernoBall1](0, 64), 0, frandom[rnd_TchernoBall1](0, 359), 2, frandom[rnd_TchernoBall1](-16, 16));
		TCHR U 1 A_NoBlocking();
		TCHR U 5 A_KillChildren; //remove summoned tentacles
		TCHR U 3 A_Fadeout;
		Wait;
	}
}

// Circle and Tentacle come from unreleased version of Hyperion mod by Drugod and Rolls3D, adapted for TCOTD2 by Ozy81
class ThamuzCircle : Actor
{
	Default
	{
		Renderstyle "Add";
		Alpha 0.65;
		+BRIGHT
		+NOINTERACTION
	}

	States
	{
	Spawn:
		UNKN ABCDEFGHIJ 4;
		UNKN JIHGFED 4;
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4;
	Summoned:
		UNKN DEFGHIJ 4;
		UNKN JIHGFED 4 A_Fadeout;
		Wait;
	}
}

class ThamuzTentacle : TCOTDMonster
{
	Default
	{
		//$Title Thamuz Tentacle
		Height 64;
		Radius 12;
		Speed 4;
		Health 25;
		Mass 3000;
		MeleeRange 80;
		PainChance 128;
		PainChance "Stab", 128 / 3;
		Scale 0.85;
		Alpha 0.25;
		Renderstyle "Add";
		ActiveSound "tentacle/down";
		DeathSound "tentacle/explode";
		SeeSound "tentacle/up";
		Bloodcolor "00 AA 00";
		Obituary "$OB_TENT";
		Tag "$FN_TENT";
		Species "Thamuz";
		// until I will make bmaps - ozy81
		+BRIGHT
		+DONTSPLASH
		+LOOKALLAROUND
		+NOTARGETSWITCH
		+THRUSPECIES
		+VISIBILITYPULSE
		-CANPUSHWALLS
		-COUNTKILL
		+TCOTDMONSTER.INFLICTBLINDNESS
	}

	protected void A_TentacleSpawnDirt(void)
	{
		A_SpawnItemEx("Drt", 0, 0, 0, frandom[rnd_tentacledrt](-3, 3), frandom[rnd_tentacledrt](-3, 3), 3);
	}

	States
	{
	Spawn:
		TENT A 1 A_Look();
		Loop;
	See:
		TENT AA 3 A_Chase();
		TENT B 0 A_TentacleSpawnDirt();
		TENT BB 3 A_Chase();
		TENT C 0 A_TentacleSpawnDirt();
		TENT CC 3 A_Chase();
		TENT D 0 A_TentacleSpawnDirt();
		TENT DD 3 A_Chase();
		TENT E 0 A_TentacleSpawnDirt();
		TENT EE 3 A_Chase();
		TENT F 0 A_TentacleSpawnDirt();
		TENT FF 3 A_Chase();
		Loop;
	Melee:
		TENT GH 4;
		TENT HHHHHH 0 A_TentacleSpawnDirt();
		TENT I 4 A_MonsterMeleeAttack(4.25, -1, "tentacle/swipe", damageType: 'Ghost', bleed: false);
		TENT HG 4;
		Goto See;
	Pain:
		TENT BCDBABCFEFFEDC 3;
		TENT B 2 A_Pain();
		Goto See;
	Death:
		TEND A 4;
		TEND B 4 A_Scream();
		TEND C 4;
		TEND D 4 A_NoBlocking();
		TEND EF 4;
		TENE A 35 A_SetTics(random[rnd_TentacleDeathTics](35, 75));
		TENF B 4 A_StartSound("tentacle/down", CHAN_AUTO);
		TENF CDEFG 4;
		Stop;
	}
}

class Drt : Actor
{
	Default
	{
		Radius 2;
		Height 4;
		Gravity 0.5;
		Alpha 0.25;
		Renderstyle "Add";
		Projectile;
		Species "Thamuz";
		+BRIGHT
		+RANDOMIZE
		+DONTSPLASH
		+THRUSPECIES
		-NOGRAVITY
	}

	States
	{
	Spawn:
		DIRT A 0;
		DIRT A 0 ThrustThingZ(0, 15, 0, 1);
		DIRT A 0 A_Jump(256, "A", "B", "C");
	A:
		DIRT ABC 5 A_FadeOut(0.03);
		Loop;
	B:
		DIRT A 0 A_StartSound("tentacle/down", CHAN_AUTO);
	B2:
		DIRT DEF 5 A_FadeOut(0.03);
		Loop;
	C:
		DIRT GHI 5 A_FadeOut(0.03);
		Loop;
	Death:
		DIRT JKL 3;
		Stop;
	}
}

class DCBMiss2 : Actor
{
	Default
	{
		Radius 12;
		Height 8;
		Speed 15;
		DamageFunction TCOTDGameplayStatics.GetAttackDamage(30);
		Alpha 0.65;
		RenderStyle "Add";
		Projectile;
		Species "Thamuz";
		SeeSound "thamuzdoom/missile";
		DeathSound "thamuzdoom/missileexplode";
		+BRIGHT
		+FORCEXYBILLBOARD
		+DONTSPLASH
		+THRUSPECIES
	}

	States
	{
	Spawn:
		CFX5 A 1 Light("ThamuzMissile_2") A_SpawnItemEx("DCBMiss2Trail");
		Loop;
	Death:
		CFX3 A 2 Light("ThamuzMissile_2X_1");
		CFX3 B 2 Light("ThamuzMissile_2X_2");
		CFX3 C 2 Light("ThamuzMissile_2X_3");
		CFX3 D 2 Light("ThamuzMissile_2X_4");
		CFX3 E 2 Light("ThamuzMissile_2X_5");
		CFX3 F 2 Light("ThamuzMissile_2X_6");
		CFX3 G 2 Light("ThamuzMissile_2X_7");
		CFX3 G 2 Light("ThamuzMissile_2X_8");
		CFX3 G 2 Light("ThamuzMissile_2X_9");
		CFX3 G 2 Light("ThamuzMissile_2X_10");
		Stop;
	}
}

class DCBMiss2Trail : DCBM1Trail
{
	Default
	{
		Scale 0.25;
		Alpha 0.4;
	}

	States
	{
	Spawn:
		FSFX DEFGHIJKL 1;
		FSFX M 1 A_FadeOut();
		Wait;
	}
}

class DCBMiss3 : Actor
{
	Default
	{
		Scale 2;
		RenderStyle "Add";
		+MISSILE // [Blue Shadow] CMF_TRACKOWNER won't work unless the caller is a missile.
		+BRIGHT
		+FORCEXYBILLBOARD
		+NOINTERACTION
	}

	States
	{
	Spawn:
		FSFX N 0 NoDelay A_StartSound("thamuzdoom/missile3");
		FSFX NNNOOOPPPQQQRRRSSSTTTNNNOOOPPPQQQRRRSSSTTT 1 Light("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra", 0, 0, 5, frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](0, 360));
		FSFX T 0 A_SpawnProjectile("DCBMiss3M", 0, angle: 180, CMF_TRACKOWNER);
		Stop;
	}
}

class DCBMiss3M : Actor
{
	Default
	{
		Radius 10;
		Height 20;
		Speed 28;
		DamageFunction 400;
		Scale 1.3;
		RenderStyle "Add";
		Projectile;
		Species "Thamuz";
		DeathSound "thamuzdoom/missile3mexplode";
		+NOEXTREMEDEATH
		+FORCEXYBILLBOARD
		+FLOORHUGGER
		+THRUSPECIES
		+DONTSPLASH
		+BRIGHT
	}

	protected void A_DCBMiss3MSpawnSFX(int count = 1)
	{
		for (int i = 0; i < count; i++)
		{
			A_SpawnItemEx("DCBMiss3Extra",
				0, 0, 5,
				frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5),
				frandom[rnd_DCDMissExtra](0, 360));
		}
	}

	States
	{
	Spawn:
		FSFX NNNOOOPPPQQQRRRSSSTTT 1 Light("ThamuzMissile_3") A_DCBMiss3MSpawnSFX();
		Loop;
	Death:
		FSFX U 3 Light("ThamuzMissile_3X_1") A_DCBMiss3MSpawnSFX(5);
		FSFX V 3 Light("ThamuzMissile_3X_2");
		FSFX W 3 Light("ThamuzMissile_3X_3");
		Stop;
	}
}

class DCBMiss3Extra : Actor
{
	Default
	{
		Scale 0.18;
		RenderStyle "Add";
		Translation "168:191=80:82";
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+BRIGHT
	}

	States
	{
	Spawn:
		CBTS SS 1;
		CBTS S 0 A_Jump(120, 1);
		Loop;
		CBTS SSS 1;
		CBTS S 1 A_FadeOut();
		Wait;
	}
}

class Explosion_Green_A1A : Actor
{
	Default
	{
		Radius 1;
		Height 2;
		+NOBLOCKMAP
		+NOGRAVITY
		+DONTSPLASH
		+FORCEXYBILLBOARD
	}

	States
	{
	Spawn:
		TNT1 A 1 NoDelay Radius_Quake(8, 140, 0, 16, 0);
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GreenEmber_A1A", frandom[rnd_TchernoExpl1](-16, 16), frandom[rnd_TchernoExpl1](-16, 16), frandom[rnd_TchernoExpl1](0, 76), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](1, 8));
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1A", frandom[rnd_TchernoExpl1](-8, 8), frandom[rnd_TchernoExpl1](-8, 8), frandom[rnd_TchernoExpl1](0, 16), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](0, 8));
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1B", frandom[rnd_TchernoExpl1](-8, 8), frandom[rnd_TchernoExpl1](-8, 8), frandom[rnd_TchernoExpl1](0, 16), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](-1, 1), frandom[rnd_TchernoExpl1](0, 8));
		TNT1 AAAAA 0 A_SpawnProjectile("SFX_Wraith", frandom[rnd_TchernoExpl1](0, 64), 0, frandom[rnd_TchernoExpl1](0, 359), 2, frandom[rnd_TchernoExpl1](-16, 16));
		Stop;
	}
}

class Fire_Green_A1A : Explosion_Green_A1A
{
	Default
	{
		RenderStyle "Add";
	}

	States
	{
	Spawn:
		AFGR KJIHGFEDCBA 1 Bright NoDelay A_FadeOut(0.02);
		Wait;
	}
}

class Fire_Green_A1B : Fire_Green_A1A
{
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(-Scale.X, Scale.Y);
		Goto Super::Spawn;
	}
}

class GreenEmber_A1A : Fire_Green_A1A
{
	Default
	{
		Scale 0.05;
	}

	States
	{
	Spawn:
		EMGR A 1 Bright A_FadeOut(0.005);
		Loop;
	}
}

class SFX_Wraith : Actor
{
	Default
	{
		Projectile;
		SeeSound "wraith/sight";
		Speed 8;
		RenderStyle "Add";
		Alpha 0.6;
		Translation "80:111=112:127";
		+FORCEXYBILLBOARD
		+DONTSPLASH
	}

	States
	{
	Spawn:
		WRAI AABB 5 A_FadeOut(0.05);
		Loop;
	}
}
