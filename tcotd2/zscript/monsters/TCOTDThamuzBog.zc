/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81, IvanDobrovski/Cruduxy/Marinelol
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

class ThamuzBog : TCOTDMonster //the original CyberBog from Bloom was in Decorate format, this has been reworked for TCOTD2
{
	Default
	{
		//$Category New Monsters (TCOTD2)
		//$Title ThamuzBog
		Health 3200;
		Radius 40;
		Height 110;
		Mass 2000;
		Speed 5.33333333; //more rapid compared to Thamuz but doesn't summon tentacles
		PainChance 35;
		MinMissileChance 5;
		SeeSound "thamuzdoom/idle";
		PainSound "thamuzdoom/pain";
		DeathSound "thamuzdoom/death";
		ActiveSound "thamuzdoom/idle";
		Obituary "$OB_THAM";
		Tag "$FN_THAM";
		+BOSS
		+MISSILEMORE
		+NORADIUSDMG
		+LOOKALLAROUND
		+MISSILEEVENMORE
		+CASTSPRITESHADOW
	}

	private void SpawnGhost(void)
	{
		A_SpawnItemEx("CyberBogGhost", flags: SXF_TRANSFERSPRITEFRAME);
	}

	protected void A_ThamuzHoof(void)
	{
		SpawnGhost();
		A_Hoof();
	}
	protected void A_ThamuzMetal(void)
	{
		SpawnGhost();
		A_Metal();
	}

	protected void A_ThamuzChase(bool stomp = false)
	{
		if (stomp) A_SpawnItemEx("BeastStompND");
		SpawnGhost();
		A_Chase();
	}

	protected void A_ThamuzInvulSpellFlames(void)
	{
		for (int i = 0; i < 360; i += 20)
		{
			A_SpawnItemEx("DCBMiss3", 72, angle: i);
		}
	}

	protected void A_ThamuzInvulSpellAOE(void)
	{
		A_Explode(TCOTDGameplayStatics.GetAttackDamage(15), 96, XF_NOTMISSILE | XF_THRUSTLESS, fulldamagedistance: 96);
	}

	protected void A_ThamuzBouncingFireballAtk(void)
	{
		A_SpawnProjectile("TCOTDLavaBarf", 62, -17, frandom[rnd_ThamuzBogLavaBarf](-10, 10), CMF_AIMDIRECTION, (ACS_NamedExecuteWithResult("SetActivatorTarget", 0, tidtohate) * 10) / frandom[rnd_ThamuzBogLavaBarf](20, 40));
	}

	protected void A_ThamuzNapalmAtk(void)
	{
		A_SpawnProjectile("TCOTDWeaponNapalmMissile", 62, -17);
	}

	States
	{
	Spawn:
		BOGC AB 10 A_Look();
		Loop;
	See:
		BOGC A 1 A_ThamuzHoof();
		BOGC AA 1 A_Chase2();
		BOGC A 1 A_ThamuzChase();
		BOGC AA 1 A_Chase2();
		BOGC B 1 A_ThamuzMetal();
		BOGC BB 1 A_Chase2();
		BOGC B 1 A_ThamuzChase(true);
		BOGC BB 1 A_Chase2();
		BOGC C 1 A_ThamuzHoof();
		BOGC CC 1 A_Chase2();
		BOGC C 1 A_ThamuzChase();
		BOGC CC 1 A_Chase2();
		BOGC D 1 A_ThamuzMetal();
		BOGC DD 1 A_Chase2();
		BOGC D 1 A_ThamuzChase(true);
		BOGC DD 1 A_Chase2();
		Loop;
	Missile:
		BOGC E 0 A_Jump(128, "Missile.HomingFireball");
	Missile.InvulSpell:
		BOGC S 0 A_Jump(160, "Missile.BouncingFireballs", "Missile.Napalm");
		BOGC G 20 { A_SetInvulnerable(); A_StartSound("thamuzdoom/circle", CHAN_6); A_SpawnItemEx("ThamuzCircle"); }
		BOGC T 2 { A_ThamuzInvulSpellFlames(); A_ThamuzInvulSpellAOE(); A_StartSound("beast/stomp", CHAN_7); }
		BOGC UV 6;
		BOGC W 6 A_ThamuzInvulSpellAOE();
		BOGC X 6;
		BOGC Y 2;
		BOGC Y 4 A_ThamuzInvulSpellAOE();
		BOGC Z 90 A_SpawnItemEx("BeastStomp");
		BOGC Y 2 A_UnSetInvulnerable();
		BOGC XWVUTS 6; // this moment grants an exploit for players to attack the boss for a better playism - ozy81
		Goto See;
	Missile.BouncingFireballs:
		BOGC E 3 A_FaceTarget();
		BOGC F 6 A_ThamuzBouncingFireballAtk();
		BOGC E 6 A_FaceTarget();
		BOGC F 6 A_ThamuzBouncingFireballAtk();
		BOGC E 6 A_FaceTarget();
		BOGC F 6 A_ThamuzBouncingFireballAtk();
		Goto See;
	Missile.Napalm:
		BOGC E 3 A_FaceTarget();
		BOGC F 6 A_ThamuzNapalmAtk();
		BOGC E 6 A_FaceTarget();
		BOGC F 6 A_ThamuzNapalmAtk();
		BOGC E 6 A_FaceTarget();
		BOGC F 6 A_ThamuzNapalmAtk();
		Goto See;
	Missile.HomingFireball:
		BOGC P 6;
		BOGC Q 3 A_FaceTarget();
		BOGC R 1 A_SpawnProjectile("DCBMiss1", 64, -25);
		Goto See;
	Pain:
		BOGC G 10 A_Pain();
		Goto See;
	Death: //include death effects here
		BOGC H 10;
		BOGC I 10 A_Scream();
		BOGC JKL 10;
		BOGC M 10 A_NoBlocking();
		BOGC NO 10;
		BOGC O 30;
		BOGC O -1;
		Stop;
	}
}

class CyberBogGhost : Actor
{
	Default
	{
		Alpha 0;
		+FORCEXYBILLBOARD
		+NOINTERACTION
	}

	States
	{
	Spawn:
		"####" "#" 3;
		"####" "#" 0 A_SetRenderStyle(0.6, STYLE_Translucent);
		"####" "#" 3 A_FadeOut();
		Wait;
	}
}

class DCBMiss1 : Actor
{
	Default
	{
		Radius 12;
		Height 12;
		Speed 6;
		Damage 30;
		Projectile;
		RenderStyle "Add";
		SeeSound "thamuzdoom/ball";
		DeathSound "thamuzdoom/ballexplode";
		+FORCEXYBILLBOARD
		+EXTREMEDEATH
		+SEEKERMISSILE
		+DONTSPLASH
		+BRIGHT
	}

	States
	{
	Spawn:
		CBTS ABCDEFGHIJ 2 Light("ThamuzMissile_1") { A_SpawnItemEx("DCBM1Trail"); A_SeekerMissile(10, 90); }
		Loop;
	Death:
		CBTS K 0
		{
			for (int i = 0; i < 76; i++)
			{
				A_SpawnItemEx("DCBMiss3Extra",
					0, 0, 5,
					frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5),
					frandom[rnd_DCDMissExtra](0, 360));
			}
		}
		CBTS K 2 Light("ThamuzMissile_1X_1") A_Explode(140);
		CBTS L 2 Light("ThamuzMissile_1X_2");
		CBTS M 2 Light("ThamuzMissile_1X_3");
		CBTS N 2 Light("ThamuzMissile_1X_4");
		CBTS O 2 Light("ThamuzMissile_1X_5");
		CBTS P 2 Light("ThamuzMissile_1X_6");
		CBTS Q 2 Light("ThamuzMissile_1X_7");
		CBTS R 2 Light("ThamuzMissile_1X_8");
		CBTS S 2 Light("ThamuzMissile_1X_9");
		CBTS T 2 Light("ThamuzMissile_1X_10");
		CBTS U 2 Light("ThamuzMissile_1X_11");
		CBTS V 2 Light("ThamuzMissile_1X_12");
		CBTS W 2 Light("ThamuzMissile_1X_13");
		Stop;
	}
}

class DCBM1Trail : Actor
{
	Default
	{
		Scale 0.8;
		RenderStyle "Add";
		Alpha 0.6;
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+BRIGHT
	}

	States
	{
	Spawn:
		TNT1 A 4;
		CBTS LMNOPQRSTUVW 2 A_FadeOut();
		Stop;
	}
}

class TCOTDLavaBarf : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 20;
		Gravity 0.75;
		Scale 0.5;
		Damage 10;
		DamageType "Fire";
		BounceType "Doom";
		BounceCount 4;
		RenderStyle "Add";
		Projectile;
		SeeSound "napalm/fire";
		DeathSound "napalm/explode";
		+FORCEXYBILLBOARD
		+BRIGHT
		+CANBOUNCEWATER
		+RANDOMIZE
		+RIPPER
		+STRIFEDAMAGE
		+DONTSPLASH
		-NOGRAVITY
	}

	States
  	{
  	Spawn:
		NBLL A 2;
		NBLL A 1 A_Jump(256, 1, 2, 3, 4, 5, 6);
		NBLL AABBCCDDEEAABBCCDDEEAABBCCDDEEAABBCCDDEEAABBCCDDEEAABBCCDDEE 1 A_Explode(4, 32);
		NBLL A 1;
  	Death:
		MISL A 1 A_Scream();
		MISL A 1 A_Explode(30, 64, 1);
		MISL B 0; //explosion effect here - ozy81
		Stop;
	}
}
