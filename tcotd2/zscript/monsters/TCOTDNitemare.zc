///////////////
// NIGHTMARE //
///////////////
class KDiZDNightmare : TCOTDMonster
{
	Default
	{
		//$Title Nightmare
		Health 140;
		Height 56;
		Speed 4.5;
		PainChance 40;
		MeleeRange 50;
		RenderStyle "Add";
		Alpha 0.125;
		StealthAlpha 0.125;
		Scale 1.1;
		SeeSound "nightmare/sight";
		PainSound "nightmare/pain";
		DeathSound "nightmare/death";
		ActiveSound "nightmare/active";
		Obituary "$OB_NITE";
		Tag "$FN_NITE";
		+GHOST
		+STEALTH
		+DONTTHRUST
		+NOBLOOD
		-SHOOTABLE
	}

	States
	{
	Spawn:
		NMRE AB 10 A_Look();
		Loop;
	See:
		NMRE A 0 A_UnSetShootable();
		NMRE A 1 A_Chase();
		NMRE AA 1 A_Chase2();
		NMRE A 1 A_Chase();
		NMRE AA 1 A_Chase2();
		NMRE B 1 A_Chase();
		NMRE BB 1 A_Chase2();
		NMRE B 1 A_Chase();
		NMRE BB 1 A_Chase2();
		NMRE C 1 A_Chase();
		NMRE CC 1 A_Chase2();
		NMRE C 1 A_Chase();
		NMRE CC 1 A_Chase2();
		NMRE D 1 A_Chase();
		NMRE DD 1 A_Chase2();
		NMRE D 1 A_Chase();
		NMRE DD 1 A_Chase2();
		Goto See + 1;
	Melee:
		NMRE E 9 { A_SetShootable(); A_FaceTarget(); }
		NMRE F 5 A_FaceTarget();
		NMRE G 7 A_MonsterMeleeAttack(11, 84, "nightmare/melee", damageType: 'Ghost');
		Goto See;
	Pain:
		NMRE H 2;
		NMRE H 15 A_Pain();
		Goto See;
	Death:
		NMRE I 3 A_ScreamAndUnblock();
		NMRE JKLMNO 3;
		Stop;
	}
}

/////////////////////
// SIREN NIGHTMARE //
/////////////////////
class SirenNightmare : TCOTDMonster
{
	Default
	{
		//$Title Siren
		Radius 10;
		Height 30;
		Speed 10;
		MeleeRange 50;
		RenderStyle "Add";
		Alpha 0.125;
		Scale 1.5;
		SeeSound "nightmare2/sight";
		//PainSound "nightmare2/pain";
		//DeathSound "nightmare2/death";
		ActiveSound "nightmare2/active";
		Obituary "$OB_NITE";
		Tag "$FN_NITE";
		-SHOOTABLE
		-COUNTKILL
	}

	void A_SirenNightmareAttack(void)
	{
		if (target && CheckMeleeRange(96))
		{
			A_StartSound("nightmare2/melee", CHAN_WEAPON);
			int damage = target.health;
			int newdam = target.DamageMobj(self, self, damage, 'Fear2', DMG_NO_ARMOR);
			target.TraceBleed(newdam > 0 ? newdam : damage, self);
		}
	}

	States
	{
	Spawn:
		NMRE AB 10 A_Look();
		Loop;
	See:
		NMRE A 1 A_Chase();
		NMRE AA 1 A_Chase2();
		NMRE A 1 A_Chase();
		NMRE AA 1 A_Chase2();
		NMRE B 1 A_Chase();
		NMRE BB 1 A_Chase2();
		NMRE B 1 A_Chase();
		NMRE BB 1 A_Chase2();
		NMRE C 1 A_Chase();
		NMRE CC 1 A_Chase2();
		NMRE C 1 A_Chase();
		NMRE CC 1 A_Chase2();
		NMRE D 1 A_Chase();
		NMRE DD 1 A_Chase2();
		NMRE D 1 A_Chase();
		NMRE DD 1 A_Chase2();
		Goto See;
	Melee:
		NMRE E 9 A_FaceTarget();
		NMRE F 5 A_FaceTarget();
		NMRE G 7 A_SirenNightmareAttack();
		Goto See;
	}
}
