////////////
// ZOMBIE //
////////////
class ZombieBlood : TCOTDMonster
{
	Default
	{
		//$Title Zombie
		Health 135;
		GibHealth 35;
		PainChance 32;
		PainChance "Stab", 32 / 4;
		Speed 4;
		Radius 24;
		Height 74;
		Mass 400;
		Scale 0.62;
		SeeSound "zombie/sight";
		AttackSound "zombie/swing";
		PainSound "zombie/pain";
		DeathSound "zombie/death";
		ActiveSound "zombie/active";
		HitObituary "$OB_ZOMB";
		Tag "$FN_ZOMB";
		DropItem "TCOTDHealthPickupHeart", 160;
		+CASTSPRITESHADOW
	}

	States
	{
	Spawn:
		BZOM A 10 A_Look();
		Loop;
	See:
		BZOM B 1 A_Chase();
		BZOM BBB 1 A_Chase2();
		BZOM C 1 A_Chase();
		BZOM CCC 1 A_Chase2();
		BZOM D 1 A_Chase();
		BZOM DDD 1 A_Chase2();
		BZOM E 1 A_Chase();
		BZOM EEE 1 A_Chase2();
		BZOM F 1 A_Chase();
		BZOM FFF 1 A_Chase2();
		BZOM G 1 A_Chase();
		BZOM GGG 1 A_Chase2();
		Loop;
	Melee:
		BZOM IJ 4 A_FaceTarget();
		BZOM K 3 A_FaceTarget();
		BZOM L 2 A_MonsterMeleeAttack(16, hitSound: "zombie/hit");
		BZOM MNN 2;
		Goto See;
	Pain:
		BZOM H 2;
		BZOM H 6 A_Pain();
		Goto See;
	Death:
		BZMD A 2;
		BZMD A 4 A_Scream();
		BZMD B 4;
		BZMD C 5 A_NoBlocking();
		BZMD D 5;
		BZMD E -1;
		Stop;
	XDeath:
		BZMD L 5 A_Scream();
		BZMD M 5;
		BZMD N 5
		{
			A_SpawnGibs();
			A_NoBlocking();
		}
		BZMD OPQR 5;
		BZMD S -1;
		Stop;
	Raise:
		BZMD FGHIJK 5;
		Goto See;
	}
}
