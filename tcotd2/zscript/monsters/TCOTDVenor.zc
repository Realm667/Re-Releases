class Venor : TCOTDMonster
{
	protected int ang;
	protected bool isPlayer;
	private int thrustState;
	private bool isThrusting;

	Default
	{
		Health 120;
		Radius 32;
		Height 25;
		Scale 0.8;
		Mass 1000;
		Speed 24;
		MaxStepHeight 32;
		MaxSlopeSteepness 0.2;
		MeleeRange 80;
		PainChance 128;
		PainChance "Stab", 128 / 3;
		DamageFactor "Melee", 0.5;
		BloodColor "Black";
		Obituary "$OB_VENOR";
		Tag "$FN_VENOR";
		PainSound "venor/pain";
		AttackSound "venor/attack";
		ActiveSound "venor/active";
		DeathSound "venor/death";
		+FLOORHUGGER
		+DROPOFF
		+SLIDESONWALLS
		+FLATSPRITE
		+INTERPOLATEANGLES
		+SEEKERMISSILE
		+MISSILEMORE
		+MISSILEEVENMORE
		+DONTGIB
		-SOLID
	}

	protected void ThrustTarget(int force, int duration)
	{
		double dist = Distance3D(Target) + 1;

		if (!Target)
		{
			SetState(SeeState);
		}

		if (!isThrusting)
		{
			thrustState = duration;
			isThrusting = true;
		}

		if (thrustState > 0)
		{
			Vector3 thrust = Vec3To(Target);
			Target.Vel -= thrust.Unit() * 100.0 * double(force) / max(Target.Mass / 10 + 100, 1) * (dist / 128.0);
		}
		else if (thrustState == 0)
		{
			isThrusting = false;
			SetState(SeeState);
		}

		thrustState--;
	}

	States
	{
	Spawn:
		VNR1 A 0 NoDelay { ang = 0; A_ClearTarget(); }
	Idle:
		VNR1 A 1
		{
			int flags = CLOFF_NOAIM | CLOFF_JUMPNONHOSTILE | CLOFF_SKIPOBJECT | CLOFF_JUMPENEMY |
				CLOFF_MUSTBESHOOTABLE | CLOFF_SETTARGET | CLOFF_ALLOWNULL | CLOFF_CHECKPARTIAL;

			bool t = CheckLOF(flags, 256, 0, ang, 0, -16);

			if (t && !(Target && Target.player && (Target.player.cheats & CF_NOTARGET)))
			{
				SetStateLabel("See");
				return;
			}

			ang += 15;
		}
		Loop;
	See:
		VNR1 A 6
		{
			if (Target && Distance3D(Target) <= 192)
			{
				isPlayer = !!Target.player;
				SetStateLabel("Wake");
			}
		}
		Loop;
	Wake:
		VNR2 A 8 A_StartSound("venor/sight");
		VNR2 BE 8;
	Chase:
		VNR2 FFGGCCDDEE 2 A_Chase("Melee", "Missile", CHF_NODIRECTIONTURN);
		Loop;
	Missile:
		VNR2 FGCDE 4
		{
			A_SeekerMissile(30, 45, SMF_LOOK);
			A_FaceTarget();

			if (Distance2D(Target) < 96)
			{
				SetStateLabel("Melee");
			}
		}
		Goto Chase;
	Still:
		VNR2 G 4
		{
			bSolid = false;
			A_StartSound("venor/chomp");
		}
		VNR2 CD 4;
		VNR2 EBABEBABE 5 A_JumpIfCloser(256, "Chase");
		VNR2 E 0
		{
			if (Target && isPlayer)
			{
				SetStateLabel("Chase");
			}
		}
		Goto Spawn;
	Melee:
		VNR2 I 3;
		VNR2 I 3
		{
			A_FaceTarget();
			ThrustTarget(100, 35);
			bSolid = true;
			A_VileTarget("VenorSpawn");
			A_SpawnItemEx("VenorSpawn", flags: SXF_TRANSFERPOINTERS);
			A_MonsterMeleeAttack(9, MeleeRange, "imp/melee");
		}
		VNR2 JKL 6
		{
			A_Stop();
			A_FaceTarget();
		}
		VNR2 L 0
		{
			if (Target.Health <= 0)
			{
				bSolid = false;
				SetStateLabel("Spawn");
			}
		}
		VNR2 L 0 A_JumpIfCloser(64, "Grab");
		VNR2 L 0 A_SpawnItemEx("VenorHandsAbort", flags: SXF_TRANSFERPOINTERS);
		VNR2 L 0 A_VileTarget("VenorHandsAbort");
		Goto Still;
	Grab:
		VNR2 J 3
		{
			A_StartSound("venor/attack", CHAN_AUTO, CHANF_OVERLAP);
			A_FaceTarget();
			ThrustTarget(100, 24);
			A_VileTarget("VenorHands");
			A_SpawnItemEx("VenorHands", flags: SXF_TRANSFERPOINTERS);
			A_MonsterMeleeAttack(3.5, MeleeRange, "imp/melee");
		}
		VNR2 IJKL 3 A_FaceTarget();
		Goto Melee + 5;
	Pain:
		VNR2 H 10
		{
			bSolid = false;
			A_Pain();
		}
		Goto Chase;
	Death:
		VNR2 L 4 A_Scream();
		VNR2 L 4 A_NoBlocking();
		VNR2 LMMMMMO 2
		{
			A_SpawnDebris("VenorGib1", false, frandom[rnd_VenorDeath](0.5, 1.0), frandom[rnd_VenorDeath](0.5, 1.5));
			A_SpawnDebris("VenorGib2", false, frandom[rnd_VenorDeath](0.5, 1.0), frandom[rnd_VenorDeath](0.5, 1.5));
			A_SpawnDebris("VenorGib3", false, frandom[rnd_VenorDeath](0.5, 1.0), frandom[rnd_VenorDeath](0.5, 1.5));
			A_SpawnDebris("VenorGib4", false, frandom[rnd_VenorDeath](0.5, 1.0), frandom[rnd_VenorDeath](0.5, 1.5));
			A_SpawnDebris("VenorBlood", false, frandom[rnd_VenorDeath](0.5, 1.0), frandom[rnd_VenorDeath](0.5, 1.5));
		}
		VNR2 OP 6;
		VNR2 Q -1;
		Stop;
	}
}

class VenorSpawn : Actor
{
	Default
	{
		Scale 0.5;
		+NOBLOCKMAP
		+NOGRAVITY
		+SPECIALFLOORCLIP
	}

	States
	{
	Spawn:
		VNRD ABBCCDDEEEFFFGGGG 2 NoDelay A_Fire;
		TNT1 A 32;
		Stop;
	}
}

class VenorHands : VenorSpawn
{
	States
	{
	Spawn:
		VNRH A 4 NoDelay A_Fire;
		VNRH BCDEFGH 4;
		TNT1 A 32;
		Stop;
	}
}

class VenorHandsAbort : VenorSpawn
{
	States
	{
	Spawn:
		VNRD G 8;
		VNRD FE 6;
		VRND DCB 4;
		VNRD A 2;
		Stop;
	}
}

class VenorBlood : Blood
{
	Default
	{
		Health 10;
		Translation "176:191=5:8";
		Scale 2.0;
		+NOINTERACTION
	}
}

class VenorGib1 : Actor
{
	Default
	{
		Mass 20;
		Health 2;
		Scale 0.3;
		Gravity 0.5;
		+CORPSE
	}

	States
	{
	Spawn:
		VNG1 ABCDEFG 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0
		{
			A_StartSound("venor/splat", CHAN_AUTO, CHANF_DEFAULT, 0.5, ATTN_NORM, frandom[rnd_VenorGibSound](1.5, 3.0), 0.25);
			A_SpawnItemEx("VenorPust1", flags: SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

class VenorGib2 : VenorGib1
{
	States
	{
	Spawn:
		VNG2 ABCDEFG 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0
		{
			A_StartSound("venor/splat", CHAN_AUTO, CHANF_DEFAULT, 0.5, ATTN_NORM, frandom[rnd_VenorGibSound](1.5, 3.0), 0.25);
			A_SpawnItemEx("VenorPust2", flags: SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

class VenorGib3 : VenorGib1
{
	Default
	{
		Scale 0.5;
	}
	States
	{
	Spawn:
		VNG3 ABCDEFGH 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0
		{
			A_StartSound("venor/splat", CHAN_AUTO, CHANF_DEFAULT, 0.5, ATTN_NORM, frandom[rnd_VenorGibSound](1.5, 3.0), 0.25);
			A_SpawnItemEx("VenorHand1", flags: SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

class VenorGib4 : VenorGib3
{
	States
	{
	Spawn:
		VNG4 ABCDEFGH 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0
		{
			A_StartSound("venor/splat", CHAN_AUTO, CHANF_DEFAULT, 0.5, ATTN_NORM, frandom[rnd_VenorGibSound](1.5, 3.0), 0.25);
			A_SpawnItemEx("VenorHand2", flags: SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

class VenorPust1 : Actor
{
	Default
	{
		Height 8;
		Radius 8;
		Health 5;
		Mass 5;
		Scale 0.3;
		BloodColor "Black";
		+SHOOTABLE
		+TOUCHY
		+FLOORCLIP
	}
	States
	{
	Spawn:
		VNG1 AH 8;
		Loop;
	Death:
		TNT1 A 0
		{
			A_StartSound("venor/splat", CHAN_AUTO, CHANF_DEFAULT, 0.3, ATTN_NORM, frandom[rnd_VenorGibSound](1.5, 3.0));
			A_SpawnDebris("VenorBlood", false, frandom[rnd_VenorPust1](0.1, 0.3), frandom[rnd_VenorPust1](0.1, 0.3));
		}
		Stop;
	}
}

class VenorPust2 : VenorPust1
{
	States
	{
	Spawn:
		VNG2 AH 8;
		Loop;
	}
}

class VenorHand1 : VenorPust1
{
	Default
	{
		Scale 0.5;
	}
	States
	{
	Spawn:
		VNG3 BC 10;
		Loop;
	}
}

class VenorHand2 : VenorHand1
{
	States
	{
	Spawn:
		VNG4 BC 10;
		Loop;
	}
}
