Class Venor : Actor
{
	int ang;
	bool isPlayer;
	int ThrustState;
	bool IsThrusting;
	Default
	{
		//$Category Monsters
		Health 120;
		Radius 32;
		Height 25;
		Scale 0.8;
		Mass 1000;
		Speed 24;
		MaxStepHeight 32;
		MaxSlopeSteepness 0.2;
		MeleeRange 80;
		PainChance 128;
		DamageFactor "Melee", 0.5;
		BloodColor "Black";
		Monster;
		+FLOORHUGGER
		+DROPOFF
		+SLIDESONWALLS
		-SOLID
		+FLATSPRITE
		+INTERPOLATEANGLES
		+SEEKERMISSILE
		+MISSILEMORE
		+MISSILEEVENMORE
		+ALWAYSFAST
		+DONTGIB
		Obituary "$OB_VENOR";
		Tag "$FN_VENOR";
		PainSound "venor/pain";
		AttackSound "venor/attack";
		ActiveSound "venor/active";
		DeathSound "venor/death";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { ang = 0; A_ClearTarget(); }
		VNR1 A 1 {
			bool t = CheckLOF(CLOFF_NOAIM | CLOFF_JUMPNONHOSTILE | CLOFF_SKIPOBJECT | CLOFF_JUMPENEMY | CLOFF_MUSTBESHOOTABLE | CLOFF_SETTARGET | CLOFF_ALLOWNULL | CLOFF_CHECKPARTIAL, 256, 0, ang, 0, -16);
			if(t) {
				return ResolveState("See");
			}
			ang += 15;
			return ResolveState(null);
		}
		Goto Spawn+1;
	See:
		VNR1 A 6 {
			if(target && Distance3D(target) <= 192) {
				if(target.player) {
					isPlayer = TRUE;
				}
				else {
					isPlayer = FALSE;
				}
				return ResolveState("Wake");
			}
			return ResolveState(null);
		}
		Loop;
	Wake:
		TNT1 A 0 A_StartSound("venor/sight");
		VNR2 ABE 8;
	Chase:
		VNR2 FFGGCCDDEE 2 A_Chase("Melee", "Missile", CHF_NODIRECTIONTURN);
		Loop;
	Missile:
		VNR2 FGCDE 4 {
			A_SeekerMissile(30, 45, SMF_LOOK);
			A_FaceTarget();
			if(Distance2D(target) < 96) {
				return ResolveState("Melee");
			}
			return ResolveState(null);
		}
		Goto Chase;
	Still:
		TNT1 A 0 {
			bSOLID = FALSE;
			A_StartSound("venor/chomp");
		}
		VNR2 GCD 4;
		VNR2 EBABEBABE 5 A_JumpIfCloser(256, "Chase");
		TNT1 A 0 {
			if(target && isPlayer) {
				return ResolveState("Chase");
			}
			return ResolveState(null);
		}
		Goto Spawn;
	Melee:
		VNR2 I 3;
		VNR2 I 3 {
			A_FaceTarget();
			ThrustTarget(100, 35);
			bSOLID = TRUE;
			A_VileTarget("VenorSpawn");
			A_SpawnItemEx("VenorSpawn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS);
			A_CustomMeleeAttack(random(4,8)*2, "imp/melee");
		}
		VNR2 JKL 6 {
			A_Stop();
			A_FaceTarget();
		}
		TNT1 A 0 {
			if(target.health<1) {
				bSOLID = FALSE;
				return ResolveState("Spawn");
				}
			return ResolveState(null);
		}
		TNT1 A 0 A_JumpIfCloser(64, "Grab");
		TNT1 A 0 A_SpawnItemEx("VenorHandsAbort", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS);
		TNT1 A 0 A_VileTarget("VenorHandsAbort");
		Goto Still;
	Grab:
		VNR2 J 3 {
			A_StartSound("venor/attack", 0, CHANF_OVERLAP);
			A_FaceTarget();
			ThrustTarget(100, 24);
			A_VileTarget("VenorHands");
			A_SpawnItemEx("VenorHands", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS);
			A_CustomMeleeAttack(random(4,8), "imp/melee");
		}
		VNR2 IJKL 3 A_FaceTarget;
		Goto Melee+5;
	Pain:
		TNT1 A 0 { bSOLID = FALSE; }
		VNR2 H 10 A_Pain;
		Goto Chase;
	Death:
		VNR2 L 4 A_Scream;
		VNR2 L 4 A_Fall;
		VNR2 LMMMMMO 2 {
			A_SpawnDebris("VenorGib1", FALSE, frandom(0.5,1.0), frandom(0.5,1.5));
			A_SpawnDebris("VenorGib2", FALSE, frandom(0.5,1.0), frandom(0.5,1.5));
			A_SpawnDebris("VenorGib3", FALSE, frandom(0.5,1.0), frandom(0.5,1.5));
			A_SpawnDebris("VenorGib4", FALSE, frandom(0.5,1.0), frandom(0.5,1.5));
			A_SpawnDebris("VenorBlood", FALSE, frandom(0.5,1.0), frandom(0.5,1.5));
		}
		VNR2 OP 6;
		VNR2 Q -1;
		Stop;
	}
	void ThrustTarget(int force, int duration)
	{
		int dist = Distance3D(target) + 1;
		if(!target)
		SetState(SeeState);

		if(!IsThrusting)
		{
			ThrustState = duration;
			IsThrusting = true;
		}
		if(ThrustState > 0)
		{
			Vector3 thrust = Vec3To(target);
			target.Vel -= thrust.Unit() * 100.0 * force / max(target.Mass/10 + 100, 1) * (dist / 128.0);
		}
		else if(ThrustState == 0)
		{
			IsThrusting = false;
			SetState(SeeState);
		}
		ThrustState--;
	}
}

Class VenorSpawn : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ZDOOMTRANS
		+SPECIALFLOORCLIP
		Scale 0.5;
	}
	States
	{
	Spawn:
		VNRD ABBCCDDEEEFFFGGGG 2 NoDelay A_Fire;
		TNT1 A 32;
		Stop;
	}
}

Class VenorHands : VenorSpawn
{
	States
	{
	Spawn:
		VNRH A 4 NoDelay A_Fire;
		VNRH BCDEFGH 4;
		TNT1 A 32;
		Stop;
	}
}

Class VenorHandsAbort : VenorSpawn
{
	States
	{
	Spawn:
		VNRD G 8;
		VNRD FE 6;
		VRND DCB 4;
		VNRD A 2;
		Stop;
	}
}

Class VenorBlood : Blood
{
	Default
	{
		Health 10;
		Translation "176:191=5:8";
		Scale 2.0;
		+NOINTERACTION
	}
}

Class VenorGib1 : Actor
{
	Default
	{
		Mass 20;
		Health 2;
		Scale 0.3;
		Gravity 0.5;
		+CORPSE
	}
	States
	{
	Spawn:
		VNG1 ABCDEFG 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0 {
			A_StartSound("venor/splat", 0, 0, 0.5, ATTN_NORM, frandom(1.5,3.0), 0.25);
			A_SpawnItemEx("VenorPust1", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

Class VenorGib2 : VenorGib1
{
	States
	{
	Spawn:
		VNG2 ABCDEFG 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0 {
			A_StartSound("venor/splat", 0, 0, 0.5, ATTN_NORM, frandom(1.5,3.0), 0.25);
			A_SpawnItemEx("VenorPust2", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

Class VenorGib3 : VenorGib1
{
	Default
	{
		Scale 0.5;
	}
	States
	{
	Spawn:
		VNG3 ABCDEFGH 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0 {
			A_StartSound("venor/splat", 0, 0, 0.5, ATTN_NORM, frandom(1.5,3.0), 0.25);
			A_SpawnItemEx("VenorHand1", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

Class VenorGib4 : VenorGib3
{
	States
	{
	Spawn:
		VNG4 ABCDEFGH 8;
		Loop;
	Crash:
		TNT1 A 0 A_Jump(224, 2);
		TNT1 A 0 {
			A_StartSound("venor/splat", 0, 0, 0.5, ATTN_NORM, frandom(1.5,3.0), 0.25);
			A_SpawnItemEx("VenorHand2", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
		TNT1 A 0;
		Stop;
	}
}

Class VenorPust1 : Actor
{
	Default
	{
		Height 8;
		Radius 8;
		Health 5;
		Mass 5;
		Scale 0.3;
		BloodColor "Black";
		+SHOOTABLE
		+TOUCHY
		+FLOORCLIP
	}
	States
	{
	Spawn:
		VNG1 AH 8;
		Loop;
	Death:
		TNT1 A 0 {
			A_StartSound("venor/splat", 0, 0, 0.3, ATTN_NORM, frandom(1.5,3.0));
			A_SpawnDebris("VenorBlood", FALSE, frandom(0.1,0.3), frandom(0.1,0.3));
		}
		Stop;
	}
}

Class VenorPust2 : VenorPust1
{
	States
	{
	Spawn:
		VNG2 AH 8;
		Loop;
	}
}

Class VenorHand1 : VenorPust1
{
	Default
	{
		Scale 0.5;
	}
	States
	{
	Spawn:
		VNG3 BC 10;
		Loop;
	}
}

Class VenorHand2 : VenorHand1
{
	States
	{
	Spawn:
		VNG4 BC 10;
		Loop;
	}
}