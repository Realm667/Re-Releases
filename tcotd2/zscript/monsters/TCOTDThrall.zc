/////////////////
// CULTIST ONE //
/////////////////
class Thrall : TCOTDMonster
{
	Default
	{
		//$Title Thrall
		Health 75;
		GibHealth 35;
		PainChance 170;
		Scale 1.1;
		Speed 3.5;
		Radius 16;
		Height 62;
		Mass 100;
		SeeSound "cultist/sight";
		AttackSound "cultist/attack";
		PainSound "cultist/pain";
		DeathSound "cultist/death";
		ActiveSound "cultist/active";
		Obituary "$OB_THRL";
		Tag "$FN_THRL";
	}

	States
	{
	Spawn:
		CULT AB 10 A_Look();
		Loop;
	See:
		CULT A 1 A_Chase();
		CULT AAA 1 A_Chase2();
		CULT B 1 A_Chase();
		CULT BBB 1 A_Chase2();
		CULT C 1 A_Chase();
		CULT CCC 1 A_Chase2();
		CULT D 1 A_Chase();
		CULT DDD 1 A_Chase2();
		Loop;
	Missile:
		CULT E 6 A_FaceTarget();
		CULT F 7 A_FaceTarget();
		CULT F 8 Bright Light("Thrall")
		{
			A_FaceTarget();
			A_StartSound("cultist/attack");
			A_SpawnProjectile("ThrallShot1", 52, -6, flags: CMF_AIMOFFSET);
			A_SpawnProjectile("ThrallShot1", 52,  6, flags: CMF_AIMOFFSET);
		}
		Goto See;
	Pain:
		CULT G 2;
		CULT G 2 A_Pain();
		Goto See;
	Death:
		CULT H 8;
		CULT I 8 A_Scream();
		CULT J 4;
		CULT K 4 A_NoBlocking();
		CULT L 4;
		CULT M -1;
		Stop;
	XDeath:
		CULT N 5;
		CULT O 5 A_XScream();
		CULT P 5
		{
			A_SpawnGibs();
			A_NoBlocking();
		}
		CULT QRSTUV 5;
		CULT W -1;
		Stop;
	Raise:
		CULT MLKJHI 5;
		Goto See;
	}
}

class ThrallShot1 : Actor
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 20;
		DamageFunction TCOTDGameplayStatics.GetAttackDamage(8);
		RenderStyle "Add";
		Alpha 0.67;
		Projectile;
		DeathSound "monster/disht1";
		+PIERCEARMOR
	}

	States
	{
	Spawn:
		CFX4 AB 2 Bright Light("ThrallShot");
		Loop;
	Death:
		CFX4 C 3 Bright Light("ThrallShotX_1");
		CFX4 D 3 Bright Light("ThrallShotX_2");
		CFX4 E 3 Bright Light("ThrallShotX_3");
		CFX4 F 3 Bright Light("ThrallShotX_4");
		Stop;
	}
}
