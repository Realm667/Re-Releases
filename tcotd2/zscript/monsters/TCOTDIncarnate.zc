/////////////////////
// DEATH INCARNATE //
/////////////////////
class Incarnate : TCOTDMonster //inheritances with Shadow, define after it!
{
	Default
	{
		//$Title Incarnate
		Health 400;
		GibHealth 35;
		Radius 20;
		Height 80;
		Mass 500;
		Speed 6;
		PainChance 32;
		Scale 1.1;
		MaxTargetRange 1000;
		SeeSound "monster/incsit";
		PainSound "skeleton/pain";
		DeathSound "monster/incdth";
		ActiveSound "monster/incact";
		AttackSound "monster/incatk";
		Obituary "$OB_INCA";
		HitObituary "$HB_INCA";
		Tag "$FN_INCA";
		DropItem "TCOTDHealthPickupHeart", 160;
		+MISSILEMORE
		+NOTARGET
	}

	protected void A_IncarnateAttack(void)
	{
		A_CustomMeleeAttack(7 * random[rnd_IncarnPunchDmg](1, 8), "monster/inchit");
	}

	States
	{
	Spawn:
		INCA AB 10 A_Look();
		Loop;
	See:
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		Loop;
	Melee:
		INCA G 0 A_Jump(128, 5);
		INCA G 0 A_FaceTarget();
		INCA G 6 A_SkelWhoosh();
		INCA H 6 A_FaceTarget();
		INCA I 6 A_IncarnateAttack();
		Goto See;
		INCA R 0 A_FaceTarget();
		INCA R 5 A_SkelWhoosh();
		INCA S 5 A_FaceTarget();
		INCA T 5 A_IncarnateAttack();
		INCA G 0 A_FaceTarget();
		INCA G 5 A_SkelWhoosh();
		INCA H 5 A_FaceTarget();
		INCA I 5 A_IncarnateAttack();
		Goto See;
	Missile:
		INCA J 10 A_FaceTarget();
		INCA U 4 Light("Incarnate") A_SpawnProjectile("ShadowBall", 64, 0, frandom[rnd_IncarnBall](20, 10));
		INCA U 4 Light("Incarnate") A_SpawnProjectile("ShadowBall", 64, 0, frandom[rnd_IncarnBall](-10, -20));
		INCA U 4 Light("Incarnate") A_SpawnProjectile("ShadowBall", 64);
		INCA K 10;
		Goto See;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain();
		Goto See;
	Death:
		INCX A 0 A_Jump(128, "XDeath");
		INCA LM 7;
		INCA N 7 A_Scream();
		INCA O 7 A_NoBlocking();
		INCA P 7;
		INCA Q 175;
		INCA Q -1 Thing_Raise(0);
		Stop;
	XDeath:
		INCX A 10 Bright Light("IncarnateX_1") A_StartSound("monster/incexp");
		INCX B 5 Bright Light("IncarnateX_2");
		INCX C 5 Bright Light("IncarnateX_3");
		INCX D 5 Bright Light("IncarnateX_4") A_NoBlocking();
		INCX D 0 A_SpawnItemEx("HS", 0, 0, 32);
		INCX E 5 Bright Light("IncarnateX_5");
		INCX F 5 Bright Light("IncarnateX_6");
		INCX G 5 Bright Light("IncarnateX_7");
		INCX H 5 Bright Light("IncarnateX_8");
		INCX I 5 Bright Light("IncarnateX_9");
		INCX J 5 Bright Light("IncarnateX_10");
		INCX K -1;
		Stop;
	Crush:
		TNT1 A 1 A_KillChildren;
		Stop;
	Raise:
		INCA Q 5 A_KillChildren;
		INCA PONML 5;
		Goto See;
	}
}

class HS : TCOTDMonster
{
	Default
	{
		//$Title Hades Sphere
		Health 35;
		Radius 16;
		Height 32;
		Mass 3000;
		Speed 5;
		Monster;
		MeleeThreshold 196;
		SeeSound "monster/hadsit";
		Obituary "$OB_HADE";
		Tag "$FN_HADE";
		+DONTFALL
		+DONTGIB
		+DONTHARMCLASS
		+FLOAT
		+LOOKALLAROUND
		+NOGRAVITY
		+NOTARGET
		+NOSPRITESHADOW
	}

	States
	{
	Spawn:
		TNT1 A 1 A_Look();
		Loop;
	See:
		HADE EFGH 4 Bright;
		HADE A 2 Bright A_StartSound("monster/hadwlk");
		HADE AABBBBCCCCDDDD 1 Bright A_Chase();
		HADE D 0 A_Jump(32, 1);
		Goto See + 4;
		HADE H 0 A_UnSetSolid();
		HADE H 0 A_UnSetShootable();
		HADE H 5 Bright A_StartSound("monster/hadtel");
		HADE GFEIJKL 4 Bright;
	Teleporting:
		TNT1 AAAAAAAAAAAAAAAA 1 A_Chase();
		TNT1 A 0 A_Jump(96,1);
		Loop;
		HADE L 3 Bright A_StartSound("monster/hadtel");
		HADE KJIEFG 4 Bright;
		HADE H 0 A_SetShootable();
		HADE H 0 A_SetSolid;
		Goto See+4;
	Melee:
		HADE HGFE 4 Bright A_SetShootable();
		HADE E 0 A_Die();
	Death:
		HADE M 0 A_NoBlocking();
		HADE M 0 A_SetTranslucent(0.9, 1);
		HADE M 0 A_StartSound("monster/hadexp");
		HADE M 4 Bright A_Explode(112, 112, XF_NOTMISSILE);
		HADE NOPQ 5 Bright;
		Stop;
	XDeath:
		HADE N 0 A_NoBlocking();
		HADE H 5 Bright A_StartSound("monster/haddth");
		HADE GFERSTUVWX 4 Bright;
		Stop;
	}
}
