/////////////////////
// DEATH INCARNATE //
/////////////////////
class Incarnate : TCOTDMonster //inheritances with Shadow, define after it!
{
	private bool bRisen;
	private int attackCount;

	Default
	{
		//$Title Incarnate
		Health 400;
		GibHealth 35;
		Radius 20;
		Height 80;
		Mass 500;
		Speed 2.5;
		PainChance 32;
		PainChance "Stab", 32 / 4;
		Scale 1.1;
		SeeSound "incarnate/sight";
		PainSound "vulgar/pain";
		DeathSound "incarnate/death";
		ActiveSound "incarnate/active";
		Obituary "$OB_INCA";
		Tag "$FN_INCA";
		DropItem "TCOTDHealthPickupHeart", 160;
		+NOTARGET
		+CASTSPRITESHADOW
	}

	protected void A_IncarnateSetAttackCount(void)
	{
		attackCount = random[rnd_IncarnAttackCount](2, 5);
	}

	protected bool A_IncarnateShouldAbortAttck(void)
	{
		return (--attackCount <= 0 || (target && target.health <= 0));
	}

	protected void A_IncarnateBeams(void)
	{
		A_FaceTarget();
		A_SpawnProjectile("IncarnateBall", 72.0,  8.0, angle, CMF_ABSOLUTEANGLE);
		A_SpawnProjectile("IncarnateBall", 72.0, -8.0, angle, CMF_ABSOLUTEANGLE);
	}

	protected void A_IncarnateDeathCheck(void)
	{
		if (bRisen || random[rnd_IncarnDeathCheck]() < int(256 * 0.6))
		{
			SetStateLabel("XDeath");
		}
	}

	protected void A_IncarnateSetRisen(void)
	{
		bRisen = true;
	}

	override bool CanResurrect(Actor other, bool passive)
	{
		return other == self;
	}

	States
	{
	Spawn:
		INCA AB 10 A_Look();
		Loop;
	See:
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA A 1 A_Chase();
		INCA A 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA B 1 A_Chase();
		INCA B 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA C 1 A_Chase();
		INCA C 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA D 1 A_Chase();
		INCA D 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA E 1 A_Chase();
		INCA E 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		INCA F 1 A_Chase();
		INCA F 1 A_Chase2();
		Loop;
	Missile:
		INCA J 7 { A_IncarnateSetAttackCount(); A_FaceTarget(); }
		INCA J 5 A_FaceTarget();
		INCA U 7 Light("Incarnate") A_IncarnateBeams();
		INCA U 0 { if (A_IncarnateShouldAbortAttck()) SetStateLabel("See"); }
		Goto Missile + 1;
	Pain:
		INCA L 5;
		INCA L 5 A_Pain();
		Goto See;
	Death:
		INCA L 7 A_IncarnateDeathCheck();
		INCA M 7;
		INCA N 7 A_Scream();
		INCA O 7 A_NoBlocking(false); // [Blue Shadow] Don't drop a life essence, as this isn't a permanent death.
		INCA P 7;
		INCA Q 175;
		INCA Q 35 CanRaise A_RaiseSelf();
		Wait;
	XDeath:
		INCX A 10 Bright Light("IncarnateX_1") A_StartSound("incarnate/explode");
		INCX B 5 Bright Light("IncarnateX_2");
		INCX C 5 Bright Light("IncarnateX_3");
		INCX D 5 Bright Light("IncarnateX_4") A_NoBlocking();
		INCX E 5 Bright Light("IncarnateX_5") A_SpawnItemEx("HS", 0, 0, 32);
		INCX F 5 Bright Light("IncarnateX_6");
		INCX G 5 Bright Light("IncarnateX_7");
		INCX H 5 Bright Light("IncarnateX_8");
		INCX I 5 Bright Light("IncarnateX_9");
		INCX J 5 Bright Light("IncarnateX_10");
		INCX K -1;
		Stop;
	Raise:
		INCA Q 5 A_IncarnateSetRisen();
		INCA PONML 5;
		Goto See;
	}
}

class IncarnateBall : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 21;
		DamageFunction TCOTDGameplayStatics.GetAttackDamage(10);
		RenderStyle "Add";
		Scale 0.8;
		Projectile;
		SeeSound "imp/attack";
		DeathSound "imp/shotx";
		Decal "DoomImpScorch";
		+RANDOMIZE
		+BRIGHT
	}

	States
	{
	Spawn:
		SBAL A 4 Light("ShadowBall");
		SBAL BBCC 2 Light("ShadowBall") A_SpawnItemEx("IncarnateBallTrail");
	SpawnLoop:
		SBAL AABBCC 2 Light("ShadowBall") A_SpawnItemEx("IncarnateBallTrail");
		Loop;
	Death:
		SBAL C 5 Light("ShadowBall");
		SBAL D 4 Light("ShadowBallX_1");
		SBAL E 4 Light("ShadowBallX_2");
		SBAL F 4 Light("ShadowBallX_3");
		SBAL G 4 Light("ShadowBallX_4");
		SBAL H 4 Light("ShadowBallX_5");
		Stop;
	}
}

class IncarnateBallTrail : Actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.6;
		Scale 0.8;
		+NOINTERACTION
		+BRIGHT
	}

	States
	{
	Spawn:
		TNT1 A 2;
		SHTR ABCDEF 4;
		Stop;
	}
}
