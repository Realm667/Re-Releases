class TCOTDCultist : TCOTDMonster
{
	Default
	{
		Radius 18;
		Height 70;
		Mass 100;
		Speed 2.66666667;
		Scale 0.7;
		PainChance 100;
		SeeSound "monster/culsit";
		PainSound "monster/culpai";
		DeathSound "monster/culdth";
		ActiveSound "monster/culact";
	}

	protected void A_CultistShotgunAttack(void)
	{
		A_CustomBulletAttack(22.5, 0, 3, random[rnd_CultistShotgunDmg](1, 5) * 3, "TCOTDBulletPuff", 0, CBAF_NORANDOM);
	}

	protected void A_CultistTommyAttack(void)
	{
		A_CustomBulletAttack(22.5, 0, 1, random[rnd_CultistTommyDmg](1, 5) * 3, "TCOTDBulletPuff", 0, CBAF_NORANDOM);
	}

	States
	{
	Spawn:
		GCUL A 0;
		Goto SpawnLoop;
	SpawnLoop:
		#### AB 10 A_Look();
		Loop;
	Frightened:
		#### A 0
		{
			bFrightened = true;
		}
		Goto See + 1;
	Cowardly:
		#### A 0
		{
			ActiveSound = "monster/culflee";
		}
		Goto See + 1;
	See:
		#### A 0 A_JumpIfHealthLower(25, "Frightened");
		#### A 1 A_Chase();
		#### AA 1 A_Chase2();
		#### A 1 A_Chase();
		#### AA 1 A_Chase2();
		#### B 1 A_Chase();
		#### BB 1 A_Chase2();
		#### B 1 A_Chase();
		#### BB 1 A_Chase2();
		#### C 1 A_Chase();
		#### CC 1 A_Chase2();
		#### C 1 A_Chase();
		#### CC 1 A_Chase2();
		#### D 1 A_Chase();
		#### DD 1 A_Chase2();
		#### D 1 A_Chase();
		#### DD 1 A_Chase2();
		#### E 1 A_Chase();
		#### EE 1 A_Chase2();
		#### E 1 A_Chase();
		#### E 1 A_Chase2();
		#### F 1 A_Chase();
		#### FF 1 A_Chase2();
		#### F 1 A_Chase();
		#### FF 1 A_Chase2();
		Loop;
	Pain:
		#### I 3;
		#### I 3 A_Pain();
		Goto See;
	Death:
		#### J 5;
		#### K 5 A_Scream();
		#### L 5 A_NoBlocking();
		#### MNOP 5;
		#### Q -1;
		Stop;
	XDeath:
		#### R 5;
		#### S 5 A_XScream();
		#### T 5
		{
			A_SpawnGibs();
			A_NoBlocking();
		}
		#### UVW 5;
		#### X -1;
		Stop;
	Raise:
		#### QPONMLKJ 5;
		Goto See;
	}
}

class TCOTDCultistTommy : TCOTDCultist
{
	Default
	{
		//$Title Cultist TommyGun
		Health 60;
		AttackSound "chainguy/attack";
		Obituary "$OB_CULT";
		Tag "$FN_CULT";
		DropItem "TCOTDAmmoTommy", 80;
	}

	States
	{
	Missile:
		#### G 20 A_FaceTarget();
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		Goto See;
	}
}

class TCOTDCultistShotgun : TCOTDCultist
{
	Default
	{
		//$Title Cultist Sawed-Off
		Health 80;
		AttackSound "shotguy/attack";
		Obituary "$OB_CUL2";
		Tag "$FN_CUL2";
		DropItem "TCOTDAmmoShell", 80; // even if he does, he drops only 2 shells, so you don't get too much ammo
	}

	States
	{
	Spawn:
		BCUL A 0;
		Goto SpawnLoop;
	Missile:
		#### G 20 A_FaceTarget();
		#### H 2 Light("CultistLight") A_CultistShotgunAttack();
		#### G 18;
		Goto See;
	}
}
