class TCOTDCultist : TCOTDMonster
{
	Default
	{
		Health 60;
		Radius 18;
		Height 70;
		Mass 100;
		Scale 0.7;
		PainChance 192;
		SeeSound "cultist/sight";
		PainSound "cultist/pain";
		DeathSound "cultist/death";
		ActiveSound "cultist/active";
	}

	override void Tick(void)
	{
		Super.Tick();

		bFrightened = health < (GetMaxHealth() / 4);
	}

	protected void A_CultistShotgunAttack(void)
	{
		if (target)
		{
			A_StartSound(AttackSound, CHAN_WEAPON);
			A_FaceTarget();
			double bangle = self.Angle;
			double bslope = AimLineAttack(bangle, MISSILERANGE);

			for (int i = 0; i < 7; i++)
			{
				double ang = bangle + random2[CultistShotgunAttack]() * (5.625 * 1.75 / 256);
				double pit = bslope + random2[CultistShotgunAttack]() * (3.5485 * 1.75 / 256);
				int damage = TCOTDGameplayStatics.GetAttackDamage(5);
				LineAttack(ang, MISSILERANGE, pit, damage, 'Hitscan', "TCOTDBulletPuff");
			}
		}
	}

	protected void A_CultistTommyAttack(void)
	{
		A_CustomBulletAttack(5.625 * 1.375, 3.5485 * 1.375, 1, TCOTDGameplayStatics.GetAttackDamage(5), "TCOTDBulletPuff", 0, CBAF_NORANDOM);
	}

	States
	{
	Spawn:
		GCUL A 0;
		Goto SpawnLoop;
	SpawnLoop:
		#### AB 10 A_Look();
		Loop;
	Cowardly:
		#### A 0
		{
			ActiveSound = "cultist/flee";
		}
		Goto See + 1;
	See:
		#### A 1 A_Chase();
		#### AA 1 A_Chase2();
		#### A 1 A_Chase();
		#### AA 1 A_Chase2();
		#### B 1 A_Chase();
		#### BB 1 A_Chase2();
		#### B 1 A_Chase();
		#### BB 1 A_Chase2();
		#### C 1 A_Chase();
		#### CC 1 A_Chase2();
		#### C 1 A_Chase();
		#### CC 1 A_Chase2();
		#### D 1 A_Chase();
		#### DD 1 A_Chase2();
		#### D 1 A_Chase();
		#### DD 1 A_Chase2();
		#### E 1 A_Chase();
		#### EE 1 A_Chase2();
		#### E 1 A_Chase();
		#### E 1 A_Chase2();
		#### F 1 A_Chase();
		#### FF 1 A_Chase2();
		#### F 1 A_Chase();
		#### FF 1 A_Chase2();
		Loop;
	Pain:
		#### I 3;
		#### I 3 A_Pain();
		Goto See;
	Death:
		#### J 5;
		#### K 5 A_Scream();
		#### L 5 A_NoBlocking();
		#### MNOP 5;
		#### Q -1;
		Stop;
	XDeath:
		#### R 5;
		#### S 5 A_XScream();
		#### T 5
		{
			A_SpawnGibs();
			A_NoBlocking();
		}
		#### UVW 5;
		#### X -1;
		Stop;
	Raise:
		#### QPONMLKJ 5;
		Goto See;
	}
}

class TCOTDCultistTommy : TCOTDCultist
{
	Default
	{
		//$Title Cultist TommyGun
		Speed 3;
		AttackSound "chainguy/attack";
		Obituary "$OB_CULT";
		Tag "$FN_CULT";
		DropItem "TCOTDAmmoTommy", 80;
	}

	States
	{
	Missile:
		#### G 17 A_FaceTarget();
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		#### H 2 Light("CultistLight") A_CultistTommyAttack();
		#### G 3;
		Goto See;
	}
}

class TCOTDCultistShotgun : TCOTDCultist
{
	Default
	{
		//$Title Cultist Sawed-Off
		Speed 4;
		AttackSound "shotguy/attack";
		Obituary "$OB_CUL2";
		Tag "$FN_CUL2";
		DropItem "TCOTDAmmoShell", 80; // even if he does, he drops only 2 shells, so you don't get too much ammo
	}

	States
	{
	Spawn:
		BCUL A 0;
		Goto SpawnLoop;
	Missile:
		#### G 17 A_FaceTarget();
		#### H 2 Light("CultistLight") A_CultistShotgunAttack();
		#### G 15;
		Goto See;
	}
}
