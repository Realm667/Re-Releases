////////////////
// APPRENTICE //
////////////////
Class ApprenticeofDsparil : TCOTDMonster
{
	Default
	{
		//$Title Apprentice
		Health 500;
		GibHealth 35;
		Height 56;
		Speed 2.66666667;
		FloatSpeed 1.33333333;
		Scale 1.1;
		PainChance 100;
		SeeSound "wizard/sight";
		PainSound "wizard/pain";
		DeathSound "wizard/death";
		ActiveSound "wizard/active";
		Obituary "$OB_ADSP";
		+FLOAT
		+FLOORCLIP
		+NOGRAVITY
		+NOSPRITESHADOW
	}

	States
	{
	Spawn:
		APDP AB 10 A_Look();
		Loop;
	See:
		APDP A 0 A_SetTranslucent(1.);
		APDP A 1 A_Chase();
		APDP AA 1 A_Chase2();
		APDP A 1 A_Chase();
		APDP AA 1 A_Chase2();
		APDP B 1 A_Chase();
		APDP BB 1 A_Chase2();
		APDP B 1 A_Chase();
		APDP BB 1 A_Chase2();
		Loop;
	Missile:
		APDP C 0 A_FaceTarget();
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(.50);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(1.);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(.50);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(1.);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(.50);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(1.);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(.50);
		APDP C 2 Light("ApprenticeOfDSparil") A_SetTranslucent(1.);
		APDP C 0 A_Jump(128, "Missile2");
		APDP C 0 A_Jump(64, "Missile3");
		APDP C 0 A_StartSound("wizard/attack");
		APDP D 0 A_SpawnProjectile("FireBlast", 30, -10, -4);
		APDP D 0 A_SpawnProjectile("FireBlast", 30);
		APDP D 6 A_SpawnProjectile("FireBlast", 30, 10, 4);
		Goto See;
	Missile2:
		APDP DDDDDDDDDD 1 A_FadeOut(.1);
		APDP D 0 A_SetInvulnerable();
		APDP DDDDDDDDDDDDDDDD 2 A_Wander();
		APDP D 0 A_FaceTarget();
		APDP D 0 A_StartSound("wizard/attack");
		APDP D 0 A_SpawnProjectile("FireBlast", 30, -10, -4);
		APDP D 0 A_SpawnProjectile("FireBlast", 30);
		APDP D 6 A_SpawnProjectile("FireBlast", 30, 10, 4);
		APDP DDDDDDDDDDDDDDDD 2 A_Wander();
		APDP D 0 A_FaceTarget();
		APDP D 0 A_StartSound("wizard/attack");
		APDP D 0 A_SpawnProjectile("FireBlast", 30, -10, -4);
		APDP D 0 A_SpawnProjectile("FireBlast", 30);
		APDP D 6 A_SpawnProjectile("FireBlast", 30, 10, 4);
		APDP DDDDDDDDDDDDDDDD 2 A_Wander();
		APDP D 0 A_FaceTarget();
		APDP D 0 A_StartSound("wizard/attack");
		APDP D 0 A_SpawnProjectile("FireBlast", 30, -10, -4);
		APDP D 0 A_SpawnProjectile("FireBlast", 30);
		APDP D 6 A_SpawnProjectile("FireBlast", 30, 10, 4);
		APDP DDDDDDDDDDDDDDDD 2 A_Wander();
		APDP D 0 A_FaceTarget();
		APDP D 0 A_StartSound("wizard/attack");
		APDP D 0 A_SpawnProjectile("FireBlast", 30, -10, -4);
		APDP D 0 A_SpawnProjectile("FireBlast", 30);
		APDP D 6 A_SpawnProjectile("FireBlast", 30, 10, 4);
		APDP DDDDDDDDDDDDDDDD 2 A_Wander();
		APDP DDDDDDDDD 1 A_FadeIn(.1);
		APDP D 1 A_UnsetInvulnerable();
		APDP D 1 A_SetTranslucent(1.);
		Goto See;
	Missile3:
		APDP D 0 A_FaceTarget();
		APDP D 0 A_StartSound("wizard/attack");
		APDP D 4 A_SpawnProjectile("SuperFireBlast", 15);
		Goto See;
	Pain:
		APDP E 2 Light("ApprenticeOfDSparilX_1") A_SetTranslucent(1.);
		APDP E 2 Light("ApprenticeOfDSparilX_1") A_Pain();
		Goto See;
	Death: //no XDeath for this creature atm - ozy81
		APDP E 4 Light("ApprenticeOfDSparilX_1") A_SetTranslucent(1.);
		APDP F 4 Light("ApprenticeOfDSparilX_2");
		APDP G 3 Light("ApprenticeOfDSparilX_3") A_Scream();
		APDP H 3 Light("ApprenticeOfDSparilX_4") A_SpawnItemEx("AoDGhost", 0, 0, 8);
		APDP I 3 Light("ApprenticeOfDSparilX_5") A_NoBlocking();
		APDP J 2 Light("ApprenticeOfDSparilX_6");
		APDP K 2 Light("ApprenticeOfDSparilX_7");
		APDP L 2 Light("ApprenticeOfDSparilX_8");
		APDP M -1;
		Stop;
	Raise:
		APDP M 8;
		APDP L 8 Light("ApprenticeOfDSparilX_8");
		APDP K 6 Light("ApprenticeOfDSparilX_7");
		APDP J 6 Light("ApprenticeOfDSparilX_6");
		APDP I 6 Light("ApprenticeOfDSparilX_5");
		Goto See;
	}
}

Class AoDGhost : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Projectile;
		RenderStyle "Translucent";
		Alpha 0.75;
		SeeSound "aod/ghost";
		+NOCLIP
	}

	States
	{
	Spawn:
	Death:
		AODG ABCDEF 5 Bright;
		AODG G 5 Bright A_FadeOut();
		Stop;
	}
}

Class FireBlast : Actor
{
	Default
	{
		Speed 25;
		Scale 0.75;
		DamageFunction (random(1, 4) * 3);
		Projectile;
		DeathSound "fireblast/explode";
	}

	States
	{
	Spawn:
		PROJ ABC 4 Bright Light("FireBlast");
		Loop;
	Death:
		PROJ D 4 Bright Light("FireBlastX_1") A_Explode(8, 64, 0);
		PROJ E 4 Bright Light("FireBlastX_2");
		PROJ F 4 Bright Light("FireBlastX_3");
		PROJ G 4 Bright Light("FireBlastX_4");
		PROJ H 4 Bright Light("FireBlastX_5");
		PROJ I 4 Bright Light("FireBlastX_6") A_FadeOut(0.50);
		Stop;
	}
}

Class SuperFireBlast : FireBlast
{
	Default
	{
		Speed 16;
		Scale 1.5;
		DamageFunction (random(1, 4) * 24);
		DeathSound "superfireblast/explode";
	}

	States
	{
	Death:
		PROJ D 4 Bright Light("FireBlastX_1") A_Explode(256, 128, 0);
		Goto Super::Death + 1;
	}
}
