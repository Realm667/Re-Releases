/*
Credits:
-Hand sprites from Fate (by Capstone)
-Pitchfork & Puff sprites from Blood
-Sounds from Thief
-Flying Pitchfork sprites by NeoWorm & Amuscaria acording to credits

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor Melee_Weapon : Weapon
{
Obituary "%o got killed by %k."
Weapon.SelectionOrder 3700
Weapon.KickBack 100
Tag "Melee"
+WEAPON.WIMPY_WEAPON
+WEAPON.NOALERT
States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkReady")
		FIST A 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
	PitchforkReady:
		FORK A 1 A_WeaponReady
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkDeselect")
		FIST A 1 A_Lower
		Loop
	PitchforkDeselect:
		FORK A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkSelect")
		FIST A 1 A_Raise
		Loop
	PitchforkSelect:
		FORK A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkFire")
		TNT1 A 0 A_Jump(160,7)
		FIST B 2
		FIST C 4 A_CustomPunch(random(3,5),0,0,"BulletPuff",0,0,0,"ArmorBonus","Fist/Hit",0)
		FIST CBAA 2
		Goto Ready
		FIST D 2
		FIST E 4 A_CustomPunch(random(3,5),0,0,"BulletPuff",0,0,0,"ArmorBonus","Fist/Hit",0)
		FIST EDAA 2
		Goto Ready
	PitchforkFire:
		FORK A 2
		TNT1 A 0
		FORK A 1 offset(0,30)
		FORK B 1
		FORK B 1 offset(0,30)
		FORK C 5 A_CustomPunch(random(5,7),0,0,"HitPuffNew",96,0,0,"ArmorBonus",0,"Pitchfork/Miss")
		FORK C 1 offset(0,34)
		FORK B 1
		FORK B 1 offset(0,34)
		FORK A 2 A_WeaponOffset(0,32)
		FORK A 1 A_ReFire
		Goto Ready
	AltFire:
		FORK B 1 A_PlaySound("Pitchfork/Throw",CHAN_WEAPON)
		FORK C 1 
		FORK D 1 A_FireCustomMissile("Pitchfork_thrown_new",0,1,0,5)
		FORK E 1 A_TakeInventory("PitchforkInv")
		FORK E 1 offset(-18,48)
		FORK E 1 offset(-36,64)
		FORK E 1 offset(-54,80)
		FORK E 1 offset(-72,92)
		TNT1 A 2
		Goto Ready
	Spawn:
		TNT1 A -1
		Stop
	}
}

Actor HitPuffNew
{
+NOBLOCKMAP
+NOGRAVITY
+NOEXTREMEDEATH
+PUFFONACTORS	// this is necessary for it to still make sound on enemies
Activesound ""
Attacksound "Pitchfork/Wall" // what sound it makes when you hit a wall with it
Seesound "Pitchfork/Flesh" // what sound it makes when you beat somebody up with it
Scale 0.5
States
	{
	Crash:
		PPUF ABCDE 2 
		Stop
	Spawn:
	Melee:
		TNT1 A 1
		Stop
	}
}

Actor Pitchfork_thrown_new
{
Radius 2
Height 1
Speed 20
Damage 5
Projectile
-NOGRAVITY
-NOBLOCKMAP
Gravity .125
+RIPPER
States
	{
	Spawn:
		XIPP A -1
		Stop
	Death:
		XIPP A 0 A_AlertMonsters
		XIPP A 4 A_SpawnItemEx("PitchforkInv")
		Stop
	}
}

Actor PitchforkInv : Inventory replaces Pitchfork
{
Inventory.PickupMessage "You picked up the Pitchfork"
Inventory.PickupSound "misc/w_pkup"
Inventory.MaxAmount 1
-INVENTORY.INVBAR
Scale 1.2
States
	{
	Spawn:
		XFOR A -1
		Stop
	}
}

/*
Credits:
-Revolver Sprites by Mike12
-Sounds from Clive Baker's Undying
-Bullet Puff sprites from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor RevolverBloodNew : Weapon replaces RevolverBlood
{
Weapon.Ammotype "Clip"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
AttackSound "Revolver/Fire"
Tag "Revolver"
Inventory.PickupMessage "You got the Revolver!"
AttackSound "Revolver/Fire"
Scale 0.6
+WEAPON.WIMPY_WEAPON
Weapon.SelectionOrder 190
Decal BulletChip
States
	{
	Select:
		REVL A 1 A_Raise
		Loop
	Deselect:
		REVL A 1 A_Lower
		Loop
	Ready:
		REVL A 1 A_WeaponReady
		Loop
	Fire:
		REVL A 2
		TNT1 A 0 A_FireBullets(5.6,0,1,9,"NewerPuff")
		REVL B 2 A_Overlay(-2,"Flash")
		REVL C 3
		REVL DEFG 1
		REVL HAA 2
		REVL A 1 A_ReFire
		goto Ready
	Flash:
		RVFL A 2 bright A_Light2
		RVFL B 2 bright A_Light1
		RVFL C 2 bright A_Light0
		Goto LightDone
	Spawn:
		RPCK A -1
		Stop
	}
}

Actor NewerPuff : BulletPuff replaces NewPuff
{
VSpeed 0
RenderStyle Normal
Alpha 1
+NOEXTREMEDEATH
Scale 0.5
States
	{
	Crash:
		RPUF ABCDEFG 1 Bright
		Stop
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

/*
Credits:
-Shotgun sprites & reload sound from Blood
-Firing sounds from Clive Baker's Undying

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/
Actor SawedOffNew : Weapon replaces SawedOff
{
Weapon.AmmoType1 "Shell"
Weapon.AmmoType2 "Shell"
Weapon.AmmoUse1 1
Weapon.AmmoUse2 2
Weapon.AmmoGive 12
Weapon.Kickback 100
Inventory.PickupMessage "You got the Sawed-off!"
Tag "Sawed-off"
Scale 0.6
Decal BulletChip
States
	{
	Spawn:
		SPCK A -1
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,2)
		SAWF A 1 A_WeaponReady
		Loop
		SAWF B 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Down1Shell")
		SAWF A 1 A_Lower
		Loop
	Down1Shell:
		SAWF B 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Up1Shell")
		SAWF A 1 A_Raise
		Loop
	Up1Shell:
		SAWF B 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,15)
		SAWF A 1 A_PlayWeaponSound("SawedOff/Fire")
		TNT1 A 0 A_Overlay(-2,"GunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF B 1 A_FireBullets(5.6,0,7,5,"NewerPuff",FBF_USEAMMO)
		SAWF B 1 offset(6,44)
		SAWF B 1 offset(10,52)
		SAWF B 1 offset(18,68)
		SAWF B 1 offset(14,56)
		SAWF B 1 offset(10,52)
		SAWF B 1 offset(8,48)
		SAWF B 1 offset(6,44)
		SAWF B 1 offset(4,40)
		SAWF B 1 offset(2,36) A_GiveInventory("SawedOffCheck",1)
		SAWF B 2
		Goto Ready
		SAWF B 1 A_PlayWeaponSound("SawedOff/Fire")
		TNT1 A 0 A_Overlay(-2,"GunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF C 1 A_FireBullets(5.6,0,7,5,"NewerPuff",FBF_USEAMMO)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(10,52)
		SAWF C 1 offset(18,68)
		SAWF C 1 offset(14,56)
		SAWF C 1 offset(10,52)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(4,40)
		SAWF C 1 offset(2,36)
		SAWF C 1
		Goto Load
	AltFire:
		SAWF A 1 A_PlayWeaponSound("SawedOff/AltFire")
		TNT1 A 0 A_Overlay(-2,"BigGunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF C 1 A_FireBullets(11.2,7.1,20,5,"NewerPuff",FBF_USEAMMO)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(15,62)
		SAWF C 1 offset(23,78)
		SAWF C 1 offset(27,86)
		SAWF C 1 offset(23,78)
		SAWF C 1 offset(19,70)
		SAWF C 1 offset(15,62)
		SAWF C 1 offset(11,56)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(4,40)
		SAWF C 1 offset(2,36)
		SAWF C 1
		Goto Load
	Load:
		TNT1 A 0 A_TakeInventory("SawedOffCheck",1)
		SAWF C 1 A_CheckReload
		SAWF E 2
		SAWF F 2 A_PlaySound("SawedOff/Reload")
		SAWF G 8 A_Overlay(2,"ElHando")
		SAWF JE 2
		Goto Ready
	ElHando:
		TNT1 A 0 A_OverlayFlags(2,PSPF_ADDWEAPON,false)
		SAWF H 1 A_OverlayOffset(2,0,32)
		SAWF H 1 A_OverlayOffset(2,-9,26)
		SAWF H 1 A_OverlayOffset(2,-18,20)
		SAWF H 1 A_OverlayOffset(2,-26,15)
		SAWF H 1 A_OverlayOffset(2,-32,10)
		SAWF I 1 A_OverlayOffset(2,0,32)
		SAWF I 1 A_OverlayOffset(2,-4,40)
		SAWF I 1 A_OverlayOffset(2,-8,48)
		Stop
	Flash:
		SAWF D 2 A_Light2
		SAWF D 2 A_Light1
		Goto LightDone
	GunSmoke:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,4)
		SFFL ABC 2 Bright A_OverlayOffset(-2,0,32)
		Stop
		SFFL ABC 2 Bright A_OverlayOffset(-2,20,32)
		Stop
	BigGunSmoke:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		SFFL DEF 2 Bright A_OverlayOffset(-2,0,32)
		Stop
	}
}

Actor SawedOffCheck : Inventory
{
Inventory.MaxAmount 1
-INVENTORY.INVBAR
}

/*
Credits:
-Tommygun sprites & sounds from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor ThompsonNew : Weapon replaces Thompson
{
Weapon.AmmoType "Clip"
Weapon.AmmoType2 "Clip"
Weapon.AmmoGive 20
Weapon.AmmoUse 1
Weapon.AmmoUse2 1
Weapon.SelectionOrder 70
Decal Bulletchip
AttackSound "Thompson/Fire"
Inventory.PickupMessage "You got the Thompson!"
Obituary "%o was mowed down by %k's Thompson."
Scale 0.6
Tag "Thompson"
States
	{
	Spawn:
		TPCK A 4
		Loop
	Select:
		THMP A 1 A_Raise
		Loop
	Deselect:
		THMP A 1 A_Lower
		Loop
	Ready:
		THMP A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_Overlay(-2,"Flash")
		TNT1 A 0 A_WeaponOffset(random(1,2),random(33,34))
		THMP B 2 A_FireBullets(5.6,0,1,9,"NewerPuff")
		THMP B 2 A_WeaponOffset(0,32)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"Flash")
		TNT1 A 0 A_WeaponOffset(random(2,3),random(34,35))
		THMP C 2 A_FireBullets(5.6,0,1,9,"NewerPuff")
		THMP C 2 A_WeaponOffset(0,32)
		TNT1 A 0 A_CheckReload
		THMP A 1 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(1,2),random(35,36))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP B 1 A_WeaponOffset(1,34)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(2,3),random(36,37))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP C 1 A_WeaponOffset(2,36)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(3,4),random(37,38))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP B 1 A_WeaponOffset(3,38)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(4,5),random(38,39))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP C 1 A_WeaponOffset(4,40)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(5,6),random(39,40))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP B 1 A_WeaponOffset(5,42)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(6,7),random(40,41))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP C 1 A_WeaponOffset(6,44)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(7,8),random(41,42))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP B 1 A_WeaponOffset(7,46)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(8,9),random(42,43))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewerPuff")
		THMP C 1 A_WeaponOffset(8,48)
		THMP A 1 A_CheckReload
		THMP A 1 A_WeaponOffset(7,46)
		THMP A 1 A_WeaponOffset(6,44)
		THMP A 1 A_WeaponOffset(5,42)
		THMP A 1 A_WeaponOffset(4,40)
		THMP A 1 A_WeaponOffset(3,38)
		THMP A 1 A_WeaponOffset(2,36)
		THMP A 1 A_WeaponOffset(1,34)
		THMP A 22 A_WeaponOffset(0,32)
		THMP A 1 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Jump(180,1,2,3)
		TMFL A 2 Bright A_Light2
		Goto LightDone
		TMFL B 2 Bright A_Light2
		Goto LightDone
		TMFL C 2 Bright A_Light2
		Goto LightDone
	AltFlash:
		TNT1 A 0 A_Jump(180,1,2,3)
		TMFL A 1 Bright A_Light2
		Goto LightDone
		TMFL B 1 Bright A_Light2
		Goto LightDone
		TMFL C 1 Bright A_Light2
		Goto LightDone
	}
}

/*
Credits:
-Napalm sprites & sounds from Blood
-Firing sound from Hexen
-Smoke sprites by Kronos I guess?

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/
Actor NapalmLauncherNew : Weapon replaces NapalmLauncher
{
Radius 20
Height 16
Weapon.SelectionOrder 250
Weapon.AmmoType "NapalmAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Inventory.PickupMessage "You got the Napalm Launcher!"
Obituary "%o rode %k's flame blast."
+WEAPON.EXPLOSIVE
Scale 0.6
Decal "Scorch"
Tag "Napalm Launcher"
States
	{
	Spawn:
		NPCK A -1
		Stop
	Ready:
		NAPL A 1 A_WeaponReady
		Loop
	Deselect:
		NAPL A 1 A_Lower
		Loop
	Select:
		NAPL A 1 A_Raise
		Loop
	Fire:
		NAPL A 3
		TNT1 A 0 A_PlaySound("Napalm/Shot")
		TNT1 A 0 A_FireCustomMissile("RocketBlood2")
		NAPL B 2 offset(15,49) A_Overlay(-2,"Flash")
		NAPL B 3 offset(21,57)
		NAPL B 3 offset(21,59)
		NAPL B 3 offset(17,53)
		NAPL B 2 offset(6,42)
		NAPL A 2 offset(2,35)
		NAPL A 2 A_WeaponOffset(0,32)
		NAPL A 4
		NAPL A 1 A_Refire
		Goto Ready
	Flash:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		TNT1 A 0 A_OverlayOffset(-2,0,32)
		NPFL AB 2 Bright A_Light2
		NPFL CD 2 Bright A_Light1
		NPFL E 2 Bright A_Light0
		NPFL FG 2 Bright
		Stop
	}
}

Actor RocketBlood2
{
Radius 8
Height 8
Speed 20
Damage 80
Projectile
+RANDOMIZE
Decal "Scorch"
SeeSound "Napalm/Fire"
DeathSound "Napalm/Impact"
Scale 0.4
States
	{
	Spawn:
		//TNT1 A 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		NBLL ABC 2 Bright A_SpawnItemEx("NRocketTrail",0,0,0,2,0,0)
		//TNT1 A 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		NBLL DEF 2 Bright A_SpawnItemEx("NRocketTrail",0,0,0,2,0,0)
		Loop
	Death:
		TNT1 A 0 A_SetScale(0.9)
		NEXP A 2 Bright A_Explode
		NEXP BCDEFGHIJKLMN 2 Bright
		Stop
	}
}

Actor NRocketTrail
{
+NOINTERACTION
Scale 0.4
Alpha 0.4
RenderStyle "Translucent"
States
	{
	Spawn:
		NSMK ABCD 2
		NSMK EFGHI 2 A_FadeOut(0.06)
		Stop
	}
}

ACTOR TCOTDPlayer_Test : PlayerPawn replaces TCOTDPlayer
{
	Scale 1.1
	Player.AttackZOffset 18.0
	Player.ViewHeight 50.0
	Player.ForwardMove 0.6,0.6
	Player.SideMove 0.6,0.6
	Player.DisplayName "Wanderer"
	Player.CrouchSprite "PLYC"
	Player.StartItem Melee_Weapon
	Player.WeaponSlot 1,Melee_Weapon
	Player.WeaponSlot 2,RevolverBloodNew
	Player.WeaponSlot 3,SawedoffNew
	Player.WeaponSlot 4,ThompsonNew
	Player.WeaponSlot 5,NapalmLauncherNew
	States
	{
	Spawn:
		PLAY Z -1
		Loop
	See:
		PLAY ABCDEF 4
		Loop
	Missile:
		PLAY Z 12
		Goto Spawn
	Melee:
		PLAY Z 6 BRIGHT
		Goto Missile
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		Goto Spawn
	Death:
		PLAY H 10
		PLAY I 10 A_PlayerScream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N 300
		PLAY NNNNNNNNNNN 3 A_FadeOut(0.1,0)
		TNT1 A -1
		Stop
	XDeath:
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RST 5
		PLAY U 300
		PLAY UUUUUUUUUUU 3 A_FadeOut(0.1,0)
		TNT1 A -1
		Stop
	}
}