//////////////
// CIVILIAN //
//////////////
ACTOR Civilian 29237
{
	Health 999999
	GibHealth 35
	Radius 16
	Height 56
	Mass 999999
	Scale 0.7
	Monster
	-COUNTKILL
	+FLOORCLIP
	+NOBLOOD
	Obituary "$OB_CIVL"
	States
	{
	Spawn:
		CIVI PQ 30 A_Wander
		Loop
	Pain:
		CIVI A 3
		CIVI G 3 A_Pain
		Goto Spawn
	Death:
		CIVI G 5
		CIVI H 5 A_Scream
		CIVI I 5 A_NoBlocking
		CIVI JKLMN 5
		CIVI O -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		Stop
	}
}

////////////////
// APPRENTICE //
////////////////

ACTOR ApprenticeofDsparil
{
	Health 500
	GibHealth 35
	Height 56
	Speed 2.66666667
	FloatSpeed 1.33333333
	PainChance 100
	Monster
	+FLOAT
	+FLOORCLIP
	+NOGRAVITY
	SeeSound "wizard/sight"
	PainSound "wizard/pain"
	DeathSound "wizard/death"
	ActiveSound "wizard/active"
	Obituary "$OB_ADSP"
	States
	{
	Spawn:
		APDP AB 10 A_Look
		Loop
	See:
		APDP A 0 A_SetTranslucent(1)
		APDP A 1 A_Chase
		APDP AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		APDP A 1 A_Chase
		APDP AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		APDP B 1 A_Chase
		APDP BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		APDP B 1 A_Chase
		APDP BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		APDP C 0 A_FaceTarget
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(.50)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(1)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(.50)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(1)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(.50)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(1)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(.50)
		APDP C 2 LIGHT("ApprenticeOfDSparil") A_SetTranslucent(1)
		APDP C 0 A_Jump(128,"Missile2")
		APDP C 0 A_Jump(64,"Missile3")
		APDP C 0 A_PlaySound("wizard/attack")
		APDP D 0 A_CustomMissile("FireBlast",30,-10,-4)
		APDP D 0 A_CustomMissile("FireBlast",30)
		APDP D 6 A_CustomMissile("FireBlast",30,10,4)
		Goto See
	Missile2:
		APDP DDDDDDDDDD 1 A_FadeOut(.1,0)
		APDP D 0 A_SetInvulnerable
		APDP DDDDDDDDDDDDDDDD 2 A_Wander
		APDP D 0 A_FaceTarget
		APDP D 0 A_PlaySound("wizard/attack")
		APDP D 0 A_CustomMissile("FireBlast",30,-10,-4)
		APDP D 0 A_CustomMissile("FireBlast",30)
		APDP D 6 A_CustomMissile("FireBlast",30,10,4)
		APDP DDDDDDDDDDDDDDDD 2 A_Wander
		APDP D 0 A_FaceTarget
		APDP D 0 A_PlaySound("wizard/attack")
		APDP D 0 A_CustomMissile("FireBlast",30,-10,-4)
		APDP D 0 A_CustomMissile("FireBlast",30)
		APDP D 6 A_CustomMissile("FireBlast",30,10,4)
		APDP DDDDDDDDDDDDDDDD 2 A_Wander
		APDP D 0 A_FaceTarget
		APDP D 0 A_PlaySound("wizard/attack")
		APDP D 0 A_CustomMissile("FireBlast",30,-10,-4)
		APDP D 0 A_CustomMissile("FireBlast",30)
		APDP D 6 A_CustomMissile("FireBlast",30,10,4)
		APDP DDDDDDDDDDDDDDDD 2 A_Wander
		APDP D 0 A_FaceTarget
		APDP D 0 A_PlaySound("wizard/attack")
		APDP D 0 A_CustomMissile("FireBlast",30,-10,-4)
		APDP D 0 A_CustomMissile("FireBlast",30)
		APDP D 6 A_CustomMissile("FireBlast",30,10,4)
		APDP DDDDDDDDDDDDDDDD 2 A_Wander
		APDP DDDDDDDDD 1 A_FadeIn(.1)
		APDP D 1 A_UnsetInvulnerable
		APDP D 1 A_SetTranslucent(1)
		Goto See
	Missile3:
		APDP D 0 A_FaceTarget
		APDP D 0 A_PlaySound("wizard/attack")
		APDP D 4 A_CustomMissile("SuperFireBlast",15)
		Goto See
	Pain:
		APDP E 2 LIGHT("ApprenticeOfDSparilX_1") A_SetTranslucent(1)
		APDP E 2 LIGHT("ApprenticeOfDSparilX_1") A_Pain
		Goto See
	Death: //no XDeath for this creature atm - ozy81
		APDP E 4 LIGHT("ApprenticeOfDSparilX_1") A_SetTranslucent(1)
		APDP F 4 LIGHT("ApprenticeOfDSparilX_2")
		APDP G 3 LIGHT("ApprenticeOfDSparilX_3") A_Scream
		APDP H 3 LIGHT("ApprenticeOfDSparilX_4") A_SpawnItemEx("AoDGhost",0,0,8)
		APDP I 3 LIGHT("ApprenticeOfDSparilX_5") A_NoBlocking
		APDP J 2 LIGHT("ApprenticeOfDSparilX_6")
		APDP K 2 LIGHT("ApprenticeOfDSparilX_7")
		APDP L 2 LIGHT("ApprenticeOfDSparilX_8")
		APDP M -1
		Stop
	Raise:
		APDP M 8
		APDP L 8 LIGHT("ApprenticeOfDSparilX_8")
		APDP K 6 LIGHT("ApprenticeOfDSparilX_7")
		APDP J 6 LIGHT("ApprenticeOfDSparilX_6")
		APDP I 6 LIGHT("ApprenticeOfDSparilX_5")
		Goto See
	}
}

ACTOR AoDGhost
{
	Radius 1
	Height 1
	Projectile
	RenderStyle Translucent
	Alpha 0.75
	+NOCLIP
	SeeSound "aod/ghost"
	States
	{
	Spawn:
	Death:
		AODG ABCDEF 5 BRIGHT
		AODG G 5 BRIGHT A_FadeOut
		Stop
	}
}

ACTOR FireBlast
{
	Speed 25
	Scale 0.75
	Damage (random(1,4)*3)
	Projectile
	DeathSound "fireblast/explode"
	States
	{
	Spawn:
		PROJ ABC 4 BRIGHT LIGHT("FireBlast")
		Loop
	Death:
		PROJ D 4 BRIGHT LIGHT("FireBlastX_1") A_Explode(8,64,0)
		PROJ E 4 BRIGHT LIGHT("FireBlastX_2")
		PROJ F 4 BRIGHT LIGHT("FireBlastX_3")
		PROJ G 4 BRIGHT LIGHT("FireBlastX_4")
		PROJ H 4 BRIGHT LIGHT("FireBlastX_5")
		PROJ I 4 BRIGHT LIGHT("FireBlastX_6") A_FadeOut(0.50)
		Stop
	}
}

ACTOR SuperFireBlast : FireBlast
{
	Speed 16
	Scale 1.5
	Damage (random(1,4)*24)
	DeathSound "superfireblast/explode"
	States
	{
	Death:
		PROJ D 4 BRIGHT LIGHT("FireBlastX_1") A_Explode(256,128,0)
		Goto Super::Death+1
	}
}

/////////////
// SPIDERS //
/////////////
ACTOR MiniSpider 22290
{
	Radius 7
	Height 15
	Speed 2.5
	Health 20
	Scale .25
	Translation 0
	Monster
	+FLOORCLIP
	SeeSound "littlespider/sight"
	AttackSound "littlespider/attack"
	PainSound "littlespider/pain"
	DeathSound "littlespider/death"
	ActiveSound "littlespider/sight"
	States
	{
	Spawn:
		MNTR AB 10 A_Look
		Loop
	See:
		MNTR A 1 A_Chase
		MNTR AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MNTR B 1 A_Chase
		MNTR BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MNTR C 1 A_Chase
		MNTR CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MNTR D 1 A_Chase
		MNTR DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		MNTR UW 5 A_FaceTarget
		MNTR X 5 A_CustomMeleeAttack(random(1,8),"littlespider/pain")
		Goto See
	Death:
		MNTS F 5
		MNTS E 5 A_Scream
		MNTS D 5
		MNTS C 5 A_NoBlocking
		MNTS B 5
		MNTS A -1
		Stop
	}
}

ACTOR LargeSpider : MiniSpider
{
	Radius 14
	Height 30
	Health 150
	Scale .50
	Translation 1
	DropItem "Heart" 160
	Speed 5
	States
	{
	Missile:
		MNTR A 10 A_FaceTarget
		MNTR A 0 A_SkullAttack
		MNTR ABCD 2
		Goto Missile+2
	Melee:
		MNTR UW 5 A_FaceTarget
		MNTR X 5 A_CustomMeleeAttack(random(1,8)*2,"littlespider/pain")
		Goto See
	}
}

/////////////////
// CULTIST ONE //
/////////////////
ACTOR Thrall 30119
{
	Health 100
	GibHealth 35
	PainChance 200
	Scale 1.1
	Speed 4
	Height 56
	Mass 100
	Monster
	+FLOORCLIP
	SeeSound "monster/culsit"
	AttackSound "monster/culatk"
	PainSound "monster/culpai"
	DeathSound "monster/culdth"
	ActiveSound "monster/culact"
	Obituary "$OB_THRL"
	States
	{
	Spawn:
		CULT AB 10 A_Look
		Loop
	See:
		CULT A 1 A_Chase
		CULT A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT A 1 A_Chase
		CULT A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT B 1 A_Chase
		CULT B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT B 1 A_Chase
		CULT B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT C 1 A_Chase
		CULT C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT C 1 A_Chase
		CULT C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT D 1 A_Chase
		CULT D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CULT D 1 A_Chase
		CULT D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		CULT EE 5 A_FaceTarget
		CULT F 0 A_CustomMissile("ThrallShot1",42,-6,0,1)
		CULT F 0 A_CustomMissile("ThrallShot1",42,6,0,1)
		CULT F 8 BRIGHT LIGHT("Thrall") A_PlaySound("monster/culatk")
		Goto See
	Pain:
		CULT G 2
		CULT G 2 A_Pain
		Goto See
	Death:
		CULT H 8
		CULT I 8 A_Scream
		CULT J 4
		CULT K 4 A_NoBlocking
		CULT L 4
		CULT M -1
		Stop
	XDeath:
		CULT N 0 A_SpawnDebris("NashGore_Gib9",0)
		CULT N 0 A_SpawnDebris("NashGore_Gib10",0)
		CULT N 0 A_SpawnDebris("NashGore_Gib11",0)
		CULT N 0 A_SpawnDebris("NashGore_Gib11",0)
		CULT N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		CULT N 5
		CULT O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		CULT O 5 A_XScream
		CULT P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		CULT P 5 A_NoBlocking
		CULT QRSTUV 5
		CULT W -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		Stop
	Raise:
		CULT MLKJHI 5
		Goto See
	}
}

ACTOR ThrallShot1
{
	Radius 4
	Height 6
	Speed 14
	Damage 1
	RenderStyle Add
	Alpha 0.67
	Projectile
	DeathSound "monster/disht1"
	States
	{
	Spawn:
		CFX4 AB 2 BRIGHT LIGHT("ThrallShot")
		Loop
	Death:
		CFX4 C 3 BRIGHT LIGHT("ThrallShotX_1")
		CFX4 D 3 BRIGHT LIGHT("ThrallShotX_2")
		CFX4 E 3 BRIGHT LIGHT("ThrallShotX_3")
		CFX4 F 3 BRIGHT LIGHT("ThrallShotX_4")
		Stop
	}
}

/////////////////////
// DEATH INCARNATE //
/////////////////////
ACTOR Incarnate 30115
{
	Health 400
	GibHealth 35
	Radius 20
	Height 56
	Mass 500
	Speed 6
	PainChance 32
	MaxTargetRange 1000
	Monster
	+FLOORCLIP
	+MISSILEMORE
	+NOTARGET
	SeeSound "monster/incsit"
	PainSound "skeleton/pain"
	DeathSound "monster/incdth"
	ActiveSound "monster/incact"
	AttackSound "monster/incatk"
	Obituary "$OB_INCA"
	HitObituary "$HB_INCA"
	DropItem "Heart" 160
	States
	{
	Spawn:
		INCA AB 10 A_Look
		Loop
	See:
		INCA A 1 A_Chase
		INCA A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA A 1 A_Chase
		INCA A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA B 1 A_Chase
		INCA B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA B 1 A_Chase
		INCA B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA C 1 A_Chase
		INCA C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA C 1 A_Chase
		INCA C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA D 1 A_Chase
		INCA D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA D 1 A_Chase
		INCA D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA E 1 A_Chase
		INCA E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA E 1 A_Chase
		INCA E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA F 1 A_Chase
		INCA F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		INCA F 1 A_Chase
		INCA F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		INCA G 0 A_Jump(128,5)
		INCA G 0 A_FaceTarget
		INCA G 6 A_SkelWhoosh
		INCA H 6 A_FaceTarget
		INCA I 6 A_CustomMeleeAttack(7*random(1,8),"monster/inchit")
		Goto See
		INCA R 0 A_FaceTarget
		INCA R 5 A_SkelWhoosh
		INCA S 5 A_FaceTarget
		INCA T 5 A_CustomMeleeAttack(7*random(1,8),"monster/inchit")
		INCA G 0 A_FaceTarget
		INCA G 5 A_SkelWhoosh
		INCA H 5 A_FaceTarget
		INCA I 5 A_CustomMeleeAttack(7*random(1,8),"monster/inchit")
		Goto See
	Missile:
		INCA J 10 A_FaceTarget
		INCA U 4 LIGHT("Incarnate") A_CustomMissile("ShadowBall",64,0,random(20,10))
		INCA U 4 LIGHT("Incarnate") A_CustomMissile("ShadowBall",64,0,random(-10,-20))
		INCA U 4 LIGHT("Incarnate") A_CustomMissile("ShadowBall",64)
		INCA K 10
		Goto See
	Pain:
		INCA L 5
		INCA L 5 A_Pain
		Goto See
	Death:
		INCX A 0 A_Jump(128,"XDeath")
		INCA LM 7
		INCA N 7 A_Scream
		INCA O 7 A_NoBlocking
		INCA P 7
		INCA Q 175
		INCA Q -1 Thing_Raise(0)
		Stop
	XDeath:
		INCX A 10 BRIGHT LIGHT("IncarnateX_1") A_PlaySound("monster/incexp")
		INCX B 5 BRIGHT LIGHT("IncarnateX_2")
		INCX A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		INCX C 5 BRIGHT LIGHT("IncarnateX_3")
		INCX D 5 BRIGHT LIGHT("IncarnateX_4") A_NoBlocking
		INCX A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		INCX D 0 A_SpawnItemEx("HS",0,0,32)
		INCX E 5 BRIGHT LIGHT("IncarnateX_5")
		INCX F 5 BRIGHT LIGHT("IncarnateX_6")
		INCX A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		INCX G 5 BRIGHT LIGHT("IncarnateX_7")
		INCX H 5 BRIGHT LIGHT("IncarnateX_8")
		INCX A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,5)
		INCX I 5 BRIGHT LIGHT("IncarnateX_9")
		INCX J 5 BRIGHT LIGHT("IncarnateX_10")
		INCX K -1
		Stop
	Crush:
		TNT1 A 1 { A_KillChildren; A_SpawnItem("NashGore_CrushedMedium",0,0,0,0); }
		Stop
	Raise:
		INCA Q 5 A_KillChildren
		INCA PONML 5
		Goto See
	}
}

ACTOR HS 30116
{
	Health 35
	Radius 16
	Height 32
	Mass 3000
	Speed 5
	Monster
	+DONTFALL
	+DONTGIB
	+DONTHARMCLASS
	+FLOAT
	+LOOKALLAROUND
	+NOGRAVITY
	+NOTARGET
	MeleeThreshold 196
	SeeSound "monster/hadsit"
	Obituary "$OB_HADE"
	States
	{
	Spawn:
		TNT1 A 1 A_Look
		Loop
	See:
		HADE EFGH 4 BRIGHT
		HADE A 2 BRIGHT A_PlaySound("monster/hadwlk")
		HADE AABBBBCCCCDDDD 1 BRIGHT A_Chase
		HADE D 0 A_Jump(32,1)
		Goto See+4
		HADE H 0 A_UnSetSolid
		HADE H 0 A_UnSetShootable
		HADE H 5 BRIGHT A_PlaySound("monster/hadtel")
		HADE GFEIJKL 4 BRIGHT
	Teleporting:
		TNT1 AAAAAAAAAAAAAAAA 1 A_Chase
		TNT1 A 0 A_Jump(96,1)
		Loop
		HADE L 3 BRIGHT A_PlaySound("monster/hadtel")
		HADE KJIEFG 4 BRIGHT
		HADE H 0 A_SetShootable
		HADE H 0 A_SetSolid
		Goto See+4
	Melee:
		HADE HGFE 4 BRIGHT A_SetShootable
		HADE E 0 A_Die
	Death:
		HADE M 0 A_NoBlocking
		HADE M 0 A_SetTranslucent(.9,1)
		HADE M 0 A_PlaySound("monster/hadexp")
		HADE M 4 BRIGHT A_Explode(112,112,XF_NOTMISSILE)
		HADE NOPQ 5 BRIGHT
		Stop
	XDeath:
		HADE N 0 A_NoBlocking
		HADE H 5 BRIGHT A_PlaySound("monster/haddth")
		HADE GFERSTUVWX 4 BRIGHT
		Stop
	}
}

////////////////
// HELL HOUND //
////////////////
ACTOR HotDog 30121
{
	Health 300
	PainChance 128
	Speed 16
	Radius 30
	Height 50
	Mass 500
	Monster
	+DONTHARMCLASS
	+FLOORCLIP
	SeeSound "monster/dogsit"
	AttackSound "monster/dogatk"
	PainSound "monster/dogpai"
	DeathSound "monster/dogdth"
	ActiveSound "monster/dogact"
	MaxTargetRange 256
	DamageFactor Fire,.5
	Obituary "$OB_HOUN"
	HitObituary "$HB_HOUN"
	DropItem "Heart" 160
	States
	{
	Spawn:
		HDOG A 10 A_Look
		Loop
	See:
		HDOG AAAABBBBCCCCDDDDEEEEFFFF 1 A_Chase
		Loop
	Melee:
		HDOG GH 6 A_FaceTarget
		HDOG I 6 A_CustomMeleeAttack(7*random(1,8),"monster/dogbit")
		Goto See
	Missile:
		HDOG G 10 A_FaceTarget
		HDOG HHHHHHHHHHHH 1 A_CustomMissile("DogFire",28)
		HDOG I 6
		Goto See
	Pain:
		HDOG J 2
		HDOG J 2 A_Pain
		Goto See
	Death:
		HDOG K 8
		HDOG L 8 A_Scream
		HDOG M 4
		HDOG N 4 A_NoBlocking
		HDOG OP 4
		HDOG Q -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		HDOG QPONMLK 5
		Goto See
	}
}

ACTOR DogFire
{
	Radius 2
	Height 4
	Speed 16
	Damage 1
	Projectile
	RenderStyle Add
	DamageType Fire
	Alpha 0.67
	Scale 0.67
	SeeSound "weapons/bigbrn"
	DeathSound "weapons/bigbrn"
	Gravity .125
	States
	{
	Spawn:
		TNT1 A 2
		FRFX A 2 BRIGHT LIGHT("FlameX_1") A_Explode(1,8)
		FRFX B 2 BRIGHT LIGHT("FlameX_2") A_Explode(1,8)
		FRFX C 2 BRIGHT LIGHT("FlameX_3") A_Explode(1,8)
		FRFX D 2 BRIGHT LIGHT("FlameX_4") A_Explode(1,8)
		FRFX D 0 A_Gravity
		FRFX E 2 BRIGHT LIGHT("FlameX_5") A_Explode(1,16)
		FRFX F 2 BRIGHT LIGHT("FlameX_6") A_Explode(1,16)
		FRFX G 2 BRIGHT LIGHT("FlameX_7") A_Explode(1,16)
	Death:
		FRFX H 2 BRIGHT LIGHT("FlameX_8") A_Explode(1,32)
		FRFX I 2 BRIGHT LIGHT("FlameX_9") A_Explode(1,32)
		FRFX J 2 BRIGHT LIGHT("FlameX_10") A_Explode(1,32)
		FRFX K 2 BRIGHT LIGHT("FlameX_11") A_Explode(1,64)
		FRFX L 2 BRIGHT LIGHT("FlameX_12") A_Explode(1,64)
		FRFX M 2 BRIGHT LIGHT("FlameX_13") A_Explode(1,64)
		FRFX N 2 BRIGHT LIGHT("FlameX_14")
		FRFX O 2 BRIGHT LIGHT("FlameX_15")
		Stop
	}
}

///////////////
// NIGHTMARE //
///////////////
ACTOR KDiZDNightmare 26000
{
	Health 100
	Height 56
	Speed 5
	PainChance 144
	Monster
	+FLOORCLIP
	+STEALTH
	RenderStyle Add
	Alpha 0.4
	SeeSound "nightmare/sight"
	PainSound "nightmare/pain"
	DeathSound "nightmare/death"
	ActiveSound "nightmare/active"
	HitObituary "$HB_NITE"
	BloodType "Gore_TIvoryBlood"
	BloodColor "Ivory"
	States
	{
	Spawn:
		NMRE AB 10 A_Look
		Loop
	See:
		NMRE A 0 A_UnSetShootable
		NMRE A 1 A_Chase
		NMRE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE A 1 A_Chase
		NMRE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE B 1 A_Chase
		NMRE BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE B 1 A_Chase
		NMRE BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE C 1 A_Chase
		NMRE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE C 1 A_Chase
		NMRE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE D 1 A_Chase
		NMRE DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		NMRE D 1 A_Chase
		NMRE DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
		NMRE D 0 A_SpawnItemEx("NightmareWisp",0,0,0,0,0,0,0,0,224)
		Loop
	Melee:
		NMRE E 0 A_SetShootable
		NMRE EF 9 A_FaceTarget
		NMRE G 9 A_CustomMeleeAttack(random(1,8)*4,"nightmare/melee")
		Goto See
	Pain:
		NMRE H 2 A_SetShootable
		NMRE H 10 A_Pain
		Goto See
	Death:
		NMRE I 3 A_ScreamAndUnblock
		NMRE JKLMNO 3
		Stop
	}
}

ACTOR NightmareWisp
{
	Radius 4
	Height 8
	RenderStyle Add
	Alpha 0.4
	Projectile
	+NOCLIP
	States
	{
	Spawn:
		NMRE IJKLMNO 3
		Stop
	}
}

/////////////////////
// SIREN NIGHTMARE //
/////////////////////
ACTOR SirenNightmare : KDiZDNightmare 31111
{
	Health 300
	Radius 10
	Height 30
	Speed 10
	Scale 1.5
	SeeSound "nightmare2/sight"
	PainSound "nightmare2/pain"
	DeathSound "nightmare2/death"
	ActiveSound "nightmare2/active"
	States
	{
	Melee:
		NMRE E 0 A_SetShootable
		NMRE EF 9 A_FaceTarget
		NMRE G 9 A_CustomMeleeAttack(100*random(1,8),"nightmare2/melee")
		Goto See
	}
}

////////////
// SHADOW //
////////////
ACTOR Shadow 3126
{
	Health 300
	GibHealth 35
	Height 56
	Mass 100
	Scale 1.5
	RenderStyle Add
	Alpha 0.9
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	PainSound "Shadow/pain"
	DeathSound "Shadow/death"
	ActiveSound "Shadow/active"
	Obituary "$OB_SHDW"
	BloodType "Gore_TOnyxBlood"
	BloodColor "Onyx"
	States
	{
	Spawn:
	See:
		TROX A 15
		TROX AAA 1 A_Chase
		TROX A 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		TROX BBB 1 A_Chase
		TROX B 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		TROX CCC 1 A_Chase
		TROX C 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		TROX DDD 1 A_Chase
		TROX D 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		Goto See+1
	Missile:
		TROX E 12 A_FaceTarget
		TROX F 5
		TROX G 5 BRIGHT A_CustomMissile("ShadowBall")
		TROX F 5
		TROX E 20
		TROX E 1 A_SpidRefire
		Goto Missile+1
	Pain:
		TROX H 4
		TROX H 4 A_Pain
		Goto See+1
	Death:
		TROX I 6 A_Scream
		TROX J 5
		TROX K 5 A_CustomMissile("Shadowtorso")
		TROX L 5 A_NoBlocking
		TROX MNO 6
		TROX P -1
		Stop
	XDeath:
		TROX A 0 A_SpawnDebris("Gore_TOnyxGib11",0)
		TROX A 0 A_SpawnDebris("Gore_TOnyxGib11",0)
		TROX A 0 A_SpawnItem("Gore_GibGeneratorTOnyx",0,0,0,0)
		TROX W 5
		TROX B 0 A_SpawnItem("Gore_GibGeneratorTOnyx",0,0,0,2)
		TROX X 4 A_XScream
		TROX C 0 A_SpawnItem("Gore_GibGeneratorTOnyx",0,0,0,4)
		TROX Y 4
		TROX D 0 A_SpawnItem("Gore_GibGeneratorTOnyx",0,0,0,6)
		TROX Z 4 A_NoBlocking
		TROX "][" 4
		TROX \ -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmallTOnyx",0,0,0,0)
		Stop
	Raise:
		TROX MKLJI 8
		Goto See
	}
}

ACTOR ShadowBall
{
	Radius 6
	Height 8
	Speed 12
	Damage 4
	RenderStyle Add
	Alpha 0.75
	Projectile
	+RANDOMIZE
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		SBAL ABC 4 BRIGHT LIGHT("ShadowBall") A_SpawnItemEx("ShadowTrail")
		Loop
	Death:
		SBAL C 5 BRIGHT LIGHT("ShadowBall")
		SBAL D 4 BRIGHT LIGHT("ShadowBallX_1")
		SBAL E 4 BRIGHT LIGHT("ShadowBallX_2")
		SBAL F 4 BRIGHT LIGHT("ShadowBallX_3")
		SBAL G 4 BRIGHT LIGHT("ShadowBallX_4")
		SBAL H 4 BRIGHT LIGHT("ShadowBallX_5")
		Stop
	}
}

ACTOR ShadowGhost
{
	Radius 4
	Height 8
	Mass 75
	RenderStyle Add
	Alpha 0.3
	Projectile
	States
	{
	Spawn:
		"####" "#" 10
		Stop
	}
}

ACTOR Shadowtorso
{
	Radius 12
	Height 15
	Speed 1
	Mass 5000
	RenderStyle Add
	Alpha 0.7
	BounceType Doom
	States
	{
	Spawn:
		TROX QRS 5
		TROX T 5 A_NoBlocking
		TROX U 5
		TROX V -1
		Stop
	}
}

ACTOR ShadowTrail
{
	Radius 1
	Height 1
	Projectile
	RenderStyle Add
	Alpha 0.6
	+NOCLIP
	States
	{
	Spawn:
		SHTR ABCDEF 4 BRIGHT
		Stop
	}
}

////////////////////
// SOUL HARVESTER //
////////////////////
ACTOR SoulHarvester 3117
{
	Health 120
	GibHealth 35
	Height 56
	Speed 2.66666667
	PainChance 120
	Monster
	+FLOORCLIP
	SeeSound "harvester/sight"
	PainSound "imp/pain"
	DeathSound "harvester/death"
	ActiveSound "imp/active"
	Obituary "$OB_HARV"
	DropItem "Heart" 160
	States
	{
	Spawn:
		SLHV AB 10 A_Look
		Loop
	See:
		SLHV A 1 A_Chase
		SLHV AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV A 1 A_Chase
		SLHV AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV B 1 A_Chase
		SLHV BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV B 1 A_Chase
		SLHV BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV C 1 A_Chase
		SLHV CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV C 1 A_Chase
		SLHV CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV D 1 A_Chase
		SLHV DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SLHV D 1 A_Chase
		SLHV DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
	Missile:
		SLHV E 4 LIGHT("SoulHarvesterX_1") A_FaceTarget
		SLHV F 4 LIGHT("SoulHarvesterX_2") A_FaceTarget
		SLHV G 4 LIGHT("SoulHarvesterX_3") A_FaceTarget
		SLHV H 5 BRIGHT LIGHT("SoulHarvesterX_4") A_FaceTarget
		SLHV I 5 BRIGHT LIGHT("SoulHarvesterX_5") A_FaceTarget
		SLHV J 5 BRIGHT LIGHT("SoulHarvesterX_6") A_FaceTarget
		SLHV K 5 BRIGHT LIGHT("SoulHarvesterX_7") A_FaceTarget
		SLHV L 8 BRIGHT LIGHT("SoulHarvesterX_8") A_CustomMissile("SoulHarvesterBall")
		SLHV M 10 LIGHT("SoulHarvesterX_9")
		Goto See
	Pain:
		SLHV N 3
		SLHV N 3 A_Pain
		Goto See
	Death:
		SLHV O 7 A_Scream
		SLHV PQR 7
		SLHV S 6 A_SpawnItemEx("SoulHarvesterGhost",0,0,32)
		SLHV T 5 A_NoBlocking
		SLHV UV 5
		SLHV W -1
		Stop
	XDeath:
		SLHV X 0 A_SpawnDebris("NashGore_Gib11",0)
		SLHV X 0 A_SpawnDebris("NashGore_Gib11",0)
		SLHV X 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		SLHV X 6 A_Xscream
		SLHV Y 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		SLHV Y 6 A_SpawnItemEx("SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
		SLHV Z 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		SLHV Z 6
		SLHV [ 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		SLHV [ 6 A_Fall
		SLHV ] 6
		SLHW A -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		Stop
	Raise:
		SLHV WVUTSRQPO 5
		Goto See
	}
}

ACTOR SoulHarvesterBall
{
	Radius 8
	Height 6
	Scale 0.4
	Speed 9
	Damage 2
	+SEEKERMISSILE
	Projectile
	RenderStyle Add
	SeeSound "harvester/scream"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		SHBA A 0 A_SpawnItemEx("SoulBallTrail")
		SHBA AB 2 BRIGHT LIGHT("SoulHarvesterBall") A_SeekerMissile(5,8)
		Loop
	Death:
		SHBA C 4 BRIGHT LIGHT("SoulHarvesterBallX_1")
		SHBA D 4 BRIGHT LIGHT("SoulHarvesterBallX_2")
		SHBA E 4 BRIGHT LIGHT("SoulHarvesterBallX_3")
		SHBA F 4 BRIGHT LIGHT("SoulHarvesterBallX_4")
		SHBA G 4 BRIGHT LIGHT("SoulHarvesterBallX_5")
		SHBA H 4 BRIGHT LIGHT("SoulHarvesterBallX_6")
		SHBA I 4 BRIGHT LIGHT("SoulHarvesterBallX_7")
		SHBA J 4 BRIGHT LIGHT("SoulHarvesterBallX_8")
		SHBA K 4 BRIGHT LIGHT("SoulHarvesterBallX_9")
		SHBA L 4 BRIGHT LIGHT("SoulHarvesterBallX_10")
		Stop
	}
}

ACTOR SoulHarvesterGhost
{
	Radius 1
	Height 1
	Projectile
	RenderStyle Add
	Alpha 0.7
	+NOCLIP
	SeeSound "harvester/ghost"
	States
	{
	Spawn:
	Death:
		SHGH ABCDEFG 4 BRIGHT
		Stop
	}
}

ACTOR SoulBallTrail : SoulHarvesterGhost
{
	Alpha 0.5
	States
	{
	Spawn:
	Death:
		SBTR H 6
		SBTR ABCDEFG 4 BRIGHT
		Stop
	}
}

////////////
// THAMUZ //
////////////
ACTOR Thamuz : Cyberdemon
{
	Health 3200
	Speed 5.33333333
	PainChance 35
	Mass 2000
	Scale 1.1
	Bloodcolor "00 aa 00"
	+LOOKALLAROUND
	+MISSILEEVENMORE
	MinMissileChance 5
	SeeSound "Thamuz/Idlesight"
	PainSound "Thamuz/Pain"
	DeathSound "Thamuz/death"
	ActiveSound "Thamuz/Idlesight"
	Obituary "$OB_THAM"
	var int user_theta;
	var int user_Height;
	var int user_offset;
	States
	{
	Spawn:
		DCYB A 10 NODELAY A_Look
		Loop
	See:
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 0 A_SpawnItemEx("ThamuzGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		Loop
	Missile:
		"####" A 0 A_SetUserVar("user_Height",56)
		"####" A 0 A_SetUserVar("user_offset",-25)
		"####" A 0 A_Jump(160,"Missile2")	//160
		"####" AEE 5 A_FaceTarget
		"####" F 10 A_CustomMissile("DCBMiss1",user_Height,user_offset)
		"####" E 15 A_FaceTarget
		Goto See
	Missile2:
		"####" A 0 A_Jump(160,"Missile3") //160
		"####" A 0 A_SetInvulnerable
		"####" G 20 A_PlaySound("Apocalypse/cast")
	Shock:
		"####" G 0 A_CustomMissile("DCBMiss3",50,0,user_theta)
		"####" G 0 A_SetUserVar("user_theta",user_theta+20)
		"####" G 0 A_JumpIf(user_theta==360,1)
		Loop
		"####" G 90 A_SetUserVar("user_theta",0)
		"####" G 10 A_UnsetInvulnerable
		Goto See
	Missile3:
		"####" AE 3 A_FaceTarget
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		"####" E 4 A_FaceTarget
		"####" F 4 A_CustomMissile("DCBMiss2",user_Height,user_offset)
		Goto See
	Pain:
		"####" G 2
		"####" G 2 A_Pain
		"####" G 0 A_Jump(256,"See")
	Death:
		"####" GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_SpawnItemEx("Thamuz_Blood",Random(-32,32),Random(-32,32),Random(0,64),Random(-16,16),Random(-16,16),Random(-16,16))
		"####" H 0 A_PlaySound("SFX\Explosion1")
		"####" HHHH 5 A_SpawnItemEx("Explosion_Green_A1A",Random(-64,64),Random(-64,64),Random(0,60))
		"####" H 0 A_PlaySound("SFX\Explosion1")
		"####" HHH 5 A_SpawnItemEx("Explosion_Green_A1A",Random(-64,64),Random(-64,64),Random(0,60))
		"####" I 7 A_Scream
		"####" A 0 A_PlaySound("Thamuz/death2",5)
		"####" JJJJJJJKKKKKKKK 1 A_CustomMissile("SFX_Wraith",Random(0,64),0,Random(0,359),2,Random(-16,16))
		"####" A 0 A_SpawnItemEx("Explosion_Green_A1A",Random(-64,64),Random(-64,64),Random(0,60))
		"####" AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Thamuz_Blood",Random(-64,64),Random(-64,64),Random(0,128),Random(-16,16),Random(-16,16),Random(-16,16))
		"####" MMMMMMMNNNNNNNOOOOOOO 1 A_CustomMissile("SFX_Wraith",Random(0,64),0,Random(0,359),2,Random(-16,16))
		"####" P -1 A_NoBlocking
		Stop
	}
}

ACTOR ThamuzGhost
{
	Radius 2
	Height 2
	Scale 1.1
	Projectile
	Alpha 0
	States
	{
	Spawn:
		"####" "#" 3
		"####" "#" 0 A_SetTranslucent(.6)
		"####" "#" 3 A_FadeOut
		Wait
	}
}

ACTOR DCBMiss1
{
	Radius 12
	Height 12
	Speed 6
	Damage 30
	Projectile
	+EXTREMEDEATH
	+SEEKERMISSILE
	RenderStyle Add
	SeeSound "q2weapon/bfgball"
	DeathSound "q2weapon/bfgballexplo"
	States
	{
	Spawn:
		CBTS A 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS B 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS C 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS D 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS E 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS F 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS G 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS H 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS I 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		CBTS J 2 BRIGHT LIGHT("ThamuzMissile_1") A_SeekerMissile(10,90)
		TNT1 A 0 A_SpawnItemEx("DCBM1Trail")
		Loop
	Death:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DCBMiss3Extra",0,0,5,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		CBTS K 2 BRIGHT LIGHT("ThamuzMissile_1X_1") A_Explode(140)
		CBTS L 2 BRIGHT LIGHT("ThamuzMissile_1X_2")
		CBTS M 2 BRIGHT LIGHT("ThamuzMissile_1X_3")
		CBTS N 2 BRIGHT LIGHT("ThamuzMissile_1X_4")
		CBTS O 2 BRIGHT LIGHT("ThamuzMissile_1X_5")
		CBTS P 2 BRIGHT LIGHT("ThamuzMissile_1X_6")
		CBTS Q 2 BRIGHT LIGHT("ThamuzMissile_1X_7")
		CBTS R 2 BRIGHT LIGHT("ThamuzMissile_1X_8")
		CBTS S 2 BRIGHT LIGHT("ThamuzMissile_1X_9")
		CBTS T 2 BRIGHT LIGHT("ThamuzMissile_1X_10")
		CBTS U 2 BRIGHT LIGHT("ThamuzMissile_1X_11")
		CBTS V 2 BRIGHT LIGHT("ThamuzMissile_1X_12")
		CBTS W 2 BRIGHT LIGHT("ThamuzMissile_1X_13")
		Stop
	}
}

ACTOR DCBM1Trail
{
	Radius 1
	Height 1
	Scale 0.8
	+NOCLIP
	Projectile
	RenderStyle Add
	Alpha 0.6
	States
	{
	Spawn:
		TNT1 A 4
		CBTS LMNOPQRSTUVW 2 BRIGHT A_FadeOut
		Stop
	}
}

ACTOR DCBMiss2
{
	Radius 12
	Height 8
	Health 5
	Speed 28
	Damage 10
	RenderStyle Add
	Projectile
	Translation "160:167=120:127","208:223=120:127"
	SeeSound "q2weapon/rocketlauncherfire"
	DeathSound "q2weapon/rocketexplo"
	States
	{
	Spawn:
		THZ4 A 1 BRIGHT LIGHT("ThamuzMissile_2") A_SpawnItemEx("DCBMiss2Trail")
		Loop
	Death:
		FSFX D 2 BRIGHT LIGHT("ThamuzMissile_2X_1")
		FSFX E 2 BRIGHT LIGHT("ThamuzMissile_2X_2")
		FSFX F 2 BRIGHT LIGHT("ThamuzMissile_2X_3")
		FSFX G 2 BRIGHT LIGHT("ThamuzMissile_2X_4")
		FSFX H 2 BRIGHT LIGHT("ThamuzMissile_2X_5")
		FSFX I 2 BRIGHT LIGHT("ThamuzMissile_2X_6")
		FSFX J 2 BRIGHT LIGHT("ThamuzMissile_2X_7")
		FSFX K 2 BRIGHT LIGHT("ThamuzMissile_2X_8")
		FSFX L 2 BRIGHT LIGHT("ThamuzMissile_2X_9")
		FSFX M 2 BRIGHT LIGHT("ThamuzMissile_2X_10")
		Stop
	}
}

ACTOR DCBMiss2Trail : DCBM1Trail
{
	Scale 0.25
	Alpha 0.4
	States
	{
	Spawn:
		FSFX DEFGHIJKL 1 BRIGHT
		FSFX M 1 BRIGHT A_FadeOut
		Wait
	}
}

ACTOR DCBMiss3
{
	Radius 1
	Height 1
	Health 5
	Speed 8
	Scale 2
	RenderStyle Add
	Projectile
	+BLOODLESSIMPACT
	+DONTSPLASH
	+RIPPER
	SeeSound "h2necromancer/magiccast"
	States
	{
	Spawn:
		TNT1 A 2 A_Gravity
		Loop
	Death:
		FSFX NNNOOOPPPQQQRRRSSSTTTNNNOOOPPPQQQRRRSSSTTT 1 BRIGHT LIGHT("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra",0,0,5,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		TNT1 A 0 A_CustomMissile("DCBMiss3M",0,0,180)
		Stop
	}
}

ACTOR DCBMiss3M
{
	Radius 10
	Height 10
	Speed 28
	Damage 12
	Scale 1.3
	RenderStyle Add
	Projectile
	+FLOORHUGGER
	DeathSound "comet/explosion"
	States
	{
	Spawn:
		FSFX N 1 BRIGHT LIGHT("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3MG")
		FSFX NNOOOPPPQQQRRRSSSTTT 1 BRIGHT LIGHT("ThamuzMissile_3") A_SpawnItemEx("DCBMiss3Extra")
		Loop
	Death:
		TNT1 A 0 A_Explode(128,96)
		TNT1 AAAAA 0 A_SpawnItemEx("DCBMiss3Extra")
		FSFX U 3 BRIGHT LIGHT("ThamuzMissile_3X_1")
		FSFX V 3 BRIGHT LIGHT("ThamuzMissile_3X_2")
		FSFX W 3 BRIGHT LIGHT("ThamuzMissile_3X_3")
		Stop
	}
}

ACTOR DCBMiss3Extra
{
	Radius 1
	Height 1
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 AA 0 NODELAY A_SpawnItemEx("DCBMiss3Extra2",0,0,5,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		Stop
	}
}

ACTOR DCBMiss3Extra2 : DCBMiss3Extra
{
	Damage 2
	Scale 0.18
	RenderStyle Add
	Projectile
	+BLOODLESSIMPACT
	+RIPPER
	Translation "168:191=80:82"
	States
	{
	Spawn:
		CBTS SS 1 BRIGHT A_Explode(12,32)
		TNT1 A 0 A_Jump(120,1)
		Loop
		CBTS SSS 1 BRIGHT A_Explode(12,32)
		CBTS S 1 BRIGHT A_FadeOut
		Wait
	}
}

ACTOR DCBMiss3MG
{
	Radius 1
	Height 1
	Scale 0.8
	Damage 5
	RenderStyle Add
	Alpha 0.6
	Projectile
	+RIPPER
	States
	{
	Spawn:
		TNT1 A 4
		FSFX NOPQRST 3 BRIGHT
		Stop
	}
}

ACTOR Explosion_Green_A1A
{
	Radius 1
	Height 2
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 1 NODELAY Radius_Quake(8,140,0,16,0)
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GreenEmber_A1A",Random(-16,16),Random(-16,16),Random(0,76),Random(-1,1),Random(-1,1),Random(1,8))
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1A",Random(-8,8),Random(-8,8),Random(0,16),Random(-1,1),Random(-1,1),Random(0,8))
		TNT1 AAAAA 0 A_SpawnItemEx("Fire_Green_A1B",Random(-8,8),Random(-8,8),Random(0,16),Random(-1,1),Random(-1,1),Random(0,8))
		TNT1 AAAAA 0 A_CustomMissile("SFX_Wraith",Random(0,64),0,Random(0,359),2,Random(-16,16))
		Stop
	}
}

ACTOR Fire_Green_A1A : Explosion_Green_A1A
{
	RenderStyle Add
	States
	{
	Spawn:
		AFGR KJIHGFEDCBA 1 BRIGHT NODELAY A_FadeOut(0.02)
		Wait
	}
}

ACTOR Fire_Green_A1B : Fire_Green_A1A
{
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(-scalex,scaley)
		Goto Super::Spawn
	}
}

ACTOR GreenEmber_A1A : Fire_Green_A1A
{
	Scale 0.05
	States
	{
	Spawn:
		EMGR A 1 BRIGHT A_FadeOut(0.005)
		Loop
	}
}

ACTOR SFX_Wraith
{
	Translation "80:111=112:127"
	Projectile
	Speed 8
	RenderStyle Add
	Alpha 0.6
	SeeSound "monster/SprSit"
	States
	{
	Spawn:
		WRAI AABB 5 A_FadeOut(0.05)
		Loop
	}
}

////////////////
// TCHERNOBOG //
////////////////
ACTOR Tchernobog : Thamuz
{
	Obituary "$OB_TCHE"
	Bloodcolor "aa 00 00"
	States
	{
	Spawn:
		TCHR A 10 NODELAY A_Look
		Loop
	Missile:
		"####" A 0 A_SetUserVar("user_Height",64)
		Goto Super::Missile
	}
}

////////////
// VULGAR //
////////////
ACTOR Vulgar : DoomImp 30127
{
	Translation "16:47=112:127","168:191=114:128"
	Health 100
	Mass 120
	Speed 2.66666667
	PainChance 120
	SeeSound "monster/vulsit"
	PainSound "monster/vulpai"
	DeathSound "monster/vuldth"
	ActiveSound "monster/vulact"
	Obituary "$OB_VULG"
	HitObituary "$HB_VULG"
	BloodColor "0 a3 0"
	DropItem "Heart" 128
	States
	{
	Spawn:
		VULG AB 10 A_Look
		Loop
	See:
		VULG A 1 A_Chase
		VULG AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG A 1 A_Chase
		VULG AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG B 1 A_Chase
		VULG BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG B 1 A_Chase
		VULG BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG C 1 A_Chase
		VULG CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG C 1 A_Chase
		VULG CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG D 1 A_Chase
		VULG DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VULG D 1 A_Chase
		VULG DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		VULG E 8 A_FaceTarget
		VULG F 1 A_PlaySound("monster/vulswg")
		VULG F 7 A_FaceTarget
		VULG G 6 A_CustomMeleeAttack(4*random(1,8),"monster/vulhit")
		goto See
	Missile:
		VULG EF 8 A_FaceTarget
		VULG G 6 A_CustomMissile("VulgarShot",37)
		goto See
	Pain:
		VULG H 2
		VULG H 2 A_Pain
		goto See
	Death:
		VULG I 8
		VULG J 8 A_Scream
		VULG KL 6
		VULG M 6 A_NoBlocking
		VULG N -1
		Stop
	XDeath: //new
		VULG O 0 A_SpawnDebris("Gore_GreenGib11",0)
		VULG O 0 A_SpawnDebris("Gore_GreenGib11",0)
		VULG O 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
		VULG O 5
		VULG P 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,2)
		VULG P 5 A_XScream
		VULG Q 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,4)
		VULG Q 5
		VULG R 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,6)
		VULG R 5 A_NoBlocking
		VULG STU 5
		VULG V -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmallGreen",0,0,0,0)
		Stop
	Raise:
		VULG MLKJI 6
		Goto See
	}
}

ACTOR VulgarShot
{
	Radius 8
	Height 16
	Speed 10
	Damage 4
	PoisonDamage 32
	RenderStyle Add
	Alpha 0.67
	Projectile
	SeeSound "poly/stmdr1"
	DeathSound "weapons/bloodx"
	States
	{
	Spawn:
		FVUL AB 3 BRIGHT LIGHT("VulgarShot")
		Loop
	Death:
		FVUL C 4 BRIGHT LIGHT("VulgarShotX_1")
		FVUL D 4 BRIGHT LIGHT("VulgarShotX_2")
		FVUL E 4 BRIGHT LIGHT("VulgarShotX_3")
		FVUL F 4 BRIGHT LIGHT("VulgarShotX_4")
		Stop
	}
}

///////////////////
// ETHEREAL SOUL //
///////////////////
ACTOR ESoul 29012
{
	Health 180
	Radius 16
	Height 32
	Mass 400
	Speed 1.66666667
	FloatSpeed 1.33333333
	Damage 5
	PainChance 128
	RenderStyle Add
	Alpha 0.4
	Monster
	+DONTFALL
	+FLOAT
	+MISSILEMORE
	+NOBLOOD
	+NOGRAVITY
	+NOICEDEATH
	+STEALTH
	AttackSound "skull/melee"
	PainSound "nightmare/pain"
	DeathSound "nightmare/death"
	ActiveSound "nightmare/active"
	Obituary "$OB_ESOL"
	DropItem "Heart" 160
	States
	{
	Spawn:
		ETHS ABCD 10 BRIGHT A_Look
		Loop
	See:
		ETHS A 1 BRIGHT A_Chase
		ETHS AA 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS A 1 BRIGHT A_Chase
		ETHS AA 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS B 1 BRIGHT A_Chase
		ETHS BB 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS B 1 BRIGHT A_Chase
		ETHS BB 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS C 1 BRIGHT A_Chase
		ETHS CC 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS C 1 BRIGHT A_Chase
		ETHS CC 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS D 1 BRIGHT A_Chase
		ETHS DD 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		ETHS D 1 BRIGHT A_Chase
		ETHS DD 1 BRIGHT A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		ETHS C 0 A_Jump(128,9)
		ETHS C 6 BRIGHT A_FaceTarget
		ETHS E 4 BRIGHT A_SkullAttack
		ETHS E 0 A_FaceTarget
		ETHS F 4 BRIGHT A_SkullAttack
		ETHS F 0 A_FaceTarget
		ETHS E 4 BRIGHT A_SkullAttack
		ETHS E 0 A_FaceTarget
		ETHS F 4 BRIGHT A_SkullAttack
		goto See
		ETHS P 3 BRIGHT A_FaceTarget
		ETHS Q 4 BRIGHT A_FaceTarget
		ETHS R 5 BRIGHT A_CustomMissile("ShadowBall")
		ETHS E 4 BRIGHT
		goto See
	Pain:
		ETHS G 3 BRIGHT
		ETHS G 3 BRIGHT A_Pain
		goto See
	Death:
		ETHS F 3 BRIGHT
		ETHS H 3 BRIGHT A_ScreamAndUnblock
		ETHS IJKLMNO 3 BRIGHT
		Stop
	}
}

////////////
// WRAITH //
////////////
ACTOR DEWraith 30106
{
	Health 50
	Radius 16
	Height 32
	Mass 50
	Speed 3.33333333
	FloatSpeed 1.33333333
	Damage 2
	RenderStyle Add
	Alpha 0.67
	PainChance 64
	Monster
	+DONTFALL
	+DONTGIB
	+FLOAT
	+NOGRAVITY
	+NOTARGET
	AttackSound "monster/sprsit"
	PainSound "archvile/pain"
	DeathSound "monster/sprdth"
	Obituary "$OB_WRTH"
	HitObituary "$HB_WRTH"
	States
	{
	Spawn:
		WRAI AB 10 A_Look
		Loop
	See:
		WRAI A 1 A_VileChase
		WRAI AA 1 A_Chase("","")
		WRAI A 1 A_VileChase
		WRAI AA 1 A_Chase("","")
		WRAI B 1 A_VileChase
		WRAI BB 1 A_Chase("","")
		WRAI B 1 A_VileChase
		WRAI BB 1 A_Chase("","")
		Loop
	Missile:
		WRAI A 10 A_FaceTarget
		WRAI B 4 A_SkullAttack
		WRAI AB 4
		Goto Missile+2
	Melee:
		WRAI A 5 A_FaceTarget
		WRAI B 5 A_CustomMeleeAttack(random(1,8),"monster/spratk")
		Goto See
	Pain:
		WRAI A 3
		WRAI A 3 A_Pain
		Goto See
	Heal:
		WRAI AB 3
		TNT1 A 0 A_Die
	Death:
		WRAI E 4 A_NoBlocking
		WRAI F 5 A_Scream
		WRAI GHIJ 4
		Stop
	}
}

//////////////
// CULTISTS //
//////////////
ACTOR TommyGunCultist 29011
{
	Health 60
	Height 56
	Mass 100
	Speed 2.66666667
	Scale 0.7
	PainChance 100
	Monster
	+FLOORCLIP
	SeeSound "monster/culsit"
	AttackSound "chainguy/attack"
	PainSound "monster/culpai"
	DeathSound "monster/culdth"
	ActiveSound "monster/culact"
	Obituary "$OB_CULT"
	DropItem "ClipBox" 80
	States
	{
	Spawn:
		BCUL AB 10 A_Look
		Loop
	Frightened:
		BCUL A 0 A_ChangeFlag("Frightened",1)
		Goto See+1
	See:
		BCUL A 0 A_JumpIfHealthLower(25,"Frightened")
		BCUL A 1 A_Chase
		BCUL AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL A 1 A_Chase
		BCUL AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL B 1 A_Chase
		BCUL BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL B 1 A_Chase
		BCUL BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL C 1 A_Chase
		BCUL CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL C 1 A_Chase
		BCUL CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL D 1 A_Chase
		BCUL DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BCUL D 1 A_Chase
		BCUL DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		BCUL E 20 A_FaceTarget
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,1,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 3
		Goto See
	Pain:
		BCUL G 3
		BCUL G 3 A_Pain
		Goto See
	Death:
		BCUL H 5
		BCUL I 5 A_Scream
		BCUL J 5 A_NoBlocking
		BCUL KLM 5
		BCUL N -1
		Stop
	XDeath:
		BCUL O 0 A_SpawnDebris("NashGore_Gib9",0)
		BCUL O 0 A_SpawnDebris("NashGore_Gib10",0)
		BCUL O 0 A_SpawnDebris("NashGore_Gib11",0)
		BCUL O 0 A_SpawnDebris("NashGore_Gib11",0)
		BCUL O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BCUL O 5
		BCUL P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		BCUL P 5 A_XScream
		BCUL Q 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		BCUL Q 5 A_NoBlocking
		BCUL RST 5
		BCUL U -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		Stop
	Raise:
		BCUL NMLKJIH 5
		Goto See
	}
}

ACTOR PumpCultist : TommyGunCultist 29010
{
	Health 80
	AttackSound "shotguy/attack"
	Obituary "$OB_CUL2"
	DropItem "Shell2" 80	// even if he does, he drops only 2 shells, so you don't get too much ammo
	Translation "80:111=48:79"
	States
	{
	Missile:
		BCUL E 20 A_FaceTarget
		BCUL F 2 BRIGHT LIGHT("CultistLight") A_CustomBulletAttack(22.5,0,3,random(1,5)*3,"NewPuff",0,CBAF_NORANDOM)
		BCUL E 18
		Goto See
	}
}

ACTOR AngstHase : TommyGunCultist 29234
{
	ActiveSound "monster/culflee"
	DropItem "Clip"
	States
	{
	Missile:
		Stop
	}
}

/////////////////
// FAT ASSHOLE //
/////////////////
ACTOR Fatty : Revenant 25010
{
	GibHealth 35
	Radius 12
	Speed 1.75
	Scale 0.8
	SeeSound "fatty/sit"
	PainSound "skeleton/pain"
	DeathSound "fatty/death"
	ActiveSound "fatty/act"
	Obituary "$OB_FATY"
	HitObituary "$HB_FATY"
	DropItem "Heart" 128
	Species "Fatty"
	States
	{
	Spawn:
		BFAT AB 10 A_Look
		Loop
	See:
		BFAT A 1 A_Chase
		BFAT AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT A 1 A_Chase
		BFAT AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT B 1 A_Chase
		BFAT BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT B 1 A_Chase
		BFAT BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT C 1 A_Chase
		BFAT CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT C 1 A_Chase
		BFAT CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT D 1 A_Chase
		BFAT DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT D 1 A_Chase
		BFAT DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT E 1 A_Chase
		BFAT EEE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT E 1 A_Chase
		BFAT EEE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT F 1 A_Chase
		BFAT FFF 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAT F 1 A_Chase
		BFAT FFF 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		BFAT G 0 A_FaceTarget
		BFAT G 6 A_SkelWhoosh
		BFAT H 6 A_FaceTarget
		BFAT I 6 A_SkelFist
		Goto See
	Missile:
		BFAT J 10 A_FaceTarget
		BFAT J 0 A_Jump(128,3)
		BFAT K 10 A_SpawnProjectile("ButcherVomit1",56,0,CMF_CHECKTARGETDEAD)
		BFAT K 10 A_FaceTarget
		Goto See
		BFAT K 10 A_SpawnProjectile("ButcherVomit2",56,0,CMF_CHECKTARGETDEAD)
		BFAT K 10 A_FaceTarget
		Goto See
	Pain:
		BFAT L 5
		BFAT L 5 A_Pain
		Goto See
	Death:
		BFAT LM 9
		BFAT N 9 A_Scream
		BFAT O 9 A_NoBlocking
		BFAT P 9
		BFAT Q -1
		Stop
	XDeath:
		BBT2 M 0 A_SpawnDebris("NashGore_Gib9",0)
		BBT2 M 0 A_SpawnDebris("NashGore_Gib10",0)
		BBT2 M 0 A_SpawnDebris("NashGore_Gib11",0)
		BBT2 M 0 A_SpawnDebris("NashGore_Gib11",0)
		BBT2 M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BBT2 M 5 A_Scream
		BBT2 N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		BBT2 N 5
		BBT2 O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		BBT2 O 5 A_NoBlocking
		BBT2 P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		BBT2 P 5
		BBT2 PQRSTUV 5
		BBT2 VWVWVWVWVVWVWVWVWVVWVWVWVWVVWVWVWVWV 5
		BBT2 W -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		BFAT QPONML 5
		Goto See
	}
}

ACTOR ButcherGoo
{
	Height 2
	Radius 2
	Damage 0
	Scale 0.2
	Alpha 0.85
	RenderStyle "Add"
	+CANNOTPUSH
	+CLIENTSIDEONLY
	+LOWGRAVITY
	-SOLID
	States
	{
	Spawn:
		BARF ABCDEFABCDEFABCDEF 3 LIGHT("GGLOW2")
	Death:
		BARF AAAA 4 A_FadeOut(0.25)
		Stop
	}
}

ACTOR ButcherVomit1 : RevenantTracer
{
	Scale 0.4
	RenderStyle Add
	Alpha 0.85
	SeeSound "fatty/attack"
	DeathSound "fatty/wall"
	States
	{
	Spawn:
		BARF AB 3 LIGHT("GGLOW") A_SetAngle(angle+frandom(-5,5))
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		BARF CDEF 3 LIGHT("GGLOW") A_SetAngle(angle+frandom(-5,5))
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		BARF EF 3 LIGHT("GGLOW") A_SetAngle(angle+frandom(-5,5))
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		Loop
	Death:
		FBXX A 0 A_SetScale(1.0)
		FBXX A 8
		FBXX B 6
		FBXX C 4
		Stop
	Crash:
		TNT1 A 0 { A_PlaySound("fatty/wall"); A_SetScale(1.0); }
		FBXX A 8
		FBXX B 6
		FBXX C 4
		Stop
	}
}

ACTOR ButcherVomit2 : ButcherVomit1
{
	States
	{
	Spawn:
		BARF AB 3 LIGHT("GGLOW") { A_SetAngle(angle+frandom(-5,5)); A_SeekerMissile(10,20); }
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		BARF CDEF 3 LIGHT("GGLOW") A_SetAngle(angle+frandom(-5,5))
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		BARF EF 3 LIGHT("GGLOW") { A_SetAngle(angle+frandom(-5,5)); A_SeekerMissile(10,20); }
		BARF AA 0 A_SpawnItemEx("ButcherGoo",frandom(-2,2),frandom(-2,2),frandom(-3,3),frandom(-2,2),frandom(-2,2),frandom(-2,2))
		Loop
	}
}

////////////
// ZOMBIE //
////////////
ACTOR ZombieBlood 31002
{
	Health 150
	GibHealth 35
	PainChance 0
	Speed 3
	Radius 30
	Height 56
	Mass 400
	Scale 0.7
	Monster
	+FLOORCLIP
	SeeSound "zombieblood/sit"
	AttackSound "zombieblood/fis"
	PainSound "zombieblood/pan"
	DeathSound "zombieblood/dth"
	ActiveSound "zombieblood/act"
	HitObituary "$OB_ZOMB"
	DropItem "Heart" 160
	States
	{
	Spawn:
		BZOM AB 10 A_Look
		Loop
	See:
		BZOM A 1 A_Chase
		BZOM AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM A 1 A_Chase
		BZOM AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM B 1 A_Chase
		BZOM BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM B 1 A_Chase
		BZOM BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM C 1 A_Chase
		BZOM CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM C 1 A_Chase
		BZOM CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM D 1 A_Chase
		BZOM DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BZOM D 1 A_Chase
		BZOM DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		BZOM EF 8 A_FaceTarget
		BZOM G 8 A_CustomMeleeAttack(10*random(1,8),"zombieblood/hit")
		Goto See
	Pain:
		BZOM H 2
		BZOM H 2 A_Pain
		Goto See
	Death:
		BZOM I 8
		BZOM I 8 A_Scream
		BZOM K 4
		BZOM L 4 A_NoBlocking
		BZOM M 4
		BZOM N -1
		Stop
	XDeath:
		BZOX A 0 A_SpawnDebris("NashGore_Gib9",0)
		BZOX A 0 A_SpawnDebris("NashGore_Gib10",0)
		BZOX A 0 A_SpawnDebris("NashGore_Gib11",0)
		BZOX A 0 A_SpawnDebris("NashGore_Gib11",0)
		BZOX A 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		BZOX A 5 A_Scream
		BZOX B 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		BZOX B 5
		BZOX C 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		BZOX C 5 A_NoBlocking
		BZOX D 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		BZOX DEFG 5
		BZOX H -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
		Stop
	Raise:
		BZOM NMLKII 5
		Goto See
	}
}