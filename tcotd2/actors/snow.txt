//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
// improved by Ceeb //
//////////////////////

Actor SnowParticle
{
	DistanceCheck "tcotdsfx"
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6
	+CLIENTSIDEONLY
	+MISSILE
	+NOBLOCKMAP
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
	Death:
		SNOW A 1 A_FadeOut(0.05,FTF_REMOVE)
		Loop
	}
}

Actor SnowSpawner 23333
{
	//$Category Weather
	//$Title Snow Spawner (Simple)
	//$Color 12
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("weatherfx")==0,"Unsighted")
	SpawnLoop:
		TNT1 A 0 A_CheckRange(1024.0, "Unsighted", TRUE)
		TNT1 A 5 A_SpawnItemEx("SnowParticle",random(256,-256),random(256,-256),0,random(0,1),random(0,1),random(-1,-3))
		Loop
	Unsighted:
		TNT1 A 8
		Goto Spawn
	}
}