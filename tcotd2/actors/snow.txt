//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
// improved by Ceeb //
//////////////////////

Actor SnowParticle
{
	DistanceCheck "tcotdlod"
	Radius 1
	Height 1
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6
	+CLIENTSIDEONLY
	+DONTSPLASH
	+MISSILE
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
	Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}

Actor SnowSpawner 23333
{
	//$Category Weather
	//$Title Snow Spawner (Simple)
	//$Color 12
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CheckSight("Unsighted")
		TNT1 A 0 A_JumpIf(CallACS("weatherfx")==0,"Unsighted")
		TNT1 A 25 A_SpawnItemEx("SnowParticle",random(256,-256),random(256,-256),0,random(0,1),random(0,1),random(-1,-3))
		Loop
	Unsighted:
		TNT1 A 8
		Goto Spawn
	}
}