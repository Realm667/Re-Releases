/*
Credits:
-Hand sprites from Fate (by Capstone)
-Pitchfork & Puff sprites from Blood
-Sounds from Thief
-Flying Pitchfork sprites by NeoWorm & Amuscaria acording to credits

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor Melee_Weapon : Weapon
{
	Weapon.SelectionOrder 3700
	Weapon.KickBack 100
	Tag "Melee"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT
	States
	{
	Ready:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkReady")
		FIST A 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
	PitchforkReady:
		FORK A 1 A_WeaponReady
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkDeselect")
		FIST A 1 A_Lower
		Loop
	PitchforkDeselect:
		FORK A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkSelect")
		FIST A 1 A_Raise
		Loop
	PitchforkSelect:
		FORK A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PitchforkInv",1,"PitchforkFire")
		TNT1 A 0 A_Jump(160,7)
		FIST B 2
		FIST C 4 A_CustomPunch(random(3,5),0,0,"BulletPuff",0,0,0,"ArmorBonus","Fist/Hit",0)
		FIST CBAA 2
		Goto Ready
		FIST D 2
		FIST E 4 A_CustomPunch(random(3,5),0,0,"BulletPuff",0,0,0,"ArmorBonus","Fist/Hit",0)
		FIST EDAA 2
		Goto Ready
	PitchforkFire:
		FORK A 2
		TNT1 A 0
		FORK A 1 offset(0,30)
		FORK B 1
		FORK B 1 offset(0,30)
		FORK C 5 A_CustomPunch(random(5,7),0,0,"HitPuffNew",96,0,0,"ArmorBonus",0,"Pitchfork/Miss")
		FORK C 1 offset(0,34)
		FORK B 1
		FORK B 1 offset(0,34)
		FORK A 2 A_WeaponOffset(0,32)
		FORK A 1 A_ReFire
		Goto Ready
	AltFire:
		FORK B 1 A_PlaySound("Pitchfork/Throw",CHAN_WEAPON)
		FORK C 1 
		FORK D 1 A_FireCustomMissile("Pitchfork_Thrown",0,1,0,5)
		FORK E 1 A_TakeInventory("PitchforkInv")
		FORK E 1 offset(-18,48)
		FORK E 1 offset(-36,64)
		FORK E 1 offset(-54,80)
		FORK E 1 offset(-72,92)
		TNT1 A 2
		Goto Ready
	Spawn:
		TNT1 A -1
		Stop
	}
}

ACTOR PitchforkSpark
{
	Radius 3
	Height 4
	Speed 25
	RenderStyle "Add"
	Scale 0.7
	+CANNOTPUSH
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	+THRUGHOST
	SeeSound "Fork/Stab"
	States
	{
	Spawn:
		TNT1 A 0
		BSPR A 0 A_Jump(128,7)
		BSPR ABCDEFG 2 BRIGHT
		Stop
		BSPR HIJKLMN 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Fork/Stab")
		BLD2 A 0 A_Jump(128,"XDeath2")
		BLD2 FGHI 4
		Stop
	XDeath2:
		BLD2 OPQR 4
		Stop
	}
}

Actor HitPuffNew
{
	Scale 0.5
	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+PUFFONACTORS	// this is necessary for it to still make sound on enemies
	Activesound ""
	Attacksound "Pitchfork/Wall" // what sound it makes when you hit a wall with it
	Seesound "Pitchfork/Flesh" // what sound it makes when you beat somebody up with it
	States
	{
	Crash:
		PPUF A 0 A_SpawnItem("PitchforkSpark")
		PPUF ABCDE 2 
		Stop
	Spawn:
	Melee:
		TNT1 A 1
		Stop
	}
}

ACTOR Pitchfork_Thrown
{
	Radius 2
	Height 1
	Speed 20
	Damage 5
	Gravity .125
	Projectile
	-NOBLOCKMAP
	-NOGRAVITY
	+RIPPER
	States
	{
	Spawn:
		XIPP A -1
		Stop
	Death:
		XIPP A 0 A_AlertMonsters
		XIPP A 4 A_SpawnItemEx("PitchforkInv")
		Stop
	}
}

ACTOR PitchforkInv : Inventory replaces Chainsaw
{
	Inventory.PickupMessage "$IP_FORK"
	Inventory.PickupSound "ITEMUP"
	Inventory.MaxAmount 1
	-INVENTORY.INVBAR
	Scale 1.2
	States
	{
	Spawn:
		XFOR A -1
		Stop
	}
}

/*
Credits:
-Revolver Sprites by Mike12
-Sounds from Clive Baker's Undying
-Bullet Puff sprites from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor RevolverBlood : Pistol replaces Pistol
{
	Tag "Revolver"
	Scale 0.6
	Weapon.Ammotype "Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.SelectionOrder 190
	Inventory.PickupMessage "$IP_PIST"
	AttackSound "Revolver/Fire"
	+WEAPON.WIMPY_WEAPON
	States
	{
	Select:
		REVL A 1 A_Raise
		Loop
	Deselect:
		REVL A 1 A_Lower
		Loop
	Ready:
		REVL A 1 A_WeaponReady
		Loop
	Fire:
		REVL A 2
		TNT1 A 0 A_FireBullets(5.6,0,1,9,"NewPuff")
		REVL B 2 A_Overlay(-2,"Flash")
		REVL C 3
		REVL DEFG 1
		REVL HAA 2
		REVL A 1 A_ReFire
		goto Ready
	Flash:
		RVFL A 2 bright A_Light2
		RVFL B 2 bright A_Light1
		RVFL C 2 bright A_Light0
		Goto LightDone
	Spawn:
		RPCK A -1
		Stop
	}
}

ACTOR NewSparks2
{
	Gravity .125
	Radius 1
	Height 1
	Scale 0.60
	Projectile
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY
	+RIPPER
	-NOGRAVITY
	States
	{
	Spawn:
		WRBL KLKL 2 BRIGHT
		TNT1 A 0 A_Jump(128,1)
		Loop
		WRBL MNOP 2 BRIGHT
		WRBL P 1 BRIGHT A_FadeOut
		Wait
	}
}

ACTOR NewSparks : NewSparks2 { Scale 0.4 }

Actor NewPuff : BulletPuff
{
	Scale 0.5
	VSpeed 0
	RenderStyle Normal
	Alpha 1
	+NOEXTREMEDEATH
	States
	{
	Crash:
		RPUF A 1 BRIGHT LIGHT("PUFF") A_SpawnItemEx("NewSparks",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		RPUF BCDEFG 1 BRIGHT
		Stop
	Spawn:
	Melee:
		TNT1 A 0
		Stop
	}
}

/*
Credits:
-Shotgun sprites & reload sound from Blood
-Firing sounds from Clive Baker's Undying

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/
Actor SawedOff : Shotgun replaces Shotgun
{
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 2
	Weapon.AmmoGive 12
	Weapon.Kickback 100
	Weapon.SelectionOrder 130
	Obituary "$OB_SAWA"
	Inventory.PickupMessage "$IP_SAWA"
	Tag "Sawed-off"
	Scale 0.6
	States
	{
	Spawn:
		SPCK A -1
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,2)
		SAWF A 1 A_WeaponReady
		Loop
		SAWF B 1 A_WeaponReady(WRF_NOSECONDARY)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Down1Shell")
		SAWF A 1 A_Lower
		Loop
	Down1Shell:
		SAWF B 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,"Up1Shell")
		SAWF A 1 A_Raise
		Loop
	Up1Shell:
		SAWF B 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,15)
		SAWF A 1 A_PlayWeaponSound("SawedOff/Fire")
		TNT1 A 0 A_Overlay(-2,"GunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF B 1 A_FireBullets(5.6,0,7,5,"NewPuff",FBF_USEAMMO)
		SAWF B 1 offset(6,44)
		SAWF B 1 offset(10,52)
		SAWF B 1 offset(18,68)
		SAWF B 1 offset(14,56)
		SAWF B 1 offset(10,52)
		SAWF B 1 offset(8,48)
		SAWF B 1 offset(6,44)
		SAWF B 1 offset(4,40)
		SAWF B 1 offset(2,36) A_GiveInventory("SawedOffCheck",1)
		SAWF B 2
		Goto Ready
		SAWF B 1 A_PlayWeaponSound("SawedOff/Fire")
		TNT1 A 0 A_Overlay(-2,"GunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF C 1 A_FireBullets(5.6,0,7,5,"NewPuff",FBF_USEAMMO)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(10,52)
		SAWF C 1 offset(18,68)
		SAWF C 1 offset(14,56)
		SAWF C 1 offset(10,52)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(4,40)
		SAWF C 1 offset(2,36)
		SAWF C 1
		Goto Load
	AltFire:
		SAWF A 1 A_PlayWeaponSound("SawedOff/AltFire")
		TNT1 A 0 A_Overlay(-2,"BigGunSmoke")
		TNT1 A 0 A_GunFlash
		SAWF C 1 A_FireBullets(11.2,7.1,20,5,"NewPuff",FBF_USEAMMO)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(15,62)
		SAWF C 1 offset(23,78)
		SAWF C 1 offset(27,86)
		SAWF C 1 offset(23,78)
		SAWF C 1 offset(19,70)
		SAWF C 1 offset(15,62)
		SAWF C 1 offset(11,56)
		SAWF C 1 offset(8,48)
		SAWF C 1 offset(6,44)
		SAWF C 1 offset(4,40)
		SAWF C 1 offset(2,36)
		SAWF C 1
		Goto Load
	Load:
		TNT1 A 0 A_TakeInventory("SawedOffCheck",1)
		SAWF C 1 A_CheckReload
		SAWF E 2
		SAWF F 2 A_PlaySound("SawedOff/Reload")
		SAWF G 8 A_Overlay(2,"ElHando")
		SAWF JE 2
		Goto Ready
	ElHando:
		TNT1 A 0 A_OverlayFlags(2,PSPF_ADDWEAPON,false)
		SAWF H 1 A_OverlayOffset(2,0,32)
		SAWF H 1 A_OverlayOffset(2,-9,26)
		SAWF H 1 A_OverlayOffset(2,-18,20)
		SAWF H 1 A_OverlayOffset(2,-26,15)
		SAWF H 1 A_OverlayOffset(2,-32,10)
		SAWF I 1 A_OverlayOffset(2,0,32)
		SAWF I 1 A_OverlayOffset(2,-4,40)
		SAWF I 1 A_OverlayOffset(2,-8,48)
		Stop
	Flash:
		SAWF D 2 A_Light2
		SAWF D 2 A_Light1
		Goto LightDone
	GunSmoke:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		TNT1 A 0 A_JumpIfInventory("SawedOffCheck",1,4)
		SFFL ABC 2 Bright A_OverlayOffset(-2,0,32)
		Stop
		SFFL ABC 2 Bright A_OverlayOffset(-2,20,32)
		Stop
	BigGunSmoke:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		SFFL DEF 2 Bright A_OverlayOffset(-2,0,32)
		Stop
	}
}

Actor SawedOffCheck : Inventory
{
Inventory.MaxAmount 1
-INVENTORY.INVBAR
}

/*
Credits:
-Tommygun sprites & sounds from Blood

Original code inherited from the original TCoTD 2, modified by Gothic.
*/
Actor Thompson : Chaingun replaces Chaingun
{
	Weapon.AmmoType "Clip"
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.SelectionOrder 70
	AttackSound "Thompson/Fire"
	Inventory.PickupMessage "$IP_THOM"
	Obituary "$OB_THOM"
	Scale 0.6
	Tag "Thompson"
	States
	{
	Spawn:
		TPCK A 4
		Loop
	Select:
		THMP A 1 A_Raise
		Loop
	Deselect:
		THMP A 1 A_Lower
		Loop
	Ready:
		THMP A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_Overlay(-2,"Flash")
		TNT1 A 0 A_WeaponOffset(random(1,2),random(33,34))
		THMP B 2 A_FireBullets(5.6,0,1,9,"NewPuff")
		THMP B 2 A_WeaponOffset(0,32)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"Flash")
		TNT1 A 0 A_WeaponOffset(random(2,3),random(34,35))
		THMP C 2 A_FireBullets(5.6,0,1,9,"NewPuff")
		THMP C 2 A_WeaponOffset(0,32)
		TNT1 A 0 A_CheckReload
		THMP A 1 A_ReFire
		Goto Ready
	AltFire:
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(1,2),random(35,36))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP B 1 A_WeaponOffset(1,34)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(2,3),random(36,37))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP C 1 A_WeaponOffset(2,36)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(3,4),random(37,38))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP B 1 A_WeaponOffset(3,38)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(4,5),random(38,39))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP C 1 A_WeaponOffset(4,40)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(5,6),random(39,40))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP B 1 A_WeaponOffset(5,42)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(6,7),random(40,41))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP C 1 A_WeaponOffset(6,44)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(7,8),random(41,42))
		THMP B 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP B 1 A_WeaponOffset(7,46)
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_Overlay(-2,"AltFlash")
		TNT1 A 0 A_WeaponOffset(random(8,9),random(42,43))
		THMP C 1 A_FireBullets(5.6,0,-1,9,"NewPuff")
		THMP C 1 A_WeaponOffset(8,48)
		THMP A 1 A_CheckReload
		THMP A 1 A_WeaponOffset(7,46)
		THMP A 1 A_WeaponOffset(6,44)
		THMP A 1 A_WeaponOffset(5,42)
		THMP A 1 A_WeaponOffset(4,40)
		THMP A 1 A_WeaponOffset(3,38)
		THMP A 1 A_WeaponOffset(2,36)
		THMP A 1 A_WeaponOffset(1,34)
		THMP A 22 A_WeaponOffset(0,32)
		THMP A 1 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Jump(180,1,2,3)
		TMFL A 2 Bright A_Light2
		Goto LightDone
		TMFL B 2 Bright A_Light2
		Goto LightDone
		TMFL C 2 Bright A_Light2
		Goto LightDone
	AltFlash:
		TNT1 A 0 A_Jump(180,1,2,3)
		TMFL A 1 Bright A_Light2
		Goto LightDone
		TMFL B 1 Bright A_Light2
		Goto LightDone
		TMFL C 1 Bright A_Light2
		Goto LightDone
	}
}

/*
Credits:
-Napalm sprites & sounds from Blood
-Firing sound from Hexen
-Smoke sprites by Kronos I guess?

Original code inherited from the original TCoTD 2, modified by Gothic. Added bits from lolo_is_cool R667 submissions.
*/
Actor NapalmLauncher : RocketLauncher replaces RocketLauncher
{
	Radius 20
	Height 16
	Weapon.SelectionOrder 250
	Weapon.AmmoType "NapalmAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Inventory.PickupMessage "$IP_NAPA"
	Obituary "$OB_NAPA"
	+WEAPON.EXPLOSIVE
	Scale 0.6
	Tag "Napalm Launcher"
	States
	{
	Spawn:
		NPCK A -1
		Stop
	Ready:
		NAPL A 1 A_WeaponReady
		Loop
	Deselect:
		NAPL A 1 A_Lower
		Loop
	Select:
		NAPL A 1 A_Raise
		Loop
	Fire:
		NAPL A 3
		TNT1 A 0 A_PlaySound("Napalm/Shot")
		TNT1 A 0 A_FireCustomMissile("RocketBlood")
		NAPL B 2 offset(15,49) A_Overlay(-2,"Flash")
		NAPL B 3 offset(21,57)
		NAPL B 3 offset(21,59)
		NAPL B 3 offset(17,53)
		NAPL B 2 offset(6,42)
		NAPL A 2 offset(2,35)
		NAPL A 2 A_WeaponOffset(0,32)
		NAPL A 4
		NAPL A 1 A_Refire
		Goto Ready
	Flash:
		TNT1 A 0 A_OverlayFlags(-2,PSPF_ADDWEAPON,false)
		TNT1 A 0 A_OverlayOffset(-2,0,32)
		NPFL AB 2 Bright A_Light2
		NPFL CD 2 Bright A_Light1
		NPFL E 2 Bright A_Light0
		NPFL FG 2 Bright
		Stop
	}
}

Actor RocketBlood : Rocket
{
	Radius 8
	Height 8
	Speed 20
	Damage 80
	Scale 0.4
	Projectile
	+RANDOMIZE
	SeeSound "Napalm/Fire"
	DeathSound "Napalm/Impact"
	Obituary "$OB_NAPR"
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		NBLL ABC 2 Bright A_SpawnItemEx("NRocketTrail",0,0,0,2,0,0)
		TNT1 AA 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		NBLL DEF 2 Bright A_SpawnItemEx("NRocketTrail",0,0,0,2,0,0)
		Loop
	Death:
		TNT1 A 0 A_SetScale(0.9)
		NEXP A 2 Bright A_Explode
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		NEXP BCDEFGHIJKLMN 2 Bright
		Stop
	}
}

Actor NRocketTrail
{
	+NOINTERACTION
	Scale 0.4
	Alpha 0.4
	RenderStyle "Translucent"
	States
	{
	Spawn:
		NSMK ABCD 2
		NSMK EFGHI 2 A_FadeOut(0.06)
		Stop
	}
}

ACTOR AClip : Clip replaces Clip
{
	Scale 0.8
	Inventory.PickupMessage "$IP_PIS2"
}

ACTOR AClipBox : ClipBox replaces ClipBox
{
	Inventory.PickupMessage "$IP_THO2"
	Inventory.Amount 25
}

ACTOR Shell2 : Shell
{
	Inventory.PickupMessage "IP_SHEL"
	States
	{
	Spawn:
		SHEL B -1
		Stop
	}
}

ACTOR NapalmAmmo : Ammo replaces RocketAmmo
{
	Tag "Napalm"
	Height 26
	Inventory.PickupMessage "$IP_NAP2"
	Inventory.Icon "ROCKA0"
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ROCK A -1
		Stop
	}
}