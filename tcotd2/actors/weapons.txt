ACTOR Pitchfork : Weapon replaces Chainsaw
{
	Scale 1.2
	+WEAPON.WIMPY_WEAPON
	Weapon.SelectionOrder 3700
	Weapon.Kickback 100
	Inventory.PickupSound "ITEMUP"
	Inventory.PickupMessage "$IP_FORK"
	Obituary "$OB_FORK"
	States
	{
	Ready:
		XUNG A 1 A_WeaponReady
		Wait
	Deselect:
		XUNG A 1 A_Lower
		Wait
		TNT1 A 1 A_Lower
		Wait
	Select:
		XUNG A 1 A_Raise
		Wait
	Fire:
		XUNG BBB 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		XUNG CC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		XUNG A 0 A_PlaySound("DSSKESWG",CHAN_WEAPON)
		XUNG A 0 A_FireBullets(14, 0, 4, 2, "PitchforkPuff",1,96)
		XUNG CCBBA 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		XUNG A 0 A_ReFire
		Goto Ready
	AltFire:
		XUNG B 2 A_PlaySound("DSSKESWG",CHAN_WEAPON)
		XUNG C 2 A_FireCustomMissile("Pitchfork_thrown",0,1,0,5)
		TNT1 A 0 A_TakeInventory("Pitchfork")
		Goto Deselect+1
	Spawn:
		XFOR A -1
		Stop
	}
}

ACTOR PitchforkSpark
{
	Radius 3
	Height 4
	Speed 25
	RenderStyle "Add"
	Scale 0.7
	+CANNOTPUSH
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+RANDOMIZE
	+THRUGHOST
	SeeSound "Fork/Stab"
	States
	{
	Spawn:
		TNT1 A 0
		BSPR A 0 A_Jump(128,7)
		BSPR ABCDEFG 2 BRIGHT
		Stop
		BSPR HIJKLMN 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Fork/Stab")
		BLD2 A 0 A_Jump(128,"XDeath2")
		BLD2 FGHI 4
		Stop
	XDeath2:
		BLD2 OPQR 4
		Stop
	}
}

ACTOR PitchforkPuff
{
	Scale 0.8
	RenderStyle Translucent
	Alpha 0.5
	Decal Bullet
	+ALLOWPARTICLES
	+NOBLOCKMAP
	+NODAMAGETHRUST
	+NOGRAVITY
	+RANDOMIZE
	+THRUGHOST
	AttackSound "Fork/Attack"
	SeeSound "Fork/Stab"
	States
	{
	Spawn:
		PUFF A 0
		Stop
	Melee:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Fork/Stab")
		PUFF A 0 A_Jump(128,9)
		PUFF ABCDEFGH 4
		Stop
		PUFF IJKLMNOP 4
		Stop
	Crash:
		PUFF A 0 A_SpawnItem("PitchforkSpark")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("Fork/Stab")
		BLD2 A 0 A_Jump(128,"XDeath2")
		BLD2 FGHI 4
		Stop
	XDeath2:
		BLD2 OPQR 4
		Stop
	}
}

ACTOR Pitchfork_Thrown
{
	Radius 2
	Height 1
	Speed 20
	Damage 5
	Gravity .125
	Projectile
	-NOBLOCKMAP
	-NOGRAVITY
	+RIPPER
	States
	{
	Spawn:
		XIPP A -1
		Stop
	Death:
		XIPP A 0 A_AlertMonsters
		XIPP A 4 A_SpawnItemEx("Pitchfork")
		Stop
	}
}

ACTOR RevolverBlood : Pistol replaces Pistol
{
	Tag "Anaconda"
	Inventory.PickupMessage "$IP_PIST"
	AttackSound "weapons/pistol"
	Weapon.SelectionOrder 190
	States
	{
	Fire:
		PISG A 6
		PISG A 0 A_FireBullets(5.6,0,1,9,"NewPuff")
		PISG A 2 A_GunFlash
		PISG B 2
		PISG C 5
		PISG B 4
		PISG A 8 A_ReFire
		Goto Ready
	Flash:
		PISF A 2 BRIGHT A_Light1
		Goto LightDone
	Spawn:
		PIPP A -1
		Stop
	}
}

ACTOR Sawedoff : Shotgun replaces Shotgun
{
	Tag "Sawed-off"
	Scale .6
	Weapon.SelectionOrder 130
	Weapon.Kickback 100
	Weapon.AmmoType2 "SawedoffLoaded"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 2
	Obituary "$OB_SAWA"
	Inventory.PickupMessage "$IP_SAWA"
	AttackSound "DSSAWFIR"
	+WEAPON.AMMO_CHECKBOTH
	States
	{
	Ready:
		SHTG A 0 A_JumpIfInventory("SawedoffLoaded",0,2)
		SHTG A 0 A_JumpIfInventory("Shell",1,2)
		SHTG A 1 A_WeaponReady
		Loop
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		SHTG A 0 A_JumpIfInventory("SawedoffLoaded",1,1)
		Goto Reload
		SHTG A 3
		SHTG A 0 A_FireBullets(5.6,0,7,5,"NewPuff",0)
		SHTG A 0 A_TakeInventory("SawedoffLoaded",1)
		SHTG A 2 BRIGHT A_Gunflash
	FireFinish:
		SHTG FG 1 BRIGHT
		SHTG H 2
		SHTG GF 1
		SHTG A 0 A_JumpIfInventory("SawedoffLoaded",1,1)
		Goto Reload
		SHTG A 0 A_Refire
		Goto Ready
	Reload:
		SHTG A 0 A_CheckReload
		SHTG A 1 A_JumpIfInventory("Shell",1,1)
		Goto Ready
		SHTG EB 6
		SHTG C 6 A_PlaySound("weapons/sshotc")
		SHTG D 6
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SawedoffLoaded")
		TNT1 A 0 A_JumpIfInventory("SawedoffLoaded",0,2)
		TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadLoop")
		SHTG BEA 6
		SHTG A 7 A_Refire
		Goto Ready
	AltFire:
		SHTG A 0 A_JumpIfInventory("SawedoffLoaded",2,1)
		Goto Fire
		SHTG A 3
		SHTG A 0 A_FireBullets(11.2,7.1,20,5,"NewPuff",0)
		SHTG A 0 A_TakeInventory("SawedoffLoaded",2)
		SHTG A 0 A_PlaySound("weapons/sshotf")
		SHTG A 2 BRIGHT A_Gunflash("AltFlash")
		Goto FireFinish
	Flash:
		SHTG N 3 A_JumpIfInventory("SawedoffLoaded",2,"Flash2")
		SHTG I 2 BRIGHT A_Light2
		SHTG J 2 BRIGHT A_Light1
		SHTG K 1 BRIGHT A_Light0
		Stop
	Flash2:
		SHTG M 3
		SHTG L 2 BRIGHT A_Light2
		SHTG M 2 BRIGHT A_Light1
		SHTG N 1 BRIGHT A_Light0
		Stop
	AltFlash:
		SHTG L 3
		SHTG I 2 BRIGHT A_Light2
		SHTG J 2 BRIGHT A_Light1
		SHTG K 1 BRIGHT A_Light0
		Stop
	}
}

ACTOR SawedoffLoaded : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.MaxAmount 2
	Inventory.Icon SHOTA0
}

ACTOR Thompson : Chaingun replaces Chaingun
{
	Tag "Thompson"
	Scale .6
	Weapon.SelectionOrder 70
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 20
	Weapon.AmmoType2 "Clip"
	Inventory.PickupMessage "$IP_THOM"
	Obituary "$OB_THOM"
	AttackSound "DSPISTOL"
	States
	{
	Ready:
		CHGG B 1 A_WeaponReady
		Loop
	Deselect:
		CHGG B 1 A_Lower
		Loop
	Select:
		CHGG B 1 A_Raise
		Loop
	Fire:
		CHGG A 0 A_GunFlash
		CHGG AB 4 A_FireBullets(5.6,0,1,9,"NewPuff")
		CHGG B 0 A_ReFire
		Goto Ready
	AltFire:
		CHGG A 0 A_GunFlash
		CHGG ABABABAB 2 A_FireBullets(5.6,0,1,9,"NewPuff")
		CHGG A 30
		CHGG B 0 A_ReFire
		Goto Ready
	Flash:
		CHGF A 4 BRIGHT A_Light1
		CHGF B 4 BRIGHT A_Light2
		Goto LightDone
	AltFlash:
		CHGF A 2 BRIGHT A_Light1
		CHGF B 2 BRIGHT A_Light2
		CHGF A 2 BRIGHT A_Light1
		CHGF B 2 BRIGHT A_Light2
		CHGF A 2 BRIGHT A_Light1
		CHGF B 2 BRIGHT A_Light2
		CHGF A 2 BRIGHT A_Light1
		CHGF B 2 BRIGHT A_Light2
		Goto LightDone
	}
}

ACTOR NapalmLauncher : RocketLauncher replaces RocketLauncher
{
	Tag "Napalm Launcher"
	Scale .6
	Weapon.SelectionOrder 250
	Weapon.AmmoType "NapalmAmmo"
	Inventory.PickupMessage "$IP_NAPA"
	States
	{
	Ready:
		RLXB A 1 A_WeaponReady
		Loop
	Deselect:
		RLXB A 1 A_Lower
		Loop
	Select:
		RLXB A 1 A_Raise
		Loop
	Fire:
		RLXB A 6 A_GunFlash
		RLXB B 6 BRIGHT A_FireCustomMissile("RocketBlood")
		RLXB CD 6 BRIGHT
		RLXB A 24 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0
		Stop
	}
}

ACTOR RocketBlood : Rocket
{
	-DEHEXPLOSION
	-ROCKETTRAIL
	Damage 80
	Scale 0.6
	RenderStyle Add
	Alpha 0.7
	Obituary "$OB_NAPR"
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		XBPR ABC 3 BRIGHT LIGHT("Napalm")
		Loop
	Death:
		XBPR D 3 BRIGHT LIGHT("NapalmX_1") A_Explode
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("NewSparks2",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(1,360))
		XBPR E 3 BRIGHT LIGHT("NapalmX_2")
		XBPR F 3 BRIGHT LIGHT("NapalmX_3")
		XBPR G 3 BRIGHT LIGHT("NapalmX_4")
		XBPR H 3 BRIGHT LIGHT("NapalmX_5")
		XBPR I 3 BRIGHT LIGHT("NapalmX_6")
		XBPR J 3 BRIGHT LIGHT("NapalmX_7")
		XBPR K 3 BRIGHT LIGHT("NapalmX_8")
		XBPR L 3 BRIGHT LIGHT("NapalmX_9")
		XBPR M 3 BRIGHT LIGHT("NapalmX_10")
		XBPR N 3 BRIGHT LIGHT("NapalmX_11")
		Stop
	}
}

ACTOR AClip : Clip replaces Clip
{
	Scale 0.8
	Inventory.PickupMessage "$IP_PIS2"
}

ACTOR AClipBox : ClipBox replaces ClipBox
{
	Inventory.PickupMessage "$IP_THO2"
	Inventory.Amount 25
}

ACTOR Shell2 : Shell
{
	Inventory.PickupMessage "IP_SHEL"
	States
	{
	Spawn:
		SHEL B -1
		Stop
	}
}

ACTOR NapalmAmmo : Ammo replaces RocketAmmo
{
	Tag "Napalm"
	Height 26
	Inventory.PickupMessage "$IP_NAP2"
	Inventory.Icon "ROCKA0"
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ROCK A -1
		Stop
	}
}