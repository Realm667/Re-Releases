ACTOR FireSpawner : SwitchableDecoration 32700
{
	//$Category Special Effects
	//$Title Fire Spawner
	//$Color 12
	//$Arg0 "Size"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Small"; 1 = "Medium"; 2 = "Large"; }
	//$Arg1 "Sound"
	//$Arg1Type 11
	//$Arg1Enum { 0 = "Yes"; 1 = "No"; }
	Height 40
	Radius 30
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
	Active:
		TNT1 A 4
	Initialize:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireStart",7)
		TNT1 A 0 A_JumpIf(Args[0] == 1,"Medium")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"Large")
		TNT1 A 0 A_JumpIf(Args[1] > 0,"SmallLoop")
		TNT1 A 0 A_PlaySound("SFX/FireLoop1",6,1,1)
	SmallLoop:
		TNT1 A 0 A_SpawnItemEx("SmallFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame1B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame2A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame2B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3B")
		Loop
	Medium:
		TNT1 A 0 A_JumpIf(Args[1] > 0,"MediumLoop")
		TNT1 A 0 A_PlaySound("SFX/FireLoop2",6,1,1)
	MediumLoop:
		TNT1 A 0 A_SpawnItemEx("MediumFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame4A")
		TNT1 A 0 A_SpawnItemEx("Ember2")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame4B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame5A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame5B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame6A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame6B")
		Loop
	Large:
		TNT1 A 0 A_JumpIf(Args[1] > 0,"LargeLoop")
		TNT1 A 0 A_PlaySound("SFX/FireLoop3",6,1,1)
	LargeLoop:
		TNT1 A 0 A_SpawnItemEx("LargeFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame7A")
		TNT1 A 0 A_SpawnItemEx("Ember2")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame7B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame8A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame8B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame9A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame9B")
		Loop
	Inactive:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireDie",7)
		TNT1 A -1 A_StopSound(6)
		Stop
	}
}

//Dynamic Light actors
ACTOR SmallFlame
{
	Alpha 0.5
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 1 LIGHT("FireSpawnerSmall")
		Stop
	}
}
ACTOR MediumFlame : SmallFlame
{
	States
	{
	Spawn:
		TNT1 A 1 LIGHT("FireSpawnerMedium")
		Stop
	}
}
ACTOR LargeFlame : SmallFlame
{
	States
	{
	Spawn:
		TNT1 A 1 LIGHT("FireSpawnerLarge")
		Stop
	}
}

//Flames and particles
ACTOR Flame1A
{
	DistanceCheck "tcotdlod"
	Height 2
	Radius 1
	Scale 0.35
	RenderStyle Add
	Alpha 0.5
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,random(2,6),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" ABCDE 2 BRIGHT
		"####" FFGGHHIIJJKKLL 1 BRIGHT A_FadeOut(.05)
		stop
	}
}

ACTOR Flame1B : Flame1A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame2A : Flame1A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame2B : Flame1A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame3A : Flame1A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame3B : Flame1A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame4A : Flame1A
{
	Scale 0.7
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,Random(4,12),0,0)
		"####" A 0 ThrustThing(Random(0,255),Random(0,2),0,0)
		"####" ABCDE 2 BRIGHT
		"####" FFGGHHIIJJKKLL 1 BRIGHT A_FadeOut(.05)
		Stop
	}
}

ACTOR Flame4B : Flame4A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame5A : Flame4A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame5B : Flame4A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame6A : Flame4A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame6B : Flame4A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame7A : Flame1A
{
	Scale 1.4
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,Random(8,24),0,0)
		"####" A 0 ThrustThing(Random(0,255),Random(0,4),0,0)
		"####" ABCDE 2 BRIGHT
		"####" FFGGHHIIJJKKLL 1 BRIGHT A_FadeOut(.05)
		Stop
	}
}

ACTOR Flame7B : Flame7A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame8A : Flame7A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame8B : Flame7A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame9A : Flame7A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Flame9B : Flame7A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

ACTOR Ember1 : Flame1A
{
	Scale .1
	States
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0,random(4,16),0,0)
		EMBR A 0 ThrustThing(random(0,255),frandom(0.1,1.0),0,0)
		EMBR A 32 BRIGHT
	Death:
		EMBR A 2 BRIGHT A_FadeOut
		loop
	}
}

ACTOR Ember2 : Ember1
{
	States
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0,random(4,16),0,0)
		EMBR A 0 ThrustThing(random(0,255),frandom(0.1,1.0),0,0)
		EMBR A 64 BRIGHT
		Goto Death
	}
}

//==========================================================================================================================
//Visual enhancements for decorations:
//==========================================================================================================================
ACTOR FireParticle_Normal_A1C
{
	DistanceCheck "tcotdlod"
	Height 2
	Radius 1
	Scale 0.50
	Alpha 0.95
	RenderStyle Add
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		AFNO A 0
		"####" AABBCCDDEEFFGGHHIIJJ 1 BRIGHT A_FadeOut(0.05)
		Stop
	}
}
ACTOR FireParticle_Normal_A1D : FireParticle_Normal_A1C { Scale 0.25 }
ACTOR FireParticle_Normal_A1E : FireParticle_Normal_A1C { Scale 0.15 }

ACTOR FireParticle_Normal_B1C : FireParticle_Normal_A1C
{
	DistanceCheck "tcotdlod"
	States
	{
	Spawn:
		AFNO A 0 NODELAY A_SetScale(-scalex,scaley)
		Goto Super::Spawn+1
	}
}
ACTOR FireParticle_Normal_B1D : FireParticle_Normal_B1C { Scale 0.25 }
ACTOR FireParticle_Normal_B1E : FireParticle_Normal_B1C { Scale 0.15 }

ACTOR FireParticle_Normal_C1C : FireParticle_Normal_A1C
{
	DistanceCheck "tcotdlod"
	States
	{
	Spawn:
		AFN3 A 0
		Goto Super::Spawn+1
	}
}
ACTOR FireParticle_Normal_C1D : FireParticle_Normal_C1C { Scale 0.25 }
ACTOR FireParticle_Normal_C1E : FireParticle_Normal_C1C { Scale 0.15 }

ACTOR FireParticle_Normal_D1C : FireParticle_Normal_A1C
{
	DistanceCheck "tcotdlod"
	States
	{
	Spawn:
		AFN3 A 0 NODELAY A_SetScale(-scalex,scaley)
		Goto Super::Spawn+1
	}
}
ACTOR FireParticle_Normal_D1D : FireParticle_Normal_D1C { Scale 0.25 }
ACTOR FireParticle_Normal_D1E : FireParticle_Normal_D1C { Scale 0.15 }

//=========================================================================================================================
ACTOR Ember_Normal_A1A : FireParticle_Normal_A1C
{
	Scale 0.05
	States
	{
	Spawn:
		FPRY A 1 BRIGHT A_FadeOut(0.005)
		Loop
	}
}
ACTOR Ember_Normal_A1B : Ember_Normal_A1A { Scale 0.0350 }
ACTOR Ember_Normal_A1C : Ember_Normal_A1A { Scale 0.0225 }

//=========================================================================================================================
ACTOR Flare_Red_A1A
{
	DistanceCheck "tcotdlod"
	Height 2
	Radius 1
	Scale 1.25
	RenderStyle Add
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		FLRE A -1 BRIGHT
		Loop
	}
}

ACTOR Flare_Red_A1B : Flare_Red_A1A { Scale 0.75 }
ACTOR Flare_Red_A1C : Flare_Red_A1A
{
	Scale 0.275
	Alpha 0.5
}

//=========================================================================================================================
ACTOR TCOTDTallTorch : ShortRedTorch replaces ShortRedTorch
{
	States
	{
	Spawn:
		SMRT A 0 NODELAY A_SpawnItemEx("Flare_Red_A1A",0,0,62)
		SMRT A 0 A_CheckSight(6)
		SMRT A 1 A_Jump(8,6)
		SMRT A 0 A_SpawnItemEx("FireParticle_Normal_A1D",0,0,Random(64,66),0,0,fRandom(1.0,2.5))
		SMRT A 0 A_SpawnItemEx("FireParticle_Normal_B1D",0,0,Random(64,66),0,0,fRandom(1.0,2.5))
		SMRT A 0 A_SpawnItemEx("FireParticle_Normal_C1D",0,0,Random(64,66),0,0,fRandom(1.0,2.5))
		SMRT A 0 A_SpawnItemEx("FireParticle_Normal_D1D",0,0,Random(64,66),0,0,fRandom(1.0,2.5))
		SMRT A 1
		Goto Spawn+1
		SMRT A 1 A_SpawnItemEx("Ember_Normal_A1B",Random(-4,4),Random(-4,4),Random(64,66),Random(-2,2),Random(-2,2),Random(2,4))
		Goto Spawn+1
	}
}

ACTOR TCOTDMinotaurTorch : RedTorch replaces RedTorch
{
	States
	{
	Spawn:
		TRED A 0 NODELAY A_SpawnItemEx("Flare_Red_A1B",0,0,40)
		TRED A 0 A_CheckSight(6)
		TRED A 1 A_Jump(8,6)
		TRED A 0 A_SpawnItemEx("FireParticle_Normal_A1C",0,0,Random(38,40),0,0,fRandom(1.0,4.5))
		TRED A 0 A_SpawnItemEx("FireParticle_Normal_B1C",0,0,Random(38,40),0,0,fRandom(1.0,4.5))
		TRED A 0 A_SpawnItemEx("FireParticle_Normal_C1C",0,0,Random(38,40),0,0,fRandom(1.0,4.5))
		TRED A 0 A_SpawnItemEx("FireParticle_Normal_D1C",0,0,Random(38,40),0,0,fRandom(1.0,4.5))
		TRED A 1
		Goto Spawn+1
		TRED A 1 A_SpawnItemEx("Ember_Normal_A1A",Random(-4,4),Random(-4,4),Random(34,34),Random(-2,2),Random(-2,2),Random(2,4))
		Goto Spawn+1
	}
}

ACTOR TCOTDStreetLamp : SwitchableDecoration replaces ShortBlueTorch
{
	Height 2
	States
	{
	Spawn:
	Active:
		TNT1 A 1 A_Jump(8,6)
		TNT1 A 0 A_SpawnItemEx("FireParticle_Normal_A1D",0,0,Random(24,26),0,0,fRandom(1.0,2.5))
		TNT1 A 0 A_SpawnItemEx("FireParticle_Normal_B1D",0,0,Random(24,26),0,0,fRandom(1.0,2.5))
		TNT1 A 0 A_SpawnItemEx("FireParticle_Normal_C1D",0,0,Random(24,26),0,0,fRandom(1.0,2.5))
		TNT1 A 0 A_SpawnItemEx("FireParticle_Normal_D1D",0,0,Random(24,26),0,0,fRandom(1.0,2.5))
		TNT1 A 1
		Goto Spawn+1
		TNT1 A 1 A_SpawnItemEx("Ember_Normal_A1B",Random(-4,4),Random(-4,4),Random(64,66),Random(-2,2),Random(-2,2),Random(2,4))
		Goto Spawn+1
	InActive:
		TNT1 A -1
		Loop
	}
}

ACTOR CreepyCandle : SwitchableDecoration 21700
{
	//$Category New Lights (TCOTD2)
	//$Title Creepy Candle (ON)
	//$Color 11
	Radius 4
	Height 8
	Scale 0.8
	-SOLID
	+NOTAUTOAIMED
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Spawn:
		CDLE B 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" BCDA 4 LIGHT(CREPLIT2)
		Loop
	Off:
		"####" E -1
		Stop
	Active:
	Inactive:
		"####" E 0 {
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR CreepyCandleS : CreepyCandle 21701
{
	//$Title Creepy Candle, Stackable (ON)
	+NOGRAVITY
}

ACTOR CandelabraBloodOne : CreepyCandle 22035
{
	//$Title Creepy Candlestick (ON)
	Radius 16
	Height 60
	Scale 1.0
	+SOLID
	States
	{
	Spawn:
		XB02 B 0
		Goto CheckDormant
	On:
		"####" BCDA 4 LIGHT(CREPLIT1)
		Loop
	}
}