//===========================================================================
//
// NamiDarkImp
//
// created by: Nanami
//
//===========================================================================

actor NamiDarkImp 3100
{
	//$Category Monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkSeeker
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

actor DarkSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

//===========================================================================
//
// VoidDarkImp
//
// created by: Cyb
// sprites by: Nanami, modified by Vader
//
//===========================================================================

actor VoidDarkImp 3101
{
	//$Category Monsters
    Health 130
    Radius 20
    Height 56
    Speed 7
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkNormal
    MeleeDamage 3
    States
    {
    Spawn:
        DRKV AB 10 A_Look
        Loop
    See:
        DRKV AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_ComboAttack
        Goto See
    Missile:
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto See
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKV H 2
        DRKV H 2 A_Pain
        Goto See
    Death:
        DRKV L 8
        DRKV M 8 A_Scream
        DRKV N 5
        DRKV O 5 A_Fall
        DRKV P -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKV PO 8
        DRKV NML 6
        Goto See
    }
}

actor DarkNormal
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

actor DarkStrong
{
    Radius 8
    Height 6
    Speed 9
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    MissileType DarkTail
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB2 A 4 Bright A_MissileAttack
        DKB2 B 4 Bright A_MissileAttack
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

actor DarkTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.5
    States
    {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}

//===========================================================================
//
// STDarkImp
//
// from Skulltag
// sprites by: Nanami, modified by Vader
//
//===========================================================================

actor STDarkImp 3102
{
	//$Category Monsters
    Health 110
    Radius 20
    Height 56
    Speed 9
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkFast
    MeleeDamage 3
    States
    {
    Spawn:
        DRKS AB 10 A_Look
        Loop
    See:
        DRKS AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKS EF 8 A_FaceTarget
        DRKS G 6 A_ComboAttack
        Goto See
    Pain:
        DRKS H 2
        DRKS H 2 A_Pain
        Goto See
    Death:
        DRKS I 8
        DRKS J 8 A_Scream
        DRKS K 6
        DRKS L 6 A_Fall
        DRKS M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKS ML 8
        DRKS KJI 6
        Goto See
    }
}

actor DarkFast
{
    Radius 8
    Height 6
    Speed 18
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }   
}

