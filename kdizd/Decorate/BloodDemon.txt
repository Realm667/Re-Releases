//===========================================================================
//
// Blood Demon
//
// from Skulltag
// New sprites by: Neoworm
//
//===========================================================================

ACTOR BloodDemonClone 3110
{ 
	//$Category Monsters
    Health 300 
    Speed 12 
    Height 56
    Radius 30
    Mass 400
    PainChance 180
    Meleedamage 5 
    REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
    SeeSound "blooddemon/sight"
    PainSound "blooddemon/pain"
    DeathSound "blooddemon/death"
    ActiveSound "blooddemon/active"
    MeleeSound "blooddemon/melee"
    HitObituary "%o Was chewed up and spat out by the Blood Demon." 
    States 
    { 
    Spawn: 
       SRG2 AB 10 A_Look
       Loop 
    See: 
       SRG2 A 0 A_PlaySound(DSBLDWAL)
       SRG2 AABB 2 A_Chase
       SRG2 C 0 A_PlaySound(DSBLDWAL)
       SRG2 CCDD 2 A_Chase
       Loop 
    Melee: 
       SRG2 EF 8 A_FaceTarget
       SRG2 G 8 A_MeleeAttack
       Goto See 
    Pain: 
       SRG2 H 2
       SRG2 H 2 A_Pain
       Goto See
    Death: 
       SRG2 I 8 
       SRG2 J 0 A_CustomMissile("BloodDemonArm",32,-10,270)
       SRG2 J 8 A_Scream
       SRG2 K 4 
       SRG2 L 4 A_NoBlocking
       SRG2 M 4 
       SRG2 N -1 
       Stop 
    Raise: 
       SRG2 NMLKJI 5 
       Goto See 
    } 
} 

ACTOR BloodDemonArm
{
	Radius 8
	Height 8
	Speed 8
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
         SG2A ABCDEFGH 2
         Loop
	Death:
	 SG2A I -1
	 Loop
	}
} 

