//===========================================================================
//
// Demoncube
// by TheDarkArchon
//
// idea by: Eriance
// sprites by: NMN
//
//===========================================================================

ACTOR KDiZDDemonCube
{
	//$Category KDiZD Monsters
    Health 50
    Radius 20
    Height 20
    Speed 10
    PainChance 96
    Mass 1000
    Scale 0.5
    MONSTER
    +DONTFALL
    +FLOAT
    +NOGRAVITY
    SeeSound "demoncube/sight"
    DeathSound "demoncube/death"
    PainSound "demon/pain"
    ActiveSound "demon/active"
    AttackSound "demoncube/attack"
    Obituary "$OB_KUBE"
    MissileType DemonCubeMissile
    States
    {
    Spawn:
        DECU ABC 3 A_Look
        Loop
    See:
        DECU ABC 4 A_Chase
        Loop
    Missile:
        DECU D 8 A_FaceTarget
        DECU E 8 Bright A_MissileAttack
        DECU D 8 A_FaceTarget
        Goto See
    Pain:
        DECU D 3
        DECU D 3 A_Pain
        Goto See
    Death:
        DECU F 8 Bright A_Scream
        DECU G 8 Bright
        DECU H 8 Bright A_NoBlocking
        DECU I 8 Bright
        Stop
    }
}

ACTOR DemonCubeMissile
{
	Radius 8
	Height 8
	Speed 18
	Damage 4
	PROJECTILE
	RenderStyle ADD
	ALPHA 0.75
	SeeSound "demoncube/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		DCPF AB 2 Bright
		Loop
	Death:
		DCPF ABCDE 3 Bright
		Stop
	}
}