//===========================================================================
//
// Mauler Demon
//   - idea from Nimrod
//   - sprites by Eriance/Amuscaria
//
//===========================================================================

ACTOR MaulerDemon
{
	//$Category KDiZD Monsters
	Health 150
	PainChance 180
	GibHealth 35
	Speed 10
	FastSpeed 16
	Radius 30
	Height 56
	Mass 400
	Damage 2 //Horns = pain. I say no more
	Monster
	+FLOORCLIP
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_MAUL"
	HitObituary "$HB_MAUL"
	States
	{
	Spawn:
		MAUD AB 10 A_Look
		Loop
	See:
		MAUD AABBCCDD 2 A_Chase
		Loop
	Melee:
		MAUD EF 8 A_FaceTarget
		MAUD G 8 A_SargAttack
		Goto See
	Missile:
		MAUD E 0 A_JumpIfCloser(224, 2)
		Goto See
		MAUD E 4 A_FaceTarget
		MAUD F 10 A_SkullAttack
		MAUD G 5 A_Gravity
		Goto See
	Pain:
		MAUD H 2
		MAUD H 2 A_Pain
		Goto See
	Death:
		MAUD I 8
		MAUD J 8 A_Scream
		MAUD K 4
		MAUD L 4 A_Fall
		MAUD M 4
		MAUD N -1
		Stop
	XDeath: //new
		MAUD I 0 A_SpawnDebris("NashGore_Gib11",0)
		MAUD I 0 A_SpawnDebris("NashGore_Gib11",0)
		MAUD I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		MAUD I 8
		MAUD J 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		MAUD J 0 A_SpawnItemEx("MaulerArm",10,0,32,0,random(2,8),0,0,random(0,180))
		MAUD J 8 A_Scream
		MAUD K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		MAUD K 4
		MAUD L 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		MAUD L 4 A_Fall
		MAUD M 4
		MAUD N -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		MAUD N 5
		MAUD MLKJI 5
		Goto See
	}
}

ACTOR MaulerArm : Gore_DemonArm //new
{
	States
	{
	Spawn:
		TNT1 AA 0 A_JumpIf(waterlevel > 0, "Underwater")
		MAUA ABCDEFGH 2 A_SpawnDebris("NashGore_FlyingBlood",1)
		Loop
	Underwater:
		MAUA ABCDEFGH 2 A_SpawnDebris("NashGore_FlyingBlood", 1)
		MAUA I -1
		Stop
	Death:
		MAUA I -1
		Stop
	Death.Sky: //prevent it to appear on sky - ozy81
		MAUA A 1
		Stop
	Crash:
		MAUA I -1
		Stop
	}
}