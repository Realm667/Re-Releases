ACTOR DarkBase //base actor for projectiles  - Ozy81
{
	Radius 8
	Height 6
	Damage 3
	Speed 9
	FastSpeed 16
	Projectile
	RenderStyle ADD
	Alpha 0.67
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal DoomImpScorch
}

//===========================================================================
//
// NamiDarkImp
//
// created by: Nanami
//
//===========================================================================

ACTOR NamiDarkImp
{
	//$Category KDiZD Monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
	FastSpeed 16
    PainChance 200
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_NIMP"
    HitObituary "$HB_NIMP"
    MissileType DarkSeeker
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

ACTOR DarkSeeker : DarkBase
{
    +SEEKERMISSILE
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

//===========================================================================
//
// VoidDarkImp
//
// created by: Cyb
// sprites by: Nanami, modified by Vader
//
//===========================================================================

ACTOR VoidDarkImp
{
	//$Category KDiZD Monsters
    Health 130
    Radius 20
    Height 56
    Speed 7
	FastSpeed 14
    PainChance 200
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_VIMP"
    HitObituary "$HB_VIMP"
    MissileType DarkNormal
    MeleeDamage 3
    States
    {
    Spawn:
        DRKV AB 10 A_Look
        Loop
    See:
        DRKV AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_ComboAttack
        Goto See
    Missile:
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        DRKV E 0 A_Jump(64,4)
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto See
        DRKV IJ 8 Bright A_FaceTarget
        DRKV K 6 Bright A_CustomMissile("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKV H 2
        DRKV H 2 A_Pain
        Goto See
    Death:
        DRKV L 8
        DRKV M 8 A_Scream
        DRKV N 5
        DRKV O 5 A_Fall
        DRKV P -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKV PO 8
        DRKV NML 6
        Goto See
    }
}

ACTOR DarkNormal : DarkBase
{
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

ACTOR DarkStrong : DarkBase
{
    Damage 6
    MissileHeight 0
    MissileType DarkTail
    States
    {
    Spawn:
        DKB2 A 4 Bright A_MissileAttack
        DKB2 B 4 Bright A_MissileAttack
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

ACTOR DarkTail
{
    PROJECTILE
    +NOCLIP
    RenderStyle ADD
    ALPHA 0.5
    States
    {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}

//===========================================================================
//
// STDarkImp
//
// from Skulltag
// sprites by: Nanami, modified by Vader
//
//===========================================================================

ACTOR STDarkImp
{
	//$Category KDiZD Monsters
    Health 110
    Radius 20
    Height 56
    Speed 9
	FastSpeed 18
    PainChance 200
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_STDI"
    HitObituary "$HB_STDI"
    MissileType DarkFast
    MeleeDamage 3
    States
    {
    Spawn:
        DRKS AB 10 A_Look
        Loop
    See:
        DRKS AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKS EF 8 A_FaceTarget
        DRKS G 6 A_ComboAttack
        Goto See
    Pain:
        DRKS H 2
        DRKS H 2 A_Pain
        Goto See
    Death:
        DRKS I 8
        DRKS J 8 A_Scream
        DRKS K 6
        DRKS L 6 A_Fall
        DRKS M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKS ML 8
        DRKS KJI 6
        Goto See
    }
}

ACTOR DarkFast : DarkBase
{
    Speed 18
	FastSpeed 24
    MissileHeight 0
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}