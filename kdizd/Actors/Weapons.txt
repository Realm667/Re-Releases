//New weapon stuff by TheDarkArchon

ACTOR NewPistol : Pistol replaces Pistol
{
	AttackSound "weapons/pistol"
	Decal BulletChip
	States
	{
	Fire:
		PISG A 2
		PISG A 1 A_FireCustomMissile("PisCaseSpawn",0,0,0,1)
		PISG B 0 A_GunFlash
		PISG B 6 A_FireBullets(4,4,1,5,"NewPuff",1)
		PISG C 4
		PISG B 5 A_Refire
		Goto Ready
	Hold:
		PISG A 2
		PISG A 1 A_FireCustomMissile("PisCaseSpawn",0,0,0,1)
		PISG B 0 A_GunFlash
		PISG B 6 A_FireBullets(4,4,1,5,"NewPuff",1)
		PISG C 4
		PISG B 5 A_Refire
		Goto Ready
	}
}

ACTOR PisCaseSpawn
{
	Speed 20
	PROJECTILE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("PistolCasing",0,0,random(85,95),2,random(-10,10))
		Stop
	}
}

ACTOR PistolCasing
{
	Height 8
	Radius 6
	Speed 4
	Scale 0.45
	BounceFactor 0.4
	Mass 1
	ReactionTime 70
	PROJECTILE
	+DOOMBOUNCE
	-NOGRAVITY
	-NOBLOCKMAP
	Seesound "weapons/case1"
	States
	{
	Spawn:
	   CAS2 ABCD 2 A_Countdown
	   Goto Spawn
	Death:
	   CAS2 A 0 A_Jump(64,4)
	   CAS2 A 0 A_Jump(85,4)
	   CAS2 A 0 A_Jump(128,4)
	   CAS2 A 350
	   Stop
	   CAS2 B 350
	   Stop
	   CAS2 C 350
	   Stop
	   CAS2 D 350
	   Stop
	}
}

ACTOR NewShotgun : Shotgun replaces Shotgun
{
	AttackSound "weapons/shotgf"
	Decal BulletChip
	States
	{
	Fire:
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 7 A_FireBullets(5.6, 0, 7, 5, "NewPuff")
		SHTG BC 5
		SHTG D 1
		SHTG D 3 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_Refire
		Goto Ready
	Hold:
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 7 A_FireBullets(5,3,7,5,"NewPuff")
		SHTG BC 5
		SHTG D 1
		SHTG D 3 A_FireCustomMissile("ShotCaseSpawn",0,0,-4,-4)
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_Refire
		Goto Ready
	}
}

ACTOR NewSuperShotgun : SuperShotgun replaces SuperShotgun
{
	AttackSound "weapons/sshotf"
	Decal BulletChip
	States
	{
	Fire:
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		TNT1 A 0 A_SetBlend("Orange",0.045,4)
		SHT2 A 7 A_FireBullets(12,6,20,5,"NewPuff")
		TNT1 A 0 A_JumpIf(waterlevel > 0,3)
		SHT2 B 7 A_CheckReload
		SHT2 C 7
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), -2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), 2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
		SHT2 D 7 A_StartSound("weapons/sshoto")
		SHT2 E 7
		SHT2 F 7 A_StartSound("weapons/sshotl")
		SHT2 G 1 Offset(20,36)
		SHT2 G 1 Offset(19,40)
		SHT2 G 1 Offset(14,39)
		SHT2 G 1 Offset(10,38)
		SHT2 G 1 Offset(9,37)
		SHT2 G 1 Offset(4,36)
		SHT2 G 1 Offset(2,34)
		SHT2 G 1 Offset(0,32)
		SHT2 H 0 A_Refire
		SHT2 H 7 A_StartSound("weapons/sshotc")
		Goto Ready
	Hold:
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		TNT1 A 0 A_SetBlend("Orange",0.045,4)
		SHT2 A 7 A_FireBullets(12,6,20,5,"NewPuff")
		TNT1 A 0 A_JumpIf(waterlevel > 0,3)
		SHT2 B 7 A_CheckReload
		SHT2 C 7
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), -2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), 2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
		SHT2 D 7 A_StartSound("weapons/sshoto")
		SHT2 E 7
		SHT2 F 7 A_StartSound("weapons/sshotl")
		SHT2 A 1 Offset(20,36)
		SHT2 A 1 Offset(19,40)
		SHT2 A 1 Offset(14,39)
		SHT2 A 1 Offset(10,38)
		SHT2 A 1 Offset(9,37)
		SHT2 A 1 Offset(4,36)
		SHT2 A 1 Offset(2,34)
		SHT2 A 1 Offset(0,32)
		SHT2 H 0 A_Refire
		SHT2 H 7 A_StartSound("weapons/sshotc")
		Goto Ready
	}
}

ACTOR ShotCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("ShotgunCasing",0,0,random(-95,-85),2,random(-10,10))
		Stop
	}
}

ACTOR SShotCaseSpawn : ShotCaseSpawn //Placeholder
{
	Speed 66
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("ShotgunCasing",0,0,random(-95,-85),2,random(-10,10))
		Stop
	}
}

ACTOR ShotgunCasing : PistolCasing
{
	Height 12
	Radius 9
	Scale 0.7
	SeeSound "weapons/shell"
	ReactionTime 140
	States
	{
	Spawn:
	   D3S1 ACBHEGFD 1 A_Countdown
	   Goto Spawn
	Death:
	   D3S1 A 0 A_Jump(32,8)
	   D3S1 A 0 A_Jump(37,8)
	   D3S1 A 0 A_Jump(43,8)
	   D3S1 A 0 A_Jump(51,8)
	   D3S1 A 0 A_Jump(64,8)
	   D3S1 A 0 A_Jump(85,8)
	   D3S1 A 0 A_Jump(128,8)
	   D3S1 A 350
	   Stop
	   D3S1 B 350
	   Stop
	   D3S1 C 350
	   Stop
	   D3S1 D 350
	   Stop
	   D3S1 E 350
	   Stop
	   D3S1 F 350
	   Stop
	   D3S1 G 350
	   Stop
	   D3S1 H 350
	   Stop
	}
}

ACTOR NewChaingun : Chaingun replaces Chaingun
{
	AttackSound "weapons/chngun"
	Decal BulletChip
	States
	{
	Fire:
	   CHGG A 0 A_GunFlash
	   CHGG A 0 A_FireCustomMissile("PisCaseSpawn",0,0,-6,-8)
	   CHGG A 4 A_FireBullets(4,4,1,5,"NewPuff",1)
	   CHGG A 0 A_JumpIfNoAmmo(3)
	   CHGG B 0 A_FireCustomMissile("PisCaseSpawn",0,0,-6,-8)
	   CHGG B 4 A_FireBullets(4,4,1,5,"NewPuff",1)
	   CHGG B 0 A_Refire
	   Goto Ready
	Hold:
	   CHGG A 0 A_GunFlash
	   CHGG A 0 A_FireCustomMissile("PisCaseSpawn",0,0,-6,-8)
	   CHGG A 4 A_FireBullets(4,4,1,5,"NewPuff",1)
	   CHGG A 0 A_JumpIfNoAmmo(3)
	   CHGG B 0 A_FireCustomMissile("PisCaseSpawn",0,0,-6,-8)
	   CHGG B 4 A_FireBullets(4,4,1,5,"NewPuff",1)
	   CHGG B 0 A_Refire
	   Goto Ready
	Flash:
	   CHGF A 5 BRIGHT A_Light1
	   CHGF A 0 BRIGHT A_JumpIfNoAmmo(2)
	   CHGF B 5 BRIGHT A_Light2
	   TNT1 B 0 BRIGHT A_Light0
	   Stop
	}
}

ACTOR NewPuff : BulletPuff
{
	Alpha 1.0
	Mass 1
	States
	{
	Spawn:
	Melee:
	    PUFF A 2
	    PUFF A 0 A_Jump(128,2)
	    PUFF A 2 A_SpawnItem("Puff2")
	    PUFF BCD 4
	    Stop
	}
}

ACTOR Puff2
{
	PROJECTILE
	+NOCLIP
	Mass 1
	States
	{
	Spawn:
		PUFF E 2
		PUFF E 2 A_StartSound("puff/ric")
		PUFF FGH 4
		Stop
	}
}

ACTOR BlankPuff
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

ACTOR Rifle : Weapon replaces BFG9000
{
	Weapon.AmmoType "RifleAmmo"
	Weapon.AmmoGive 10
	Weapon.AmmoUse 1
	Inventory.PickupMessage "You got the Rifle"
	Inventory.PickupSound "misc/w_pkup"
    Weapon.SelectionOrder 50
    Weapon.Kickback 150
    AttackSound "weapons/rifle"
    States
    {
    Spawn:
    	RIFL A -1
    	Loop
    Select:
    	RIFG A 1 A_Raise
    	Loop
    Deselect:
    	RIFG A 1 A_Lower
    	Loop
    Ready:
    	RIFG A 1 A_WeaponReady
    	Loop
    Fire:
    	RIFG A 0 A_GunFlash
    	RIFG A 0 A_FireCustomMissile("RifCaseSpawn",0,0,2,-2)
    	RIFG A 0 A_FireBullets(0,0,9,10,"BlankPuff",0)
    	RIFG A 4 A_FireBullets(0,0,1,10,"NewPuff",1)
    	RIFG BCDCB 2
    	RIFG A 3
    	RIFG A 0 A_Refire
    	Goto Ready
    Flash:
    	RIFF A 2 BRIGHT A_Light1
    	RIFF B 2 BRIGHT A_Light2
    	TNT1 A 0 A_Light0
    	Stop
    }
}

ACTOR RifleAmmo : Ammo replaces Cell
{
	Inventory.Amount 5
	Inventory.MaxAmount 75
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 150
	Inventory.Icon RIFAB0
	Inventory.PickupMessage "Picked up some rifle cartridges"
	Inventory.PickupSound "misc/i_pkup"
	States
	{
	Spawn:
		RIFA A -1
		Loop
	}
}

ACTOR RifleBox : RifleAmmo replaces CellPack
{
	Inventory.Amount 25
	Inventory.PickupMessage "Picked up a box of rifle cartridges"
	States
	{
	Spawn:
		RIFP A -1
		Loop
	}
}

ACTOR RifCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("RifleCasing",0,0,random(-95,-85),2,random(-10,10))
		Stop
	}
}

ACTOR RifleCasing : PistolCasing
{
	BounceFactor 0.5
	Seesound "weapons/case2"
	ReactionTime 140
	States
	{
	Spawn:
	   CAS7 A 1
	   CAS7 A 1 A_Countdown
	   CAS7 B 1
	   CAS7 B 1 A_Countdown
	   CAS7 C 1
	   CAS7 C 1 A_Countdown
	   CAS7 D 1
	   CAS7 D 1 A_Countdown
	   Goto Spawn+1
	Death:
	   CAS7 A 0 A_Jump(43,6)
	   CAS7 A 0 A_Jump(51,6)
	   CAS7 A 0 A_Jump(64,6)
	   CAS7 A 0 A_Jump(85,6)
	   CAS7 A 0 A_Jump(128,6)
	   CAS7 A 350
	   Stop
	   CAS7 B 350
	   Stop
	   CAS7 C 350
	   Stop
	   CAS7 D 350
	   Stop
	   CAS7 E 350
	   Stop
	   CAS7 F 350
	   Stop
	}
}

ACTOR KDiZDGrenadeLauncher : Weapon replaces PlasmaRifle
{
	Tag "Grenade Launcher"
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 2
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the Grenade Launcher!"
	Inventory.PickupSound "misc/w_pkup"
	Weapon.SelectionOrder 2600
	Weapon.Kickback 600
	States
	{
	Spawn:
		GLAU A -1
		Loop
	Select:
		GLAG A 1 A_Raise
		Loop
	Deselect:
		GLAG A 1 A_Lower
		Loop
	Ready:
		GLAG A 1 A_WeaponReady
		Loop
	Fire:
		GLAG B 0 BRIGHT A_GunFlash
		GLAG B 0 BRIGHT A_StartSound("weapons/grenadefire")
		GLAG B 4 BRIGHT A_FireCustomMissile("KDiZDGrenade",0,1,0,0)
		GLAG I 4
		GLAG A 0 A_CheckReload //I can't believe I forgot this.
		GLAG C 7
		GLAG D 5
		GLAG E 5 A_StartSound("weapons/sshoto")
		GLAG F 5
		GLAG G 5 A_FireCustomMissile("GrnCaseSpawn",0,0,-4,-4)
		GLAG H 5 A_StartSound("weapons/sshotl")
		GLAG FE 5
		GLAG D 5 A_StartSound("weapons/sshotc")
		GLAG A 3
		GLAG A 0 A_Refire
		Goto Ready
	AltFire:
		GLAG B 0 BRIGHT A_GunFlash
		GLAG B 0 BRIGHT A_StartSound("weapons/grenadefire")
		GLAG B 4 BRIGHT A_FireCustomMissile("KDiZDGrenade2",0,1,0,0)
		GLAG I 4
		GLAG A 0 A_CheckReload //I can't believe I forgot this.
		GLAG C 7
		GLAG D 5
		GLAG E 5 A_StartSound("weapons/sshoto")
		GLAG F 5
		GLAG G 5 A_FireCustomMissile("GrnCaseSpawn",0,0,-4,-4)
		GLAG H 5 A_StartSound("weapons/sshotl")
		GLAG FE 5
		GLAG D 5 A_StartSound("weapons/sshotc")
		GLAG A 3
		GLAG A 0 A_Refire
		Goto Ready
     Flash:
    	TNT1 A 2 BRIGHT A_Light1
    	TNT1 A 2 BRIGHT A_Light2
    	TNT1 A 0 A_Light0
    	Stop
	}
}


ACTOR GrnCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 AA 1 A_CustomMissile("GrenadeCasing",0,0,random(95,85),2,random(-10,10))
		Stop
	}
}

ACTOR GrenadeCasing : PistolCasing
{
	BounceFactor 0.6
	Scale 0.55
	Seesound "weapons/shell"
	ReactionTime 140
	States
	{
	Spawn:
	   CAS6 A 1
	   CAS6 A 1 A_Countdown
	   CAS6 B 1
	   CAS6 B 1 A_Countdown
	   CAS6 C 1
	   CAS6 C 1 A_Countdown
	   CAS6 D 1
	   CAS6 D 1 A_Countdown
	   Goto Spawn+1
	Death:
	   CAS6 A 0 A_Jump(43,6)
	   CAS6 A 0 A_Jump(51,6)
	   CAS6 A 0 A_Jump(64,6)
	   CAS6 A 0 A_Jump(85,6)
	   CAS6 A 0 A_Jump(128,6)
	   CAS6 A 350
	   Stop
	   CAS6 B 350
	   Stop
	   CAS6 C 350
	   Stop
	   CAS6 D 350
	   Stop
	   CAS6 E 350
	   Stop
	   CAS6 F 350
	   Stop
	}
}

ACTOR KDiZDGrenade
{
	Radius 11
	Height 8
	Speed 20
	Damage (15*random(1,16))
	PROJECTILE
	+DOOMBOUNCE
	+EXPLODEONWATER
	-NOGRAVITY
	BounceFactor 0.8
	BounceCount 6
	Seesound "weapons/grenadebounce"
	Deathsound "weapons/rocklx"
	States
	{
	Spawn:
		GRND A 1
		GRND A 0 ThrustThingZ(0, 8, 0, 0)
		GRND A 1 A_CustomMissile("GrenadePuff", 3, 0, 0, 1)
		Wait
	Death:
		MISL B 0 BRIGHT A_NoGravity
		MISL B 0 BRIGHT A_SetTranslucent(0.5,1)
		MISL B 8 BRIGHT A_Explode(256,160,1)
		MISL C 6 BRIGHT
		MISL D 4 BRIGHT
		Stop
	}
}

ACTOR KDiZDGrenade2 : KDiZDGrenade
{
	Speed 35
	+HEXENBOUNCE
	-DOOMBOUNCE
	ReactionTime 139
	States
	{
	Spawn:
		GRND A 0 A_CountDown
		GRND A 1 A_CustomMissile("GrenadePuff", 3, 0, 0, 1)
		Loop
	}
}

ACTOR GrenadePuff : BulletPuff
{
	States
	{
	Spawn:
	   PUFF ABCD 4
	   Stop
	}
}