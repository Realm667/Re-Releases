//===========================================================================
//
// Hell's Fury
// Rotating Skull sprites by Vader
// Custom XDeath by Ozymandias81
//
//===========================================================================
ACTOR HellsFury
{
	//$Category KDiZD Monsters
	Health 1000
	Radius 24
	Height 64
	Speed 8
	FastSpeed 16
	PainChance 50
	GibHealth 35
	Mass 1000
	MONSTER
	+FLOORCLIP
	SeeSound "baron/sight"
	ActiveSound "baron/active"
	PainSound "baron/pain"
	DeathSound "baron/death"
	MeleeSound "baron/melee"
	Obituary "$OB_FURY"
	HitObituary "$HB_FURY"
	MissileType HellsFuryBall
	MeleeDamage 11
	States
	{
	Spawn:
		HFRY AB 10 A_Look
		Loop
	See:
		HFRY AABBCCDD 4 A_Chase
		Loop
	Missile:
		HFRY E 0 A_Jump(96,4)
		HFRY EF 8 A_FaceTarget
		HFRY G 8 A_ComboAttack
		Goto See
		HFRY P 8 A_FaceTarget
		HFRY Q 0 A_SpawnProjectile("SoulHarvesterBall", 60, 0, -45)
		HFRY Q 0 A_SpawnProjectile("SoulHarvesterBall", 60, 0, 0)
		HFRY Q 8 A_SpawnProjectile("SoulHarvesterBall", 60, 0, 45)
		Goto See
		Melee:
		HFRY EF 8 A_FaceTarget
		HFRY G 8 A_ComboAttack
		Goto See
	Pain:
		HFRY H 2
		HFRY H 2 A_Pain
		Goto See
	Death:
		HFRY I 8 A_Scream
		HFRY J 0
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_SpawnProjectile("HellsFurySkull", 60, 0, 20)
		Goto Death+12
		HFRY J 0 A_SpawnProjectile("HellsFurySkull", 60, 0, 10)
		Goto Death+12
		HFRY J 0 A_SpawnProjectile("HellsFurySkull", 60, 0, 0)
		Goto Death+12
		HFRY J 0 A_SpawnProjectile("HellsFurySkull", 60, 0, -10)
		Goto Death+12
		HFRY J 0 A_SpawnProjectile("HellsFurySkull", 60, 0, -20)
		Goto Death+12
		HFRY J 8
		HFRY K 8
		HFRY L 8 A_Fall
		HFRY MN 8
		HFRY O -1
		Stop
	XDeath: //new
		HFRY I 0 A_SpawnDebris("NashGore_Gib11",0)
		HFRY I 0 A_SpawnDebris("Gore_GreyGib11",0)
		HFRY I 0 A_SpawnDebris("NashGore_Gib11",0)
		HFRY I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		HFRY I 0 A_SpawnItem("Gore_GibGeneratorGrey",0,0,0,1)
		HFRY I 8 A_Scream
		HFRY J 0 A_FaceTarget
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_Jump(51,5)
		HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 20, 128)
		Goto XDeath+22
		HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 10, 128)
		Goto XDeath+22
		HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 0, 128)
		Goto XDeath+22
		HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, -10, 128)
		Goto XDeath+22
		HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, -20, 128)
		Goto XDeath+22
		HFRY J 0 A_SpawnItem("Gore_GibGeneratorGrey",0,0,0,2)
		HFRY J 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,3)
		HFRY J 8
		HFRY K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		HFRY K 0 A_SpawnItem("Gore_GibGeneratorGrey",0,0,0,5)
		HFRY K 8
		HFRY L 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		HFRY L 0 A_SpawnItem("Gore_GibGeneratorGrey",0,0,0,7)
		HFRY L 8 A_Fall
		HFRY MN 8
		HFRY O -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		HFRY NMLKJI 8
		Goto See
 	}
}

ACTOR HellsFuryBall : BaronBall
{
	Radius 6
	Height 8
	Speed 20
	FastSpeed 30
	Damage 11
	Translation 2
	RenderStyle ADD
	Decal BaronScorch
}

ACTOR HellsFurySkull
{
	Radius 5
	Height 5
	Speed 5
	Mass 5000
	+DOOMBOUNCE
	+DROPOFF
	+MISSILE
	States
	{
	Spawn:
		FSKL ABCDEFGHIJK 2
		Loop
	Death:
		FSKL L -1
		Loop
	}
}