//===========================================================================
//
// Zombie Scientist
//
// created by: scalliano
// sprites by: scalliano, Xim, JoeyTD, iD Software
//
//===========================================================================

ACTOR ZombieScientist
{
	//$Category KDiZD Monsters
	Health 20
	Mass 90
	Speed 8
	Radius 20
	Height 52
	PainChance 200
	MONSTER
	+FLOORCLIP
	SeeSound "grunt/sight"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "$OB_ZSCI"
	States
	{
	Spawn:
		SCZA AB 10 A_Look
		Loop
	See:
		SCZA AABBCCDD 4 A_Chase
		Loop
	Melee:
		SCZA E 8 A_FaceTarget
		SCZA F 16 A_CustomMeleeAttack (random (2, 6) *5, "KnifeHit", "skeleton/swing")
		SCZA E 4
		Goto See
	Pain:
		SCZA G 3
		SCZA G 3 A_Pain
		Goto See
	Death:
		SCZA H 5
		SCZA I 5 A_Scream
		SCZA J 5 A_NoBlocking
		SCZA K 5
		SCZA L 5
		SCZA M 5
		SCZA N -1
		stop
	XDeath:
		SCZA O 5
		SCZA P 5 A_XScream
		SCZA Q 5 A_NoBlocking
		SCZA RSTUV 5
		SCZA W -1
		stop
	Raise:
		SCZA MLKJIH 5
		Goto See
	}
}