ACTOR DarkBase //base actor for projectiles  - Ozy81
{
	Radius 8
	Height 6
	Damage 3
	Speed 9
	FastSpeed 16
	Projectile
	RenderStyle ADD
	Alpha 0.67
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal DoomImpScorch
}

ACTOR DarkFast : DarkBase
{
    Speed 18
	FastSpeed 24
    MissileHeight 0
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

ACTOR DarkSeeker : DarkBase
{
    +SEEKERMISSILE
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

ACTOR DarkNormal : DarkBase
{
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

ACTOR DarkStrong : DarkBase
{
    Damage 6
    MissileHeight 0
    MissileType DarkTail
    States
    {
    Spawn:
        DKB2 A 4 Bright A_MissileAttack
        DKB2 B 4 Bright A_MissileAttack
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

ACTOR DarkTail
{
    PROJECTILE
    +NOCLIP
    RenderStyle ADD
    ALPHA 0.5
    States
    {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}

//===========================================================================
//
// STDarkImp
//
// from Skulltag
// sprites by: Nanami, modified by Vader (original)
// new sprites by Amuscaria from Hell Forged
//
//===========================================================================

ACTOR STDarkImp
{
	//$Category KDiZD Monsters
    Health 110
    Radius 20
    Height 56
    Speed 9
	FastSpeed 18
    MeleeDamage 3
    PainChance 200
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_STDI"
    HitObituary "$HB_STDI"
    States
    {
    Spawn:
        DAKP AB 10 A_Look
        Loop
    See:
        DAKP AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile: //HIJ unused yet - ozy81
        DAKP E 0 A_Jump(128,"Missile.Normal","Missile.Strong","Missile.Seeker")
        DAKP EF 8 A_FaceTarget
        DAKP G 6 A_BasicAttack(3,"imp/melee","DarkFast",32)
        Goto See
    Missile.Normal:
        DAKP EF 8 A_FaceTarget
        DAKP G 6 A_BasicAttack(3,"imp/melee","DarkNormal",32)
        Goto See
    Missile.Strong:
        DAKP EF 8 A_FaceTarget
        DAKP G 6 A_BasicAttack(5,"imp/melee","DarkStrong",32) //strong does 2+ melee damage
        Goto See
    Missile.Seeker:
        DAKP EF 8 A_FaceTarget
        DAKP G 6 A_BasicAttack(3,"imp/melee","DarkSeeker",32)
        Goto See
    Pain:
        DAKP K 2
        DAKP K 2 A_Pain
        Goto See
    Death:
        DAKP L 8
        DAKP M 8 A_Scream
        DAKP N 6
        DAKP O 6 A_Fall
        DAKP P -1
        Stop
    XDeath:
        DAKP Q 5
        DAKP R 5 A_XScream
        DAKP S 5
        DAKP T 5 A_Fall
        DAKP UVW 5
        DAKP X -1
        Stop
    Raise:
        DAKP PO 8
        DAKP NML 6
        Goto See
    }
}