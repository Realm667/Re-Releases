//===========================================================================
//
// Mauler Demon
//   - idea from Nimrod
//   - sprites by Eriance/Amuscaria
//
//===========================================================================

ACTOR MaulerDemon
{
	//$Category KDiZD Monsters
	Health 150
	PainChance 180
	GibHealth 35
	Speed 10
	FastSpeed 16
	Radius 30
	Height 56
	Mass 400
	Damage 2 //Horns = pain. I say no more
	Monster
	+FLOORCLIP
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_MAUL"
	HitObituary "$HB_MAUL"
	States
	{
	Spawn:
		SAR2 AB 10 A_Look
		Loop
	See:
		"####" AABBCCDD 2 A_Chase
		Loop
	Melee:
		"####" EF 8 A_FaceTarget
		"####" G 8 A_SargAttack
		Goto See
	Missile:
		"####" E 0 A_JumpIfCloser(224, 2)
		Goto See
		"####" E 4 A_FaceTarget
		"####" F 10 A_SkullAttack
		"####" G 5 A_Gravity
		Goto See
	Pain:
		"####" H 2
		"####" H 2 A_Pain
		Goto See
	Death:
		"####" I 8
		"####" J 8 A_Scream
		"####" K 4
		"####" L 4 A_Fall
		"####" M 4
		"####" N -1
		Stop
	XDeath: //new
		"####" I 0 A_SpawnDebris("NashGore_Gib11",0)
		"####" I 0 A_SpawnDebris("NashGore_Gib11",0)
		"####" I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		"####" I 8
		"####" J 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		"####" J 0 A_SpawnItemEx("MaulerArm",10,0,32,0,random(2,8),0,0,random(0,180))
		"####" J 8 A_Scream
		"####" K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		"####" K 4
		"####" L 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		"####" L 4 A_Fall
		"####" M 4
		"####" N -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		"####" N 5
		"####" MLKJI 5
		Goto See
	}
}

ACTOR MaulerArm : Gore_DemonArm //new
{
	States
	{
	Spawn:
		TNT1 AA 0 A_JumpIf(waterlevel > 0, "Underwater")
		MAUA ABCDEFGH 2 A_SpawnDebris("NashGore_FlyingBlood",1)
		Loop
	Underwater:
		MAUA ABCDEFGH 2 A_SpawnDebris("NashGore_FlyingBlood", 1)
		MAUA I -1
		Stop
	Death:
		MAUA I -1
		Stop
	Death.Sky: //prevent it to appear on sky - ozy81
		MAUA A 1
		Stop
	Crash:
		MAUA I -1
		Stop
	}
}