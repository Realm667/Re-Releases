//===========================================================================
//
// Hell Warrior
//
// by Tormentor667
// new sprites and additional scripting by: Vader
// Custom XDeath by Ozymandias81
//
//===========================================================================

ACTOR HellWarrior
{
	//$Category KDiZD Monsters
	Health 400
	Speed 8
	FastSpeed 14
	Radius 24
	Height 64
	PainChance 50
	Mass 1000
	Meleedamage 8
	SeeSound "hellwarrior/sight"
	PainSound "knight/pain"
	DeathSound "knight/death"
	ActiveSound "knight/active"
	MeleeSound "baron/melee"
	ReactionTime 8
	MONSTER
	+FLOORCLIP
	Missiletype HellWarriorBall
	Obituary "$OB_HWAR"
	HitObituary "$HB_HWAR"
	States
	{
	Spawn:
		HWAR AB 10 A_Look
		Loop
	See:
		HWAR A 0 A_UnSetInvulnerable
		HWAR AABBCCDD 3 A_Chase
		Loop
	Missile:
		HWAR E 0 A_Jump(128, 4)
		HWAR EF 8 A_FaceTarget
		HWAR G 8 A_ComboAttack
		Goto See
		HWAR H 0 A_Jump(128, 6)
		HWAR H 0 A_SetInvulnerable
		HWAR H 6 A_FaceTarget
		HWAR I 4 BRIGHT A_SpawnProjectile(HellWarriorBall2, 25, 0, 0)
		HWAR H 6 A_FaceTarget
		HWAR H 0 A_UnSetInvulnerable
		Goto See
		HWAR H 0 A_SetInvulnerable
		HWAR H 6 A_FaceTarget
		HWAR I 4 BRIGHT A_SpawnProjectile(HellWarriorBall2, 25, 0, 0)
		HWAR H 16 A_SpidRefire
		HWAR I 4 BRIGHT A_SpawnProjectile(HellWarriorBall2, 25, 0, 0)
		HWAR H 6 A_FaceTarget
		HWAR H 0 A_UnSetInvulnerable
		Goto See
		Melee:
		HWAR EF 8 A_FaceTarget
		HWAR G 8 A_ComboAttack
		Goto See
	Pain:
		HWAR J 6 A_Pain
		HWAR H 0 A_SetInvulnerable
		HWAR HHHHHHH 6 A_FaceTarget
		HWAR H 0 A_UnSetInvulnerable
		Goto See
	Death:
		HWAR K 5 A_SpawnProjectile(HellWarriorShield, 25, 0, 60)
		HWAR L 5 A_Scream
		HWAR M 5
		HWAR N 5 A_NoBlocking
		HWAR OPQRS 5
		HWAR T -1
		Stop
	XDeath: //new
		HWAR K 0 A_SpawnDebris("NashGore_Gib11",0)
		HWAR K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
		HWAR K 0 A_FaceTarget
		HWAR K 5 A_SpawnItemEx("HellWarriorShield", 0, 0, 25, 6, 0, 0, 60, 128)
		HWAR K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
		HWAR L 5 A_Scream
		HWAR M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
		HWAR M 5
		HWAR K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
		HWAR N 5 A_NoBlocking
		HWAR O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,8)
		HWAR OPQRS 5
		HWAR T -1
		Stop
	Crush:
		TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
		Stop
	Raise:
		HWAR QPONMLK 5
		Goto See
	}
}

ACTOR HellWarriorBall
{
	Radius 6
	Height 8
	Speed 14
	FastSpeed 20
	Damage 8
	PROJECTILE
	RenderStyle ADD
	ALPHA 0.7
	+BRIGHT
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		HWFB AB 5
		Loop
	Death:
		HWFB CDE 5
		Stop
	}
}

ACTOR HellWarriorBall2
{
	Radius 6
	Height 8
	Speed 8
	FastSpeed 12
	Damage 2
	Scale 0.4
	PROJECTILE
	RenderStyle ADD
	ALPHA 0.8
	+BRIGHT
	+RANDOMIZE
	+RIPPER
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		GRBA ABCDEFGH 2
		Loop
	Death:
		GRBA AIJKLMN 3
		Stop
	}
}

ACTOR HellWarriorShield
{
	Radius 8
	Height 8
	Speed 6
	+DOOMBOUNCE
	+DROPOFF
	+MISSILE
	States
	{
	Spawn:
		HWSH ABCDEFGH 3
		Loop
	Death:
		HWSH I -1
		Loop
	}
}