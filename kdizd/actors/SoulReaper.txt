//===========================================================================
//
// Soul Reaper
//
// created by: Vader
// note: it has inheritance with Soul Harvester
//
//===========================================================================
ACTOR SoulReaper
{
	//$Category KDiZD Monsters
    Health 720
    Radius 24    
	Height 64
	Mass 750
    Speed 7
	Scale 0.9
    MeleeDamage 6
    PainChance 35
    MONSTER 
    +FLOORCLIP
    SeeSound "reaper/sight"
    PainSound "reaper/pain"
    DeathSound "reaper/death"
    ActiveSound "reaper/active"
    MeleeSound "imp/melee"
    Obituary "$OB_SREA"
	HitObituary "$HB_SREA"
    MissileType SoulBall
    States
    {
    Spawn:
        SLRP AB 10 A_Look
        Loop
    See:
        SLRP AABBCCDD 3 A_Chase
        Loop
    Melee:
	SLRP EF 5 A_Facetarget
	SLRP F 0 ThrustThing(angle*256/360, 3, 0, 0)
	SLRP G 5 A_MeleeAttack
	SLRP HI 5 A_Facetarget
	SLRP H 0 ThrustThing(angle*256/360, 3, 0, 0)
	SLRP J 5 A_MeleeAttack
	Goto See
    Missile:
	SLRP E 0 A_Jump(128, 6)
	SLRF AB 4 A_FaceTarget
	SLRF C 8 A_FaceTarget
	SLRF D 8 A_Custommissile("SoulReaperBall",32,0,0)
	Goto See
        SLRP KLM 6 BRIGHT A_FaceTarget
        SLRP N 5 BRIGHT A_CustomMissile("SoulBall",40,0,0)
        SLRP M 5 BRIGHT A_FaceTarget
		SLRP N 5 BRIGHT A_CustomMissile("SoulBall",40,0,random(5,20))
        SLRP M 5 BRIGHT A_FaceTarget
		SLRP N 5 BRIGHT A_CustomMissile("SoulBall",40,0,random(-5,-20))
        SLRP M 6 BRIGHT A_FaceTarget
        SLRP O 6
        Goto See
    Pain:
    	SLRP P 3
    	SLRP P 3 A_Pain
    	Goto See
    Death:
        SLRP Q 7 A_Scream
        SLRP RSTU 7 
        SLRP V 6 A_SpawnItemEx("SoulReaperGhost", 0, 0, 50, 0, 0, 0, 0, 128)
        SLRP W 6 A_Fall
        SLRP XY 6
        SLRP Z -1
        Stop
    Raise:
        SLRP ZYXWVUTSRQ 5
        Goto See
    }
}

ACTOR SoulReaperGhost
{
    Radius 1
    Height 1
    Speed 0
	Scale 0.5
    Projectile
    RenderStyle ADD
    Alpha 0.7
    +NOCLIP
    SeeSound "reaper/ghost"
    States
    {
    Spawn:
    Death:
        SRGH A 0
        SRGH A 0 A_PlaySound("Reaper/Ghost")
        SRGH ABCDEFGHI 4 BRIGHT
        Stop
    }
}

ACTOR SoulBall
{
    Radius 8
    Height 6
    Speed 10
    Damage 4
    Scale 0.4
    Projectile
    +SEEKERMISSILE
    Renderstyle ADD
    SeeSound "harvester/scream"
    DeathSound "imp/shotx"
	Decal DoomImpScorch
    States
    {
    Spawn:
		SHBA A 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
        SHBA A 2 Bright
		SHBA B 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
		SHBA B 2 Bright
		SHBA A 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
        SHBA A 2 Bright
		SHBA B 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
		SHBA B 2 Bright
    	SHBA A 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
        SHBA A 2 BRIGHT A_SeekerMissile(random(4,10),random(8, 16),SMF_PRECISE)
		SHBA B 0 BRIGHT A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
        SHBA B 2 BRIGHT A_SeekerMissile(random(4,10),random(8, 16),SMF_PRECISE)
        Goto Spawn+8
    Death:
        SHBA CDEFGHIJKL 6 BRIGHT
        Stop
    }
}

Actor SoulReaperBall
{
  Radius 6
  Height 16
  Speed 17
  FastSpeed 22
  Damage 10
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "baron/attack"
  DeathSound "baron/shotx"
  Decal "BaronScorch"
  States
  {
  Spawn:
    SRBL AB 4 Bright
    Loop
  Death:
    SRBL CDEF 6 Bright
    Stop
  }
}