Actor Daedabus
{
	//$Category Z2M3_Monsters
	Health 1000
	Radius 48
	Height 64
	Mass 1000
	Speed 8
	PainChance 80
	BloodColor "73 00 00"
	SeeSound "Daedabus/Sight"
	PainSound "Daedabus/Pain"
	DeathSound "Daedabus/Death"
	ActiveSound "Daedabus/Active"
	MONSTER
	+FLOORCLIP
	+DONTHURTSPECIES
	Obituary "$OB_DAED"
	States
	{
	Spawn:
		DAED AB 15 A_Look
		Loop
	See:
		DAED AABBCCDDEEFF 4 A_Chase
		Loop
	Pain:
		DAED J 3
		DAED J 3 A_Pain
		Goto See
	Missile:
		DAED F 0 A_Jump(128, "Missile2")
		DAED F 0 A_Jump(255, "Missile3")
	Missile2:
		DAED G 0 A_PlaySound("Daedabus/Attack")
		DAED G 20 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, fRandom(2.75,5.625))
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, -5.625)
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 5.625)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, fRandom(-2.75,-5.625))
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire",28, 0, 11.25)
		DAED H 0 A_CustomMissile("DaedFire",28, 0, 5.625)
		DAED H 0 A_CustomMissile("DaedFire",28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire",28, 0, -5.625)
		DAED H 0 A_CustomMissile("DaedFire",28, 0, -11.25)
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		Goto See
	Missile3:
		DAED G 0 A_PlaySound("Daedabus/Attack")
		DAED G 20 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 5.625)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, fRandom(-2.75,-5.625))
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, fRandom(2.75,5.625))
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, -5.625)
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 11.25)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 5.625)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, 0)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, -5.625)
		DAED H 0 A_CustomMissile("DaedFire", 28, 0, -11.25)
		DAED H 10 Bright
		DAED IG 5 A_FaceTarget
		Goto See
	Death:
		DAED K 6
		DAED L 6 A_Scream
		DAED M 6 A_NoBlocking
		DAED NOPQRS 6
		DAED T -1 A_BossDeath
		Stop
	Raise:
		DAED RQPONMLK 5
		Goto See
	}
}

ACTOR DaedFire : FatShot
{
	Speed 22
	Damage 6
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.96
	Decal Scorch
	States
	{
	Spawn:
		DAEF AAAABBBB 1 BRIGHT A_SpawnItemEx("DaedTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		DAEF C 8
		DAEF D 6
		DAEF E 4
		Stop
	}
}

Actor DaedTrail
{
	Scale 0.65
	RenderStyle Add
	Alpha 0.5
	+BRIGHT
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	States
	{
	Spawn:
		DAET ABCDE 2
		Stop
	}
}