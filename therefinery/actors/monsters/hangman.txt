actor Hangman 32123
{
//$Category Z2M3_Monsters
  obituary "%o was ambushed by a hangman."
  health 150
  radius 20
  height 56
  mass 0x7fffffff
  speed 0
  painchance 75
  seesound "hanger/sight"
  painsound "hanger/pain"
  deathsound "hanger/death"
  activesound "hanger/active"
  MONSTER
  +FLOORCLIP
  +NOGRAVITY
  +SPAWNCEILING
  +CEILINGHUGGER
  +DONTFALL
  states
  {
  Spawn:
    HANG B 10 A_Look
    loop
  See:
    HANG B 1 A_FaceTarget
    HANG B 0 A_Chase
    loop
  Melee:
  Missile:
    HANG B 8 A_FaceTarget
    HANG A 6 A_CustomMissile ("HangerBall", 32, 0, 0, 0)
    goto See
  Pain:
    HANG B 2
    HANG A 2 A_Pain
    goto See
  Death:
    HANG A 30 A_Scream
    HANG A 10 A_ChangeFlag("noclip", 1)
    HANG C 0 A_NoBlocking
    HANG C 0 A_SpawnItemEx ("HangerBoom", 0, 0, 24, 0, 0, 0, 0, 160)
    HANG C 0 A_PlaySoundEx ("weapons/rocklx", "Soundslot5")
    HANG C 0 A_PlaySoundEx ("hanger/gib", "Soundslot6")
    HANG CCCCCCCCCCCCCCC 0 A_SpawnItemEx (HangerSplatSpawner, 0, 0, 24, random (2,5), random (2,5), random (3,-3), random (0,360), 160)
  DeathDrip:
    HANG C 256 A_SpawnItemEx (CorpseBloodDrip, 0, 0, 32, 0, 0, -8, 0, 160)
    loop
  }
}

actor HangerBall
{
  Radius 4
  Height 6
  Speed 10
  FastSpeed 15
  Damage 4
  SeeSound "hanger/attack"
  DeathSound "slimeball/splat"
  Scale 0.5
  Translation "112:127=162:167"
  Projectile
  +RANDOMIZE
  -NOGRAVITY
  +LOWGRAVITY
  +SPAWNSOUNDSOURCE
  states
  {
  Spawn:
    BAL7 AAA 1 A_SpawnItemEx (HangerTrail, random (-2,2), random (-2,2), random (-2,2), 0, 0, 0, 0, 160)
	BAL7 BBB 1 A_SpawnItemEx (HangerTrail, random (-2,2), random (-2,2), random (-2,2), 0, 0, 0, 0, 160)
    loop
  Death:
    BAL7 C 0 A_ChangeFlag ("NOGRAVITY", 0)
    BAL7 CDE 4
    stop
  }
}

ACTOR HangerTrail
{   
   Radius 0
   Height 1
   Translation "112:127=162:167"
   Renderstyle Add
   Alpha 0.75
   PROJECTILE
   States
   {
   Spawn:
      TNT1 A 2
      SSFX ABCDEFG 2 
      Stop 
   }
}

actor HangerBoom
{
  Radius 2
  Height 2
  Speed 0
  Damage 0
  Scale 0.75
  renderstyle Add
  Projectile
  +NOCLIP
  States
  {
  Spawn:
  Death:
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

actor HangerSplatSpawner
{
  Radius 11
  Height 8
  Speed 40
  Damage 0
  Projectile
  -NOGRAVITY
  States
  {
  Spawn:
    TNT1 AAAAAAAAAA 1 A_SpawnItemEx (HangerSplatTrail, 0, 0, 0, 0, 0, 0, 0, 160)
  Death:
    TNT1 A 4
    stop
  }
}

actor HangerSplatTrail
{
  +NOGRAVITY
  +NOBLOCKMAP
  +NOTELEPORT
  States
  {
  Spawn:
    BLUD CBA 2
    Stop
  }
}
