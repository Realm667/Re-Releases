ACTOR Swarm 32617
{
//$Category Z2M3_Monsters
	MONSTER
	+FLOAT
	+NOGRAVITY
	+LOOKALLAROUND
	+NONSHOOTABLE
	-SOLID
	+CANTSEEK
	Health 10
	Mass 10000
	Radius 24
	Height 48
	Speed 10
	DeathSound "SwarmFlyDeath"
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory ("SwarmGibbed", 1, "SpawnedAlready")
		TNT1 A 0 A_SpawnItemEX ("SwarmBody", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		goto SpawnedAlready
	SpawnedAlready:
		TNT1 AA 4 A_Look
		goto SpawnedAlready
	See:
		TNT1 A 0 A_JumpIfInventory ("SwarmGibbed", 1, "SeeGibbed")
		TNT1 A 0 A_JumpIfCloser (256, "SeeGib")
		TNT1 A 4
		goto See
	SeeGib:
		TNT1 A 0 A_PlaySoundEx ("SwarmFlySight", Auto)
		TNT1 A 0 A_SpawnItemEX ("SwarmGibber", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAA 2 A_CustomMissile("SwarmFly", 0, 0, random (0, 360), 2, random (-40, 40))
		TNT1 A 0 A_GiveInventory ("SwarmGibbed", 1)
		goto SeeGibbed
	SeeGibbed:
		TNT1 AAAA 4 A_Chase
		TNT1 A 0 A_PlaySoundEx ("SwarmFly", Auto)
		Loop
	Melee:
		TNT1 A 4 A_CustomMeleeAttack (3, "SwarmFlyAttack", "SwarmFlyAttack", Normal, 1)
		goto SeeGibbed
	Death:
		TNT1 A 0
		TNT1 A 0 A_Fall
		TNT1 A 0 A_Scream
		TNT1 A 0 A_TakeInventory ("SwarmGibbed", 1)
		TNT1 A 35
		Stop
	}
}

ACTOR SwarmBody
{
	Radius 1
	Height 1
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump (256, "SpawnPlayer", "SpawnDemon", "SpawnImp", "SpawnZombie")
	SpawnPlayer:
		SBOD A -1
		Stop
	SpawnDemon:
		SBOD B -1
		Stop
	SpawnImp:
		SBOD C -1
		Stop
	SpawnZombie:
		SBOD D -1
		Stop
	}
}

ACTOR Swarm_CastCall : Swarm
{
	Seesound "SwarmFlySight"
	States
	{
	Spawn:
		SBOD D -1
	Melee:
		"####" A 4 A_CustomMeleeAttack (3, "SwarmFlyAttack", "SwarmFlyAttack", Normal, 1)
		goto SeeGibbed
	Death:
		"####" A 0
		"####" A 0 A_Fall
		"####" A 0 A_Scream
		"####" A 35
		Stop
	}		
}

ACTOR SwarmFly
{
	Radius 1
	Height 1
	Speed 5
	PROJECTILE
	+NOCLIP
	ReactionTime 4
	Scale 0.25
	States
	{
	Spawn:
		SFLY AB 2 ThrustThingZ(0, random (-1, 1), random (1, 0), 1)
		SFLY A 0 A_CountDown
 		Loop
	Death:
		SFLY A 0
		SFLY A 0 A_JumpIfInTargetInventory ("SwarmGibbed", 1, "SpawnNewFly")
		TNT1 A 35 A_SpawnItemEX ("DeadFly", 0, 0, 0)
		Stop
	SpawnNewFly:
		SFLY A 2 A_CustomMissile("SwarmFly", 0, 0, random (-40, 40))
		stop
	}
}

ACTOR DeadFly
{
	Radius 1
	Height 1
	Scale 0.25
	States
	{
	Spawn:
		SFLY A 0
		SFLY A 35
		Stop
	}
}

ACTOR SwarmGibbed : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR SwarmGibber
{
	+NOCLIP
	Radius 1
	Height 1
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 35 A_SpawnDebris ("SwarmGibs")
		Stop
	}
}

ACTOR SwarmGibs
{
	Health 20
	Radius 1
	Height 1
	States
	{
	Spawn:
		SGIB A -1
		SGIB B -1
		SGIB C -1
		SGIB D -1
		SGIB E -1
		SGIB F -1

	}
}

Actor TaggSwarm : Swarm 22295
{
Health 40
-Countkill
States
{
	SeeGib:
		TNT1 A 0 A_PlaySoundEx ("SwarmFlySight", Auto)
		TNT1 A 0 A_SpawnItemEX ("SwarmGibber", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAA 2 A_CustomMissile("SwarmTagg", 0, 0, random (0, 360), 2, random (-40, 40))
		TNT1 A 0 A_GiveInventory ("SwarmGibbed", 1)
		goto SeeGibbed
	}
}

ACTOR SwarmTagg
{
	Radius 1
	Height 1
	Speed 5
	PROJECTILE
	+NOCLIP
	ReactionTime 4
	Scale 0.1
	States
	{
	Spawn:
		TAGG ABCD 1 ThrustThingZ(0, random (-1, 1), random (1, 0), 1)
		TAGG A 0 A_CountDown
 		Loop
	Death:
		TAGG A 0
		TAGG A 0 A_JumpIfInTargetInventory ("SwarmGibbed", 1, "SpawnNewFly")
		TNT1 A 35 A_SpawnItemEX ("DeadTagg", 0, 0, 0)
		Stop
	SpawnNewFly:
		TAGG A 2 A_CustomMissile("SwarmTagg", 0, 0, random (-40, 40))
		stop
	}
}

ACTOR DeadTagg
{
	Radius 1
	Height 1
	Scale 0.1
	States
	{
	Spawn:
		TAGG A 0
		TAGG A 35
		Stop
	}
}