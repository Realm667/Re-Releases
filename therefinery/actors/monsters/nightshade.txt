//Monster-Nightshade
actor Nightshade 30131
{
//$Category Z2M3_Monsters
  obituary "%o couldn't escape the Nightshade."
  health 80
  radius 20
  height 56
  mass 100
  speed 12
  painchance 160
  seesound "monster/nshsit"
  painsound "monster/nshpai"
  deathsound "monster/nshdth"
  activesound "monster/nshact"
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
    NSHA AB 10 A_Look
    loop
  See:
    NSHA A 0 A_SetTranslucent(0.75,0)
    NSHA AABBCCDD 3 A_Chase
    goto See+1
  Missile:
    NSHA E 0 A_SetTranslucent(1.0,0)
    NSHA EE 4 Bright A_FaceTarget
    NSHA F 8 Bright A_CustomMissile("ShadeMissile",32,0,0,0,0)
    NSHA B 4 A_SetTranslucent(0.87,0)
    goto See
  Pain:
    NSHA G 0 A_SetTranslucent(1.0,0)
    NSHA G 2
    NSHA G 2 A_Pain
    goto See
  Death:
    NSHA H 4 A_SetTranslucent(1.0,0)
    NSHA I 4 A_SetTranslucent(0.9,0)
    NSHA I 0 A_Scream
    NSHA J 4 A_SetTranslucent(0.8,0)
    NSHA K 0 A_NoBlocking
    NSHA K 4 A_SetTranslucent(0.7,0)
    NSHA L 4 A_SetTranslucent(0.6,0)
    NSHA M 4 A_SetTranslucent(0.5,0)
    NSHA N 4 A_SetTranslucent(0.4,0)
    NSHA O 4 A_SetTranslucent(0.3,0)
    NSHA P 4 A_SetTranslucent(0.2,0)
    stop
  }
}

ACTOR ShadeMissile : CacodemonBall
{
Damage 4
Speed 10
Alpha 0.80
DamageType normal
+THRUGHOST
+SEEKERMISSILE
+FORCEXYBILLBOARD
SeeSound "Monster/nshatk"
DeathSound "Monster/nshexp"
 states
  {
  Spawn:
    NBAL A 5 bright A_SpawnItemEx("ShadePuff",0,0,0,0,0,0)
    NBAL A 0 bright A_Seekermissile(5,7)
    NBAL B 5 bright A_SpawnItemEx("ShadePuff",0,0,0,0,0,0)
    NBAL B 0 bright A_Seekermissile(5,7)  
    loop
  Death:
    NBAL HIJKLM 3 bright
    stop
  }
}

ACTOR ShadePuff
{   
   Radius 3
   Height 3
   RENDERSTYLE ADD
   Alpha 0.67
   +NOGRAVITY
   +NOBLOCKMAP
   +DONTSPLASH
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
      NULL A 3 Bright
      NBAL CDEFG 3 BRIGHT
      Stop 
   }
}
//End of Nightshade
