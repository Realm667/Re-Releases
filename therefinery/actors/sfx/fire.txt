///////////////////////////////////////////////////////////////////////////////////////////////
//FIRE USAGE										     									 //
//===========================================================================================//
//Place on map and use the first argument to control the size of the fire. 0 is small,	     //
//1 is medium, 2 and above is large.							     						 //
//											     											 //
//The second argument prevents it from playing sounds. 0 will play the sounds, 1 will not.   //
///////////////////////////////////////////////////////////////////////////////////////////////

Actor FireSpawner : SwitchableDecoration
{
	//$Category Special Effects
	//$Title Fire Spawner
	//$Color 12
	//$Arg0 "Size"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Small"; 1 = "Medium"; 2 = "Large"; }
	//$Arg1 "Sound"
	//$Arg1Type 11
	//$Arg1Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg2 "Smoke"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Dynamic Light"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg4 "Fire"
	//$Arg4Type 11
	//$Arg4Enum { 0 = "Yes"; 1 = "No"; }
	Height 40
	Radius 30
	+CLIENTSIDEONLY
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	Species "FireSpawner"
	States
	{
	Spawn:
	Active:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[4] > 0, "Smoke")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
		TNT1 A 0 A_SpawnItemEx("SmallFlame", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySoundEx("SFX/FireStart", "SoundSlot7", 0)
		TNT1 A 0 A_JumpIf(Args[0] == 1, "Medium")
		TNT1 A 0 A_JumpIf(Args[0] > 1, "Large")
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySoundEx("SFX/FireLoop1", "SoundSlot6", 1)
		TNT1 A 0 A_JumpIf(Args[2] > 0, 4)
		TNT1 AAA 0 A_SpawnItemEx("Smoke1A", random(-8,8), random(-8,8), random(0,32), 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump(216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame1A", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
		TNT1 A 0 A_Jump(176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame1B", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
		TNT1 A 0 A_Jump(136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame2A", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
		TNT1 A 0 A_Jump(96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame2B", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
		TNT1 A 0 A_Jump(56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame3A", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
		TNT1 A 1 A_SpawnItemEx("Flame3B", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto Active+4
	Medium:
		TNT1 A 0 A_JumpIf(Args[4] > 0, "Smoke")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
		TNT1 A 0 A_SpawnItemEx("MediumFlame", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySoundEx("SFX/FireLoop2", "SoundSlot6", 1)
		TNT1 A 0 A_JumpIf(Args[2] > 0, 4)
		TNT1 AAA 0 A_SpawnItemEx("Smoke2A", random(-16,16), random(-16,16), random(0,64), 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump(216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame4A", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
		TNT1 A 0 A_Jump(176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame4B", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
		TNT1 A 0 A_Jump(136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame5A", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
		TNT1 A 0 A_Jump(96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame5B", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
		TNT1 A 0 A_Jump(56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame6A", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
		TNT1 A 1 A_SpawnItemEx("Flame6B", random(-16, 16), random(-16, 16), 0, 0, 0, 0, 0, 128)
		Goto Medium+3
	Large:
		TNT1 A 0 A_JumpIf(Args[4] > 0, "Smoke")
		TNT1 A 0 A_JumpIf(Args[3] > 0, 2)
		TNT1 A 0 A_SpawnItemEx("LargeFlame", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySoundEx("SFX/FireLoop3", "SoundSlot6", 1)
		TNT1 A 0 A_JumpIf(Args[2] > 0, 4)
		TNT1 AAA 0 A_SpawnItemEx("Smoke1A", random(-48,48), random(-48,48), random(0,128), 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump (216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame7A", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
		TNT1 A 0 A_Jump (176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame7B", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
		TNT1 A 0 A_Jump (136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame8A", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
		TNT1 A 0 A_Jump (96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame8B", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
		TNT1 A 0 A_Jump (56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame9A", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
		TNT1 A 1 A_SpawnItemEx("Flame9B", random(-32, 32), random(-32, 32), 0, 0, 0, 0, 0, 128)
		Goto Large+3
	Smoke:
		TNT1 A 1 {
		A_SpawnItemEx("Smoke1B", random(-8,8), random(-8,8), random(0,32), 0, 0, 0, 0, 128, 64);
		A_SpawnItemEx("EmberParticle_Fire", random(-16,16), random(-16,16), random(8,32), frandom(0.0,0.75), frandom(0.0,0.75), frandom(0.75,1.5) , 0, 0, 224);
		}
		Goto Smoke
	Inactive:
		TNT1 A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySoundEx("SFX/FireDie", "SoundSlot7", 0)
		TNT1 A -1 A_StopSoundEx("SoundSlot6")
		Goto Inactive+3
	}
}

//////////////////////////
// Dynamic Light actors //
//////////////////////////

Actor SmallFlame
{ 
	Species "FireSpawner"
	+CLIENTSIDEONLY
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

Actor MediumFlame : SmallFlame { }
Actor LargeFlame : SmallFlame { }

///////////////////////////////
// Flames and particles ///////
///////////////////////////////

Actor Flame1A
{
	Height 2
	Radius 1
	+NOGRAVITY
	+NOBLOCKMAP
	+MISSILE
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.2
	Scale 0.35
	States
	{
	Spawn:
		FLM1 A 0
	Burn:
		"####" A 0 A_SpawnItemEx("EmberParticle_Fire", random(-8,8), random(-8,8), random(8,16), frandom(0.0,0.5), frandom(0.0,0.5), frandom(1.25,2.5) , 0, 16)
		"####" A 0 ThrustThingZ(0, random(4, 16), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(1.0, 2.0), 0, 0)
		"####" AAAA 2 Bright
		"####" AAAAAAAAAAAAAAAAAAAAAAAA 1		
		{
		A_FadeOut(frandom(0.002,0.0025));
		A_SetScale(ScaleX - frandom(0.01,0.015), ScaleY - frandom(0.01,0.015));
		}
		Stop
	}
}

Actor Flame1B : Flame1A
{
	States
	{
	Spawn:
		FLM2 A 0
		goto Burn
	}
}

Actor Flame2A : Flame1A
{
	States
	{
	Spawn:
		FLM3 A 0
		goto Burn
	}
}

Actor Flame2B : Flame1A
{
	States
	{
	Spawn:
		FLM4 A 0
		goto Burn
	}
}

Actor Flame3A : Flame1A
{
	States
	{
	Spawn:
		FLM5 A 0
		goto Burn
	}
}

Actor Flame3B : Flame1A
{
	States
	{
	Spawn:
		FLM6 A 0
		goto Burn
	}
}

Actor Flame4A : Flame1A
{
	Scale 0.7
	States
	{
	Spawn:
		FLM1 A 0
		goto Burn
	Burn:
		"####" A 0 A_SpawnItemEx("EmberParticle_Fire", random(-16,16), random(-16,16), random(32,64), frandom(0.0,0.75), frandom(0.0,0.75), frandom(1.5,3.0) , 0, 16)
		"####" A 0 ThrustThingZ(0, random(8, 24), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(1.8, 3.6), 0, 0)
		"####" AAAA 2 Bright
		"####" AAAAAAAAAAAAAAAAAAAAAAAA 1		
		{
		A_FadeOut(frandom(0.002,0.0025));
		A_SetScale(ScaleX - frandom(0.01,0.015), ScaleY - frandom(0.01,0.015));
		}
		Stop
	}
}

Actor Flame4B : Flame4A
{
	States
	{
	Spawn:
		FLM2 A 0
		goto Burn
	}
}

Actor Flame5A : Flame4A
{
	States
	{
	Spawn:
		FLM3 A 0
		goto Burn
	}
}

Actor Flame5B : Flame4A
{
	States
	{
	Spawn:
		FLM4 A 0
		goto Burn
	}
}

Actor Flame6A : Flame4A
{
	States
	{
	Spawn:
		FLM5 A 0
		goto Burn
	}
}

Actor Flame6B : Flame4A
{
	States
	{
	Spawn:
		FLM6 A 0
		goto Burn
	}
}

Actor Flame7A : Flame1A
{
	Scale 1.1
	States
	{
	Spawn:
		FLM1 A 0
		goto Burn
	Burn:
		"####" A 0 A_SpawnItemEx("EmberParticle_Fire", random(-32,32), random(-32,32), random(64,128), frandom(0.0,1.0), frandom(0.0,1.0), frandom(2.0,4.0) , 0, 16)
		"####" A 0 ThrustThingZ(0, random(10, 32), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(3.0, 6.0), 0, 0)
		"####" AAAA 2 Bright
		"####" AAAAAAAAAAAAAAAAAAAAAAAA 1		
		{
		A_FadeOut(frandom(0.002,0.0025));
		A_SetScale(ScaleX - frandom(0.01,0.015), ScaleY - frandom(0.01,0.015));
		}
		Stop
		}
}

Actor Flame7B : Flame7A
{
	States
	{
	Spawn:
		FLM2 A 0
		goto Burn
	}
}

Actor Flame8A : Flame7A
{
	States
	{
	Spawn:
		FLM3 A 0
		goto Burn
	}
}

Actor Flame8B : Flame7A
{
	States
	{
	Spawn:
		FLM4 A 0
		goto Burn
	}
}

Actor Flame9A : Flame7A
{
	States
	{
	Spawn:
		FLM5 A 0
		goto Burn
	}
}

Actor Flame9B : Flame7A
{
	States
	{
	Spawn:
		FLM6 A 0
		goto Burn
	}
}

//////////////////////////////// Fast emerging smoke 

Actor Smoke1A : Flame1A 
{
	RenderStyle Translucent
	Alpha 0.0
	Scale 0.3
	States
	{
	Spawn:
		SMOC A 0 NoDelay
	Smoke:
		"####" A 0 A_Jump(144, "Death")
		"####" A 0 ThrustThingZ(0, random(10, 40), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(1.0, 2.0), 0, 0)
		"####" AAAAAAA 1 A_FadeIn(0.05)
		"####" A 1		
		{
		A_FadeOut(frandom(0.005,0.007));
		A_SetScale(ScaleX + frandom(0.01,0.015), ScaleY + frandom(0.01,0.015));
		A_ChangeVelocity(VelX/1.05, VelY/1.05, VelZ/1.05, CVF_REPLACE);
		}
		Goto Smoke+10
	Death:
		SMOC A 0
		Stop
	}
}

Actor Smoke2A : Smoke1A { Scale 0.6 }

Actor Smoke3A : Smoke1A { Scale 1.5 }

//////////////////////////////// Slow emerging smoke 

Actor Smoke1B : Flame1A 
{
	RenderStyle Translucent
	Alpha 0.0
	Scale 0.3
	States
	{
	Spawn:
		SMOC A 0 NoDelay
	Smoke:
		"####" A 0 A_Jump(144, "Death")
		"####" A 0 ThrustThingZ(0, random(3, 10), 0, 0)
		"####" A 0 ThrustThing(random(0, 255), frandom(1.0, 2.0), 0, 0)
		"####" AAAAAAA 1 A_FadeIn(0.05)
		"####" A 1		
		{
		A_FadeOut(frandom(0.005,0.007));
		A_SetScale(ScaleX + frandom(0.01,0.015), ScaleY + frandom(0.01,0.015));
		A_ChangeVelocity(VelX/1.05, VelY/1.05, VelZ/1.05, CVF_REPLACE);
		}
		Goto Smoke+10
	Death:
		SMOC A 0
		Stop
	}
}

Actor Smoke2B : Smoke1B { Scale 0.6 }

Actor Smoke3B : Smoke1B { Scale 1.5 }

///////////////////// Ember Particle

Actor EmberParticle_Fire
{
	+THRUACTORS
	+MISSILE
	+NOGRAVITY
	+FLOATBOB
	+FORCEXYBILLBOARD
	Radius 1
	Height 1
	BounceType Doom
	RenderStyle Add
	Alpha 0.0
	YScale 0.5
	XScale 0.5
	States
	{
	Spawn:
		EMB5 AAAAA 1 A_FadeIn(0.2)
		EMB5 AAAAAABCDEFHI 5 Bright 
		{
		A_Weave(1, 1, 1.0, 1.0);
		A_ChangeVelocity(VelX/1.025, VelY/1.025, VelZ/1.025, CVF_REPLACE);		
		}
		EMB5 A 0 A_Die
	Death: 
		EMB5 IIIIII 1 Bright A_FadeOut(0.2)
		Loop
	}
}