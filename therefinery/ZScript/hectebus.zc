Class HectebusClone : Actor
{
  Default
  {
    Health 1500;
    Radius 56;
    Height 80;
    Scale 1.25;
    Speed 12;
    PainChance 100;
    BloodColor "73 00 00";
    Mass 1500;
    SeeSound "Hectebus/Sight";
    PainSound "Hectebus/Pain";
    DeathSound "Hectebus/Death";
    ActiveSound "Hectebus/Active";
    Obituary "%o was cremated by a Hectebus.";
    Monster;
    +FloorClip
    +MissileMore
  }

  States
  {
  Spawn:
    HECT AAAAAAAAAABBBBBBBBBB 1 A_Look();
    Loop;
  See:
    HECT ABC 6 A_Chase();
    HECT D 0 A_PlaySound("Hectebus/Step", CHAN_7);
    HECT DEF 6 A_Chase();
    HECT A 0 A_PlaySound("Hectebus/Step", CHAN_7);
    Loop;
  Missile:
    HECT G 0 A_PlaySound("Hectebus/Attack", CHAN_6);
    HECT GGGG 4 A_FaceTarget();
    HECT G 0 A_Jump(64, "Missile2");
  Missile1:
    HECT U 4 Bright A_SpawnProjectile("Hecteball", 35, 41, 0);
    HECT IGG 4 A_FaceTarget();
    HECT K 0 A_SpidReFire();
    HECT V 4 Bright A_SpawnProjectile("Hecteball", 35, -41, 0);
    HECT IGG 4 A_FaceTarget();
    HECT K 0 A_Jump(96, "See");
    HECT K 0 A_SpidReFire();
    Loop;
  Missile2:
    HECT H 4 Bright
    {
      A_SpawnProjectile("Hecteball", 35,  41, Random(-4, 2));
      A_SpawnProjectile("Hecteball", 35, -41, Random(-2, 4));
    }
    HECT IGG 4 A_FaceTarget();
    HECT H 4 Bright
    {
      A_SpawnProjectile("Hecteball", 35,  41, Random(-4, 2));
      A_SpawnProjectile("Hecteball", 35, -41, Random(-2, 4));
    }
    HECT IGG 4 A_FaceTarget();
    HECT H 4 Bright
    {
      A_SpawnProjectile("Hecteball", 35,  41, Random(-4, 2));
      A_SpawnProjectile("Hecteball", 35, -41, Random(-2, 4));
    }
    HECT IGG 4 A_FaceTarget();
    HECT K 0 A_Jump(200, "See");
    HECT K 0 A_SpidReFire();
    Loop;
  Pain:
    HECT J 8 A_Pain();
    Goto See;
  Death:
    HECT K 6 A_Scream();
    HECT LM 6;
    HECT N 6 A_Fall();
    HECT OPQRS 6;
    HECT T -1;
    Stop;
  }
}

Class Hecteball : Actor //Hectebus Fireball
{
  Default
  {
    Radius 10;
    Height 8;
    Speed 20;
    Damage 16;
    RenderStyle "Add";
    Alpha 0.97;
    SeeSound "Imp/Attack";
    DeathSound "Imp/ShotX";
    Decal "Scorch";
    Projectile;
  }

  States
  {
  Spawn:
    HECF AB 4 Bright;
    Loop;
  Death:
    HECF CDE 4 Bright;
    Stop;
  }
}
