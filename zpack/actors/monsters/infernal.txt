//the spider boss
ACTOR InfernalSpider
{
	Radius 72
	Height 120
	Health 12000
	Speed 30
	mass 99999
	meleerange 76
	meleedamage 12
	painchance 3
	translation "152:159=216:223,9:9=223:223,76:70=216:223,129:132=216:223"
	scale 2.0
	SeeSound "spider2/sight"
	DeathSound "spider2/death"
	PainSound "spider2/pain"
	ActiveSound "spider2/active"
	Obituary "%o was burned to ashes by the infernalspider"
	HitObituary "%o was eaten by the infernalspider"
	MONSTER
	+BOSS
	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHURTSPECIES
	+NOTARGET
	+FLOORCLIP
	States
	{
	Spawn:
		SPI2 A 1 A_Look
		loop
	See:
		TNT1 A 0 A_jumpifcloser(1024,9)
		SPI2 E 12 A_Facetarget
		MNTR E 0 A_Playsound ("spider/step")
		MNTR E 0 A_setinvulnerable
		MNTR E 0 A_Skullattack
		SPI2 U 5 Thrustthingz (0, 82, 0, 1)
		SPI2 U 38 A_JumpIf (z-floorz<24,1)
		MNTR A 0
		MNTR A 0 A_Playsound ("spider/land")
		MNTR A 0 A_Unsetinvulnerable
		SPI2 A 1 A_stop
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		SPI2 ABCD 2 A_Chase
		MNTR A 0 A_playsound ("spider/step")
		loop
	Melee:
		MNTR E 0 A_Facetarget
		MNTR E 0 A_XScream
		SPI2 E 5 A_Meleeattack
		SPI2 A 13 A_Facetarget
		goto see
	Missile:
		MNTR V 0 A_Jump (82,15)
		MNTR V 0 A_Jump (32,8)
		SPI2 U 5 A_Facetarget
		SPI2 X 2 A_custommissile("flameshot", 24, 0, -8, 0)
		SPI2 X 2 A_custommissile("flameshot", 24, 0, 0, 0)
		SPI2 X 2 A_custommissile("flameshot", 24, 0, 8, 0)
		SPI2 X 3 A_Facetarget
		SPI2 U 5
		goto see
		SPI2 E 5 A_Facetarget
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		SPI2 E 5
		goto see
		SPI2 E 5 A_Facetarget
		SPI2 U 5 A_Playsound("spider2/sight")
		MNTR W 0 A_Facetarget
		SPI2 X 5 A_Custommissile("Hellripper", 128, 0, 0, 0)
		SPI2 X 17 A_Facetarget
		SPI2 X 2 A_custommissile("flameshot", 24, 0, -8, 0)
		SPI2 X 2 A_custommissile("flameshot", 24, 0, 0, 0)
		SPI2 X 2 A_custommissile("flameshot", 24, 0, 8, 0)
		SPI2 U 5 A_Facetarget
		goto see
	Pain:
		SPI2 E 2 A_Pain
		MNTR E 0 A_Facetarget
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		SPI2 E 6 A_throwgrenade("minispider", 85, 10, 20)
		goto see
	Death:
		MNTR A 0 A_Scream
		MNTR A 0 A_Setfloorclip
		MNTR A 0 A_Burst("archvilefire2")
		stop
	}
}

ACTOR flameshot : fatshot
{
	Speed 25
	SeeSound "skeleton/attack"
	Decal Scorch
	States
	{
	Spawn:
		SXAL ABC 5 BRIGHT
		loop
	Death:
		SXAL DEFGH 3 BRIGHT
		stop
	}
}

Actor Hellripper : fatshot
{
	Health 5
	Speed 50
	Damage 30
	Explosionradius 64
	ExplosionDamage 50
	SeeSound "skeleton/attack"
	Decal Scorch
	States
	{
	Spawn:
		TNT1 A 1 Bright A_Custommissile("archvilefire2",0,0,90,0)
		TNT1 A 0 Bright A_Custommissile("archvilefire2",0,0,-90,0)
		TNT1 A 0 A_JumpIf (z-floorz<24,1)
		loop
		TNT1 A 0 A_Explode
		TNT1 A 0 ThrustThingZ (0, random(2,3), 0, 0)
		loop
	Death:
		TNT1 B 5 A_Burst("archvilefire2")
		stop
	}
}

Actor Archvilefire2 : archvilefire
{
	Damage 5
	Speed 4
}

ACTOR MiniSpider
{
	health 200
	radius 26
	height 42
	speed 38
	Painchance 200
	Obituary "%o was eaten by a spider"
	HitObituary "%o was eaten by a spider"
	SeeSound "spider2/sight"
	DeathSound "spider2/pain"
	PainSound "spider2/pain"
	ActiveSound "spider2/sight"
	translation 1
	translation "96:111=64:79"
	scale 1.0
	meleedamage 6
	MONSTER
	+LOOKALLAROUND
	+NOTARGET
	States
	{
	spawn:
		GAYS A 1 A_Look
		loop
	See:
		GAYS ABCD 1 A_chase
		loop
	Melee:
		GAYS E 5 A_Facetarget
		GAYS E 0 A_XScream
		GAYS E 8 A_Meleeattack
		goto see
	Pain:
		GAYS A 2 A_Pain
		GAYS A 2
		goto see
	Death:
		GAYR FEDC 5 A_Setfloorclip
		GAYR B 5 A_Scream
		GAYR A 5 A_Die
		stop
	}
}