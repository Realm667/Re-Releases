//SoulHarvester
ACTOR SoulHarvester
{
	//$Category "ZPack Monsters"
    Health 100
    Radius 20
    Height 56
    Speed 8
    PainChance 160
    MONSTER
    +FLOORCLIP
    SeeSound "harvester/sight"
    PainSound "imp/pain"
    DeathSound "harvester/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "$OB_SOULH"
    MissileType SoulHarvesterBall
    MeleeDamage 3
    States
    {
    Spawn:
        SLHV AB 10 A_Look
        Loop
    See:
        SLHV AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        SLHV EFG 4 A_FaceTarget
        SLHV HIJK 5 BRIGHT A_FaceTarget
        SLHV L 8 BRIGHT A_MissileAttack
        SLHV M 10
        Goto See
    Pain:
    	SLHV N 3
    	SLHV N 3 A_Pain
    	Goto See
    Death:
        SLHV O 7 A_Scream
        SLHV PQR 7
        SLHV S 6 A_CustomMissile("SoulHarvesterGhost", 32, 0, 0)
        SLHV T 5 A_Fall
        SLHV UV 5
        SLHV W -1
        Stop
    XDeath:
        SLHV X 6 A_XScream
        SLHV Y 6 A_CustomMissile("SoulHarvesterGhost", 32, 0, 0)
        SLHV Z 6
        SLHV [ 6 A_Fall
        SLHV ] 6
        SLHW A -1
        Stop
    Raise:
        SLHV WVUTSRQPO 5
        Goto See
    }
}

ACTOR SoulHarvesterBall
{
    Radius 8
    Height 6
    Scale 0.4
    Speed 9
    Damage 2
    +SEEKERMISSILE
    PROJECTILE
    RenderStyle Add
    SeeSound "harvester/scream"
    DeathSound "imp/shotx"
	Decal DoomImpScorch
    States
    {
    Spawn:
    	SHBA A  0 BRIGHT A_CustomMissile("SoulBallTrail", 0, 0, 180)
        SHBA AB 2 BRIGHT A_SeekerMissile (10,20)
        Loop
    Death:
        SHBA CDEFGHIJKL 4 BRIGHT
        Stop
    }
}

ACTOR SoulHarvesterGhost
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RenderStyle Add
    Alpha 0.7
    +NOCLIP
    SeeSound "harvester/ghost"
    States
    {
    Spawn:
    Death:
        SHGH ABCDEFG 4 BRIGHT
        Stop
    }
}

ACTOR SoulBallTrail : SoulHarvesterGhost
{
    Alpha 0.5
    States
    {
    Spawn:
    Death:
    	SBTR H 6
        SBTR ABCDEFG 4 BRIGHT
        Stop
    }
}