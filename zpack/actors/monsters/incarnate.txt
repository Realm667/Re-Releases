// Death Incarnate
ACTOR Incarnate 30115 
{ 
Health 400 
Radius 20 
Height 56 
Mass 500 
Speed 12
Damage 1
MeleeDamage 7
PainChance 32 
MONSTER
+FLOORCLIP 
+MISSILEMORE 
+SHORTMISSILERANGE
+DONTHURTSPECIES
+NOTARGET
- COUNTKILL
Obituary "%o was killed by a Death Incarnate's glaring eyes."
HitObituary "%o was beaten to death by a Death Incarnate."
SeeSound "monster/incsit" 
PainSound "skeleton/pain" 
DeathSound "monster/incdth" 
ActiveSound "monster/incact"
AttackSound "monster/incatk"
MeleeSound "monster/inchit"
States 
   { 
   Spawn: 
       INCA AB 10 A_Look 
       Loop 
   See: 
       INCA AABBCCDDEEFF 2 A_Chase 
       Loop 
   Melee:
       INCA G 0 A_Jump(128,5)
       INCA G 0 A_FaceTarget 
       INCA G 6 A_SkelWhoosh 
       INCA H 6 A_FaceTarget 
       INCA I 6 A_MeleeAttack
       Goto See
       INCA R 0 A_FaceTarget 
       INCA R 5 A_SkelWhoosh 
       INCA S 5 A_FaceTarget 
       INCA T 5 A_MeleeAttack
       INCA G 0 A_FaceTarget 
       INCA G 5 A_SkelWhoosh 
       INCA H 5 A_FaceTarget 
       INCA I 5 A_MeleeAttack
       Goto See 
   Missile: 
       INCA J 5 A_FaceTarget
       INCA J 5
       INCA U 10 Bright A_CustomBulletAttack(6,3,3,3,"DIpuff",1024)
       INCA K 10
       Goto See 
   Pain: 
       INCA L 5 
       INCA L 5 A_Pain 
       Goto See 
   Death:
       INCA L 0 A_Jump(192,7)
       INCA LM 7 
       INCA N 7 A_Scream 
       INCA O 7 A_NoBlocking 
       INCA P 7 
       INCA Q -1 
       Stop
       INCA LM 7 
       INCA N 7 A_Scream 
       INCA O 7 A_NoBlocking
       INCA P 7 
       INCA Q 175
       INCA Q 0 A_Jump(128,7)
       INCA Q 120
       INCA Q 0 A_Jump(128,1)
       INCA Q 80
       INCA Q 0 A_Jump(128,1)
       INCA Q 80
       INCA Q 0 A_Jump(128,1)
       INCA Q 80
       INCA Q 0 A_SpawnItem("DIHealer",0,0)
       INCA Q -1       
       Stop
   XDeath: 
       INCX A 10 Bright A_Playsound("monster/incexp")
       INCX BC 5 Bright
       INCX D 5 Bright A_NoBlocking
       INCX D 0 Bright A_SpawnItem("HS",0,32) 
       INCX EFGHIJ 5 Bright
       INCX K -1 
   Raise:
       INCA Q 0 A_KillChildren
       INCA QPONML 5
       Goto See 
   } 
}

ACTOR DIHealer 
{ 
Health 1
Radius 0 
Height 1 
Mass 1000 
Speed 0 
Damage 0
MeleeDamage 0
RENDERSTYLE NONE
PAINCHANCE 0
ReactionTime 8
MONSTER
+DONTGIB
+NOTARGET
+INVULNERABLE
+NOCLIP
+NONSHOOTABLE
+LOOKALLAROUND
- COUNTKILL
States 
   { 
   Spawn: 
       WRAI AB 10 A_Look 
       Loop 
   See: 
       WRAI AABB 3 A_VileChase
       WRAI A 0 A_CountDown
       Loop 
   Heal:
       WRAI A 1
       Goto Death 
   Pain: 
       WRAI A 3  
       WRAI A 3 A_Pain 
       Goto See 
   Death: 
       WRAI E 4 A_Noblocking
       WRAI F 5 A_Scream 
       WRAI GHIJ 4  
       Stop 
   } 
}