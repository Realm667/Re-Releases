// Afrit
ACTOR Afrit
{
	//$Category "ZPack Monsters"
	Health 800
	Speed 6
	Radius 24
	Height 72
	PainChance 50
	Mass 500
	MeleeDamage 10
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	MeleeSound "baron/melee"
	Missiletype "AfritBall"
	MissileHeight 44
	ReactionTime 8
	Monster
	+NORADIUSDMG
	+NOGRAVITY
	+NOBLOOD
	+FLOATBOB
	+FLOAT
	Obituary "$OB_AFRIT"
	HitObituary "$HB_AFRIT"
	States
	{
	Spawn:
		FRIT ABCD 4 BRIGHT A_Look
		Loop
	See:
		FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0)
		FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180)
		FRIT AABBCCDD 2 BRIGHT A_Chase
		Goto See +2
	Melee:
		FRIT S 0 A_Jump(64,4)
		FRIT ST 6 BRIGHT A_FaceTarget
		FRIT U 6 BRIGHT A_ComboAttack
		Goto See +2
		FRIT F 0 BRIGHT A_CustomMissile("HellFireFX", 44, 0, 0)
		FRIT E 6 BRIGHT A_FaceTarget
		FRIT F 6 BRIGHT A_CustomMissile("HellFire", 44, 0, 0)
		FRIT G 6 BRIGHT A_FaceTarget
		Goto See +2
	Missile:
		FRIT S 0 A_Jump(64,8)
		FRIT S 0 A_Jump(128,4)
		FRIT ST 6 BRIGHT A_FaceTarget
		FRIT U 6 BRIGHT A_ComboAttack
		Goto See +2
		FRIT EF 6 BRIGHT A_FaceTarget
		FRIT G 6 BRIGHT A_CustomMissile("Comet", 44, 0, 0)
		Goto See +2
		FRIT F 0 BRIGHT A_CustomMissile("HellFireFX", 44, 0, 0)
		FRIT E 6 BRIGHT A_FaceTarget
		FRIT F 6 BRIGHT A_CustomMissile("HellFire", 44, 0, 0)
		FRIT G 6 BRIGHT A_FaceTarget
		Goto See +2
	Pain:
		FRIT HH 4 BRIGHT A_Pain
		Goto See +2
	Death:
		FRIT I 0 BRIGHT A_NoGravity
		FRIT I 6 BRIGHT A_Scream
		FRIT I 0 BRIGHT A_PlaySound("dragonfam/attack")
		FRIT J 5 BRIGHT A_NoBlocking
		FRIT KLMNOPQR 4 BRIGHT
		Stop
	}
}

ACTOR AfritBall
{
    Radius 6
    Height 8
    Speed 15
    Damage 8
    PROJECTILE
    RenderStyle Add
    Alpha 0.8
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        FRTM AB 5 BRIGHT
        Loop
    Death:
        FRTM CDE 6 BRIGHT
        Stop
    }
}

ACTOR Comet
{
    Radius 6
    Height 8
    Speed 10
    Damage 5
    ExplosionDamage 40
    ExplosionRadius 80
    Scale 0.55
    PROJECTILE
    SeeSound "comet/launch"
    DeathSound "comet/explosion"
    MissileType CometTail
    MissileHeight 0
    States
    {
    Spawn:
        COMT AAAABBBBCCCC 1 BRIGHT A_MissileAttack
        Loop
    Death:
        COMT D 3 BRIGHT A_CustomMissile("CometDeathGlow", 0, 0, 0)
        COMT E 3 BRIGHT
        COMT E 0 A_CustomMissile("CometDeath", 0, 0, 0)
        Stop
    }
}

ACTOR CometTail
{
    PROJECTILE
    RenderStyle Add
    +NOCLIP
    Alpha 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1 BRIGHT
        Stop
    }
}

ACTOR CometDeathGlow : CometTail
{
    Scale 2.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3 BRIGHT
        Stop
    }
}

ACTOR CometDeath : CometTail
{
    Alpha 0.7
    States
    {
    Spawn:
        COMT FGHI 3 BRIGHT
        Stop
    }
}

ACTOR OrbitComet : Comet
{
	+NOCLIP
	Scale 0.28
	Speed 5
	SeeSound ""
	MissileType OrbitCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COMT AABBCC 1 BRIGHT A_MissileAttack
		COMT A 0 A_CustomMissile("OrbitComet", 0, 0, 75)
		COMT A 0 A_MissileAttack
		Stop
	}
}

ACTOR OrbitComet2 : OrbitComet
{
	MissileType OrbitCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COMT AABBCC 1 BRIGHT A_MissileAttack
		COMT A 0 A_CustomMissile("OrbitComet2", 0, 0, -75)
		COMT A 0 A_MissileAttack
		Stop
	}
}

ACTOR OrbitCometTail : CometTail { Scale 0.5 }

ACTOR HellFireFX
{
	PROJECTILE
	RenderStyle Add
	Alpha 0.8
	Speed 1
	Damage 0
	Radius 6
	Height 8
	States
	{
	Spawn:
		COMT I 0
		COMT I 3 BRIGHT A_SetTranslucent(0.5)
		COMT HGF 3 BRIGHT A_SetTranslucent(0.8)
		COMT E 3 BRIGHT A_SetTranslucent(0.6)
		COMT D 3 BRIGHT A_SetTranslucent(0.4)
		COMT C 3 BRIGHT A_SetTranslucent(0.2)
		Stop
	}
}

ACTOR HellFire
{
	+NOBLOCKMAP
	+MISSILE
	+DROPOFF
	Speed 5
	Damage 0
	SeeSound "catharsi/shotdth"
	DeathSound "dragonfam/attack"
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 0 BRIGHT A_CustomMissile("HellFire1", 0, 0, -180)
		TNT1 A 0 BRIGHT A_CustomMissile("HellFire1", 0, 16, -175)
		TNT1 A 0 BRIGHT A_CustomMissile("HellFire1", 0, 32, -170)
		TNT1 A 0 BRIGHT A_CustomMissile("HellFire1", 0, -16, -185)
		TNT1 A 0 BRIGHT A_CustomMissile("HellFire1", 0, -32, -190)
		Stop
	}
}

ACTOR HellFire1
{
	+NOCLIP
	+MISSILE
	+DROPOFF
	Speed 2
	Damage 2
	States
	{
	Spawn:
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
		TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
		TNT1 A 0 A_PlaySound("dragonfam/attack")
		Stop
	}
}

ACTOR HellFire2 : HellFire1
{
	+RANDOMIZE
	Scale 1.5
	Speed 1
	RenderStyle Add
	Alpha 0.67
	ExplosionDamage 3
	ExplosionRadius 64
	DeathSound "dragonfam/attack"
	States
	{
	Spawn:
		TNT1 A 8
		FRTF A 3 BRIGHT
		FRTF A 0 A_Explode
		FRTF B 3 BRIGHT
		FRTF B 0 A_Explode
		FRTF C 3 BRIGHT
		FRTF C 0 A_Explode
		FRTF C 0
		FRTF C 0 A_Jump(191,4)
		FRTF C 0 A_CustomMissile("HellFire2", 0, 24, 0)
		FRTF C 0 A_CustomMissile("HellFire2", 0, 0, 0)
		FRTF C 0 A_CustomMissile("HellFire2", 0, -24, 0)
		TNT1 A 0 A_PlaySound("afrit/hellfire")
		FRTF DEFGHIJKLMNO 3 BRIGHT
		Stop
	}
}