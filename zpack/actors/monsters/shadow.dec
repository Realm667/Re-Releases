//Shadow
ACTOR Shadow
{
	//$Category ZPack Monsters
	Health 80
	Radius 20
	Height 56
	Mass 100
	RenderStyle "Translucent"
	Alpha 0.7
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	PainSound "Shadow/pain"
	DeathSound "Shadow/death"
	ActiveSound "Shadow/active"
	Obituary "$OB_SHADW"
	MissileType ShadowBall
	States
	{
	Spawn:
	See:
		SHDW A 15
		SHDW AAA 1 A_Chase
		SHDW A 0 A_CustomMissile("ShadowGhostA", 0, 0, 0)
		SHDW BBB 1 A_Chase
		SHDW B 0 A_CustomMissile("ShadowGhostB", 0, 0, 0)
		SHDW CCC 1 A_Chase
		SHDW C 0 A_CustomMissile("ShadowGhostC", 0, 0, 0)
		SHDW DDD 1 A_Chase
		SHDW D 0 A_CustomMissile("ShadowGhostD", 0, 0, 0)
		SHDW AAA 1 A_Chase
		SHDW A 0 A_CustomMissile("ShadowGhostA", 0, 0, 0)
		SHDW BBB 1 A_Chase
		SHDW B 0 A_CustomMissile("ShadowGhostB", 0, 0, 0)
		SHDW CCC 1 A_Chase
		SHDW C 0 A_CustomMissile("ShadowGhostC", 0, 0, 0)
		SHDW DDD 1 A_Chase
		SHDW D 0 A_CustomMissile("ShadowGhostD", 0, 0, 0)
		Goto See+1
	Missile:
		SHDW E 30 A_FaceTarget
		SHDW F 5
		SHDW G 5 BRIGHT A_MissileAttack
		SHDW F 5
		SHDW E 20
		SHDW E 1 A_SpidRefire
		Goto Missile+1
	Pain:
		SHDW H 4
		SHDW H 4 A_Pain
		Goto See+1
	Death:
		SHDW I 6 A_Scream
		SHDW J 5
		SHDW K 5 A_CustomMissile("ShadowTorso", 0, 0, 0)
		SHDW L 5 A_Fall
		SHDW MNO 6
		SHDW P -1
		Stop
	XDeath:
		SHDX A 8
		SHDX B 6 A_XScream
		SHDX C 6
		SHDX D 6 A_Fall
 		SHDX E 6
 		SHDX F 6
		SHDX G -1
		Stop
	Raise:
		SHDW M 8
		SHDW KLJI 8
		Goto See
	}
}

ACTOR ShadowBall
{
	Radius 6
	Height 8
	Speed 12
	Damage 4
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	SeeSound "Shadow/attack"
	DeathSound "imp/shotx"
	MissileType ShadowTrail
	MissileHeight 0
	States
	{
	Spawn:
		SBAL ABC 4 BRIGHT A_MissileAttack
		Loop
	Death:
		SBAL C 5 BRIGHT
		SBAL DEFGH 4 BRIGHT
		Stop
	}
}

ACTOR ShadowGhostA
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		SHDW A 10
		Stop
	}
}

ACTOR ShadowGhostB : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW B 10
		Stop
	}
}

ACTOR ShadowGhostC : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW C 10
		Stop
	}
}

ACTOR ShadowGhostD : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW D 10
		Stop
	}
}

ACTOR ShadowTorso
{
	Radius 12
	Height 15
	Speed 1
	Mass 5000
	RenderStyle "Translucent"
	Alpha 0.7
	+DOOMBOUNCE
	States
	{
	Spawn:
		SHDW QRS 5
		SHDW T 5 A_Fall
		SHDW U 5
		SHDW V -1
		Stop
	}
}

ACTOR ShadowTrail
{
	Radius 1
	Height 1
	Speed 0
	PROJECTILE
	RenderStyle Add
	Alpha 0.6
	+NOCLIP
	States
	{
	Spawn:
		SHTR ABCDEF 4 BRIGHT
		Stop
	}
}