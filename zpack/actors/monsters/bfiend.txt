// Blood Fiend
ACTOR Bloodfiend : Demon
{
	//$Category "ZPack Monsters"
	Health 250
	PainChance 140
	Speed 12
	Radius 30
	Height 56
	Mass 450
	MONSTER
	+FLOORCLIP
	+LONGMELEERANGE
	+DONTHURTSPECIES
	Obituary "$OB_BFIEN"
	HitObituary "$HB_BFIEN"
	SeeSound "monster/sg2sit"
	AttackSound "monster/sg2atk"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/sg2act"
	States
	{
	Spawn:
		SAR2 AB 10 A_Look
		Loop
	See:
		SAR2 AABBCCDD 2 A_Chase
		Loop
	Melee:
		SAR2 EF 8 A_FaceTarget
		SAR2 G 8 A_SargAttack
		Goto See
	Missile:
		SAR2 GF 8 A_FaceTarget
		SAR2 E 0 A_PlaySound ("Weapons/bloodf")
		SAR2 E 0 A_CustomMissile ("Bloodshot",22,0,-1,0,0)
		SAR2 E 8 A_CustomMissile ("Bloodshot",22,0,1,0,0)
		Goto See
	Pain:
		SAR2 H 2
		SAR2 H 2 A_Pain
		Goto See
	Death:
		SAR2 I 8
		SAR2 J 8 A_Scream
		SAR2 K 4
		SAR2 L 4 A_NoBlocking
		SAR2 M 4
		SAR2 N -1
		Stop
	XDeath:
		SAR2 O 5
		SAR2 P 5 A_XScream
		SAR2 Q 5 A_NoBlocking
		SAR2 RSTUV 5
		SAR2 W -1
		Stop
	Raise:
		SAR2 NMLKJI 5
		Goto See
	}
}

ACTOR Bloodshot
{
	Radius 3
	Height 3
	Speed 20
	Damage 2
	PROJECTILE
	+THRUGHOST
	RenderStyle Translucent
	Alpha 0.80
	DeathSound "weapons/bloodx"
	States
	{
	Spawn:
		FBLD A 2 BRIGHT A_CStaffMissileSlither
		FBLD A 0 A_SpawnItem("Bloodtrail",0,0)
		FBLD B 2 BRIGHT A_CStaffMissileSlither
		FBLD B 0 A_SpawnItem("Bloodtrail",0,0)
		Loop
	Death:
		FBLD CDE 3 BRIGHT
		Stop
	}
}

ACTOR Bloodtrail
{
	Radius 1
	Height 1
	PROJECTILE
	RenderStyle Translucent
	Alpha 0.67
	States
	{
	Spawn:
		TNT1 A 3
		FBLD FGH 3 BRIGHT
		Stop
	}
}

ACTOR Lurker : Bloodfiend
{
	RenderStyle Translucent
	Obituary "$OB_LURKR"
	HitObituary "$HB_LURKR"
	//DropItem BloodAmmo
	Alpha 0.25
}