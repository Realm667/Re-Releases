Class Cybruiser : BaronOfHell
{
	Default
	{
		//$Category ZPack Monsters
		Health 2000;
		GibHealth 35;
		Radius 24;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 20;
		SeeSound "monster/brusit";
		PainSound "baron/pain";
		DeathSound "monster/brudth";
		Obituary "$OB_CYBRS";
		HitObituary "$HB_CYBRS";
		MONSTER;
		+BOSS
		+DONTHARMCLASS
		+FLOORCLIP
		+MISSILEMORE
	}

	States
	{
	Spawn:
		BRU2 AB 10 A_Look();
		Loop;
	See:
		BRU2 A 1 A_StartSound("monster/bruwlk");
		BRU2 A 2 A_Chase();
		BRU2 A 3 A_Chase();
		BRU2 BB 3 A_Chase();
		BRU2 C 1 A_StartSound("monster/bruwlk");
		BRU2 C 2 A_Chase();
		BRU2 C 3 A_Chase();
		BRU2 DD 3 A_Chase();
		Loop;
	Missile:
		BRU2 E 8 A_FaceTarget();
		BRU2 F 12 A_SpawnProjectile("BruiserMissile",38,15,0,0);
		BRU2 E 12 A_FaceTarget();
		BRU2 F 12 A_SpawnProjectile("BruiserMissile",38,15,0,0);
		BRU2 E 12 A_FaceTarget();
		BRU2 F 12 A_SpawnProjectile("BruiserMissile",38,15,0,0);
		Goto See;
	Melee:
		BRU2 GH 8 A_FaceTarget();
		BRU2 I 8 A_CustomMeleeAttack(10*random(1,8), "baron/melee");
		Goto See;
	Pain:
		BRU2 J 2;
		BRU2 J 2 A_Pain();
		Goto See;
	Death:
		BRU2 K 6 A_Scream();
		BRU2 LMN 6;
		BRU2 O 6 A_NoBlocking();
		BRU2 QR 6;
		BRU2 S 6;
		BRU2 T -1 A_BossDeath();
		Stop;
	}
}

Class BruiserMissile : Actor
{
	Default
	{
		Radius 8;
		Height 12;
		Speed 20;
		Damage 10;
		DamageType "Fire";
		Scale 1.0;
		RenderStyle "STYLE_Normal";
		SeeSound "monster/brufir";
		DeathSound "weapons/hellex";
		PROJECTILE;
		+BRIGHT
		+THRUGHOST
	}

	States
	{
	Spawn:
		FBRS A 1;
		FBRS A 1 A_SpawnItemEx("BruiserTrail", flags: SXF_CLIENTSIDE);
		Loop;
	Death:
		BAL3 C 0 A_SetTranslucent(0.67,1);
		BAL3 C 6;
		BAL3 D 6 A_Explode(64,64,0);
		BAL3 E 6;
		Stop;
	}
}

Class BruiserTrail : Actor
{
	Default
	{
		Radius 3;
		Height 3;
		RenderStyle "STYLE_Translucent";
		Alpha 0.67;
		PROJECTILE;
		+BRIGHT
	}

	States
	{
	Spawn:
		TNT1 A 3;
		PUFF ABCD 4;
		Stop;
	}
}