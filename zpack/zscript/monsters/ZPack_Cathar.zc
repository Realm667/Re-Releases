Class Catharsi : Actor
{
	int sweep;

	Default
	{
		//$Category ZPack Monsters
		Health 120;
		GibHealth 35;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 140;
		SeeSound "catharsi/sit";
		PainSound "catharsi/pain";
		DeathSound "catharsi/death";
		ActiveSound "imp/active";
		Obituary "$OB_CATHS";
		MONSTER;
		+FLOORCLIP
	}

	States
	{
	Spawn:
		SDDM AB 10 A_Look();
		Loop;
	See:
		SDDM AABBCCDD 3 A_Chase();
		Loop;
	Missile:
		// [Ghastly] Changed to randompick because ZScript already isn't in Zandronum as of the time of writing.
		SDDM E 0 { sweep = randompick(-1,1); }
		SDDM E 4;
		SDDM E 10 A_FaceTarget();
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, -4*sweep);
		SDDM E 2;
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, 0);
		SDDM E 2;
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, 4*sweep);
		SDDM E 2;
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, 8*sweep);
		SDDM E 2;
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, 12*sweep);
		SDDM E 2;
		SDDM F 2 A_SpawnProjectile("CatharsiBall", 30, 8, 16*sweep);
		SDDM E 2;
		Goto See;
	Pain:
		SDDM G 3;
		SDDM G 3 A_Pain();
		Goto See;
	Death:
		SDDM H 5;
		SDDM I 5 A_Scream();
		SDDM J 5 A_NoBlocking();
		SDDM J 0 A_SpawnItem("CatharsiDetonator", 0, 30);
		SDDM KLM 5;
		SDDM N -1;
		Stop;
	XDeath:
		SDDM O 5;
		SDDM P 5 A_XScream();
		SDDM Q 5 A_NoBlocking();
		SDDM RSTUV 5;
		SDDM W -1;
		Stop;
	Raise:
		SDDM NMLKJIH 5;
		Goto See;
	}
}

Class CatharsiBall : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 15;
		Damage 3;
		RenderStyle "STYLE_Add";
		SeeSound "catharsi/shotsit";
		DeathSound "catharsi/shotdth";
		PROJECTILE;
		+BRIGHT
		+RANDOMIZE
	}

	States
	{
	Spawn:
		SDFB A 4;
		Loop;
	Death:
		SDFB BCDE 6;
		Stop;
	}
}

Class CatharsiDetonator : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		DeathSound "catharsi/shotdth";
		Obituary "$OB_CATHD";
		+DONTFALL
		+FLOATBOB
		+ISMONSTER
		+NOBLOOD
		+NOGRAVITY
		+RANDOMIZE
		+SHOOTABLE
		+SOLID
	}

	States
	{
	Spawn:
		THRM A 4 A_StartSound("catharsi/thermtick");
		THRM B 16;
		THRM A 4 A_StartSound("catharsi/thermtick");
		THRM B 16;
		THRM A 4 A_StartSound("catharsi/thermtick");
		THRM B 16;
		THRM A 4 A_StartSound("catharsi/thermtick");
		THRM B 16;
		THRM A 4 A_StartSound("catharsi/thermtick");
		THRM B 16;
		SDFB B 0
		{
			A_SpawnProjectile("CatharsiBall", 4, 2, 0);
			A_SpawnProjectile("CatharsiBall", 4, 2, 20);
			A_SpawnProjectile("CatharsiBall", 4, 2, 40);
			A_SpawnProjectile("CatharsiBall", 4, 2, 60);
			A_SpawnProjectile("CatharsiBall", 4, 2, 80);
			A_SpawnProjectile("CatharsiBall", 4, 2, 100);
			A_SpawnProjectile("CatharsiBall", 4, 2, 120);
			A_SpawnProjectile("CatharsiBall", 4, 2, 140);
			A_SpawnProjectile("CatharsiBall", 4, 2, 160);
			A_SpawnProjectile("CatharsiBall", 4, 2, 180);
			A_SpawnProjectile("CatharsiBall", 4, 2, 200);
			A_SpawnProjectile("CatharsiBall", 4, 2, 220);
			A_SpawnProjectile("CatharsiBall", 4, 2, 240);
			A_SpawnProjectile("CatharsiBall", 4, 2, 260);
			A_SpawnProjectile("CatharsiBall", 4, 2, 280);
			A_SpawnProjectile("CatharsiBall", 4, 2, 300);
			A_SpawnProjectile("CatharsiBall", 4, 2, 320);
			A_SpawnProjectile("CatharsiBall", 4, 2, 340);
			A_Die();
		}
	Death:
		SDFB B 0 { bFloatBob=false; }
		SDFB B 6 Bright A_Scream();
		SDFB CDE 6 Bright;
		Stop;
	}
}