Class BruiserDemon : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Health 1500;
		Radius 24;
		Height 64;
		Mass 2000;
		Speed 8;
		PainChance 15;
		SeeSound "superbaron/scream";
		PainSound "superbaron/pain";
		DeathSound "superbaron/death";
		ActiveSound "superbaron/act";
		Scale 1.25;
		Obituary "$OB_BRUSD";
		HitObituary "$HB_BRUSD";
		MONSTER;
		+BOSS
		+FLOORCLIP
		+LOOKALLAROUND
		+MISSILEEVENMORE
		+NORADIUSDMG
	}

	States
	{
	Spawn:
		BRUS AB 10 A_Look();
		Loop;
	See:
		BRUS AABBCCDD 3 A_Chase();
		Loop;
	Melee:
		BRUS EF 6 A_FaceTarget();
		BRUS G 6 A_BasicAttack(20, "baron/melee", "BruiserBall", 32);
		Goto See;
	Missile:
		BRUS E 0 A_Jump(144, "Missile2");
		BRUS EF 6 A_FaceTarget();
		BRUS G 6 A_SpawnProjectile("BruiserBall");
		BRUS G 0 A_Jump(96, 1);
		Goto See;
		BRUS HI 6 A_FaceTarget();
		BRUS J 6 A_SpawnProjectile("BruiserBall");
		Goto See;
	Missile2:
		BRUS E 0 A_Jump(96, "Missile3");
		BRUS E 6 A_FaceTarget();
		BRUS F 4 A_FaceTarget();
		BRUS F 2 A_SpawnProjectile("BruiserBall2",56,0,-40,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0,-30,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0,-20,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0,-10,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0,  0,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0, 10,0);
		BRUS G 1 A_SpawnProjectile("BruiserBall2",56,0, 20,0);
		BRUS G 0 A_Jump(96, 1);
		Goto See;
		BRUS H 6 A_FaceTarget();
		BRUS I 4 A_FaceTarget();
		BRUS I 2 A_SpawnProjectile("BruiserBall2",56,0, 40,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0, 30,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0, 20,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0, 10,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0,  0,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0,-10,0);
		BRUS J 1 A_SpawnProjectile("BruiserBall2",56,0,-20,0);
		Goto See;
	Missile3:
		BRUS KL 6 A_FaceTarget();
		BRUS M 2 A_SpawnProjectile("BruiserFireSpawner",16,0,-15,0);
		BRUS M 2 A_SpawnProjectile("BruiserFireSpawner",16,0,  0,0);
		BRUS M 2 A_SpawnProjectile("BruiserFireSpawner",16,0, 15,0);
		Goto See;
	Pain:
		BRUS N 5 A_Pain();
		Goto See;
	Death:
		BRUD A 6 A_Scream();
		BRUD BCDEFG 4;
		BRUD H 4 A_Fall();
		BRUD IJKLMNOP 4;
		BRUD QRSTUV 4;
		BRUD W -1;
		Stop;
	}
}

Class BruiserBall : Actor
{
	Default
	{
		Radius 16;
		Height 16;
		Speed 18;
		Damage 16;
		RenderStyle "STYLE_Add";
		Alpha 0.9;
		SeeSound "imp/attack";
		DeathSound "imp/shotx";
		PROJECTILE;
		+BRIGHT
		+RANDOMIZE;
	}

	States
	{
	Spawn:
		BRBA AABB 2 A_SpawnItemEx("BruiserBallTrail", flags: SXF_CLIENTSIDE);
		Loop;
	Death:
		BRBA KLMNOPQRSTUVWX 3;
		Stop;
	}
}

Class BruiserBall2 : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 10;
		Damage 8;
		RenderStyle "STYLE_Add";
		Alpha 0.9;
		SeeSound "imp/attack";
		DeathSound "imp/shotx";
		PROJECTILE;
		+BRIGHT
		+RANDOMIZE
	}

	States
	{
	Spawn:
		BRB2 AB 6;
		Loop;
	Death:
		BRB2 CDEFGHI 3;
		Stop;
	}
}

Class BruiserFireSpawner : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 12;
		Damage 0;
		PROJECTILE;
		+RIPPER
		+FLOORHUGGER
		+BLOODLESSIMPACT
	}

	States
	{
	Spawn:
		TNT1 A 3 Bright A_SpawnProjectile("BruiserFire", 0, 0, 0, CMF_TRACKOWNER);
		TNT1 AA 3;
		Loop;
	Death:
		TNT1 A 6;
		Stop;
	}
}

Class BruiserBallTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RenderStyle "STYLE_Add";
		Alpha 0.70;
		PROJECTILE;
		+BRIGHT
		+NOCLIP
	}

	States
	{
	Spawn:
		BRBA CDEFGHIJ 4;
		Stop;
	}
}

Class BruiserFire : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		Damage 20;
		RenderStyle "STYLE_Add";
		Alpha 0.9;
		SeeSound "weapons/rocklx";
		PROJECTILE;
		+BRIGHT
		+FLOORHUGGER
		+NOCLIP
	}

	States
	{
	Spawn:
		XXBF AB 3;
		XXBF C 3 A_Explode(48,128,0);
		XXBF DEFGHIJKLMNOPQRS 3;
		Stop;
	}
}