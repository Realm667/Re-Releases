//the spider boss
Class InfernalSpider : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Radius 72;
		Height 120;
		Health 12000;
		Speed 30;
		Mass 99999;
		MeleeRange 76;
		PainChance 3;
		Translation "152:159=216:223,9:9=223:223,76:70=216:223,129:132=216:223";
		Scale 2.0;
		SeeSound "spider2/sight";
		DeathSound "spider2/death";
		PainSound "spider2/pain";
		ActiveSound "spider2/active";
		Species "Infernal";
		MONSTER;
		+BOSS
		+DONTHARMCLASS
		+FLOORCLIP
		+MISSILEEVENMORE
		+MISSILEMORE
		+NORADIUSDMG
		+NOTARGET
		Obituary "$OB_INFSP";
		HitObituary "$HB_INFSP";
	}

	States
	{
	Spawn:
		SPI2 A 1 A_Look();
		Loop;
	See:
		TNT1 A 0 A_JumpIfCloser(1024,9);
		SPI2 E 12 A_FaceTarget();
		SPI2 E 0 A_StartSound("spider/step");
		SPI2 E 0 A_SetInvulnerable();
		SPI2 E 0 A_SkullAttack();
		SPI2 U 5 ThrustThingZ(0, 82, 0, 1);
		SPI2 U 38 A_JumpIf(Pos.Z-FloorZ<24,1);
		SPI2 A 0;
		SPI2 A 0 A_StartSound("spider/land");
		SPI2 A 0 A_UnSetInvulnerable();
		SPI2 A 1 A_Stop();
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		SPI2 ABCD 2 A_Chase();
		SPI2 A 0 A_StartSound("spider/step");
		Loop;
	Melee:
		SPI2 E 5 A_SpiderMelee();
		SPI2 A 13 A_FaceTarget();
		Goto See;
	Missile:
		SPI2 V 0 A_Jump(82,15);
		SPI2 V 0 A_Jump(32,8);
		SPI2 U 5 A_FaceTarget();
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, -8, 0);
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, 0, 0);
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, 8, 0);
		SPI2 X 3 A_FaceTarget();
		SPI2 U 5;
		Goto See;
		SPI2 E 5 A_FaceTarget();
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		SPI2 E 5;
		Goto See;
		SPI2 E 5 A_FaceTarget();
		SPI2 U 5 A_StartSound("spider2/sight");
		SPI2 W 0 A_FaceTarget();
		SPI2 X 5 A_SpawnProjectile("HellRipper", 128, 0, 0, 0);
		SPI2 X 17 A_FaceTarget();
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, -8, 0);
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, 0, 0);
		SPI2 X 2 A_SpawnProjectile("FlameShot", 24, 0, 8, 0);
		SPI2 U 5 A_FaceTarget();
		Goto See;
	Pain:
		SPI2 E 2 A_Pain();
		SPI2 E 0 A_FaceTarget();
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		SPI2 E 6 A_ThrowGrenade("MiniSpider", 85, 10, 20);
		Goto See;
	Death:
		SPI2 A 0 A_Scream();
		SPI2 A 0 A_SetFloorClip();
		SPI2 A 0 A_Burst("ArchvileFire2");
		Stop;
	}

	protected void A_SpiderMelee(void)
	{
		A_FaceTarget();
		A_XScream();
		A_CustomMeleeAttack(12);
	}
}

Class FlameShot : FatShot
{
	Default
	{
		Speed 25;
		SeeSound "skeleton/attack";
		+BRIGHT
	}

	States
	{
	Spawn:
		SXAL ABC 5;
		Loop;
	Death:
		SXAL DEFGH 3;
		Stop;
	}
}

Class HellRipper : FatShot
{
	Default
	{
		Health 5;
		Speed 50;
		Damage 30;
		SeeSound "skeleton/attack";
	}

	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_SpawnProjectile("ArchvileFire2",0,0,90,0);
		TNT1 A 0 BRIGHT A_SpawnProjectile("ArchvileFire2",0,0,-90,0);
		TNT1 A 0 A_JumpIf(Pos.Z-FloorZ<24,1);
		Loop;
		TNT1 A 0 A_Explode(50,64,0);
		TNT1 A 0 ThrustThingZ(0, random(2,3), 0, 0);
		Loop;
	Death:
		TNT1 B 5 A_Burst("ArchvileFire2");
		Stop;
	}
}

Class ArchvileFire2 : ArchvileFire
{
	Default
	{
		Damage 5;
		Speed 4;
	}
}

Class MiniSpider : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Health 200;
		Radius 26;
		Height 42;
		Speed 38;
		PainChance 200;
		Scale 1.0;
		Translation 1;
		Translation "96:111=64:79";
		SeeSound "spider2/sight";
		DeathSound "spider2/pain";
		PainSound "spider2/pain";
		ActiveSound "spider2/sight";
		Species "Infernal";
		MONSTER;
		+LOOKALLAROUND
		+NOTARGET
		+THRUSPECIES
		Obituary "$OB_MINSP";
		HitObituary "$OB_MINSP"; //same as above
	}

	States
	{
	Spawn:
		GAYS A 1 A_Look();
		Loop;
	See:
		GAYS ABCD 1 A_Chase();
		Loop;
	Melee:
		GAYS E 5 A_FaceTarget();
		GAYS E 0 A_XScream();
		GAYS E 8 A_CustomMeleeAttack(6);
		Goto See;
	Pain:
		GAYS A 2 A_Pain();
		GAYS A 2;
		Goto See;
	Death:
		GAYR FEDC 5 A_SetFloorClip();
		GAYR B 5 A_Scream();
		GAYR A 5 A_Die();
		Stop;
	}
}