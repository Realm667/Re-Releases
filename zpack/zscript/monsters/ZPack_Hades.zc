//Hades Sphere
Class HS : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Health 35;
		Radius 24; //the decorate version was set to 16 on ZPack
		Height 48;
		Mass 3000;
		Speed 10;
		DamageFactor "IncarnateShot", 0; //because of new Death Incarnate - ozy81
		MeleeThreshold 196; //here because ZPack variant had deprecated flag LONGMELEERANGE - ozy81
		RenderStyle "STYLE_Add"; //new addition by Ozy81, so the creature movement is more interesting
		Alpha 0.35; //new addition by Ozy81, so the creature movement is more interesting
		Obituary "$OB_HADES";
		SeeSound "monster/hadsit";
		DeathSound "monster/hadexp";
		MONSTER;
		+DONTFALL
		+DONTGIB
		+FLOAT
		+LOOKALLAROUND
		+NOBLOOD
		+NOBLOODDECALS
		+NOGRAVITY
		+NOTARGET
		+VISIBILITYPULSE //new addition by Ozy81, so the creature movement is more interesting
		//+DONTHARMCLASS // [MagicWazard] See "notes" below
	}

	States
	{
	Spawn:
		TNT1 A 1 Bright A_Look();
		Loop;
	See:
		HADE EFGH 4 Bright;
	SeeLoop:
		HADE A 2 Bright A_StartSound("monster/hadwlk");
		HADE ABBCCDD 2 Bright HS_SpecialChase();  // [Blue Shadow] Conditional A_Chase
		TNT1 A 0
		{
			if(!random(0, 7) && !bSkullfly) //A_Jump(32, "TeleportOut") when the SKULLFLY flag is off
			return ResolveState("TeleportOut");
			return ResolveState(null);
		}
		Loop;
	TeleportOut:
		TNT1 A 0 A_UnSetSolid();
		TNT1 A 0 A_UnSetShootable();
		HADE H 5 Bright A_StartSound("monster/hadtel");
		HADE GFEIJKL 4 Bright;
	SeeTeleport:
		TNT1 AAAAAAAA 2 A_Chase(null, null);
		TNT1 A 0 A_Jump(96, "TeleportIn");
		Loop;
	TeleportIn:
		HADE L 3 Bright A_StartSound("monster/hadtel");
		HADE KJIEFG 4 Bright;
		TNT1 A 0 A_SetShootable();
		TNT1 A 0 A_SetSolid();
		Goto SeeLoop;
	Melee:
		TNT1 A 0 A_SetInvulnerable();
		HADE HGFE 4 Bright;
		TNT1 A 0 A_UnSetInvulnerable();
		TNT1 A 0 A_SetShootable();
		TNT1 A 0 A_Die();
		Stop;
	Death:
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 A_SetTranslucent(0.9, 1);
		TNT1 A 0 A_Scream();
		HADE M 4 Bright A_Explode(112, 112, XF_NOTMISSILE);
		HADE NOPQ 5 Bright;
		Stop;
	XDeath:
		TNT1 A 0 A_NoBlocking();
		HADE H 5 Bright A_StartSound("monster/haddth");
		HADE GFE 4 Bright;
		HADE RSTUVWX 4 Bright;
		Stop;
	Death.Ice:
		"####" "#" 0 { bNoGravity=false; }
		Goto GenericFreezeDeath;
	}

	// [Blue Shadow] Depending on whether or not the Hades Sphere is currently flying
	// (due to A_SkullAttack), choose the appropriate "chase mode".
	// [ Ghastly ] This is now a new function, rather than an inventory item.
	void HS_SpecialChase()
	{
		if( bSkullFly )
		A_Chase(null, null);
		else
		A_Chase();
	}
}

// [MagicWazard] NOTES: +DONTHARMCLASS was originally enabled, but HS were still
// able to damage each other with self-destructs due to way the self-destruct
// was coded. Now that the self-destruct code has been changed, they are
// invulnerable to each others' explosions if that flag is enabled. Keep
// +DONTHARMCLASS disabled if you want the HS to be vunlerable to each others'
// explosions! Remove the comments and enable the flag if you want the HS to be
// immune to other HS' self-destruct damage.