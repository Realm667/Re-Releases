Class Diabloist : Archvile
{
	Default
	{
		//$Category ZPack Monsters
		Health 1400;
		GibHealth 35;
		Radius 20;
		Height 56;
		Mass 500;
		Speed 15;
		PainChance 10;
		BloodColor "08 08 08";
		SeeSound "monster/diasit";
		PainSound "monster/diapai";
		DeathSound "monster/diadth";
		ActiveSound "monster/diaact";
		Obituary "$OB_DIABL";
		MONSTER;
		+BOSS
		+DONTHARMCLASS
		+FIRERESIST
		+FLOORCLIP
		+MISSILEMORE
		+NORADIUSDMG
		+NOTARGET
	}

	States
	{
	Spawn:
		DIAB AB 10 A_Look();
		Loop;
	See:
		DIAB AABBCCDDEEFF 2 A_Chase();
		Loop;
	Missile:
		DIAB G 0 A_Jump(128, "Missile2");
		DIAB GH 3 A_FaceTarget();
		DIAB I 3 A_SpawnProjectile("DFire", 32, 0, 0);
		DIAB HGHIHGHIHGHIGHI 3 A_FaceTarget();
		DIAB G 0 A_Jump(128, "Missile3");
		Goto See;
	Missile2:
		DIAB N 5 A_FaceTarget();
		DIAB O 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB P 0 A_FaceTarget();
		DIAB P 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB N 0 A_FaceTarget();
		DIAB N 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB O 0 A_FaceTarget();
		DIAB O 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB P 0 A_FaceTarget();
		DIAB P 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB N 0 A_FaceTarget();
		DIAB N 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB O 0 A_FaceTarget();
		DIAB O 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB P 0 A_FaceTarget();
		DIAB P 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB N 0 A_FaceTarget();
		DIAB N 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB O 0 A_FaceTarget();
		DIAB O 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB P 0 A_FaceTarget();
		DIAB P 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB N 0 A_FaceTarget();
		DIAB N 5 A_SpawnProjectile("DFlare", 40, 0, Random(-4, 4));
		DIAB O 0 A_FaceTarget();
		DIAB P 5 Bright;
		DIAB P 0 A_Jump(64, "Missile3");
		DIAB P 0 A_Jump(128, "Missile4");
		Goto See;
	Missile3:
		DIAB GHIJKLM 5 A_FaceTarget();
		DIAB N 5 A_FaceTarget();
		DIAB O 1 A_StartSound("weapons/firbfi");
		DIAB O 4 A_SpawnProjectile ("DMissile",32,0,0);
		DIAB P 5;
		DIAB P 0 A_Jump(128,"Missile4");
		Goto See;
	Missile4:
		DIAB [ 8 A_FaceTarget();
		DIAB \ 8 A_FaceTarget();
		DIAB ] 0 A_SpawnProjectile("DTracer",0,-40,0);
		DIAB ] 8 A_SpawnProjectile("DTracer",0, 40,0);
		Goto See;
	Pain:
		DIAB Q 5;
		DIAB Q 5 A_Pain();
		Goto See;
	Death:
		DIAB Q 7;
		DIAB R 7 A_Scream();
		DIAB S 7 A_NoBlocking();
		DIAB TUVW 7;
		DIAB XY 5;
		DIAB Z -1;
		Stop;
	}
}

Class DFire : Actor
{
	Default
	{
		Radius 0;
		Height 1;
		Speed 0;
		RenderStyle "STYLE_Add";
		DamageType "Fire";
		Alpha 1.00;
		Obituary "$OB_DIABL";
		+BRIGHT
		+NODAMAGETHRUST
		+NOGRAVITY
		+NOTARGET
		+SEEKERMISSILE
	}

	States
	{
	Spawn:
		HLFR A 2 A_StartFire;
		HLFR BAB 2 DiabolistFire(4);
		HLFR C 2 DiabolistCrackle(4);
		HLFR B 2 DiabolistFire(4);
		HLFR CBCDCDCDEDED 2 DiabolistFire(5);
		HLFR E 2 DiabolistCrackle(4);
		HLFR F 2 DiabolistFire(4);
		HLFR EFE 2 DiabolistFire(3);
		HLFR FGH 2 DiabolistFire(2);
		HLFR GHGH 2 DiabolistFire(1);
		Stop;
}

void DiabolistFire(int damage)
{
	A_Explode(damage, 32);
	A_Fire();
}

void DiabolistCrackle(int damage)
{
	A_Explode(damage, 32);
	A_FireCrackle();
}
}

Class DFlare : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 25;
		Damage 4;
		RenderStyle "STYLE_Add";
		DamageType "Fire";
		Alpha 0.85;
		Seesound "weapons/firmfi";
		DeathSound "weapons/firex4";
		Obituary "$OB_DIABL";
		PROJECTILE;
		+BRIGHT
		+THRUGHOST
	}

	States
	{
	Spawn:
		VBAL AB 3 A_SpawnItemEx("MFlareFX", 0, 0, 0, 0, 0, 0, 0, 128);
		Loop;
	Death:
		CBAL CDEFG 3;
		Stop;
	}
}

Class DMissile : CFlameMissile
{
	Default
	{
		+THRUGHOST
	}
}

Class DTracer : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 15;
		ReactionTime 175;
		Damage 10;
		DamageType "Fire";
		RenderStyle "STYLE_Add";
		Alpha 0.67;
		Seesound "weapons/diasht";
		DeathSound "weapons/firex3";
		Obituary "$OB_DIABL";
		PROJECTILE;
		+BRIGHT
		+FLOORHUGGER
		+SEEKERMISSILE
		+THRUGHOST
		-NOGRAVITY
	}

	States
	{
	Spawn:
		TNT1 A 1 A_SeekerMissile(10, 15);
		TNT1 A 0 A_Countdown();
		TNT1 A 0 A_SpawnItemEx("DTracerPuff", 0, 0, 0, 0, 0, 0, 0, 128);
		Loop;
	Death:
		FTRA K 4;
		FTRA L 4 A_Explode(64, 64);
		FTRA MNO 3;
		Stop;
	}
}

Class MFlareFX : Actor
{
	Default
	{
		Radius 0;
		Height 1;
		Speed 0;
		RenderStyle "STYLE_Add";
		Alpha 0.67;
		PROJECTILE;
		+BRIGHT
	}

	States
	{
	Spawn:
		FDFX ABCDEF 4;
		Stop;
	}
}

Class DTracerPuff : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RenderStyle "STYLE_Add";
		DamageType "Fire";
		Alpha 0.67;
		PROJECTILE;
		+BRIGHT
		+FLOORHUGGER
		-NOGRAVITY
	}

	States
	{
	Spawn:
		FTRA ABCDEFGHIJ 3 A_Explode(4, 8);
		Stop;
	}
}

Class DMissileTrail : CFlameFloor
{
	Default
	{
		+THRUGHOST
	}
}

Class DMissileCircle : CircleFlame
{
	Default
	{
		+THRUGHOST
	}
}

Class XXLBall : Actor //is this used anywhere? original creature doesn't have it, so this comes from ZPack, kept for sake - ozy81
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 10;
		Damage 9;
		RenderStyle "STYLE_Translucent";
		Alpha 0.3;
		SeeSound "";
		DeathSound "weapons/firex4";
		PROJECTILE;
		+BRIGHT
		+RANDOMIZE
	}
	
	States
	{
	Spawn:
		XXL1 AB 4;
		Loop;
	Death:
		XXL1 CDE 6;
		Stop;
	}
}