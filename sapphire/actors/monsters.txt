//===========================================
//
//Recolored Blood - Kept for not breaking map
//
//===========================================

ACTOR BaronOfHellGreen : NashGore_BaronOfHell { BloodColor "77 EF 01" }
ACTOR CacoDemonBlue : NashGore_Cacodemon { BloodColor "01 C8 DF" }
ACTOR HellKnightGreen : NashGore_HellKnight { BloodColor "77 EF 01" }
ACTOR LostSoulSmoke : NashGore_LostSoul { -NOBLOODDECALS }

//========================
//
//Reworked Boss Demolisher
//
//========================

ACTOR HoleFlash : BeamFlash
{
	States
	{
	Pickup:
		TNT1 A 1 A_SetBlend("Purple",1,100)
		Stop
	}
}

ACTOR Demolisher
{
	Health 6000
	Radius 96
	Height 100
	Mass 1000
	Speed 16
	PainChance 20
	Monster
	+BOSS
	+FLOORCLIP
	+MISSILEEVENMORE
	+NORADIUSDMG
	+NOTARGET
	Obituary "$OB_DEMO"
	SeeSound "monster/demsit"
	AttackSound "monster/dematk"
	PainSound "spider/pain"
	DeathSound "monster/demdth"
	ActiveSound "spider/active"
	States
	{
	Spawn:
		DEMO AB 10 A_Look
		Loop
	See:
		DEMO A 3 A_Metal
		DEMO ABB 3 A_Chase
		DEMO C 3 A_Metal
		DEMO CDD 3 A_Chase
		DEMO E 3 A_Metal
		DEMO EFF 3 A_Chase
		Loop
	Missile:
		DEMO A 0 A_Jump (64,"Chaingun")
		DEMO A 20 A_FaceTarget
		DEMO T 1 A_StartSound("SDCHARGE",CHAN_WEAPON,CHANF_DEFAULT,1.0)
		DEMO T 4 A_FaceTarget
		DEMO T 4 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 0 { A_StartSound("SDBEAM",CHAN_AUTO,CHANF_DEFAULT,1.0); A_GiveToTarget("HoleFlash"); }
		DEMO U 10 A_SpawnProjectile ("DMBall",32,0,0,0,CMF_CHECKTARGETDEAD)
		DEMO T 0 A_Jump (128,11)
		DEMO T 1 A_StartSound("SDCHARGE",CHAN_WEAPON,CHANF_DEFAULT,1.0)
		DEMO T 4 A_FaceTarget
		DEMO T 4 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 5 A_FaceTarget
		DEMO T 0 { A_StartSound("SDBEAM",CHAN_AUTO,CHANF_DEFAULT,1.0); A_GiveToTarget("HoleFlash"); }
		DEMO U 10 A_SpawnProjectile ("DMBall",32,0,0,0,CMF_CHECKTARGETDEAD)
		DEMO T 0 A_Jump (192,1)
		Goto See
	Chaingun:
		DEMO A 20 A_FaceTarget
		DEMO G 0 A_SPosAttack
		DEMO G 4 A_SPosAttack
		DEMO H 0 A_SPosAttack
		DEMO H 4 A_SPosAttack
		DEMO H 1 A_SpidRefire
		Goto Missile+21
	Pain:
		DEMO I 3
		DEMO I 3 A_Pain
		Goto See
	Death:
		DEMO J 20 A_Scream
		DEMO K 10 A_NoBlocking
		DEMO LMNOPQR 10
		DEMO S 30
		DEMO S -1 A_BossDeath
		Stop
	}
}