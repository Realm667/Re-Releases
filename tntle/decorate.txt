//******************************************************************************************

actor NamiDarkImp 20000
{
    //$Category monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkSeeker
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

//******************************************************************************************

actor VoidDarkImp 19999
{
    //$Category monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkNormal
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Missile:
        DRKI E 0 A_Jump(64,4)
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkStrong", 32, 0, 0)
        DRKI E 0 A_Jump(64,4)
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto See
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

//******************************************************************************************

actor STDarkImp 19998
{
    //$Category monsters
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkFast
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

//******************************************************************************************

actor StoneImp 19997
{
    //$Category monsters
    Health 300
    Radius 20
    Height 56
    Speed 6
    PainChance 50
    Mass 400
    MONSTER 
    +FLOORCLIP
    +NOBLOOD
    +NORADIUSDMG
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "MaceHit"
    Obituary "%o was smashed by a stone imp."
    MeleeDamage 12
    States
    {
    Spawn:
        GIMP AB 10 A_Look
        Loop
    See:
        GIMP AABBCCDD 3 A_Chase
        Loop
    Melee:
        GIMP EF 8 A_FaceTarget
        GIMP G 6 A_MeleeAttack
        Goto See
    Pain:
        GIMP H 2
        GIMP H 2 A_Pain
        Goto See
    Death:
        GIMP I 8
        GIMP J 8 A_Scream
        GIMP K 6
        GIMP L 6 A_Fall
        GIMP M 6
        GIMP N -1
        Stop
    Raise:
        GIMP ML 8
        GIMP KJI 6
        Goto See
    }
}

//******************************************************************************************

actor Arachnophyte 19996
{
    //$Category monsters
    Health 500
    Radius 64
    Height 100
    Speed 6
    PainChance 0
    Mass 1000
    MONSTER 
    +FLOORCLIP
    +NOGRAVITY
    +FLOATBOB
    +FLOAT
    SeeSound "spider/sight"
    DeathSound "spider/death"
    Obituary "%o was killed by an Arachnophyte."
    States
    {
    Spawn:
        APYT AB 4 A_Look
        Loop
    See:
        APYT AABB 2 A_Chase
        Loop
    Missile:
        APYT BABAB 4 A_FaceTarget
        APYT CD 4 A_SPosAttack
        APYT D 1 A_SpidRefire
        Goto Missile+5
    Death:
        APYT E 8
        APYT F 8 A_Scream
        APYT G 6
        APYT H 6 A_Fall
        APYT I 6
        APYT J 6
        Stop
    }
}

//******************************************************************************************

actor ImpWarlord 19995
{
    //$Category monsters
    Health 180
    Radius 20
    Height 56
    Speed 9
    PainChance 128
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "MaceHit"
    Obituary "%o couldn't run from the imp warlord's fireball."
    HitObituary "%o was beaten to death by an imp warlord."
    MeleeDamage 7
    States
    {
    Spawn:
        WARI AB 10 A_Look
        Loop
    See:
        WARI AABBCCDD 3 A_Chase
        Loop
    Melee:
        WARI E 6 A_FaceTarget
        WARI E 0 A_PlaySound("MaceSwing")
        WARI F 4 A_FaceTarget
        WARI G 5 A_MeleeAttack
        Goto See
    Missile:
        WARI E 0 A_Jump(38,23)  //20% chance: jump to familiar drop
        WARI E 0 A_Jump(77,2)   //30% chance: jump to heat seekers
        WARI E 0 A_Jump(128,11) //50% chance: jump to green fireballs
        Goto Missile+28         //Otherwise : jump to red fireball
        WARI I 0 A_SetInvulnerable
        WARI I 6 A_FaceTarget
        WARI J 0 A_PlaySound("ShieldShot")
        WARI J 6 bright A_FaceTarget
        WARI J 0 A_Jump(128,4)
        WARI K 0 bright A_CustomMissile("YellowSkullSeeker", 32, 0, -45)
        WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 45)
        WARI K 0 A_UnSetInvulnerable
        Goto See
        WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 0)
        WARI K 0 A_UnSetInvulnerable
        Goto See
        WARI L 7 A_FaceTarget
        WARI M 0 A_PlaySound("MaceSwing")
        WARI M 5 A_FaceTarget
        WARI M 0 A_PlayWeaponSound("imp/attack")
        WARI M 0 A_Jump(128,3)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 40, 0, -8, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 24, 0, 8, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 32, 0, 0, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 36, 0, -4, 1)
        WARI N 6 A_CustomMissile("GreenPoisonBall", 28, 0, 4, 1)
        Goto See
        WARI GF 8 A_FaceTarget
        WARI F 0 A_Jump(128,2)
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, -30)
        Goto See
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, 30)
        Goto See
        WARI O 0 A_Jump(128,5)
        WARI O 7 A_FaceTarget
        WARI P 0 A_PlaySound("MaceSwing")
        WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        WARI O 7 A_FaceTarget
        WARI P 0 A_PlaySound("MaceSwing")
        WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        Goto See
    Pain:
        WARI H 0 A_Jump(128,2)
        WARI H 8 A_Pain
        Goto See
        WARI I 0 A_SetInvulnerable
        WARI IIIIII 4 A_FaceTarget
        WARI I 0 A_Jump(160,5)
        WARI IIII 4 A_FaceTarget
        Goto Pain+4
        WARI I 0 A_UnSetInvulnerable
        Goto See
    Death:
        WARI R 0 A_CustomMissile("WarlordMace", 32, 0, -90)
        WARI R 0 A_CustomMissile("WarlordShield", 32, 0, 90)
        WARI R 8
        WARI S 8 A_Scream
        WARI T 6
        WARI U 6 A_Fall
        WARI V -1
        Stop
    Raise:
        WARI VU 8
        WARI TSR 6
        Goto See
    }
}

//******************************************************************************************

actor DragonFamiliar 19994
{
    //$Category monsters
    Health 25
    Radius 16
    Height 32
    Speed 10
    PainChance 200
    MONSTER 
    +FLOORCLIP
    +NOGRAVITY
    +FLOAT
    +STRIFEDAMAGE
    SeeSound "DragonSee"
    PainSound "DragonPain"
    DeathSound "DragonDeath"
    ActiveSound "DragonIdle"
    Obituary "%o was cooked by a dragon familiar."
    States
    {
    Spawn:
        DFAM ABCD 10 A_Look
        Loop
    See:
        DFAM AABBCCDD 3 A_Chase
        Loop
    Missile:
        DFAM E 0 A_PlaySound("DragonAttack")
        DFAM E 0 A_Jump(208,7)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto See
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto Missile+1
    Pain:
        DFAM F 2
        DFAM F 2 A_Pain
        Goto See
    Death:
        DFAM G 0 A_Die
        DFAM G 8 A_NoBlocking 
        DFAM H 8 A_Scream
        DFAM I 6 A_Fall
        DFAM JKL 6
        DFAM M -1
        Stop
    }
}

//******************************************************************************************

actor BatFamiliar 19993
{
    //$Category monsters
    Health 10
    Radius 12
    Height 24
    Speed 14
    PainChance 200
    MONSTER 
    +FLOORCLIP
    +NOGRAVITY
    +FLOAT
    SeeSound "BatIdle"
    PainSound "BatPain"
    DeathSound "BatDeath"
    ActiveSound "BatIdle"
    MeleeSound "BatIdle"
    HitObituary "%o was scratched by a bat familiar"
    MISSILEHEIGHT 0
    MeleeDamage 1
    States
    {
    Spawn:
        BFAM ABCB 3 A_Look
        Loop
    See:
        BFAM ABCB 3 A_Chase
        Loop
    Melee:
        BFAM A 3 A_FaceTarget
        BFAM B 3 A_MeleeAttack
        BFAM CB 3 A_FaceTarget
        Goto See
    Pain:
        BFAM A 2
        BFAM A 2 A_Pain
        Goto See
    Death:
        BFAM D 0 A_Die
        BFAM D 0 A_NoBlocking 
        BFAM D 5 A_Scream
        BFAM E 5 A_Fall
        BFAM FDEF 5
        Goto Death+5
    Crash:
        BFAM G 6 A_PlaySound("BatCrash")
        BFAM H 7
        BFAM I 8
        BFAM J -1
        Stop
    }
}

//******************************************************************************************

ACTOR SnakeImp 19992
{
	//$Category monsters
        Health		100
	Radius		20
	Height		60
	Speed		8
	MeleeDamage	8
	
	SeeSound	"imp/sight"
	ActiveSound	"imp/active"
	PainSound	"imp/pain"
	MeleeSound	"imp/melee"
	DeathSound	"imp/death"
	AttackSound	"imp/attack"
	
	Obituary "%o tasted the venom of the snake imp."
	HitObituary "%o was shredded by a snake imp."
	
	MONSTER
	+FLOORCLIP
	
	States
	{
		Spawn:
			SIMP AB 8 A_Look
			Loop
		See:
			SIMP AABBCCDD 4 A_Chase
			Loop
		Missile:
			SIMP EF 8 A_FaceTarget 
			SIMP G 8 A_CustomMissile("SnakeImpFireball", 40, 0, 0, 1)
			Goto See
		Pain:
			SIMP H 4 A_Pain
			SIMP H 4
			Goto See
		Death:
			SIMP I 6
			SIMP J 6 A_Scream
			SIMP KL 6
			SIMP M 6 A_Fall
			SIMP M -1
			Stop
		XDeath:
			SIMP N 6 A_XScream
			SIMP O 6 A_Fall
			SIMP PQRST 6
			SIMP U -1
			Stop
		Raise:
			SIMP LKJI 5
			Goto See
	}
}

//******************************************************************************************

actor Satyr 19991
{
    //$Category monsters
    Health 400
    Radius 24
    Height 64
    Speed 8
    PainChance 50
    Mass 1000
    MONSTER 
    +FLOORCLIP
    Scale .9
    SeeSound "knight/sight"
    PainSound "knight/pain"
    DeathSound "knight/death"
    ActiveSound "knight/active"
    MeleeSound "baron/melee"
    HitObituary "%o was mauled by a satyr."
    MeleeDamage 8
    States
    {
    Spawn:
        STYR AB 10 A_Look
        Loop
    See:
        STYR AABBCCDD 3 A_Chase
        Loop
    Melee:
        STYR EF 8 A_FaceTarget
        STYR G 8 A_MeleeAttack
        Goto See
    Pain:
        STYR H 2
        STYR H 2 A_Pain
        Goto See
    Death:
        STYR I 8
        STYR J 8 A_Scream
        STYR K 8
        STYR L 8 A_Fall
        STYR MN 8
        STYR O -1
        Stop
    Raise:
        STYR ONMLKJI 8
        Goto See
    }
}

//******************************************************************************************

ACTOR BloodDemonClone 19990
{ 
    //$Category monsters
    Health 300 
    Speed 12 
    Height 56
    Radius 30
    Mass 400
    PainChance 180
    Meleedamage 5 
    REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
    SeeSound "demon/sight"
    PainSound "demon/pain"
    DeathSound "demon/death"
    ActiveSound "demon/active"
    MeleeSound "demon/melee"
    HitObituary "%o Was chewed up and spat out by the Blood Demon." 
    States 
    { 
    Spawn: 
       SRG2 AB 10 A_Look
       Loop 
    See: 
       SRG2 AABBCCDD 2 A_Chase
       Loop 
    Melee: 
       SRG2 EF 8 A_FaceTarget
       SRG2 G 8 A_MeleeAttack
       Goto See 
    Pain: 
       SRG2 H 2
       SRG2 H 2 A_Pain
       Goto See
    Death: 
       SRG2 I 8 
       SRG2 J 8 A_Scream
       SRG2 K 4 
       SRG2 L 4 A_NoBlocking
       SRG2 M 4 
       SRG2 N -1 
       Stop 
    Raise: 
       SRG2 NMLKJI 5 
       Goto See 
    } 
} 

//******************************************************************************************

ACTOR CacolanternClone 19989
{ 
   //$Category monsters
   Health 800 
   Speed 8 
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6 
   REACTIONTIME 8 
   MONSTER
   +FLOORCLIP
   +NOGRAVITY
   +FLOAT
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   Missiletype CacolanternBall 
   HitObituary "%o admired the Cacolantern's teeth." 
   Obituary "%o couldn't take %h eyes off the eye." 
   States 
   { 
   Spawn: 
      HED2 A 10 bright A_Look 
      Loop 
   See: 
      HED2 A 3 bright A_Chase 
      Loop 
   Missile: 
      HED2 BC 5 bright A_FaceTarget 
      HED2 D 5 bright A_ComboAttack
      Goto See 
   Pain: 
      HED2 E 4 bright
      HED2 E 4 bright A_Pain 
      HED2 F 5 bright
      Goto See 
   Death: 
      HED2 G 8 bright
      HED2 H 8 bright A_Scream 
      HED2 I 8 bright
      HED2 J 8
      HED2 K 8 A_NoBlocking 
      HED2 L -1 
      Stop 
   Raise: 
      HED2 LKJ 8
      HED2 IHG 8 bright 
      Goto See 
   } 
} 

//******************************************************************************************

ACTOR BelphegorClone 19988
{ 
   //$Category monsters
   Health 1500 
   Speed 8
   Radius 24
   Height 64
   PainChance 50
   Mass 1000
   Meleedamage 15 
   SeeSound "baron/sight"
   PainSound "baron/pain"
   DeathSound "baron/death"
   ActiveSound "baron/active"
   MeleeSound "baron/melee"
   REACTIONTIME 8 
   MONSTER 
   Missiletype BelphegorBall 
   Obituary "%o was incinerated by a Belphegor." 
   HitObituary "the last thing %o saw was the Belphegor's abs." 
   States 
   { 
   Spawn: 
      BOS3 AB 10 A_Look 
      Loop 
   See: 
      BOS3 AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
      BOS3 E 8 A_FaceTarget 
      BOS3 F 8 A_FaceTarget 
      BOS3 G 8 A_ComboAttack 
      BOS3 F 8 A_FaceTarget 
      BOS3 G 8 A_ComboAttack 
      BOS3 F 8 A_FaceTarget 
      BOS3 G 8 A_ComboAttack 
      Goto See 
   Melee: 
      BOS3 EF 8 A_FaceTarget 
      BOS3 G 8 A_ComboAttack 
      Goto See 
   Pain: 
      BOS3 H 2 
      BOS3 H 2 A_Pain 
      Goto See 
   Death: 
      BOS3 I 8 
      BOS3 J 8 A_Scream 
      BOS3 K 8 
      BOS3 L 8 A_NoBlocking 
      BOS3 MN 8  
      BOS3 O -1 
      Stop 
   Raise: 
      BOS3 ONMLKJI 8 
      Goto See 
   } 
}

//******************************************************************************************
//******************************************************************************************

ACTOR ZombieMarine 19986
{ 

    //$Category monsters
    Health 100
    Radius 20
    Height 56
    Speed 9
    PainChance 200
    Damage 1
    MONSTER 
    +FLOORCLIP
    Obituary "%o was killed by A Zombie Marine!"
    SeeSound "grunt/sight"
    AttackSound "RifleShot"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    DropItem Clip
    DropItem Clip
    States
    {
    Spawn:
        UDM2 AB 10 A_Look
        Loop
    See:
        UDM2 AABBCCDD 4 A_Chase
        Loop
    Missile:
        UDM2 E 2 A_FaceTarget
        UDM2 F 2 A_BulletAttack
        UDM2 E 2 A_Cposrefire
        Loop
    Pain:
        UDM2 G 5
        UDM2 G 3 A_Pain
        Goto See
    Death:
        UDM2 H 5
        UDM2 I 5 A_Scream
        UDM2 J 5 A_Fall
        UDM2 KLM 5
        UDM2 N -1
        Stop
    XDeath:
        UDM2 O 5
        UDM2 P 5 A_XScream
        UDM2 Q 5 A_Fall
        UDM2 RSTU 5
        UDM2 V -1
        Stop
    Raise:
        UDM2 NMLKJIH 5
        Goto See
    }
} 

//******************************************************************************************

ACTOR RapidFireTrooper 19985
{
   //$Category monsters
   Health 30
   Radius 20
   Height 56
   Speed 8
   PainChance 200
   MONSTER 
   +FLOORCLIP
   DropItem Clip
   SeeSound "grunt/sight"
   AttackSound "grunt/attack"
   PainSound "grunt/pain"
   DeathSound "grunt/death"
   ActiveSound "grunt/active"
   Obituary "%o was surprised by the zombie's weaponry." 
   States 
   { 
   Spawn: 
      RFTR AB 10 A_Look 
      Loop 
   See: 
      RFTR AABBCCDD 4 A_Chase 
      Loop 
   Missile: 
      RFTR E 10 A_FaceTarget 
      RFTR F 5 A_PosAttack 
      RFTR E 5 A_CPosRefire 
      Goto Missile+1 
   Pain: 
      RFTR G 3 
      RFTR G 3 A_Pain 
      Goto See 
   Death: 
      RFTR H 5 
      RFTR I 5 A_Scream 
      RFTR J 5 A_Fall 
      RFTR K 5 
      RFTR L -1 
      Stop
    XDeath:
      RFTR M 5
      RFTR N 5 A_XScream
      RFTR N 5
      RFTR P 5 A_Fall
      RFTR QRST 5
      RFTR U -1
      Stop
   Raise: 
      RFTR KJ 5 
      RFTR IH 5 
      Goto See 
   } 
} 

//******************************************************************************************

actor HellGuard 19984
{
    //$Category monsters
    Health 300
    Radius 24
    Height 64
    Speed 8
    PainChance 50
    Mass 500
    MONSTER 
    +FLOORCLIP
    SeeSound "knight/sight"
    PainSound "knight/pain"
    DeathSound "knight/death"
    ActiveSound "knight/active"
    Obituary "%o was fried by a hell guard."
    MeleeDamage 8
    States
    {
    Spawn:
        HLGD AB 10 A_Look
        Loop
    See:
        HLGD AABBCCDD 3 A_Chase
        Loop
    Missile:
        HLGD E 8 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto Missile+6
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto Missile+6
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto Missile+6
        HLGD E 4 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto Missile+12
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto Missile+12
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto Missile+12
        HLGD E 4 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto See
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto See
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto See
    Pain:
        HLGD G 2
        HLGD G 2 A_Pain
        Goto See
    Death:
        HLGD H 6
        HLGD I 6 A_Scream
        HLGD J 6
        HLGD K 6 A_Fall
        HLGD LM 6
        HLGD N -1
        Stop
    Raise:
        HLGD ONMLKJI 8
        Goto See
    }
}

//******************************************************************************************

actor CyberMastermind 19983
{
    //$Category monsters
    Health 5000
    Radius 128
    Height 100
    Speed 12
    PainChance 30
    Mass 1000
    MONSTER 
    +FLOORCLIP
    +BOSS
    +NORADIUSDMG 
    SeeSound "cyber/sight"
    PainSound "spider/pain"
    DeathSound "cyber/death"
    ActiveSound "spider/active"
    Obituary "%o was destroyed by a cyber mastermind."
    States
    {
    Spawn:
        CYMM AB 10 A_Look
        Loop
    See:
        CYMM AABB 3 A_Chase
        CYMM B 0 A_PlaySound("spider/walk")
        CYMM CCDD 3 A_Chase
        CYMM D 0 A_PlaySound("spider/walk")
        CYMM EEFF 3 A_Chase
        CYMM F 0 A_PlaySound("spider/walk")
        Loop
    Missile:
        CYMM A 20 A_FaceTarget
        CYMM GHGH 4 A_SPosAttack
        CYMM H 0 A_FaceTarget
        CYMM I 0 A_CyberAttack
        CYMM IJ 4 A_SPosAttack
        CYMM J 1 A_SpidRefire
        Goto Missile+1
    Pain:
        CYMM K 4
        CYMM K 4 A_Pain
        Goto See
    Death:
        CYMM L 8
        CYMM M 8 A_Scream
        CYMM N 6
        CYMM O 6 A_Fall
        CYMM PQRST 6
        CYMM U -1
        Stop
    }
}

//******************************************************************************************


//******************************************************************************************

actor SoulHarvester 19981
{
    //$Category monsters
    Health 100
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o couldn't evade the soul harvester's attack."
    HitObituary "%o was electrocuted by the soul harvester."
    MissileType SoulHarvesterBall
    MeleeDamage 3
    States
    {
    Spawn:
        SLHV AB 10 A_Look
        Loop
    See:
        SLHV AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        SLHV EFEFEF 7 A_FaceTarget
        SLHV G 6 A_MissileAttack
        Goto See
    Pain:
        SLHV H 2
        SLHV H 2 A_Pain
        Goto See
    Death:
        SLHV I 5
        SLHV J 5 A_XScream
        SLHV K 5 A_CustomMissile("SoulHarvesterGhost", 8, 0, 0)
        SLHV L 5 A_Fall
        SLHV MNO 5
        SLHV P -1
        Stop
    Raise:
        SLHV PONMLKJI 5
        Goto See
    }
}

//******************************************************************************************

ACTOR EnhancedCacodemon 19980
{ 
    //$Category monsters
    Health 300
   Speed 8 
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6 
   REACTIONTIME 8 
   MONSTER
   +FLOORCLIP
   +NOGRAVITY
   +FLOAT
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   HitObituary "%o was mowed down by an enhanced cacodemon." 
   Obituary "%o was made into a snack by the enhanced cacodemon." 
   States 
   { 
   Spawn: 
      ENCD A 10 A_Look 
      Loop 
   See: 
      ENCD A 3 A_Chase 
      Loop 
   Missile: 
      ENCD BC 5 A_FaceTarget 
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, 0, 1)
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, -8, 1)
      ENCD B 5 bright A_CustomMissile("CacodemonBall", 24, 0, 8, 1)
      Goto See 
   Pain: 
      ENCD D 4 
      ENCD D 4 A_Pain 
      ENCD D 5 
      Goto See 
   Death: 
      ENCD E 8 
      ENCD F 8 A_Scream 
      ENCD GH 8 
      ENCD I 8 A_NoBlocking 
      ENCD J -1 
      Stop 
   Raise: 
      ENCD JIHGFE 8 
      Goto See 
   } 
} 

//******************************************************************************************

ACTOR HellWarrior 19979
{ 
   //$Category monsters
   Health 400 
   Speed 8
   Radius 24
   Height 64
   PainChance 50
   Mass 1000
   Meleedamage 15 
   SeeSound "knight/sight"
   PainSound "knight/pain"
   DeathSound "knight/death"
   ActiveSound "knight/active"
   MeleeSound "baron/melee"
   REACTIONTIME 8 
   MONSTER 
   Missiletype HellWarriorBall
   Obituary "%o was tagged by the hell warrior's fireball." 
   HitObituary "%o was torn apart by a hell warrior." 
   States 
   { 
   Spawn: 
      HWAR AB 10 A_Look 
      Loop 
   See: 
      HWAR AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
   Melee: 
      HWAR EF 8 A_FaceTarget 
      HWAR G 8 A_ComboAttack 
      Goto See 
   Pain:
      HWAR H 0 A_Pain 
      HWAR H 0 A_SetInvulnerable
      HWAR HHHHHHHH 6 A_FaceTarget 
      HWAR H 2 A_UnSetInvulnerable
      Goto See 
   Death: 
      HWAR I 8 
      HWAR J 8 A_Scream
      HWAR K 8
      HWAR L 8 A_NoBlocking 
      HWAR MN 8  
      HWAR O -1 
      Stop 
   Raise: 
      HWAR ONMLKJI 5 
      Goto See 
   } 
}

//******************************************************************************************

ACTOR RocketGuy 19978
{ 
   //$Category monsters
   Health 50 
   Radius 20 
   Height 56 
   Mass 100 
   Speed 8 
   PainChance 200 
   Monster 
   +FLOORCLIP 
   SeeSound "grunt/sight" 
   PainSound "grunt/pain" 
   Deathsound "grunt/death" 
   ActiveSound "grunt/active"
   DropItem RocketAmmo
   Obituary "%o was splattered by the zombie's rocket." 
   States 
   { 
   Spawn: 
      MPOS AB 10 A_Look 
      Loop 
   See: 
      MPOS AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
      MPOS EE 10 A_FaceTarget 
      MPOS F 5 
      MPOS G 13 A_CyberAttack 
      Goto See 
   Pain: 
      MPOS H 2 
      MPOS H 2 A_Pain 
      Goto See 
   Death: 
      MPOS I 5 
      MPOS J 5 A_Scream 
      MPOS K 5 
      MPOS L 5 A_Fall 
      MPOS M -1 
      Stop 
   XDeath: 
      MPOS N 5 
      MPOS O 5 A_XScream 
      MPOS P 5 
      MPOS Q 5 A_Fall 
      MPOS RSTU 5 
      MPOS V -1    
      Stop 
   Raise: 
      MPOS MJKJI 5 
      Goto See 
   } 
} 

//******************************************************************************************

//******************************************************************************************

ACTOR ChainsawZombie 19976
{ 

    //$Category monsters 
    Health 30 
    Radius 20 
    Height 56 
    Speed 13 
    MeleeDamage 1 
    PainChance 200 
    MONSTER 
    +FLOORCLIP 
    Obituary "%o was sliced by a madman zombie with a chainsaw!" 
    SeeSound "grunt/sight" 
    MeleeSound "weapons/sawhit" 
    PainSound "grunt/pain" 
    DeathSound "grunt/death" 
    ActiveSound "grunt/active" 
    DropItem Chainsaw 
    States 
    { 
    Spawn: 
        CHAN A 4 A_Look 
        CHAN A 4 A_PlayWeaponSound("weapons/sawidle") 
        CHAN A 4 A_Look 
        CHAN B 4 A_PlayWeaponSound("weapons/sawidle") 
        CHAN B 4 A_Look 
        CHAN B 4 A_PlayWeaponSound("weapons/sawidle") 
        Loop 
    See: 
        CHAN A 4 A_Chase 
        CHAN A 4 A_Chase 
        CHAN B 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN B 4 A_Chase 
        CHAN B 4 A_Chase 
        CHAN C 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN C 4 A_Chase 
        CHAN C 4 A_Chase 
        CHAN D 0 A_PlayWeaponSound("weapons/sawidle") 
        CHAN D 4 A_Chase 
        CHAN D 4 A_Chase 
        CHAN A 0 A_PlayWeaponSound("weapons/sawidle") 
        Loop 
    Melee: 
        CHAN E 1 A_FaceTarget 
        CHAN F 3 A_MeleeAttack 
        Goto See 
    Pain: 
        CHAN G 5 
        CHAN G 3 A_Pain 
        Goto See 
    Death: 
        CHAN H 5 
        CHAN I 5 A_Scream 
        CHAN J 5 A_Fall 
        CHAN KLM 5 
        CHAN N -1 
        Stop 
    XDeath:
        CHAN O 5
        CHAN P 5 A_XScream
        CHAN Q 5 A_Fall
        CHAN RSTUV 5
        CHAN W -1
        Stop
    Raise: 
        CHAN NMLKJIH 5 
        Goto See 
    } 
} 

//******************************************************************************************

ACTOR PlasmaZombie 19975
{
    //$Category monsters
    Health 50
    Radius 20
    Height 56
    Speed 8
    PainChance 100
    MONSTER 
    +FLOORCLIP
    SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary "%o got burned by a plasma zombie."
    DropItem Cell
    MissileType PlasmaBall
    States
    {
    Spawn:
        ZMAY AB 10 A_Look
        Loop
    See:
        ZMAY AABBCCDD 4 A_Chase
        Loop
    Missile:
        ZMAY E 10 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        Goto See
    Pain:
        ZMAY G 3
        ZMAY G 3 A_Pain
        Goto See
    Death:
        ZMAY H 5
        ZMAY I 5 A_Scream
        ZMAY J 5 A_Fall
        ZMAY K 5
        ZMAY L -1
        Stop
    XDeath:
        ZMAY O 5
        ZMAY P 5 A_XScream
        ZMAY Q 5 A_Fall
        ZMAY RSTU 5
        ZMAY V -1
        Stop
    Raise:
        ZMAY KJIH 5
        Goto See
    }
}

//******************************************************************************************


//******************************************************************************************

ACTOR ZombieRailgunner 19973 
{ 
    //$Category monsters
    Health 45 
    Radius 20 
    Height 56 
    Speed 8
    Damage 100
    PainChance 200 
    MONSTER 
    +FLOORCLIP 
    Obituary "%o was killed by A Zombie Railgunner!" 
    SeeSound "grunt/sight" 
    PainSound "grunt/pain" 
    DeathSound "grunt/death" 
    ActiveSound "grunt/active" 
    States 
    { 
    Spawn: 
        RPOS AB 10 A_Look 
        Loop 
    See: 
        RPOS AABBCCDD 4 A_Chase 
        Loop 
    Missile: 
        RPOS EE 10 A_FaceTarget 
        RPOS F 5 A_MonsterRail 
        RPOS E 4 A_FaceTarget 
        Goto See 
    Pain: 
        RPOS G 5 
        RPOS G 3 A_Pain 
        Goto See 
    Death: 
        RPOS H 5 
        RPOS I 5 A_Scream 
        RPOS J 5 A_Fall 
        RPOS K 5 
        RPOS L -1 
        Stop 
    XDeath: 
        RPOS M 5 
        RPOS N 5 A_XScream 
        RPOS O 5 A_Fall 
        RPOS PQRST 5 
        RPOS U -1 
        Stop 
    Raise: 
        RPOS LKJIH 5 
        Goto See 
    } 
} 

//******************************************************************************************

ACTOR Afrit 19972
{ 
   //$Category monsters
   Health 800 
   Speed 6
   Radius 24
   Height 72
   PainChance 50
   Mass 500
   Meleedamage 10 
   SeeSound "baron/sight"
   PainSound "baron/pain"
   DeathSound "baron/death"
   ActiveSound "baron/active"
   MeleeSound "baron/melee"
   Missiletype "AfritBall"
   MissileHeight 44
   REACTIONTIME 8
   MONSTER
   +NORADIUSDMG
   +FLOORCLIP
   +NOGRAVITY
   +FLOATBOB
   +FLOAT
   Obituary "%o was scorched by an Afrit" 
   HitObituary "%o found the Afrit too hot to handle." 
   States 
   { 
   Spawn:
      FRIT ABCD 4 Bright A_Look 
      Loop
   See:
      FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0)
      FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180)
      FRIT AABBCCDD 2 Bright A_Chase 
      Goto See +2
   Melee:
      FRIT ST 8 Bright A_FaceTarget
      FRIT U 6 Bright A_ComboAttack
      Goto See +2
   Missile:
      FRIT E 0 A_Jump(128,4)
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 6 Bright A_ComboAttack
      Goto See +2
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 6 Bright A_CustomMissile("Comet", 44, 0, 0)
      Goto See +2
   Pain: 
      FRIT HH 4 Bright A_Pain 
      Goto See +2
   Death: 
      FRIT I 0 Bright A_NoGravity
      FRIT I 6 Bright A_Scream
      FRIT J 5 Bright A_NoBlocking
      FRIT KLMNOPQR 4 Bright
      Stop 
   } 
}
//******************************************************************************************

ACTOR LordofHeresy 19971
{ 

    //$Category monsters
    Health 1250 
    Radius 20 
    Height 56 
    Speed 13 
    PainChance 50
    Mass 1000
    MONSTER 
    Obituary "%o was annihilated by the heresy lord." 
    SeeSound "baron/sight" 
    PainSound "demon/pain" 
    DeathSound "baron/death" 
    ActiveSound "demon/active" 
    States 
    { 
    Spawn: 
        LOHS AB 10 A_Look 
        Loop 
    See: 
        LOHS AABBCCDD 4 A_Chase 
        Loop 
    Missile: 
        LOHS E 0 A_Jump(85,5) 
        LOHS E 0 A_Jump(128,9)
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 8 A_BruisAttack 
        Goto See 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 8) 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, -8) 
        Goto See 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, -22) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 22) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, -12) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 12) 
        Goto See 
    Pain: 
        LOHS H 5 
        LOHS H 5 A_Pain 
        Goto See 
    Death: 
        LOHS I 8 
        LOHS J 8 A_Scream 
        LOHS K 8 A_Fall 
        LOHS LMN 8 
        LOHS O -1 
        Stop 
    Raise: 
        LOHS ONMLKJI 5 
        Goto See 
    } 
} 

//******************************************************************************************

ACTOR ShotgunSoldier 19970
{ 
    //$Category monsters
    Health 100 
    Radius 20 
    Height 56 
    Speed 8
    Damage 7
    PainChance 150 
    MONSTER 
    +FLOORCLIP 
    Obituary "%o tasted the shotgun soldier's buckshot." 
    SeeSound "shotguy/sight" 
    PainSound "shotguy/pain" 
    DeathSound "shotguy/death" 
    ActiveSound "shotguy/active" 
    AttackSound "shotguy/attack"
    DropItem Shotgun
    DropItem Shell 128
    States 
    { 
    Spawn: 
        SOLD AB 10 A_Look 
        Loop 
    See: 
        SOLD AABBCCDD 4 A_Chase 
        Loop 
    Missile: 
        SOLD E 6 A_FaceTarget 
        SOLD F 8 A_BulletAttack
        SOLD E 6 A_FaceTarget 
        Goto See 
    Pain: 
        SOLD G 5 
        SOLD G 3 A_Pain 
        Goto See 
    Death: 
        SOLD H 5 
        SOLD I 5 A_Scream 
        SOLD J 5 A_Fall 
        SOLD KLM 5 
        SOLD N -1 
        Stop 
    XDeath: 
        SOLD O 5 
        SOLD P 5 A_XScream 
        SOLD Q 5 A_Fall 
        SOLD RSTUV 5 
        SOLD W -1 
        Stop 
    Raise: 
        SOLD NMLKJIH 5 
        Goto See 
    } 
} 

//******************************************************************************************
//******************************************************************************************
//******************************************************************************************
//******************************************************************************************
//******************************************************************************************

ACTOR FamiliarSpawner
{
     +MISSILE
     +DOOMBOUNCE
     Speed 8
     Damage 0
     DeathSound "FamiliarSpawn"
     DropItem DragonFamiliar
     States
     {
     Spawn:
          WLP3 ABCD 4
          Loop
     Death:
          WLP3 E 4
          WLP3 F 6
          WLP3 F 0 A_Jump(85,3)
          WLP3 G 8 A_CustomMissile("BatFamiliar", 32, 0, 0)
          WLP3 H 350
          Stop
          WLP3 G 8 A_CustomMissile("DragonFamiliar", 32, 0, 0)
          WLP3 H 350
          Stop
     }
}

//******************************************************************************************

actor DarkSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

//******************************************************************************************

actor DarkNormal
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

//******************************************************************************************

actor DarkStrong
{
    Radius 8
    Height 6
    Speed 9
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    MissileType Blood
    States
    {
    Spawn:
        DKB2 A 4 Bright A_MissileAttack
        DKB2 B 4 Bright
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

//******************************************************************************************

actor DarkFast
{
    Radius 8
    Height 6
    Speed 18
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

//******************************************************************************************

actor YellowSkullSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    scale 0.67
    ALPHA 0.67
    DeathSound "ShieldShotHit"
    States
    {
    Spawn:
        WLP2 ABCD 3 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        WLP2 EFG 4 Bright
        Stop
    }
}

//******************************************************************************************

actor GreenPoisonBall
{
    Radius 8
    Height 6
    Speed 10
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        WLP1 ABCD 4
        Loop
    Death:
        WLP1 EFGH 4
        Stop
    }
}

//******************************************************************************************

actor RedSkullBall
{
    Radius 8
    Height 6
    Speed 15
    Damage 12
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        WLP4 ABCB 4
        Loop
    Death:
        WLP4 DEFG 4
        Stop
    }
}

//******************************************************************************************

actor MiniFirePuff
{
    Radius 6
    Height 10
    Speed 7
    Damage 1
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    States
    {
    Spawn:
        DFFP ABCDEFGHIJKLMNOPQR 2 Bright
        Stop
    Death:
        DFFP IJKLMNOPQR 2 Bright
        Stop
    }
}

//******************************************************************************************

actor MiniFirePuffNoDamage : MiniFirePuff
{
    Damage 0
}

//******************************************************************************************

actor WarlordShield
{
    +DOOMBOUNCE
    Speed 5
    States
    {
    Spawn:
        WLI1 ABCDEF 5
        WLI1 G -1
        Stop
    }
}

//******************************************************************************************

actor WarlordMace
{
    +DOOMBOUNCE
    Speed 4
    States
    {
    Spawn:
        WLI2 ABCDEF 5
        WLI2 G -1
        Stop
    }
}

//******************************************************************************************

ACTOR SnakeImpFireball
{
        Health	1000
        Radius	8
        Height	16
        Speed	16
        Damage	12
        Mass	200
        
        SeeSound	"imp/attack"
        DeathSound	"imp/shotx"
        
        PROJECTILE
        
        States
        {
	        spawn:
	                SIBA AB 8
	                loop
	        death:
	                SIBA CDE 5
	                stop
        }
}

//******************************************************************************************

actor CacolanternBall
{
    Radius 13
    Height 8
    Speed 20
    Damage 9
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        BAL8 ABCDEFGH 5 Bright
        Loop
    Death:
        BAL8 IJKL 4 Bright
        Stop
    }
}

//******************************************************************************************

actor BelphegorBall
{
    Radius 6
    Height 8
    Speed 20
    Damage 10
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        BAL7 AB 5 Bright
        Loop
    Death:
        BAL7 CDE 6 Bright
        Stop
    }
}

//******************************************************************************************

actor HellGuardBall
{
    Radius 13
    Height 8
    Speed 10
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        HGFB ABCD 2 Bright
        Loop
    Death:
        HGFB EFGH 4 Bright
        Stop
    }
}

//******************************************************************************************

actor HellWarriorBall
{
    Radius 6
    Height 8
    Speed 15
    Damage 8
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        HWFB A 5 Bright
        Loop
    Death:
        HWFB BCDE 6 Bright
        Stop
    }
}

//******************************************************************************************

actor SoulHarvesterBall
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "HarvesterBallScream"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        SHFB A 0 bright A_PlaySound("HarvesterBallScream")
        SHFB ABCABC 4 Bright A_SeekerMissile (10,20)
        Loop
    Death:
        SHFB DEFG 4 Bright
        Stop
    }
}

//******************************************************************************************

actor SoulHarvesterGhost
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE TRANSLUCENT
    ALPHA 0.75
    +NOCLIP
    SeeSound "HarvesterGhostScream"
    States
    {
    Spawn:
    Death:
        SHGH ABCDEFG 5 Bright
        Stop
    }
}

//******************************************************************************************

//*************************************************************************
actor Comet
{
    Radius 6
    Height 8
    Speed 10
    Damage 5
    ExplosionDamage 40
    ExplosionRadius 80
    Scale .55
    PROJECTILE
    SeeSound "CometLaunch"
    DeathSound "CometExplosion"
    MissileType CometTail
    MissileHeight 0
    States
    {
    Spawn:
        COMT AAAABBBBCCCC 1 Bright A_MissileAttack
        Loop
    Death:
        COMT D 3 Bright A_CustomMissile("CometDeathGlow", 0, 0, 0)
        COMT E 3 Bright A_Explode
        COMT E 0 A_CustomMissile("CometDeath", 0, 0, 0)
        Stop
    }
}

//******************************************************************************************

actor CometTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1 Bright
        Stop
    }
}

//******************************************************************************************

actor CometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.7
    States
    {
    Spawn:
        COMT FGHI 3 Bright
        Stop
    }
}

//******************************************************************************************

actor CometDeathGlow : CometTail
{
    Scale 2.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3 Bright
        Stop
    }
}

//******************************************************************************************

actor OrbitCometTail : CometTail
{
    Scale .5
}

//******************************************************************************************

actor OrbitComet : Comet
{
     SeeSound ""
     +NOCLIP
     Scale .28
     Speed 5
     MissileType OrbitCometTail
     MissileHeight 0
     States
     {
     Spawn:
         COMT AABBCC 1 Bright A_MissileAttack
         COMT A 0 A_CustomMissile("OrbitComet", 0, 0, 75)
         COMT A 0 A_MissileAttack
         Stop
     }
}

//******************************************************************************************

actor OrbitComet2 : OrbitComet
{
     MissileType OrbitCometTail
     MissileHeight 0
     States
     {
     Spawn:
         COMT AABBCC 1 Bright A_MissileAttack
         COMT A 0 A_CustomMissile("OrbitComet2", 0, 0, -75)
         COMT A 0 A_MissileAttack
         Stop
     }
}

//******************************************************************************************

actor AfritBall
{
    Radius 6
    Height 8
    Speed 15
    Damage 8
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        FRTM AB 5 Bright
        Loop
    Death:
        FRTM CDE 6 Bright
        Stop
    }
}

