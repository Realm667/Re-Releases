Class ImpWarlord : Actor
{
	Default
	{
		//$Category TNTLE Monsters
		Health 180;
		Radius 20;
		Height 56;
		Speed 9;
		PainChance 128;
		SeeSound "imp/sight";
		PainSound "imp/pain";
		DeathSound "imp/death";
		ActiveSound "imp/active";
		Obituary "$OB_IMWA";
		HitObituary "$HB_IMWA";
		MONSTER;
		+FLOORCLIP
	}
	
    States
    {
    Spawn:
        WARI AB 10 A_Look();
        Loop;
    See:
        WARI AABBCCDD 3 A_Chase();
        Loop;
    Melee:
        WARI E 6 A_FaceTarget();
        TNT1 A 0 A_StartSound("implord/maceswing");
        WARI F 4 A_FaceTarget();
        WARI G 5 A_CustomMeleeAttack(Random(1,8)*7, "implord/macehit", "None");
        Goto See;
    Missile:
        TNT1 A 0 A_Jump(38, 23);	//20% chance: jump to familiar drop
        TNT1 A 0 A_Jump(77, 2);		//30% chance: jump to heat seekers
        TNT1 A 0 A_Jump(128, 11);	//50% chance: jump to green fireballs
        Goto Missile+28;			//Otherwise : jump to red fireball
        TNT1 A 0 A_SetInvulnerable();
        WARI I 6 A_FaceTarget();
        TNT1 A 0 A_StartSound("implord/shieldshot");
        WARI J 6 A_FaceTarget();
        TNT1 A 0 A_Jump(128, 4);
        TNT1 A 0 A_SpawnProjectile("YellowSkullSeeker", 32, 0, -45);
        WARI K 6 A_SpawnProjectile("YellowSkullSeeker", 32, 0, 45);
        TNT1 A 0 A_UnSetInvulnerable();
        Goto See;
        WARI K 6 A_SpawnProjectile("YellowSkullSeeker", 32, 0, 0);
        TNT1 A 0 A_UnSetInvulnerable();
        Goto See;
        WARI L 7 A_FaceTarget();
        TNT1 A 0 A_StartSound("implord/maceswing");
        WARI M 5 A_FaceTarget();
        TNT1 A 0 A_StartSound("imp/attack", CHAN_WEAPON);
        TNT1 A 0 A_Jump(128, 3);
        TNT1 A 0 A_SpawnProjectile("GreenPoisonBall", 40, 0, -8, CMF_AIMOFFSET);
        TNT1 A 0 A_SpawnProjectile("GreenPoisonBall", 24, 0, 8, CMF_AIMOFFSET);
		TNT1 A 0 A_SpawnProjectile("GreenPoisonBall", 32, 0, 0, CMF_AIMOFFSET);
        TNT1 A 0 A_SpawnProjectile("GreenPoisonBall", 36, 0, -4, CMF_AIMOFFSET);
        WARI N 6 A_SpawnProjectile("GreenPoisonBall", 28, 0, 4, CMF_AIMOFFSET);
        Goto See;
        WARI GF 8 A_FaceTarget();
        TNT1 A 0 A_Jump(128, 2);
        WARI E 8 A_SpawnProjectile("FamiliarSpawner", 32, 0, -30);
        Goto See;
        WARI E 8 A_SpawnProjectile("FamiliarSpawner", 32, 0, 30);
        Goto See;
        TNT1 A 0 A_Jump(128, 5);
        WARI O 7 A_FaceTarget();
        TNT1 A 0 A_StartSound("implord/maceswing");
        WARI P 5 A_FaceTarget();
        WARI Q 6 A_SpawnProjectile("RedSkullBall", 32, 0, 0);
        WARI O 7 A_FaceTarget();
        TNT1 A 0 A_StartSound("implord/maceswing");
	    WARI P 5 A_FaceTarget();
        WARI Q 6 A_SpawnProjectile("RedSkullBall", 32, 0, 0);
        Goto See;
    Pain:
        TNT1 A 0 A_Jump(128, 2);
        WARI H 8 A_Pain();
        Goto See;
	    WARI I 1;
        TNT1 A 0 A_SetInvulnerable();
	    WARI IIIIII 4 A_FaceTarget();
        TNT1 A 0 A_Jump(160, 5);
        WARI IIII 4 A_FaceTarget();
        Goto Pain+4;
        TNT1 A 0 A_UnSetInvulnerable();
		Goto See;
    Death:
        TNT1 A 0 A_FaceTarget();
        TNT1 A 0 A_SpawnItemEx("WarlordMace", 0, 0, 32, 4, 0, 0, -90, 128);
        WARI R 8 A_SpawnItemEx("WarlordShield", 0, 0, 32, 5, 0, 0, 90, 128);
        WARI S 8 A_Scream();
        WARI T 6;
	    WARI U 6 A_Fall();
        WARI V -1;
        Stop;
    Raise:
        WARI VU 8;
        WARI TSR 6;
        Goto See;
    }
}

Class YellowSkullSeeker : Actor
{
	Default
	{
		Radius 8;
		Height 6;
		Speed 9;
		Damage 3;
		RenderStyle "STYLE_Add";
		Scale 0.67;
		Alpha 0.67;
		DeathSound "implord/shieldshothit";
		Projectile;
		+BRIGHT
		+SEEKERMISSILE
	}
	
    States
    {
    Spawn:
        WLP2 ABCD 3 A_SeekerMissile(5, 10);
        Loop;
    Death:
        WLP2 EFG 4;
        Stop;
    }
}

Class GreenPoisonBall : Actor
{
	Default
	{
		Radius 8;
		Height 6;
		Speed 10;
		Damage 3;
		RenderStyle "STYLE_Add";
		Alpha 0.67;
		DeathSound "imp/shotx";
		Projectile;
	}
	
    States
    {
    Spawn:
        WLP1 ABCD 4;
        Loop;
    Death:
        WLP1 EFGH 4;
        Stop;
    }
}

Class FamiliarSpawner : Actor
{
	Default
	{
		Speed 8;
		+BLOODLESSIMPACT
		+BOUNCEONACTORS
		+CANBOUNCEWATER
		+DOOMBOUNCE
		+MISSILE
		+PAINLESS
	}
	
	States
	{
	Spawn:
		WLP3 AAAABBBBCCCCDDDD 1 A_JumpIf(WaterLevel >= 2, "Death");
		Loop;
	Death:
		TNT1 A 0 {bMISSILE = false;}
		WLP3 E 4 A_StartSound("implord/familiarspawn");
		WLP3 F 6;
		TNT1 A 0 A_Jump(85, 2);
		WLP3 G 8 A_SpawnItemEx("SummonedBatFamiliar", 0, 0, 32, 0, 0, 0, 0, SXF_SETMASTER);
		Goto Death+6;
		WLP3 G 8 A_SpawnItemEx("DragonFamiliar", 0, 0, 32, 0, 0, 0, 0, SXF_SETMASTER);
		WLP3 H 350;
		WLP3 HHHHHHHHHHHHHHHHHHHH 1 A_FadeOut(0.05,FTF_REMOVE);
		Stop;
	}
}

Class RedSkullBall : Actor
{
	Default
	{
		Radius 8;
		Height 6;
		Speed 15;
		Damage 12;
		RenderStyle "STYLE_Add";
		Alpha 0.67;
		SeeSound "imp/attack";
		DeathSound "imp/shotx";
		Projectile;
	}
	
    States
    {
    Spawn:
        WLP4 ABCB 4;
        Loop;
    Death:
        WLP4 DEFG 4;
        Stop;
    }
}

Class WarlordMace : Actor
{
	Default
	{
		Speed 4;
		BounceType "Doom";
	}
	
    States
    {
    Spawn:
        WLI2 ABCDEF 5;
        WLI2 G -1;
        Stop;
    }
}

Class WarlordShield : WarlordMace
{
	Default
	{
		Speed 5;
	}
	
    States
    {
    Spawn:
        WLI1 ABCDEF 5;
        WLI1 G -1;
        Stop;
    }
}

Class SummonedBatFamiliar : Actor
{
	Default
	{
		//$Category TNTLE Monsters
		Health 10;
		Radius 12;
		Height 24;
		Speed 14;
		PainChance 200;
		SeeSound "batfam/idle";
		PainSound "batfam/pain";
		DeathSound "batfam/death";
		ActiveSound "batfam/idle";
		HitObituary "$HB_BATF";
		MONSTER;
		+FLOAT
		+NEVERRESPAWN
		+NOGRAVITY
	}
	
    States
    {
    Spawn:
        BFAM ABCB 3 A_Look();
        Loop;
    See:
        BFAM ABCB 3 A_Chase();
        Loop;
    Melee:
        BFAM A 3 A_FaceTarget();
        BFAM B 3 A_CustomMeleeAttack(Random(1,8)*1, "batfam/idle");
        BFAM CB 3 A_FaceTarget();
        Goto See;
    Pain:
        BFAM A 2;
        BFAM A 2 A_Pain();
        Goto See;
    Death:
        TNT1 A 0 A_Die();
        TNT1 A 0 A_NoBlocking();
        BFAM D 5 A_Scream();
        BFAM E 5;
        BFAM FDEF 5;
        Goto Death+5;
    Crash:
        TNT1 A 0 A_SetFloorClip();
        BFAM G 6 A_StartSound("batfam/crash");
        BFAM H 7;
        BFAM I 8;
        BFAM J -1;
        Stop;
    }
}

Class DragonFamiliar : Actor
{
	Default
	{
		//$Category TNTLE Monsters
		Health 25;
		Radius 16;
		Height 32;
		Speed 10;
		PainChance 200;
		MaxTargetRange 78;
		SeeSound "dragonfam/see";
		PainSound "dragonfam/pain";
		DeathSound "dragonfam/death";
		ActiveSound "dragonfam/idle";
		Obituary "$OB_DRAG";
		MONSTER;
		+FLOAT
		+NEVERRESPAWN
		+NOGRAVITY
		+STRIFEDAMAGE
	}
	
    States
    {
    Spawn:
        DFAM ABCD 10 A_Look();
        Loop;
    See:
        DFAM AABBCCDD 3 A_Chase();
        Loop;
    Missile:
        TNT1 A 0 A_JumpIfCloser(270, 1);
	    Goto See;
	    TNT1 A 0 A_StartSound("dragonfam/attack");
        DFAM E 2 Bright A_CPosRefire();
        TNT1 A 0 A_SpawnProjectile("MiniFirePuffNoDamage", 12, 0, 0);
        DFAM E 2 Bright A_CPosRefire();
        TNT1 A 0 A_SpawnProjectile("MiniFirePuff", 12, 0, Random(1,2));
        DFAM E 2 Bright A_CPosRefire();
        TNT1 A 0 A_SpawnProjectile("MiniFirePuff", 12, 0, Random(-2,-1));
		TNT1 A 0 A_JumpIfCloser(270, 1);
        Goto See;
		TNT1 A 0 A_Jump(48, "See");
		Goto Missile+2;
    Pain:
        DFAM F 2;
        DFAM F 2 A_Pain();
        Goto See;
    Death:
        TNT1 A 0 A_Die();
        DFAM G 8 A_NoBlocking();
        DFAM H 8 A_Scream();
        DFAM I 6 A_SetFloorClip();
        DFAM JKL 6;
        DFAM M -1;
        Stop;
    }
}

Class MiniFirePuff : Actor
{
	Default
	{
		Radius 6;
		Height 10;
		Speed 7;
		Damage 1;
		RenderStyle "STYLE_Add";
		Alpha 0.67;
		Projectile;
		+BRIGHT
	}
	
    States
    {
    Spawn:
        DFFP ABCDEFGH 2;
    Death:
        DFFP IJKLMNOPQR 2;
        Stop;
    }
}

Class MiniFirePuffNoDamage : MiniFirePuff
{
	Default
	{
		Damage 0;
	}
}