ACTOR ImpWarlord
{
    //$Category TNTLE Monsters
    Health 180
    Radius 20
    Height 56
    Speed 9
    PainChance 128
    MeleeDamage 7
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "MaceHit"
    Obituary "$OB_IMWA"
    HitObituary "$HB_IMWA"
    States
    {
    Spawn:
        WARI AB 10 A_Look
        Loop
    See:
        WARI AABBCCDD 3 A_Chase
        Loop
    Melee:
        WARI E 6 A_FaceTarget
        WARI E 0 A_StartSound("MaceSwing")
        WARI F 4 A_FaceTarget
        WARI G 5 A_MeleeAttack
        Goto See
    Missile:
        WARI E 0 A_Jump(38,23)  //20% chance: jump to familiar drop
        WARI E 0 A_Jump(77,2)   //30% chance: jump to heat seekers
        WARI E 0 A_Jump(128,11) //50% chance: jump to green fireballs
        Goto Missile+28         //Otherwise : jump to red fireball
        WARI I 0 A_SetInvulnerable
        WARI I 6 A_FaceTarget
        WARI J 0 A_StartSound("ShieldShot")
        WARI J 6 Bright A_FaceTarget
        WARI J 0 A_Jump(128,4)
        WARI K 0 Bright A_CustomMissile("YellowSkullSeeker", 32, 0, -45)
        WARI K 6 Bright A_CustomMissile("YellowSkullSeeker", 32, 0, 45)
        WARI K 0 A_UnSetInvulnerable
        Goto See
        WARI K 6 Bright A_CustomMissile("YellowSkullSeeker", 32, 0, 0)
        WARI K 0 A_UnSetInvulnerable
        Goto See
        WARI L 7 A_FaceTarget
        WARI M 0 A_StartSound("MaceSwing")
        WARI M 5 A_FaceTarget
        WARI M 0 A_PlayWeaponSound("imp/attack")
        WARI M 0 A_Jump(128,3)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 40, 0, -8, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 24, 0, 8, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 32, 0, 0, 1)
        WARI N 0 A_CustomMissile("GreenPoisonBall", 36, 0, -4, 1)
        WARI N 6 A_CustomMissile("GreenPoisonBall", 28, 0, 4, 1)
        Goto See
        WARI GF 8 A_FaceTarget
        WARI F 0 A_Jump(128,2)
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, -30)
        Goto See
        WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, 30)
        Goto See
        WARI O 0 A_Jump(128,5)
        WARI O 7 A_FaceTarget
        WARI P 0 A_StartSound("MaceSwing")
        WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        WARI O 7 A_FaceTarget
        WARI P 0 A_StartSound("MaceSwing")
        WARI P 5 A_FaceTarget
        WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
        Goto See
    Pain:
        WARI H 0 A_Jump(128,2)
        WARI H 8 A_Pain
        Goto See
        WARI I 0 A_SetInvulnerable
        WARI IIIIII 4 A_FaceTarget
        WARI I 0 A_Jump(160,5)
        WARI IIII 4 A_FaceTarget
        Goto Pain+4
        WARI I 0 A_UnSetInvulnerable
        Goto See
    Death:
        WARI R 0 A_CustomMissile("WarlordMace", 32, 0, -90)
        WARI R 0 A_CustomMissile("WarlordShield", 32, 0, 90)
        WARI R 8
        WARI S 8 A_Scream
        WARI T 6
        WARI U 6 A_Fall
        WARI V -1
        Stop
    Raise:
        WARI VU 8
        WARI TSR 6
        Goto See
    }
}

ACTOR DragonFamiliar
{
    //$Category TNTLE Monsters
    Health 25
    Radius 16
    Height 32
    Speed 10
    PainChance 200
    MONSTER
    +FLOORCLIP
    +NOGRAVITY
    +FLOAT
    +STRIFEDAMAGE
    SeeSound "DragonSee"
    PainSound "DragonPain"
    DeathSound "DragonDeath"
    ActiveSound "DragonIdle"
    Obituary "$OB_DRAG"
    States
    {
    Spawn:
        DFAM ABCD 10 A_Look
        Loop
    See:
        DFAM AABBCCDD 3 A_Chase
        Loop
    Missile:
        DFAM E 0 A_StartSound("DragonAttack")
        DFAM E 0 A_Jump(208,7)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto See
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
        DFAM E 2 Bright A_FaceTarget
        DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
        Goto Missile+1
    Pain:
        DFAM F 2
        DFAM F 2 A_Pain
        Goto See
    Death:
        DFAM G 0 A_Die
        DFAM G 8 A_NoBlocking
        DFAM H 8 A_Scream
        DFAM I 6 A_Fall
        DFAM JKL 6
        DFAM M -1
        Stop
    }
}

ACTOR BatFamiliar
{
    //$Category TNTLE Monsters
    Health 10
    Radius 12
    Height 24
    Speed 14
    PainChance 200
    MeleeDamage 1
    MissileHeight 0
    MONSTER
    +FLOORCLIP
    +NOGRAVITY
    +FLOAT
    SeeSound "BatIdle"
    PainSound "BatPain"
    DeathSound "BatDeath"
    ActiveSound "BatIdle"
    MeleeSound "BatIdle"
    HitObituary "$OB_BATF"
    States
    {
    Spawn:
        BFAM ABCB 3 A_Look
        Loop
    See:
        BFAM ABCB 3 A_Chase
        Loop
    Melee:
        BFAM A 3 A_FaceTarget
        BFAM B 3 A_MeleeAttack
        BFAM CB 3 A_FaceTarget
        Goto See
    Pain:
        BFAM A 2
        BFAM A 2 A_Pain
        Goto See
    Death:
        BFAM D 0 A_Die
        BFAM D 0 A_NoBlocking
        BFAM D 5 A_Scream
        BFAM E 5 A_Fall
        BFAM FDEF 5
        Goto Death+5
    Crash:
        BFAM G 6 A_StartSound("BatCrash")
        BFAM H 7
        BFAM I 8
        BFAM J -1
        Stop
    }
}

ACTOR FamiliarSpawner
{
	+MISSILE
	+DOOMBOUNCE
	Speed 8
	Damage 0
	DeathSound "FamiliarSpawn"
	DropItem DragonFamiliar
	States
	{
	Spawn:
		WLP3 ABCD 4
		Loop
	Death:
		WLP3 E 4
		WLP3 F 6
		WLP3 F 0 A_Jump(85,3)
		WLP3 G 8 A_CustomMissile("BatFamiliar", 32, 0, 0)
		WLP3 H 350
		Stop
		WLP3 G 8 A_CustomMissile("DragonFamiliar", 32, 0, 0)
		WLP3 H 350
		Stop
	}
}

ACTOR YellowSkullSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
	+BRIGHT
    +SEEKERMISSILE
    PROJECTILE
    RenderStyle STYLE_Add
    Scale 0.67
    Alpha 0.67
    DeathSound "ShieldShotHit"
    States
    {
    Spawn:
        WLP2 ABCD 3 A_SeekerMissile (5,10)
        Loop
    Death:
        WLP2 EFG 4
        Stop
    }
}

ACTOR GreenPoisonBall
{
    Radius 8
    Height 6
    Speed 10
    Damage 3
    PROJECTILE
    RenderStyle STYLE_Add
    Alpha 0.67
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        WLP1 ABCD 4
        Loop
    Death:
        WLP1 EFGH 4
        Stop
    }
}

ACTOR RedSkullBall
{
    Radius 8
    Height 6
    Speed 15
    Damage 12
    PROJECTILE
    RenderStyle STYLE_Add
    Alpha 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        WLP4 ABCB 4
        Loop
    Death:
        WLP4 DEFG 4
        Stop
    }
}

ACTOR MiniFirePuff
{
    Radius 6
    Height 10
    Speed 7
    Damage 1
    RenderStyle STYLE_Add
    Alpha 0.67
    PROJECTILE
	+BRIGHT
    States
    {
    Spawn:
        DFFP ABCDEFGHIJKLMNOPQR 2
        Stop
    Death:
        DFFP IJKLMNOPQR 2
        Stop
    }
}

ACTOR MiniFirePuffNoDamage : MiniFirePuff
{
    Damage 0
}

ACTOR WarlordShield
{
    Speed 5
    +DOOMBOUNCE
    States
    {
    Spawn:
        WLI1 ABCDEF 5
        WLI1 G -1
        Stop
    }
}

ACTOR WarlordMace
{
    Speed 4
    +DOOMBOUNCE
    States
    {
    Spawn:
        WLI2 ABCDEF 5
        WLI2 G -1
        Stop
    }
}