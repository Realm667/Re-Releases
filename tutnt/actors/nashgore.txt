Actor MCBloodFXMain replaces Blood
{
	+NOINTERACTION
	+NOTIMEFREEZE
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		BLUD B 0 A_SpawnItemEx("NashGore_FlyingBlood",0,0,8,random(-4,4),random(-4,4),random(2,5),0,143,176) // [Nash] it used to use A_SpawnItem, but we're getting rid of that crap
		TNT1 A 2 A_SpawnItemEx("MCBloodParticle",frandom(0,2),0,random(1,4),frandom(0,1.5),0,frandom(0.1,2.0),random(0,359),33)
		TNT1 AA 0 A_SpawnItemEx("MCBloodParticle",frandom(0,2),0,random(1,4),frandom(0,1.5),0,frandom(0.1,2.0),random(0,359),33)
		Stop
	}
}

Actor MCBloodParticle : Blood
{
	+THRUACTORS
	+DONTSPLASH
	+DONTBLAST
	//Translation "0:255=%[0,0,0]:[1,0,0]"
	Renderstyle Translucent
	Gravity 0.2
	Scale 0.38
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12)
	 	X057 A 0 A_Jump(256,"End")
		XA57 A 0 A_Jump(256,"End")
		XB57 A 0 A_Jump(256,"End")
		XC57 A 0 A_Jump(256,"End")
		X058 A 0 A_Jump(256,"End")
		XA58 A 0 A_Jump(256,"End")
		XB58 A 0 A_Jump(256,"End")
		XC58 A 0 A_Jump(256,"End")
		X059 A 0 A_Jump(256,"End")
		XA59 A 0 A_Jump(256,"End")
		XB59 A 0 A_Jump(256,"End")
		XC59 A 0 A_Jump(256,"End")
	End:
		"####" ABCD 2 A_FadeOut(0.03)
		"####" EFGHIJ 1 A_FadeOut(0.04)
		"####" KLM 1 A_FadeOut(0.05)
		Wait
	}
}

Actor MCBloodSplatter : MCBloodParticle replaces BloodSplatter{}

actor NashGore_FlyingBlood
{
	+CLIENTSIDEONLY
	scale 0.75
	health 1
	radius 8
	height 1
	mass 1
	gravity 0.5 // [Nash] make it fall slower. pretty!
	+CORPSE
	+NOTELEPORT
	+NOBLOCKMAP
	states
	{
	Spawn:
		FBLD AABBCCDD 2 NODELAY A_SpawnItemEx("NashGore_FlyingBloodTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		loop
	Crash:
		TNT1 A 0 A_PlaySound("bloodsplat")
		TNT1 A 0 A_Jump(256,1,2,3,4)
		BSPT ABCD 0 A_Jump(256,"End")
		// make the blood spots stay on the ground. 1500 is the default duration.
		// Fadeout added by Zero Prophet for effect.
	End:
		BSPT "#" 1500
		BSPT "#" 1 A_FadeOut(0.02)
		Wait
	}
}

actor NashGore_FlyingBloodTrail : MCBloodParticle
{
	Scale 0.28
}

// Gib generator fixed by LilWhiteMouse - thanks LWM!
actor NashGore_GibGenerator
{
	states
	{
	Spawn:
		TNT1 A 1 NODELAY A_SpawnDebris("NashGore_Gib",1)
		stop
	}
}

actor NashGore_GibGeneratorBlue : NashGore_GibGenerator
{
	translation "16:47=193:207","168:191=192:207"
}

actor NashGore_GibGeneratorGreen : NashGore_GibGenerator
{
	translation "16:47=112:127","168:191=112:127"
}

// Gib 1
actor NashGore_Gib
{
	health 1
	radius 8
	height 8
	mass 1
	scale 0.75
	+CORPSE
	+NOTELEPORT
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8)
		GIB1 A 0 A_Jump(256,"Fly")
		GIB2 A 0 A_Jump(256,"Fly")
		GIB3 A 0 A_Jump(256,"Fly")
		GIB4 A 0 A_Jump(256,"Fly")
		GIB5 A 0 A_Jump(256,"Fly")
		GIB6 A 0 A_Jump(256,"Fly")
		GIB7 A 0 A_Jump(256,"Fly")
		GIB8 A 0 A_Jump(256,"Fly")
	Fly:
		"####" A 8 A_SpawnDebris("NashGore_FlyingBlood",1)
		Wait
	Crash:
		"####" A 2100
		"####" A 1 A_FadeOut(0.02)
		Wait
	}
}

// Gib 9 (hand)
actor NashGore_Gib9 : NashGore_Gib
{
	states
	{
	Spawn:
		GIB9 A 8 A_SpawnDebris("NashGore_FlyingBlood",1)
		loop
	}
}

// Gib 10 (arm)
actor NashGore_Gib10 : NashGore_Gib
{
	states
	{
	Spawn:
		GIB0 A 8 A_SpawnDebris("NashGore_FlyingBlood",1)
		loop
	}
}

// Gib 11 (eyeball)
actor NashGore_Gib11 : NashGore_Gib
{
	states
	{
	Spawn:
		GIBI A 8 A_SpawnDebris("NashGore_FlyingBlood",1)
		loop
	}
}

// Demon Gib - arm
actor NashGore_GibSarg : NashGore_Gib
{
	states
	{
	Spawn:
		SARM ABCD 4 A_SpawnDebris("NashGore_FlyingBlood",1)
		Goto Spawn+3
	Crash:
		SARM EF 4
		SARM G 2100
		SARM G 1 A_FadeOut(0.02)
		Wait
	}
}

ACTOR NashgoreMasterGenerator
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("NashGore_GibPieceGenerator9")
		TNT1 A 0 A_SpawnItemEx("NashGore_GibPieceGenerator10")
		TNT1 AA 0 A_SpawnItemEx("NashGore_GibPieceGenerator11")
		TNT1 A 10 A_SpawnItemEx("NashGore_GibGenerator")
		TNT1 AAA 5 A_SpawnItemEx("NashGore_GibGenerator")
		Stop
	}
}

//Some client-side spawners by Ghastly_dragon. Since A_SpawnDebris lags, spawn
//these spawners client-side, so it won't.

Actor NashGore_GibPieceGenerator1
{
	+CLIENTSIDEONLY
	+NonShootable
	Height 56
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnDebris("NashGore_Gib",0)
		Stop
	}
}

Actor NashGore_GibPieceGenerator9 : NashGore_GibPieceGenerator1
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnDebris("NashGore_Gib9",0)
		Stop
	}
}

Actor NashGore_GibPieceGenerator10 : NashGore_GibPieceGenerator1
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnDebris("NashGore_Gib10",0)
		Stop
	}
}

Actor NashGore_GibPieceGenerator11 : NashGore_GibPieceGenerator1
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnDebris("NashGore_Gib11",0)
		Stop
	}
}

Actor NashGore_GibPieceGeneratorSarg : NashGore_GibPieceGenerator1
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnDebris("NashGore_GibSarg",0)
		Stop
	}
}