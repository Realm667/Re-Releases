/*
 * Copyright (c) 2011-2021 Drugod, Ozymandias81, IvanDobrovski/Cruduxy/Marinelol
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

class ThamuzBog : ThamuzBase //the original CyberBog from Bloom was in Decorate format, this has been reworked for TCOTD2
{
	Default
	{
		//$Category New Monsters (TCOTD2)
		//$Title ThamuzBog
		Speed 5.33333333; //more rapid compared to Thamuz but doesn't summon tentacles
		SeeSound "thamuzdoom/idle";
		PainSound "thamuzdoom/pain";
		DeathSound "thamuzdoom/death";
		ActiveSound "thamuzdoom/idle";
	}

	protected void A_ThamuzHoof(void)
	{
		A_ThamuzSpawnGhost();
		A_Hoof();
	}
	protected void A_ThamuzMetal(void)
	{
		A_ThamuzSpawnGhost();
		A_Metal();
	}

	protected void A_ThamuzNapalmAtk(void)
	{
		A_FaceTarget();
		A_SpawnProjectile("ThamuzNapalmMissile", 62, -17);
	}

	protected void A_ThamuzHomingFireballAtk(void)
	{
		A_FaceTarget();
		A_SpawnProjectile("DCBMiss1", 88);
	}

	States
	{
	Spawn:
		BOGC AB 10 A_Look();
		Loop;
	See:
		BOGC A 1 A_ThamuzHoof();
		BOGC AA 1 A_Chase2();
		BOGC A 1 A_ThamuzChase();
		BOGC AA 1 A_Chase2();
		BOGC B 1 A_ThamuzMetal();
		BOGC BB 1 A_Chase2();
		BOGC B 1 A_ThamuzChase(true);
		BOGC BB 1 A_Chase2();
		BOGC C 1 A_ThamuzHoof();
		BOGC CC 1 A_Chase2();
		BOGC C 1 A_ThamuzChase();
		BOGC CC 1 A_Chase2();
		BOGC D 1 A_ThamuzMetal();
		BOGC DD 1 A_Chase2();
		BOGC D 1 A_ThamuzChase(true);
		BOGC DD 1 A_Chase2();
		Loop;
	Missile:
		BOGC E 0 A_ThamuzAttackDecide();
	Missile.Spell:
		BOGC G 20 A_ThamuzCircle();
		BOGC T 2 { A_ThamuzFlamesSpellFlames(); A_ThamuzFlamesSpellAOE(); A_StartSound("beast/stomp", CHAN_7); }
		BOGC UV 6;
		BOGC W 6 A_ThamuzFlamesSpellAOE();
		BOGC X 6;
		BOGC Y 2;
		BOGC Y 4 A_ThamuzFlamesSpellAOE();
		BOGC Z 90 A_SpawnItemEx("BeastStomp");
		BOGC Y 2 A_ThamuzSpellEnd();
		BOGC XWVUTS 6; // this moment grants an exploit for players to attack the boss for a better playism - ozy81
		Goto See;
	Missile.Primary:
		BOGC E 6 A_FaceTarget();
		BOGC F 9 A_ThamuzNapalmAtk();
		BOGC E 9 A_FaceTarget();
		BOGC F 9 A_ThamuzNapalmAtk();
		BOGC E 9 A_FaceTarget();
		BOGC F 9 A_ThamuzNapalmAtk();
		Goto See;
	Missile.Secondary:
		BOGC PP 6 A_FaceTarget();
		BOGC Q 6 A_FaceTarget();
		BOGC R 9 A_ThamuzHomingFireballAtk();
		Goto See;
	Pain:
		BOGC G 5;
		BOGC G 5 A_Pain();
		Goto See;
	Death: //include death effects here
		BOGC H 10;
		BOGC I 10 A_Scream();
		BOGC JKL 10;
		BOGC M 10 A_NoBlocking();
		BOGC NO 10;
		BOGC O 30;
		BOGC O -1;
		Stop;
	}
}

class ThamuzNapalmMissile : TCOTDWeaponNapalmMissile
{
	Default
	{
		DamageFunction TCOTDGameplayStatics.GetAttackDamage(70);
	}

	States
	{
	Death:
		NEXP A 2 Bright { A_Explode(TCOTDGameplayStatics.GetAttackDamage(30), 128); A_ExplosionRumble(); A_SetScale(0.9); }
		Goto Super::Death + 1;
	}
}

class DCBMiss1 : Actor
{
	Default
	{
		Radius 12;
		Height 24;
		Speed 6;
		DamageFunction TCOTDGameplayStatics.GetAttackDamage(35);
		Projectile;
		RenderStyle "Add";
		SeeSound "thamuzdoom/ball";
		DeathSound "thamuzdoom/ballexplode";
		+FORCEXYBILLBOARD
		+SEEKERMISSILE
		+DONTSPLASH
		+BRIGHT
	}

	States
	{
	Spawn:
		CBTS ABCDEFGHIJ 2 Light("ThamuzMissile_1") { A_SpawnItemEx("DCBM1Trail"); A_SeekerMissile(10, 90); }
		Loop;
	Death:
		CBTS K 0
		{
			for (int i = 0; i < 76; i++)
			{
				A_SpawnItemEx("DCBMiss3Extra",
					0, 0, 5,
					frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5), frandom[rnd_DCDMissExtra](-5, 5),
					frandom[rnd_DCDMissExtra](0, 360));
			}
		}
		CBTS K 2 Light("ThamuzMissile_1X_1") A_Explode(TCOTDGameplayStatics.GetAttackDamage(25), 96);
		CBTS L 2 Light("ThamuzMissile_1X_2");
		CBTS M 2 Light("ThamuzMissile_1X_3");
		CBTS N 2 Light("ThamuzMissile_1X_4");
		CBTS O 2 Light("ThamuzMissile_1X_5");
		CBTS P 2 Light("ThamuzMissile_1X_6");
		CBTS Q 2 Light("ThamuzMissile_1X_7");
		CBTS R 2 Light("ThamuzMissile_1X_8");
		CBTS S 2 Light("ThamuzMissile_1X_9");
		CBTS T 2 Light("ThamuzMissile_1X_10");
		CBTS U 2 Light("ThamuzMissile_1X_11");
		CBTS V 2 Light("ThamuzMissile_1X_12");
		CBTS W 2 Light("ThamuzMissile_1X_13");
		Stop;
	}
}

class DCBM1Trail : Actor
{
	Default
	{
		Scale 0.8;
		RenderStyle "Add";
		Alpha 0.6;
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+BRIGHT
	}

	States
	{
	Spawn:
		TNT1 A 4;
		CBTS LMNOPQRSTUVW 2 A_FadeOut();
		Stop;
	}
}
