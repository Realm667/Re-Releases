// -------------------------------------------------------------------------------------------------
//
// -------------------------------------------------------------------------------------------------

class TCOTDPuzzleItem : Inventory abstract
{
	Default
	{
		Inventory.InterHubAmount 0;
		Inventory.PickupSound "misc/k_pkup";
		+DONTGIB
		+FORCEXYBILLBOARD
		+Inventory.INVBAR
	}

	override void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);
		if (Owner && Owner is "TCOTDPlayer")
		{
			// run custom scripts for the summon artifacts
			SetCollected();
		}
	}

	override bool TryPickup(in out Actor toucher)
	{
		// give everyone the pickup item
		if (Super.TryPickup(toucher))
		{
			TCOTDGameplayStatics.GiveItemToAllPlayers(self.GetClass());
			return true;
		}

		return false;
	}

	override bool ShouldStay(void)
	{
		if (TCOTDGameplayStatics.IsCoop())
			return false;
		return Super.ShouldStay();
	}

	override bool ShouldRespawn(void)
	{
		if (TCOTDGameplayStatics.IsCoop())
			return false;
		return Super.ShouldRespawn();
	}

	virtual void SetCollected(void) {}
}

class TCOTDKeyTransBook : TCOTDPuzzleItem
{
	Default
	{
		//$Category New Items (TCOTD2)
		//$Title Transcribed Book
		Radius 32;
		Height 8;
		Tag "$TAG_TRANSBOOK";
		Inventory.Icon "TRANA0";
		Inventory.PickupMessage "$IP_TRANSBOOK";
	}

	States
	{
	Spawn:
		TRAN A -1;
		Stop;
	}
}

class TCOTDKeySafeKey : TCOTDPuzzleItem
{
	Default
	{
		//$Category New Items (TCOTD2)
		//$Title Safe Key
		Tag "$TAG_SAFEKEY";
		Scale 0.7;
		Inventory.Icon "SFKYA0";
		Inventory.PickupMessage "$IP_SAFEKEY";
	}

	States
	{
	Spawn:
		SFKY A -1;
		Stop;
	}
}

class TCOTDKeySkullOfThamuz : TCOTDPuzzleItem
{
	Default
	{
		//$Title Thamuz Skull
		Scale 0.7;
		Tag "$TAG_THAMUZKEY";
		Inventory.Icon "SCHAA0";
		Inventory.PickupMessage "$IP_THAMUZKEY";
	}

	override void SetCollected(void)
	{
		Owner.ACS_ScriptCall("SetSkullCollected", 0);
	}

	States
	{
	Spawn:
		SCHA A -1;
		Stop;
	}
}

class TCOTDKeyHourGlass : TCOTDPuzzleItem
{
	Default
	{
		//$Title Hourglass
		Scale 0.7;
		Tag "$TAG_HOURGLASSKEY";
		Inventory.Icon "ZEUHA0";
		Inventory.PickupMessage "$IP_HOURGLASSKEY";
	}

	override void SetCollected(void)
	{
		Owner.ACS_ScriptCall("SetHourGlassCollected", 0);
	}

	States
	{
	Spawn:
		ZEUH A -1;
		Stop;
	}
}

class TCOTDKeyDiamond : TCOTDPuzzleItem
{
	Default
	{
		//$Title Diamond Key
		Scale 0.7;
		Tag "$TAG_DIAMONDKEY";
		Inventory.Icon "DIAMA0";
		Inventory.PickupMessage "$IP_DIAMONDKEY";
	}

	override void SetCollected(void)
	{
		Owner.ACS_ScriptCall("SetDiamondCollected", 0);
	}

	States
	{
	Spawn:
		DIAM A -1;
		Stop;
	}
}
