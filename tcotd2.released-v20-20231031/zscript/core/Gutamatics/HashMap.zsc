class TCOTDGutamaticsHasher abstract {
	abstract virtualscope uint hash(Object o);
	abstract virtualscope bool eq(Object o0, Object o1);
	static void hashCombine(out uint seed, uint other) {
		seed ^= other + 0x9E3779B9 + (seed << 6) + (seed >> 2);
	}
}

class TCOTDGutamaticsBoxedInt {
	int val;
	static TCOTDGutamaticsBoxedInt create(int val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	TCOTDGutamaticsBoxedInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class TCOTDGutamaticsIntHasher : TCOTDGutamaticsHasher {
	override uint hash(Object o) { return TCOTDGutamaticsBoxedInt(o).val; }
	override bool eq(Object o0, Object o1) { return TCOTDGutamaticsBoxedInt(o0).val == TCOTDGutamaticsBoxedInt(o1).val; }
}

class TCOTDGutamaticsBoxedUInt {
	uint val;
	static TCOTDGutamaticsBoxedUInt create(uint val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	TCOTDGutamaticsBoxedUInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class TCOTDGutamaticsUIntHasher : TCOTDGutamaticsHasher {
	override uint hash(Object o) { return TCOTDGutamaticsBoxedUInt(o).val; }
	override bool eq(Object o0, Object o1) { return TCOTDGutamaticsBoxedUInt(o0).val == TCOTDGutamaticsBoxedUInt(o1).val; }
}

class TCOTDGutamaticsBoxedDouble {
	double val;
	static TCOTDGutamaticsBoxedDouble create(double val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	TCOTDGutamaticsBoxedDouble clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class TCOTDGutamaticsBoxedString {
	String val;
	static TCOTDGutamaticsBoxedString create(String val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	TCOTDGutamaticsBoxedString clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class TCOTDGutamaticsHashSentinel {}

class TCOTDGutamaticsHashUnoccupied {}

class TCOTDGutamaticsKVStorage {
	Array<Object> keys;
	Array<Object> values;
	int loadCount;

	TCOTDGutamaticsHashUnoccupied unoccupied;

	TCOTDGutamaticsHashSentinel empty;
	TCOTDGutamaticsHashSentinel tombstone;

	Object get(Object key, TCOTDGutamaticsHasher hasher) {
		let hash = hasher.hash(key);
		let size = uint(self.keys.size());
		let index = hash % size;
		let i = index;
		while (true) {
			let kb = self.keys[i];
			if (kb == self.empty) {
				break;
			}
			if (kb != self.tombstone && hasher.eq(kb, key)) {
				return self.values[i];
			}
			i = (i + 1) % size;
		}
		return self.unoccupied;
	}

	TCOTDGutamaticsKVStorage resized(uint newSize, TCOTDGutamaticsHasher hasher) {
		let newKV = new("TCOTDGutamaticsKVStorage");
		newKV.keys.resize(newSize);
		newKV.values.resize(newSize);

		newKV.unoccupied = self.unoccupied;
		newKV.empty = self.empty;
		newKV.tombstone = self.tombstone;

		newKV.clear();

		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				newKV.insert(k, self.values[i], hasher);
			}
		}

		return newKV;
	}

	void insert(Object key, Object value, TCOTDGutamaticsHasher hasher) {
		let hash = hasher.hash(key);
		let size = uint(self.keys.size());
		let index = hash % size;
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					self.values[i] = value;
					return;
				}
				i = (i + 1) % size;
			}
		}
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty || kb == self.tombstone) {
					self.keys[i] = key;
					self.values[i] = value;
					if (kb == self.empty) {
						self.loadCount += 1;
					}
					break;
				}
				i = (i + 1) % size;
			}
		}
	}

	Object remove(Object key, TCOTDGutamaticsHasher hasher) {
		let hash = hasher.hash(key);
		let size = uint(self.keys.size());
		{
			let index = hash % size;
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					let val = self.values[i];
					self.keys[i] = self.tombstone;
					self.values[i] = NULL;
					return val;
				}
				i = (i + 1) % size;
			}
		}
		return self.unoccupied;
	}

	bool isEmpty() {
		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				return false;
			}
		}
		return true;
	}

	void clear() {
		for (let i = 0; i < self.keys.size(); i++) {
			self.keys[i] = self.empty;
			self.values[i] = NULL;
		}
		self.loadCount = 0;
	}
}

class TCOTDGutamaticsHashMapIterator {
	private TCOTDGutamaticsKVStorage m;
	private int i;

	static TCOTDGutamaticsHashMapIterator internalCreate(TCOTDGutamaticsKVStorage kv) {
		let ret = new();
		ret.m = kv;
		ret.i = 0;
		return ret;
	}

	Object, Object next() {
		while (self.i < m.keys.size()) {
			let k = m.keys[self.i];
			let v = m.values[self.i];
			self.i += 1;
			if (k != self.m.empty && k != self.m.tombstone) {
				return k, v;
			}
		}
		return self.m.unoccupied, NULL;
	}
}

class TCOTDGutamaticsHashMap {
	private TCOTDGutamaticsKVStorage kv;
	private TCOTDGutamaticsHasher hasher;

	static TCOTDGutamaticsHashMap create(TCOTDGutamaticsHasher hasher) {
		let ret = new();
		ret.kv = new("TCOTDGutamaticsKVStorage");
		ret.kv.keys.resize(8);
		ret.kv.values.resize(8);
		ret.kv.unoccupied = new("TCOTDGutamaticsHashUnoccupied");
		ret.kv.empty = new("TCOTDGutamaticsHashSentinel");
		ret.kv.tombstone = new("TCOTDGutamaticsHashSentinel");
		ret.kv.clear();
		ret.hasher = hasher;
		return ret;
	}

	Object get(Object key) {
		return self.kv.get(key, self.hasher);
	}

	bool containsKey(Object key) {
		return !(self.kv.get(key, self.hasher) is "TCOTDGutamaticsHashUnoccupied");
	}

	void resize(uint newSize) {
		self.kv = self.kv.resized(newSize, hasher);
	}

	void insert(Object key, Object value) {
		if (self.kv.loadCount + 1 > self.kv.keys.size() / 2) {
			self.resize(self.kv.keys.size() * 2);
		}
		self.kv.insert(key, value, self.hasher);
	}

	Object remove(Object key) {
		return self.kv.remove(key, self.hasher);
	}

	bool isEmpty() {
		return self.kv.isEmpty();
	}
	
	void clear() {
		self.kv.clear();
	}

	TCOTDGutamaticsHashMapIterator iter() {
		return TCOTDGutamaticsHashMapIterator.internalCreate(self.kv);
	}

	String debugRepr() {
		let ret = "";
		for (let i = 0; i < self.kv.keys.size(); i++) {
			ret.appendFormat("%d : ", i);
			let k = self.kv.keys[i];
			if (k == self.kv.empty) {
				ret.appendFormat("EMPTY");
			} else if (k == self.kv.tombstone) {
				ret.appendFormat("TOMBSTONE");
			} else {
				ret.appendFormat("%p -> %p", k, self.kv.values[i]);
			}
			if (i != self.kv.keys.size() - 1) {
				ret.appendFormat("\n");
			}
		}
		return ret;
	}
}

class TCOTDGutamaticsHashSetIterator {
	private TCOTDGutamaticsHashMapIterator i;

	static TCOTDGutamaticsHashSetIterator internalCreate(TCOTDGutamaticsHashMapIterator i) {
		let ret = new();
		ret.i = i;
		return ret;
	}

	Object next() {
		return self.i.next();
	}
}

class TCOTDGutamaticsHashSet {
	private TCOTDGutamaticsHashMap hm;

	static TCOTDGutamaticsHashSet create(TCOTDGutamaticsHasher hasher) {
		let ret = new();
		ret.hm = TCOTDGutamaticsHashMap.create(hasher);
		return ret;
	}

	bool contains(Object key) {
		return self.hm.containsKey(key);
	}

	void resize(uint newSize) {
		self.hm.resize(newSize);
	}

	void insert(Object key) {
		self.hm.insert(key, NULL);
	}

	Object remove(Object key) {
		return self.hm.remove(key);
	}

	bool isEmpty() {
		return self.hm.isEmpty();
	}

	void clear() {
		self.hm.clear();
	}

	TCOTDGutamaticsHashSetIterator iter() {
		return TCOTDGutamaticsHashSetIterator.internalCreate(self.hm.iter());
	}

	String debugRepr() {
		return self.hm.debugRepr();
	}
}
