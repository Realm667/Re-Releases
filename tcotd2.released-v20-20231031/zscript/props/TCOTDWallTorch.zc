/*
* TCOTD2 Game Code
* Copyright (C) 2020 - 2023  Nash Muhandes
*
* This program is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

//===========================================================================
//
// TCOTDWallTorch
//
//===========================================================================

class TCOTDWallTorch : TCOTDSwitchableDecoration replaces BlueTorch
{
	Default
	{
		Radius 16;
		Height 68;
		-SOLID
	}

	//===========================================================================
	//
	//
	//
	//===========================================================================

	States
	{
	Spawn:
	Active:
		TBLX A 3 Bright NoDelay Light("WALLTORCH")
		{
			if (!CheckRange(1000.0, true))
			{
				if (!IsActorPlayingSound(CHAN_VOICE, "sfx/fireloop1"))
					A_StartSound("sfx/fireloop1", CHAN_VOICE, CHANF_LOOP, volume: 0.1, startTime: frandom[rnd_WallTorchFireSpawn](0.f, S_GetLength("sfx/fireloop1")));

				A_SpawnItemEx("TCOTDWallTorchFlame",
					frandom[rnd_WallTorchFireSpawn](-2.0, 2.0), frandom[rnd_WallTorchFireSpawn](-2.0, 2.0), frandom[rnd_WallTorchFireSpawn](74.0, 76.0),
					0.001 * frandom[rnd_WallTorchFireSpawn](10.0, 200.0), 0.001 * frandom[rnd_WallTorchFireSpawn](10.0, 200.0), 0.001 * frandom[rnd_WallTorchFireSpawn](500.0, 1000.0));
			}
			else if (IsActorPlayingSound(CHAN_VOICE, "sfx/fireloop1"))
			{
				A_StopSound(CHAN_VOICE);
			}
		}
		Loop;
	Inactive:
		TBLX A 1
		{
			A_StopSound(CHAN_VOICE);
		}
		TBLX A -1;
		Stop;
	}
}

//===========================================================================
//
// TCOTDWallTorchFlame
//
//===========================================================================

class TCOTDWallTorchFlame : TCOTDFire
{
	Default
	{
		Scale 0.175;
	}

	States
	{
	Spawn:
		FIR1 A 0 NoDelay;
		Goto SpawnLoop;
	}
}
