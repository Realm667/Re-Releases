ACTOR NewPistol : Pistol replaces Pistol
{
	AttackSound "weapons/pistol"
	Decal BulletChip
	States
	{
	Fire:
	Hold:
		PISG A 3
		PISG B 0 A_GunFlash
		PISG B 6 A_FireBullets(5.6, 0, 1, 5, "NewPuff")
		PISG B 0 A_FireProjectile("PisCaseSpawn",20,0,4,0,FPF_NOAUTOAIM ,0)
		PISG C 4
		PISG B 5 A_Refire
		Goto Ready
	}
}

ACTOR NewShotgun : Shotgun replaces Shotgun
{
	AttackSound "weapons/shotgf"
	Decal BulletChip
	States
	{
	Fire:
	Hold:
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 7 A_FireBullets(5.6, 0, 7, 5, "NewPuff")
		SHTG BC 5
		SHTG D 1
		SHTG D 3 A_FireProjectile("ShotCaseSpawn",0,0,-4,0,FPF_NOAUTOAIM ,0)
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_Refire
		Goto Ready
	}
}

ACTOR NewSuperShotgun : SuperShotgun replaces SuperShotgun
{
	AttackSound "weapons/sshotf"
	Decal BulletChip
	States
	{
	Fire:
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		SHT2 A 7 A_FireBullets(11.2, 7.1, 20, 5, "NewPuff")
		SHT2 B 7 A_CheckReload
		SHT2 C 7
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", 20, -2, 30, 0, 0, 0, 180, 128)
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", 20, 2, 30, 0, 0, 0, 180, 128)
		SHT2 D 7 A_PlaySound("weapons/sshoto")
		SHT2 E 7
		SHT2 F 7 A_PlaySound("weapons/sshotl")
		SHT2 G 7
		SHT2 H 0 A_Refire
		SHT2 H 7 A_PlaySound("weapons/sshotc")
		Goto Ready
	}
}

ACTOR NewChaingun : Chaingun replaces Chaingun
{
	attacksound "weapons/chngun"
	Decal BulletChip
	States
	{
	Fire:
	Hold:
		CHGG A 0 A_GunFlash
		CHGG A 0 A_FireProjectile("PisCaseSpawn",40,0,-4,-8,FPF_NOAUTOAIM ,0)
		CHGG A 4 A_FireBullets(5.6, 0, 1, 5, "NewPuff")
		CHGG A 0 A_JumpIfNoAmmo(3)
		CHGG B 0 A_FireProjectile("PisCaseSpawn",40,0,-4,-8,FPF_NOAUTOAIM ,0)
		CHGG B 4 A_FireBullets(5.6, 0, 1, 5, "NewPuff")
		CHGG B 0 A_Refire
		Goto Ready
	Flash:
		CHGF A 5 BRIGHT A_Light1
		CHGF A 0 BRIGHT A_JumpIfNoAmmo(2)
		CHGF B 5 BRIGHT A_Light2
		TNT1 B 0 BRIGHT A_Light0
	Stop
	}
}

ACTOR NewPuff : BulletPuff //replaces BulletPuff
{
	Alpha 1.0
	//Mass 12 //I like the fact puffs make big splashes.
	// But as long as there are terrain sounds for solid floors puffs must not do them!
	States
	{
	Spawn:
	Melee:
		PUFF A 2
		PUFF A 0 A_Jump(128,2)
		PUFF A 2 A_SpawnItemEx("Puff2", 0, 0, 0, 0, 0, 0, 0, 128)
		PUFF BCD 3
		Stop
	}
}

ACTOR Puff2
{
	PROJECTILE
	+NOCLIP
	Mass 1
	Scale 0.5
	States
	{
	Spawn:
		PUFF E 1 Bright
		PUFF E 1 Bright A_PlaySound("puff/ric")
		PUFF FGH 2 Bright
		Stop
	}
}

ACTOR BlankPuff
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

ACTOR RocketNew : Rocket Replaces Rocket
{
-RocketTrail
States
{
Spawn:
    MISL A 1 Bright A_SpawnItemEx("RefRockettrail", -16, frandom(-0.1,0.1), frandom(3.0,4.0), 0, frandom(-0.4,0.4), frandom(0.0,0.5))
    Loop
}
}

actor RefRockettrail
{
	Radius 1
	Height 1
	Speed 0
	Damage 0
	Scale 0.30
	RenderStyle Translucent
	Alpha 1.0
	+NOGRAVITY
	States
	{
	Spawn:
		PDBA V 0
		PDBA V 0 A_Jump(255,1,2,3,4)
		PDBA VWXYZ 1 
		{
			A_FadeOut(frandom(0.04,0.05));
			A_SetScale(ScaleX + frandom(0.013,0.016), ScaleY + frandom(0.013,0.016));
		}
		Wait
	}
}