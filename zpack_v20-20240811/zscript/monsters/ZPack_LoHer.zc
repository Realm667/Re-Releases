Class LordofHeresy : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Health 1250;
		Radius 20;
		Height 56;
		Speed 13;
		Mass 1000;
		PainChance 50;
		BloodColor "DarkOliveGreen";
		ActiveSound "LordOfHeresy/act";
		DeathSound "LordOfHeresy/death";
		PainSound "demon/pain";
		SeeSound "LordOfHeresy/sight";
		Obituary "$OB_LOHSY";
		MONSTER;
		+FLOORCLIP
	}

	States
	{
	Spawn:
		LOHS AB 10 A_Look();
		Loop;
	See:
		LOHS AABBCCDD 4 A_Chase();
		Loop;
	Missile:
		LOHS E 0 A_Jump(85, "Missile2");
		LOHS E 0 A_Jump(128, "Missile3");
		LOHS EF 8 A_FaceTarget();
		LOHS G 8 A_BruisAttack();
		Goto See;
	Missile2:
		LOHS E 8 A_FaceTarget();
		LOHS F 8 A_FaceTarget();
		LOHS G 8
			{
				A_SpawnProjectile("BaronBall", 32, 0,  8);
				A_SpawnProjectile("BaronBall", 32, 0,  0);
				A_SpawnProjectile("BaronBall", 32, 0, -8);
			}
		Goto See;
	Missile3:
		LOHS E 8 A_FaceTarget();
		LOHS F 8 A_FaceTarget();
		LOHS G 8
			{
				A_SpawnProjectile("BaronBall", 32, 0, -20);
				A_SpawnProjectile("BaronBall", 32, 0,   0);
			}
		LOHS E 8 A_FaceTarget();
		LOHS F 8 A_FaceTarget();
		LOHS G 8
			{
				A_SpawnProjectile("BaronBall", 32, 0,  20);
				A_SpawnProjectile("BaronBall", 32, 0,   0);
			}
		LOHS E 8 A_FaceTarget();
		LOHS F 8 A_FaceTarget();
		LOHS G 8
			{
				A_SpawnProjectile("BaronBall", 32, 0, -12);
				A_SpawnProjectile("BaronBall", 32, 0,  12);
			}
		Goto See;
	Pain:
		LOHS H 5;
		LOHS H 5 A_Pain();
		Goto See;
	Death:
		LOHS I 8;
		LOHS J 8 A_Scream();
		LOHS K 8 A_Fall();
		LOHS LMN 8;
		LOHS O -1;
		Stop;
	Raise:
		LOHS ONMLKJI 5;
		Goto See;
	}
}