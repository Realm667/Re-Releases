Class Shadow : Actor
{
	Default
	{
		//$Category ZPack Monsters
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		RenderStyle "STYLE_Translucent";
		Alpha 0.7;
		PainSound "Shadow/pain";
		DeathSound "Shadow/death";
		ActiveSound "Shadow/active";
		Obituary "$OB_SHADW";
		MONSTER;
		+FLOORCLIP
	}

	States
	{
	Spawn:
	See:
		SHDW E 15;
		SHDW AAA 1 A_Chase();
		SHDW A 0 A_SpawnItemEx("ShadowGhostA", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW BBB 1 A_Chase();
		SHDW B 0 A_SpawnItemEx("ShadowGhostB", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW CCC 1 A_Chase();
		SHDW C 0 A_SpawnItemEx("ShadowGhostC", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW DDD 1 A_Chase();
		SHDW D 0 A_SpawnItemEx("ShadowGhostD", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW AAA 1 A_Chase();
		SHDW A 0 A_SpawnItemEx("ShadowGhostA", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW BBB 1 A_Chase();
		SHDW B 0 A_SpawnItemEx("ShadowGhostB", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW CCC 1 A_Chase();
		SHDW C 0 A_SpawnItemEx("ShadowGhostC", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		SHDW DDD 1 A_Chase();
		SHDW D 0 A_SpawnItemEx("ShadowGhostD", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		Goto See+1;
	Missile:
		SHDW EEEEEEEEEEEEEEE 2 A_FaceTarget();
	Shoot:
		SHDW F 5 A_FaceTarget();
		SHDW G 5 A_SpawnProjectile("ShadowBall");
		SHDW F 5 A_FaceTarget();
		SHDW EEEEEEEEEE 2 A_FaceTarget();
		SHDW E 1 A_SpidRefire();
		Loop;
	Pain:
		SHDW H 4;
		SHDW H 4 A_Pain();
		Goto See+1;
	Death:
		SHDW I 6 A_Scream();
		SHDW J 5;
		SHDW K 5 A_SpawnItemEx("ShadowTorso", 0,0,0, 1,0,0, 0, SXF_CLIENTSIDE);
		SHDW L 5 A_Fall();
		SHDW MNO 6;
		SHDW P -1;
		Stop;
	XDeath:
		SHDX A 8;
		SHDX B 6 A_XScream();
		SHDX C 6;
		SHDX D 6 A_Fall();
		SHDX E 6;
		SHDX F 6;
		SHDX G -1;
		Stop;
	Raise:
		SHDW MKLJI 8;
		Goto See;
	}
}

Class ShadowBall : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 12;
		Damage 4;
		RenderStyle "STYLE_Add";
		Alpha 0.75;
		SeeSound "Shadow/attack";
		DeathSound "imp/shotx";
		PROJECTILE;
		+BRIGHT
		+RANDOMIZE
	}

	States
	{
	Spawn:
		SBAL ABC 4 A_SpawnItemEx("ShadowTrail", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
		Loop;
	Death:
		SBAL C 5;
		SBAL DEFGH 4;
		Stop;
	}
}

Class ShadowGhostA : Actor
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 0;
		Damage 0;
		Mass 75;
		RenderStyle "STYLE_Translucent";
		Alpha 0.3;
		PROJECTILE;
	}

	States
	{
	Spawn:
		SHDW A 10;
		Stop;
	}
}

Class ShadowGhostB : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW B 10;
		Stop;
	}
}

Class ShadowGhostC : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW C 10;
		Stop;
	}
}

Class ShadowGhostD : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW D 10;
		Stop;
	}
}

Class ShadowTorso : Actor
{
	Default
	{
		Radius 12;
		Height 15;
		Speed 1;
		Mass 5000;
		RenderStyle "STYLE_Translucent";
		Alpha 0.7;
		BounceType "Doom";
	}

	States
	{
	Spawn:
		SHDW QRS 5;
		SHDW T 5 A_Fall();
		SHDW U 5;
		SHDW V -1;
		Stop;
	}
}

Class ShadowTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		RenderStyle "STYLE_Add";
		Alpha 0.6;
		PROJECTILE;
		+BRIGHT
		+NOCLIP
	}

	States
	{
	Spawn:
		SHTR ABCDEF 4;
		Stop;
	}
}