Class Bloodfiend : Demon
{
	Default
	{
		//$Category ZPack Monsters
		Health 250;
		GibHealth 35;
		PainChance 140;
		Speed 12;
		Radius 30;
		Height 56;
		Mass 450;
		MeleeThreshold 196; //equivalent of deprecated LONGMELEERANGE flag
		SeeSound "Monster/sg2sit";
		AttackSound "Monster/sg2atk";
		PainSound "demon/Pain";
		DeathSound "monster/sg2dth";
		ActiveSound "demon/sg2act";
		Obituary "$OB_BFIEN";
		HitObituary "$HB_BFIEN";
		MONSTER;
		+DONTHARMCLASS
		+FLOORCLIP
	}

	States
	{
	Spawn:
		SAR2 AB 10 A_Look();
		Loop;
	See:
		SAR2 AABBCCDD 2 A_Chase();
		Loop;
	Melee:
		SAR2 EF 8 A_FaceTarget();
		SAR2 G 8 A_SargAttack();
		Goto See;
	Missile:
		SAR2 GF 8 A_FaceTarget();
		SAR2 E 8 A_BloodShots();
		Goto See;
	Pain:
		SAR2 H 2;
		SAR2 H 2 A_Pain();
		Goto See;
	Death:
		SAR2 I 8;
		SAR2 J 8 A_Scream();
		SAR2 K 4;
		SAR2 L 4 A_NoBlocking();
		SAR2 M 4;
		SAR2 N -1;
		Stop;
	XDeath: //new
		SAR2 O 5;
		SAR2 P 5 A_XScream();
		SAR2 Q 5 A_NoBlocking();
		SAR2 RSTUV 5;
		SAR2 W -1;
		Stop;
	Raise:
		SAR2 NMLKJI 5;
		Goto See;
	}

	protected void A_BloodShots(void)
	{
		A_StartSound("weapons/bloodf");
		A_SpawnProjectile("Bloodshot",22,0,-1,0,0);
		A_SpawnProjectile("Bloodshot",22,0,1,0,0);
	}
}

Class BloodShot : Actor
{
	Default
	{
		Radius 3;
		Height 3;
		Speed 20;
		Damage 2;
		RenderStyle "STYLE_Translucent";
		Scale 0.8;
		Alpha 0.80;
		DeathSound "weapons/bloodx";
		PROJECTILE;
		+BRIGHT
		+THRUGHOST
	}

	States
	{
	Spawn:
		FGOR A 2 A_BloodMissile();
		FGOR B 2 A_BloodMissile();
		Loop;
	Death:
		FGOR CDE 3;
		Stop;
	}

	protected void A_BloodMissile(void) //here because A_CStaffMissileSlither is deprecated mainly - ozy81
	{
		A_Weave(3,0,1.0,0.0);
		A_SpawnItemEx("BloodTrail",flags: SXF_CLIENTSIDE);
	}
}

Class BloodTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		RenderStyle "STYLE_Translucent";
		Scale 0.5;
		Alpha 0.67;
		PROJECTILE;
	}

	States
	{
	Spawn:
		TNT1 A 3;
		FGOR FGH 3 BRIGHT;
		Stop;
	}
}

Class Lurker : BloodFiend
{
	Default
	{
		RenderStyle "STYLE_Translucent";
		Obituary "$OB_LURKR";
		HitObituary "$HB_LURKR";
		Alpha 0.25;
		//DropItem "BloodAmmo";
	}
}