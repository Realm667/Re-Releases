ACTOR RedFire //straight from Demonic Uprising
{
	DistanceCheck "tcotd1lod"
	RenderStyle Add
	Scale 0.175
	+BRIGHT
	+FORCEXYBILLBOARD
	+NOGRAVITY
	States
	{
	Spawn:
		FIR1 A 0
		FIR1 A 0 A_Jump(128, "Spawn2")
		FIR1 ABCDEFG 3
		FIR1 HIJKL 2 A_FadeOut(0.15)
		Stop
	Spawn2:
		FIR2 ABCDEFG 3
		FIR2 HIJKL 2 A_FadeOut(0.15)
		Stop
	}
}

ACTOR TCOTDWallTorch : SwitchableDecoration replaces BlueTorch
{
	Radius 16
	Height 68
	ProjectilePassHeight -16
	+SOLID
	States
	{
	Active:
		TBLX A 0 A_StartSound("Ignite")
		"####" A 0 {bDormant = FALSE;}
	Spawn:
		TBLX A 0 NODELAY
		TBLX A 3 BRIGHT Light("WALLTORCH") A_SpawnItemEx("RedFire", Random(-2, 2), Random(-2, 2), Random(74, 76), 0.001 * Random(10, 200), 0.001 * Random(10, 200), 0.001 * Random(1000, 2000), 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Loop
	Inactive:
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		TBLX A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR TCOTDStickTorch : SwitchableDecoration replaces ShortRedTorch
{
	Radius 16
	Height 68
	ProjectilePassHeight -16
	+SOLID
	States
	{
	Active:
		TRCH A 0 A_StartSound("Ignite")
		"####" A 0 {bDormant = FALSE;}
	Spawn:
		TRCH A 0 NODELAY
		TRCH A 3 BRIGHT Light("WALLTORCH") A_SpawnItemEx("RedFire", Random(-2, 2), Random(-2, 2), Random(34, 30), 0.001 * Random(10, 200), 0.001 * Random(10, 200), 0.001 * Random(1000, 2000), 0, SXF_SETMASTER | SXF_CLIENTSIDE)
		Loop
	Inactive:
		"####" A 0 A_RemoveChildren(TRUE, RMVF_MISC)
		TRCH A -1 {bDormant = TRUE;}
		Stop
	}
}

ACTOR CreepyCandle : SwitchableDecoration
{
	//$Category New Lights (TCOTD1)
	//$Title Creepy Candle (ON)
	//$Color 11
	Radius 4
	Height 8
	Scale 0.8
	-SOLID
	+NOTAUTOAIMED
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Spawn:
		CDLE B 0
		Goto CheckDormant
	CheckDormant:
		"####" "#" 0  {
			if (bDormant) {
				return ResolveState("Off");
			}
			return ResolveState("On");
		}
	On:
		"####" BCDA 4 LIGHT(CREPLIT2)
		Loop
	Off:
		"####" E -1
		Stop
	Active:
	Inactive:
		"####" E 0 {
			bDormant = !bDormant;
			return ResolveState("CheckDormant");}
	}
}

ACTOR CandelabraBloodOne : CreepyCandle
{
	//$Title Creepy Candlestick (ON)
	Radius 16
	Height 60
	Scale 1.0
	+SOLID
	States
	{
	Spawn:
		XB02 B 0
		Goto CheckDormant
	On:
		"####" BCDA 4 LIGHT(CREPLIT1)
		Loop
	}
}