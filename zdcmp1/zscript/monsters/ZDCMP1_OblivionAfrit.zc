Class OblivionDemon : Actor
{
	Default
	{
		//$Category ZDCMP1 Monsters
		Health 6000;
		Radius 64;
		Height 300;
		Mass 16000;
		Speed 12;
		Scale 2.0;
		PainChance 12;
		SeeSound "OblivionAfrit/Sight";
		PainSound "OblivionAfrit/Pain";
		DeathSound "OblivionAfrit/Death";
		ActiveSound "OblivionAfrit/Active";
		Obituary "$OB_OBLI";
		HitObituary "$HB_OBLI";
		Damagefactor "HellFire", 0.0;
		Damagefactor "Fire", 0.0;
		Damagefactor "Ice", 1.5;
		Monster;
		+BOSS
		+DONTHARMCLASS
		+FLOAT
		+FLOORCLIP
		+LOOKALLAROUND
		+MISSILEEVENMORE
		+NOBLOOD
		+NOGRAVITY
		+NORADIUSDMG
	}

	States
	{
	Spawn:
		BFRT A 0 NoDelay
		{
			A_SpawnProjectile("OrbitComet", 44, 0, 0, 2, 0);
			A_SpawnProjectile("OrbitComet2", 44, 0, 180, 2, 0);
		}
		//Intentional fall-through to Idle state.
	Idle:
		BFRT AB 10 Bright A_Look();
		Loop;
	See:
		BFRT AABBCCDD 2 Bright A_Chase();
		Goto See+2;
	Melee:
		BFRT A 0 Bright A_Jump(104, "Melee2");
		BFRT EF 6 Bright A_FaceTarget();
		BFRT G 6 Bright A_CustomComboAttack("BruiserBall2", 44, 20, "Baron/Melee");
		BFRT G 0 Bright A_JumpIfTargetOutsideMeleeRange("See");
		BFRT HI 6 Bright A_FaceTarget();
		BFRT J 6 Bright A_CustomComboAttack("BruiserBall2", 44, 20, "Baron/Melee");
		Goto See;
	Melee2:
		BFRT KL 6 Bright A_FaceTarget();
		BFRT M 6 Bright A_SpawnProjectile("HellFire", 44);
		Goto See;
	Missile:
		BFRT E 0 Bright A_JumpIfCloser(512,"MissileCheck");
	Missile0:
		BFRT E 0 Bright A_Jump(144, "Missile2");
		BFRT EF 6 Bright A_FaceTarget();
		BFRT G 6 Bright A_CustomComboAttack("BruiserBall", 44, 20, "Baron/Melee");
		BFRT G 0 Bright A_MonsterRefire(64,"See");
		BFRT HI 6 Bright A_FaceTarget();
		BFRT J 6 Bright A_CustomComboAttack("BruiserBall", 44, 20, "Baron/Melee");
		Goto See;
	Missile2:
		BFRT E 0 Bright A_Jump(128, "Missile3");
		BFRT EF 6 Bright A_FaceTarget();
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0,-40,0);
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0,-30,0);
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0,-20,0);
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0,-10,0);
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0,  0,0);
		BFRT G 0 Bright A_SpawnProjectile("BruiserBall2",56,0, 10,0);
		BFRT G 6 Bright A_SpawnProjectile("BruiserBall2",56,0, 20,0);
		BFRT G 0 Bright A_MonsterRefire(32,"See");
		BFRT HI 6 Bright A_FaceTarget();
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0, 40,0);
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0, 30,0);
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0, 20,0);
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0, 10,0);
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0,  0,0);
		BFRT J 0 Bright A_SpawnProjectile("BruiserBall2",56,0,-10,0);
		BFRT J 6 Bright A_SpawnProjectile("BruiserBall2",56,0,-20,0);
		Goto See;
	Missile3:
		BFRT KL 6 Bright A_FaceTarget();
		BFRT M 0 Bright A_SpawnProjectile("BruiserAfritComet",56,0,-15,0);
		BFRT M 0 Bright A_SpawnProjectile("BruiserAfritComet",56,0,  0,0);
		BFRT M 6 Bright A_SpawnProjectile("BruiserAfritComet",56,0, 15,0);
		Goto See;
	MissileCheck:
		BFRT E 0 Bright A_Jump(192,"Missile0");
		Goto Melee2;
	Pain:
		BFRT N 4 Bright;
		BFRT N 4 Bright A_Pain();
		Goto See;
	Death:
		BFRT O 5 Bright A_Fall();
		BFRT P 4 Bright A_Scream();
		BFRT QR 4 Bright;
		BFRT S 4 Bright A_NoBlocking();
		BFRT TUVW 4 Bright;
		BFRT XYZ[ 4;
		BFRT \ -1;
		Stop;
	}
}
////////////////////////////////
Class BruiserFireSpawner : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 12;
		Damage 0;
		Projectile;
		+BLOODLESSIMPACT
		+FLOORHUGGER
		+RIPPER
	}

	States
	{
	Spawn:
		TNT1 A 3 Bright A_SpawnProjectile("BruiserFire", 0, 0, 0, CMF_TRACKOWNER);
		TNT1 AA 3;
		Loop;
	Death:
		TNT1 A 6;
		Stop;
	}
}

Class BruiserBallTrail : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    Speed 0;
    RenderStyle "Add";
    Alpha 0.70;
    Projectile;
    +NoClip
  }

  States
  {
  Spawn:
    BRBA CDEFGHIJ 4 Bright;
    Stop;
  }
}

Class BruiserFire : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    Speed 0;
    Damage 20;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "weapons/rocklx";
    Projectile;
    +NoClip
    +FloorHugger
  }

  States
  {
  Spawn:
    XXBF AB 3 Bright;
    XXBF C 3 Bright A_Explode(48,128,0);
    XXBF DEFGHIJKLMNOPQRST 3 Bright;
    Stop;
  }
}
////////////////////////////////
Class BruiserBall : Actor
{
  Default
  {
    Radius 16;
    Height 16;
    Speed 18;
    Damage 16;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "Scorch";
    Projectile;
    +Randomize;
  }

  States
  {
  Spawn:
    BRBA AABB 2 Bright A_SpawnItemEx("BruiserBallTrail");
    Loop;
  Death:
    BRBA KLMNOPQRSTUVWX 3 Bright;
    Stop;
  }
}

Class BruiserBall2 : Actor
{
  Default
  {
    Radius 8;
    Height 8;
    Speed 10;
    Damage 8;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "BaronScorch";
    Projectile;
    +Randomize
  }

  States
  {
  Spawn:
    BRB2 AB 6 Bright;
    Loop;
  Death:
    BRB2 CDEFGHI 3 Bright;
    Stop;
  }
}

////////////////////////////////
//Bruiser-Afrit original classes
////////////////////////////////

Class BruiserAfritComet : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 12;
		Damage 5;
		Scale 0.55;
		Projectile;
		SeeSound "BruiserAfrit/CometFire";
		DeathSound "BruiserAfrit/CometHit";
		Decal "Scorch";
	}
   
	States
	{
	Spawn:
		COMT AAABBBCCC 1 Bright A_SpawnItemEx("CometTail");
		COMT C 0 Bright A_SpawnItemEx("BruiserAfritCometBall", 0, 0, -8, 0, 0, 4, 0, SXF_TRANSFERPOINTERS);
		Loop;
		Death:
		COMT D 0 Bright A_SetRenderStyle(0.7, STYLE_Add);
		COMT D 0 Bright A_SetScale(1, 1);
		COMT D 3 Bright A_SpawnItemEx("CometDeathGlow");
		COMT E 3 Bright A_Explode(30, 80, 0);
		COMT F 3 Bright A_SpawnProjectile("HellFire",44,0,0,CMF_TRACKOWNER);
		COMT GHI 3 Bright;
		Stop;
  }
}

Class BruiserAfritCometBall : Actor
{
	Default
	{
		Radius 4;
		Height 6;
		Damage 3;
		Scale 0.8;
		Projectile;
		RenderStyle "ADD";
		Alpha 0.9;
		DeathSound "BruiserAfrit/Explosion";
		+RANDOMIZE
		-NOGRAVITY
	}
	States
	{
	Spawn:
		XXBF OS 4 Bright;
		Loop;
	Death:
		TNT1 A 0 A_SetScale(1, 1);
		XXBF AB 2 Bright;
		XXBF C 2 Bright A_Explode(48, 128,0);
		XXBF DEFGHIJKLMNOPQRST 2 Bright;
		Stop;
	}
}

/* original decorate boss
//Oblivion Boss and related code

ACTOR OblivionDemon 222
{
	Health 6000
	Speed 12
	Radius 64
	Height 200
	Scale 3.0
	PainChance 12
	Mass 16000
	Meleedamage 40
	SeeSound "OblivSight"
	PainSound "baron/pain"
	DeathSound "OblivDeath"
	ActiveSound "baron/active"
	MeleeSound "baron/melee"
	Missiletype "OblivionBall"
	MissileHeight 88
	REACTIONTIME 8
	MONSTER
	+BOSS
	+FLOORCLIP
	+NORADIUSDMG
	+NOGRAVITY
	+FLOATBOB
	+FLOAT
	Obituary "%o was nuked into oblivion by the Oblivion Demon"
	HitObituary "%o found the Oblivion Demon too hot to handle."
	States
	{
	Spawn:
		FRIT ABCD 4 Bright A_Look
		Loop
	See:
		FRIT A 0 A_CustomMissile("OblivionOrbitComet", 44, 0, 0)
		FRIT A 0 A_CustomMissile("OblivionOrbitComet2", 44, 0, 180)
		FRIT AABBCCDD 2 Bright A_Chase
		Goto See +2
	Melee:
		FRIT ST 8 Bright A_FaceTarget
		FRIT U 6 Bright A_MeleeAttack
		Goto See +2
	Missile:
		FRIT E 0 A_Jump(196,15)
		FRIT EF 6 BRIGHT A_FaceTarget
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 0)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 30)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 60)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 90)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 120)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 150)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 180)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -150)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -120)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -90)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -60)
		FRIT G 6 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -30)
		GoTo See +2
		FRIT E 0 A_Jump(160,4)
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile ("OblivionBall", 88, 0, 0)
		Goto See +2
		FRIT E 0 A_Jump(128,8)
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 0 BRIGHT A_StartSound("OblivAttack")
		FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 0)
		FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 30)
		FRIT G 5 BRIGHT A_CustomMissile("OblivionComet", 88, 0, -30)
		FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 15)
		FRIT G 5 Bright A_CustomMissile("OblivionComet", 88, 0, -15)
		Goto See +2
		FRIT EF 6 BRIGHT A_FaceTarget
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, 0)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, 10)
		FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, -10)
		GoTo See +2
	Pain:
		FRIT HH 4 Bright A_Pain
		Goto See +2
	Death:
		FRIT I 0 Bright A_NoGravity
		FRIT I 6 Bright A_Scream
		FRIT J 5 Bright A_NoBlocking
		FRIT KLMNOPQR 4 Bright
		Stop
	}
}

ACTOR OblivionComet //no frames?
{
	Radius 12
	Height 16
	Speed 10
	Damage 20
	ExplosionDamage 160
	ExplosionRadius 320
	Scale 1.1
	PROJECTILE
	SeeSound "OvlivAttack"
	DeathSound "CometExplosion"
	MissileType OblivionCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COML AAAABBBBCCCC 1 Bright A_MissileAttack
		Loop
	Death:
		COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
		COML E 3 Bright A_Explode
		COML E 0 A_CustomMissile("OblivionCometDeath", 0, 0, 0)
		Stop
	}
}

ACTOR OblivionCometTail
{
	PROJECTILE
	RENDERSTYLE ADD
	+BRIGHT
	+NOCLIP
	Scale 2.0
	ALPHA 0.4
	States
	{
	Spawn:
		FRTB ABCDEFGHI 1
		Stop
	}
}

ACTOR OblivionCometDeath //no frames?
{
    PROJECTILE
    RENDERSTYLE ADD
	+BRIGHT
    +NOCLIP
    Scale 2.0
    ALPHA 0.7
    States
    {
    Spawn:
        COML FGHI 3
        Stop
    }
}

ACTOR OblivionCometDeathGlow : OblivionCometTail
{
    Scale 4.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3
        Stop
    }
}

ACTOR OblivionOrbitCometTail : OblivionCometTail
{
    Scale 1.0
}

ACTOR OblivionOrbitComet : OblivionComet //no frames?
{
	Scale 1.0
	Speed 10
	+NOCLIP
	MissileType OblivionOrbitCometTail
	MissileHeight 0
	SeeSound ""
	States
	{
	Spawn:
		COML AABBCC 1 Bright A_MissileAttack
		COML A 0 A_CustomMissile("OblivionOrbitComet", 0, 0, 75)
		COML A 0 A_MissileAttack
		Stop
	}
}

ACTOR OblivionOrbitComet2 : OblivionOrbitComet //no frames?
{
	Speed 10
	Scale 1.0
	MissileType OblivionOrbitCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COML AABBCC 1 Bright A_MissileAttack
		COML A 0 A_CustomMissile("OblivionOrbitComet2", 0, 0, -75)
		COML A 0 A_MissileAttack
		Stop
	}
}

ACTOR OblivionBall
{
    Radius 12
    Height 16
    Speed 30
    Damage 10
    Scale 2.0
    DONTHURTSHOOTER //to be removed, apply on A_Explode
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
	+BRIGHT
    SeeSound "OblivAttack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        FRTM AB 5
        Loop
    Death:
        FRTM CDE 6
        Stop
    }
}

ACTOR OblivionFireSummoner //no frames?
{
	spawnID 238
	Radius 0
	Height 0
	Speed 20
	Scale 0.25
	PROJECTILE
	-NOGRAVITY
	States
	{
	Spawn:
		COML ABCD 4
		Loop
	Death:
		COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
		COML E 3 Bright A_Explode
		COML E 0 A_CustomMissile("BurningDeath", 0, 0, 150)
		COML E 0 A_CustomMissile("BurningDeath", 0, 0, -150)
		COML E 0 A_CustomMissile("BurningDeath", 0, 0, 180)
		Stop
	}
}

ACTOR BurningDeath
{
	Height 60
	Radius 24
	Speed 36
	Damage 4
	PROJECTILE
	RenderStyle ADD
	+BRIGHT
	+FLOORHUGGER
	+RIPPER
	SeeSound "OblivAttack"
	DeathSound "CometExplosion"
	States
	{
	Spawn:
		FIRE ABCD 4
		FIRE EF 4
		GoTo Spawn+4
	Death:
		FIRE EFGH 4
		Stop
	}
}

ACTOR Fake_OblivionComet
{
	SpawnID 237
	Radius 12
	Height 16
	Speed 10
	Damage 5
	ExplosionDamage 5
	ExplosionRadius 16
	Scale 1.1
	PROJECTILE
	SeeSound "OvlivAttack"
	DeathSound "CometExplosion"
	MissileType OblivionCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COML AAAABBBBCCCC 1 Bright A_MissileAttack
		Loop
	Death:
		COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
		COML E 3 Bright A_Explode
		COML E 0 A_CustomMissile("OblivionCometDeath", 0, 0, 0)
		Stop
	}
}
*/