/*Do you have any idea how long it took to clean this wad up? Don't you dare change this back 
  to LWM'S way!!!! 
      Grrr, GIEST! What did you have to go and add an A at the beginning of everything for?
  Okay, this is worse than I thought... I'll get you for this giest :D.
  -Lexus Alyus (Resident Cleaner)
 */

actor NamiDarkImp : DoomImp 3100
{
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkSeeker
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

actor DarkSeeker
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    +SEEKERMISSILE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        DKB1 AB 4 Bright A_SeekerMissile (5,10)
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

actor VoidDarkImp : DoomImp 3101
{
    Health 120
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DarkNormal
    MeleeDamage 3
    States
    {
    Spawn:
        DRKI AB 10 A_Look
        Loop
    See:
        DRKI AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_ComboAttack
        Goto See
    Missile:
        DRKI E 0 A_Jump(64,4)
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto Missile+7
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkStrong", 32, 0, 0)
        DRKI E 0 A_Jump(64,4)
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkNormal", 32, 0, 0)
        Goto See
        DRKI EF 8 A_FaceTarget
        DRKI G 6 A_CustomMissile("DarkStrong", 32, 0, 0)
        Goto See
    Pain:
        DRKI H 2
        DRKI H 2 A_Pain
        Goto See
    Death:
        DRKI I 8
        DRKI J 8 A_Scream
        DRKI K 6
        DRKI L 6 A_Fall
        DRKI M -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKI ML 8
        DRKI KJI 6
        Goto See
    }
}

actor DarkNormal
{
    Radius 8
    Height 6
    Speed 9
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

actor DarkStrong
{
    Radius 8
    Height 6
    Speed 9
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    MISSILEHEIGHT 0
    MissileType Blood
    States
    {
    Spawn:
        DKB2 A 4 Bright A_MissileAttack
        DKB2 B 4 Bright
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

Actor StoneImp 3103
{
	Health 300
	Radius 20
	Height 56
	Speed 6
	PainChance 50
	Mass 400
	MONSTER 
	+FLOORCLIP
	+NOBLOOD
	+NORADIUSDMG
	SeeSound "imp/sight"
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	MeleeSound "MaceHit"
	Obituary "%o was smashed by a stone imp."
	MeleeDamage 12
	States
	{
	Spawn:
		GIMP AB 10 A_Look
		Loop
	See:
		GIMP AABBCCDD 3 A_Chase
		Loop
	Melee:
		GIMP EF 8 A_FaceTarget
		GIMP G 6 A_MeleeAttack
		Goto See
	Pain:
		GIMP H 2
		GIMP H 2 A_Pain
		Goto See
	Death:
		GIMP I 8
		GIMP J 8 A_Scream
		GIMP K 6
		GIMP L 6 A_Fall
		GIMP M 6
		GIMP N -1
		Stop
	Raise:
		GIMP ML 8
		GIMP KJI 6
		Goto See
	}
}

ACTOR BloodDemonClone 3110
{ 
    Health 300 
    Speed 12 
    Height 56
    Radius 30
    Mass 400
    PainChance 180
    Meleedamage 5 
    REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
    SeeSound "demon/sight"
    PainSound "demon/pain"
    DeathSound "demon/death"
    ActiveSound "demon/active"
    MeleeSound "demon/melee"
    HitObituary "%o Was chewed up and spat out by the Blood Demon." 
    States 
    { 
    Spawn: 
       SRG2 AB 10 A_Look
       Loop 
    See: 
       SRG2 AABBCCDD 2 A_Chase
       Loop 
    Melee: 
       SRG2 EF 8 A_FaceTarget
       SRG2 G 8 A_MeleeAttack
       Goto See 
    Pain: 
       SRG2 H 2
       SRG2 H 2 A_Pain
       Goto See
    Death: 
       SRG2 I 8 
       SRG2 J 8 A_Scream
       SRG2 K 4 
       SRG2 L 4 A_NoBlocking
       SRG2 M 4 
       SRG2 N -1 
       Stop 
    Raise: 
       SRG2 NMLKJI 5 
       Goto See 
    } 
}

actor HellGuard 3114
{
    Health 300
    Radius 24
    Height 64
    Speed 8
    PainChance 50
    Mass 500
    MONSTER 
    +FLOORCLIP
    SeeSound "knight/sight"
    PainSound "knight/pain"
    DeathSound "knight/death"
    ActiveSound "knight/active"
    Obituary "%o was fried by a hell guard."
    MeleeDamage 8
    States
    {
    Spawn:
        HLGD AB 10 A_Look
        Loop
    See:
        HLGD AABBCCDD 3 A_Chase
        Loop
    Missile:
        HLGD E 8 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto Missile+6
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto Missile+6
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto Missile+6
        HLGD E 4 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto Missile+12
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto Missile+12
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto Missile+12
        HLGD E 4 A_FaceTarget
        HLGD F 0 A_Jump(85,4)
        HLGD F 0 A_Jump(128,2)
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, -4, 1)
        Goto See
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 4, 1)
        Goto See
        HLGD F 4 Bright A_CustomMissile("HellGuardBall", 28, -8, 0, 1)
        Goto See
    Pain:
        HLGD G 2
        HLGD G 2 A_Pain
        Goto See
    Death:
        HLGD H 6
        HLGD I 6 A_Scream
        HLGD J 6
        HLGD K 6 A_Fall
        HLGD LM 6
        HLGD N -1
        Stop
    Raise:
        HLGD ONMLKJI 8
        Goto See
    }
}

actor HellGuardBall
{
    Radius 13
    Height 8
    Speed 10
    Damage 6
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        HGFB ABCD 2 Bright
        Loop
    Death:
        HGFB EFGH 4 Bright
        Stop
    }
}

ACTOR HellWarrior 3119
{ 
   Health 400 
   BloodColor "ff d3 00"
   Speed 8
   Radius 24
   Height 64
   PainChance 50
   Mass 1000
   Meleedamage 15 
   SeeSound "knight/sight"
   PainSound "knight/pain"
   DeathSound "knight/death"
   ActiveSound "knight/active"
   MeleeSound "baron/melee"
   REACTIONTIME 8 
   MONSTER 
   Missiletype HellWarriorBall
   Obituary "%o was tagged by the hell warrior's fireball." 
   HitObituary "%o was torn apart by a hell warrior." 
   States 
   { 
   Spawn: 
      HWAR AB 10 A_Look 
      Loop 
   See: 
      HWAR AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
   Melee: 
      HWAR EF 8 A_FaceTarget 
      HWAR G 8 A_ComboAttack 
      HWAR EF 8 A_FaceTarget 
      HWAR G 8 A_ComboAttack 
      Goto See 
   Pain: 
      HWAR H 1 A_Pain 
      HWAR H 0 A_SetInvulnerable 
      HWAR HHHHHHHH 6 A_FaceTarget 
      HWAR H 2 A_UnSetInvulnerable 
      Goto See 
   Death: 
      HWAR I 8 
      HWAR J 8 A_Scream
      HWAR K 8
      HWAR L 8 A_NoBlocking 
      HWAR MN 8  
      HWAR O -1 
      Stop 
   Raise: 
      HWAR ONMLKJI 5 
      Goto See 
   } 
}

actor HellWarriorBall
{
    Radius 6
    Height 8
    Speed 15
    Damage 8
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        HWFB A 5 Bright
        Loop
    Death:
        HWFB BCDE 6 Bright
        Stop
    }
}

ACTOR LordofHeresy 3121
{ 

    Health 1250 
    Radius 20 
    Height 56 
    Speed 13 
    PainChance 50
    Mass 1000
    MONSTER 
    Obituary "%o was annihilated by the heresy lord." 
    SeeSound "baron/sight" 
    PainSound "demon/pain" 
    DeathSound "baron/death" 
    ActiveSound "demon/active" 
    States 
    { 
    Spawn: 
        LOHS AB 10 A_Look 
        Loop 
    See: 
        LOHS AABBCCDD 4 A_Chase 
        Loop 
    Missile: 
        LOHS E 0 A_Jump(85,5) 
        LOHS E 0 A_Jump(128,9)
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 8 A_BruisAttack 
        Goto See 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 8) 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, -8) 
        Goto See 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, -20) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, 20) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 0) 
        LOHS E 8 A_FaceTarget 
        LOHS F 8 A_FaceTarget 
        LOHS G 0 A_Custommissile ("BaronBall", 32, 0, -12) 
        LOHS G 8 A_Custommissile ("BaronBall", 32, 0, 12) 
        Goto See 
    Pain: 
        LOHS H 5 
        LOHS H 5 A_Pain 
        Goto See 
    Death: 
        LOHS I 8 
        LOHS J 8 A_Scream 
        LOHS K 8 A_Fall 
        LOHS LMN 8 
        LOHS O -1 
        Stop 
    Raise: 
        LOHS ONMLKJI 5 
        Goto See 
    } 
}

ACTOR CacolanternClone 3111
{ 
   Health 800 
   BloodColor "ff 80 00"
   Speed 8 
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6 
   REACTIONTIME 8 
   MONSTER
   +FLOORCLIP
   +NOGRAVITY
   +FLOAT
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   Missiletype CacolanternCloneBall 
   HitObituary "%o admired the Cacolantern's teeth." 
   Obituary "%o couldn't take %h eyes off the eye." 
   States 
   { 
   Spawn: 
      HED2 A 10 bright A_Look 
      Loop 
   See: 
      HED2 A 3 bright A_Chase 
      Loop 
   Missile: 
      HED2 BC 5 bright A_FaceTarget 
      HED2 D 5 bright A_ComboAttack
      Goto See 
   Pain: 
      HED2 E 4 bright
      HED2 E 4 bright A_Pain 
      HED2 F 5 bright
      Goto See 
   Death: 
      HED2 G 8 bright
      HED2 H 8 bright A_Scream 
      HED2 I 8 bright
      HED2 J 8
      HED2 K 8 A_NoBlocking 
      HED2 L -1 
      Stop 
   Raise: 
      HED2 LKJ 8
      HED2 IHG 8 bright 
      Goto See 
   } 
} 

actor CacolanternCloneBall
{
    Radius 13
    Height 8
    Speed 20
    Damage 9
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal CacoScorch
    States
    {
    Spawn:
        BAL8 ABCDEFGH 5 Bright
        Loop
    Death:
        BAL8 IJKL 4 Bright
        Stop
    }
}

ACTOR EasyUberImp : DoomImp 202
{
     Radius 38
     Height 100
     PainChance 25
     Health 500
     Speed 8
     +BOSS
     Mass 1000
     Scale 1.25
     Obituary "%o was incinerated by the Uber Imp."
     Translation "128:152=173:191", "64:79=173:191", "48:63=173:191", "168:191=192:207"
     States
     {
     Missile:
          TROO EF 7 A_FaceTarget
          TROO GG 0 A_CustomMissile ("FatShot", 40, 0, 0)
          TROO G 0 A_CustomMissile ("DoomImpBall", 40, 0, -16)
          TROO G 7 A_CustomMissile ("DoomImpBall", 40, 0, 16)
          GoTo See
     }
}

ACTOR NormalUberImp : DoomImp 203
{
     Radius 38
     Height 100
     PainChance 25
     Health 750
     Speed 8
     +BOSS
     Mass 1000
     Scale 1.5
     Obituary "%o was incinerated by the Uber Imp."
     Translation "128:152=173:191", "64:79=173:191", "48:63=173:191", "168:191=192:207"
     States
     {
     Missile:
          TROO EF 7 A_FaceTarget
          TROO G 0 A_CustomMissile ("FatShot", 40, 0, 0)
          TROO G 0 A_CustomMissile ("DoomImpBall", 40, 0, 8)
          TROO G 0 A_CustomMissile ("DoomImpBall", 40, 0, -8)
          TROO G 0 A_CustomMissile ("DoomImpBall", 40, 0, -16)
          TROO G 7 A_CustomMissile ("DoomImpBall", 40, 0, 16)
          GoTo See
     }
}

ACTOR HardUberImp : DoomImp 204
{
     Radius 38
     Height 100
     PainChance 25
     Health 1000
     Speed 8
     +BOSS
     Mass 1000
     Scale 1.75
     Obituary "%o was incinerated by the Uber Imp."
     Translation "128:152=173:191", "64:79=173:191", "48:63=173:191", "168:191=192:207"
     States
     {
     Missile:
          TROO EF 7 A_FaceTarget
          TROO GG 0 A_CustomMissile ("FatShot", 40, 0, 0)
          TROO G 0 A_CustomMissile ("FatShot", 40, 0, 8)
          TROO G 0 A_CustomMissile ("FatShot", 40, 0, -8)
          TROO G 0 A_CustomMissile ("DoomImpBall", 40, 0, -16)
          TROO G 7 A_CustomMissile ("DoomImpBall", 40, 0, 16)
          GoTo See
     }
}

pickup 2.0.64
{
	DoomEdNum 6001
	Sprite 2064
	Frames "A*"
	PickupMessage "OMG!!! YOU FOUND ZDOOM 2.0.64!!! OMG!!!!!!!!!"
	PickupSound "64cabsnd"
}

actor FreeFiRE
{
    SpawnID 239
    +NOGRAVITY
    +NOCLIP
    States
    {
    Spawn:
        FIRE A 3 BRIGHT
        FIRE B 3 BRIGHT
        FIRE C 3 BRIGHT
        FIRE D 3 BRIGHT
        FIRE E 3 BRIGHT
        FIRE F 3 BRIGHT
        FIRE G 3 BRIGHT
        FIRE H 3 BRIGHT
        Stop
    }
}

actor fake_OblivionComet
{
    SpawnID 237
    Radius 12
    Height 16
    Speed 10
    Damage 5
    ExplosionDamage 5
    ExplosionRadius 16
    Scale 1.1
    PROJECTILE
    SeeSound "OvlivAttack"
    DeathSound "CometExplosion"
    MissileType OblivionCometTail
    MissileHeight 0
    States
    {
    Spawn:
        COML AAAABBBBCCCC 1 Bright A_MissileAttack
        Loop
    Death:
        COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
        COML E 3 Bright A_Explode
        COML E 0 A_CustomMissile("OblivionCometDeath", 0, 0, 0)
        Stop
    }
}

/* Grubber ****************************************************/

GrubberStatue
{
	DoomEdNum 6004
	Radius 24
	Height 96
	Sprite GRB0
	Frames "A"
	Solid
}

ACTOR OblivionDemon 222
{ 
   Health 6000 
   Speed 12
   Radius 64
   Height 200
   PainChance 12
   Mass 16000
   +BOSS
   Meleedamage 40 
   SeeSound "OblivSight"
   PainSound "baron/pain"
   DeathSound "OblivDeath"
   ActiveSound "baron/active"
   MeleeSound "baron/melee"
   Missiletype "OblivionBall"
   MissileHeight 88
   REACTIONTIME 8
   MONSTER
   Scale 3.0
   +FLOORCLIP
   +NORADIUSDMG
   +NOGRAVITY
   +FLOATBOB
   +FLOAT
   Obituary "%o was nuked into oblivion by the Oblivion Demon" 
   HitObituary "%o found the Oblivion Demon too hot to handle." 
   States 
   { 
   Spawn:
      FRIT ABCD 4 Bright A_Look 
      Loop
   See:
      FRIT A 0 A_CustomMissile("OblivionOrbitComet", 44, 0, 0)
      FRIT A 0 A_CustomMissile("OblivionOrbitComet2", 44, 0, 180)
      FRIT AABBCCDD 2 Bright A_Chase 
      Goto See +2
   Melee:
      FRIT ST 8 Bright A_FaceTarget
      FRIT U 6 Bright A_MeleeAttack
      Goto See +2
   Missile:
      FRIT E 0 A_Jump(196,15)
      FRIT EF 6 BRIGHT A_FaceTarget
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 0)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 30)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 60)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 90)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 120)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 150)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, 180)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -150)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -120)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -90)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -60)
      FRIT G 6 BRIGHT A_CustomMissile ("OblivionFireSummoner", 88, 0, -30)
      GoTo See +2
      FRIT E 0 A_Jump(160,4)
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 6 Bright A_CustomMissile ("OblivionBall", 88, 0, 0)
      Goto See +2
      FRIT E 0 A_Jump(128,8)
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 0 BRIGHT A_PlaySound("OblivAttack")
      FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 0)
      FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 30)
      FRIT G 5 BRIGHT A_CustomMissile("OblivionComet", 88, 0, -30)
      FRIT G 0 Bright A_CustomMissile("OblivionComet", 88, 0, 15)
      FRIT G 5 Bright A_CustomMissile("OblivionComet", 88, 0, -15)
      Goto See +2
      FRIT EF 6 BRIGHT A_FaceTarget
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, 0)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, 10)
      FRIT G 0 BRIGHT A_CustomMissile ("OblivionBall", 88, 0, -10)
      GoTo See +2
   Pain: 
      FRIT HH 4 Bright A_Pain 
      Goto See +2
   Death: 
      FRIT I 0 Bright A_NoGravity
      FRIT I 6 Bright A_Scream
      FRIT J 5 Bright A_NoBlocking
      FRIT KLMNOPQR 4 Bright
      Stop 
   } 
}

actor OblivionComet
{
    Radius 12
    Height 16
    Speed 10
    Damage 20
    ExplosionDamage 160
    ExplosionRadius 320
    Scale 1.1
    PROJECTILE
    SeeSound "OvlivAttack"
    DeathSound "CometExplosion"
    MissileType OblivionCometTail
    MissileHeight 0
    States
    {
    Spawn:
        COML AAAABBBBCCCC 1 Bright A_MissileAttack
        Loop
    Death:
        COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
        COML E 3 Bright A_Explode
        COML E 0 A_CustomMissile("OblivionCometDeath", 0, 0, 0)
        Stop
    }
}

actor OblivionCometTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    Scale 2.0
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1 Bright
        Stop
    }
}

actor OblivionCometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    Scale 2.0
    ALPHA 0.7
    States
    {
    Spawn:
        COML FGHI 3 Bright
        Stop
    }
}

actor OblivionCometDeathGlow : OblivionCometTail
{
    Scale 4.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3 Bright
        Stop
    }
}

actor OblivionOrbitCometTail : OblivionCometTail
{
    Scale 1.0
}

actor OblivionOrbitComet : OblivionComet
{
     SeeSound ""
     +NOCLIP
     Scale 1.0
     Speed 10
     MissileType OblivionOrbitCometTail
     MissileHeight 0
     States
     {
     Spawn:
         COML AABBCC 1 Bright A_MissileAttack
         COML A 0 A_CustomMissile("OblivionOrbitComet", 0, 0, 75)
         COML A 0 A_MissileAttack
         Stop
     }
}

actor OblivionOrbitComet2 : OblivionOrbitComet
{
     Scale 1.0
     MissileType OblivionOrbitCometTail
     Speed 10
     MissileHeight 0
     States
     {
     Spawn:
         COML AABBCC 1 Bright A_MissileAttack
         COML A 0 A_CustomMissile("OblivionOrbitComet2", 0, 0, -75)
         COML A 0 A_MissileAttack
         Stop
     }
}

actor OblivionBall
{
    Radius 12
    Height 16
    Speed 30
    Damage 10
    DONTHURTSHOOTER
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    Scale 2.0
    SeeSound "OblivAttack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        FRTM AB 5 Bright
        Loop
    Death:
        FRTM CDE 6 Bright
        Stop
    }
}

actor OblivionFireSummoner
{
    spawnID 238
    Radius 0
    Height 0
    Speed 20
    Scale 0.25
    PROJECTILE
    - NOGRAVITY
    States
    {
    Spawn:
        COML ABCD 4
        Loop
    Death:
        COML D 3 Bright A_CustomMissile("OblivionCometDeathGlow", 0, 0, 0)
        COML E 3 Bright A_Explode
        COML E 0 A_CustomMissile("BurningDeath", 0, 0, 150)
        COML E 0 A_CustomMissile("BurningDeath", 0, 0, -150)
        COML E 0 A_CustomMissile("BurningDeath", 0, 0, 180)
        Stop
    }
}

ACTOR BurningDeath
{
     +FLOORHUGGER
     PROJECTILE
     Speed 36
     +RIPPER
    SeeSound "OblivAttack"
    DeathSound "CometExplosion"
     Damage 4
     RenderStyle ADD
     Height 60
     Radius 24
     States
     {
     Spawn:
          FIRE ABCD 4 BRIGHT
          FIRE EF 4 BRIGHT
          GoTo Spawn+4
     Death:
          FIRE EFGH 4 BRIGHT
          Stop
     }
}

ACTOR Afrit 3120
{ 
   Health 800 
   Speed 6
   Radius 24
   Height 72
   PainChance 50
   Mass 500
   Meleedamage 10 
   SeeSound "baron/sight"
   PainSound "baron/pain"
   DeathSound "baron/death"
   ActiveSound "baron/active"
   MeleeSound "baron/melee"
   Missiletype "AfritBall"
   MissileHeight 44
   REACTIONTIME 8
   MONSTER
   +NORADIUSDMG
   +FLOORCLIP
   +NOGRAVITY
   +FLOATBOB
   +FLOAT
   Obituary "%o was scorched by an Afrit" 
   HitObituary "%o found the Afrit too hot to handle." 
   States 
   { 
   Spawn:
      FRIT ABCD 4 Bright A_Look 
      Loop
   See:
      FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0)
      FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180)
      FRIT AABBCCDD 2 Bright A_Chase 
      Goto See +2
   Melee:
      FRIT ST 8 Bright A_FaceTarget
      FRIT U 6 Bright A_ComboAttack
      Goto See +2
   Missile:
      FRIT E 0 A_Jump(128,4)
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 6 Bright A_ComboAttack
      Goto See +2
      FRIT EF 6 Bright A_FaceTarget
      FRIT G 6 Bright A_CustomMissile("Comet", 44, 0, 0)
      Goto See +2
   Pain: 
      FRIT HH 4 Bright A_Pain 
      Goto See +2
   Death: 
      FRIT I 0 Bright A_NoGravity
      FRIT I 6 Bright A_Scream
      FRIT J 5 Bright A_NoBlocking
      FRIT KLMNOPQR 4 Bright
      Stop 
   } 
}

actor Comet
{
    Radius 6
    Height 8
    Speed 10
    Damage 5
    ExplosionDamage 40
    ExplosionRadius 80
    Scale .55
    PROJECTILE
    SeeSound "CometLaunch"
    DeathSound "CometExplosion"
    MissileType CometTail
    MissileHeight 0
    States
    {
    Spawn:
        COMT AAAABBBBCCCC 1 Bright A_MissileAttack
        Loop
    Death:
        COMT D 3 Bright A_CustomMissile("CometDeathGlow", 0, 0, 0)
        COMT E 3 Bright A_Explode
        COMT E 0 A_CustomMissile("CometDeath", 0, 0, 0)
        Stop
    }
}

//******************************************************************************************

actor CometTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1 Bright
        Stop
    }
}

//******************************************************************************************

actor CometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.7
    States
    {
    Spawn:
        COMT FGHI 3 Bright
        Stop
    }
}

//******************************************************************************************

actor CometDeathGlow : CometTail
{
    Scale 2.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3 Bright
        Stop
    }
}

//******************************************************************************************

actor OrbitCometTail : CometTail
{
    Scale .5
}

//******************************************************************************************

actor OrbitComet : Comet
{
     SeeSound ""
     +NOCLIP
     Scale .28
     Speed 5
     MissileType OrbitCometTail
     MissileHeight 0
     States
     {
     Spawn:
         COMT AABBCC 1 Bright A_MissileAttack
         COMT A 0 A_CustomMissile("OrbitComet", 0, 0, 75)
         COMT A 0 A_MissileAttack
         Stop
     }
}

//******************************************************************************************

actor OrbitComet2 : OrbitComet
{
     MissileType OrbitCometTail
     MissileHeight 0
     States
     {
     Spawn:
         COMT AABBCC 1 Bright A_MissileAttack
         COMT A 0 A_CustomMissile("OrbitComet2", 0, 0, -75)
         COMT A 0 A_MissileAttack
         Stop
     }
}

//******************************************************************************************

actor AfritBall
{
    Radius 6
    Height 8
    Speed 15
    Damage 8
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        FRTM AB 5 Bright
        Loop
    Death:
        FRTM CDE 6 Bright
        Stop
    }
}

// Give the Hexen monster a doomednum
actor Serpent_ : Serpent 10000
{
}


//===========================================================================
//
// Nuke launcher
//
//===========================================================================

ACTOR NukeLauncher : RocketLauncher
{
	Weapon.AmmoType NukeAmmo
	Weapon.AmmoUse 1
	States
	{
	Fire:
	Hold:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireCustomMissile("NukeShot")
		MISG B 0 A_ReFire
		Goto Ready
	}
}


ACTOR NukeShot
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	SeeSound "weapons/rocklf"
	DeathSound "weapons/rocklx"
	+ROCKETTRAIL
	Decal Scorch
	States
	{
	Spawn:
		NUKS A 4
		Loop
	Death:
		MISL B 0 BRIGHT A_SetTranslucent(1,3)
		MISL B 4 BRIGHT A_Mushroom
		MISL C 6 BRIGHT
		MISL D 4 BRIGHT
		Stop
	}
}

ACTOR NukeAmmo : Ammo 6002
{
	States
	{
	Spawn:
		NUKE A	-1  	 
	}
	Inventory.PickupMessage "Picked up some nukes."
	Inventory.Amount 2
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 40
	Inventory.Icon NUKEA0
}

//===========================================================================
//
// Freezer
//
//===========================================================================
ACTOR Freezer : Weapon 6003
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the Freezer!"
	Weapon.AmmoType Cell
	Weapon.AmmoGive 40
	Weapon.AmmoUse 1
	Inventory.Icon FREZA0
	States
	{
	Spawn:
		FREZ A -1
		Loop
	Ready:
		FRZZ A 1 A_WeaponReady
		Loop
	Deselect:
		FRZZ A 1 A_Lower
		Loop
	Select:
		FRZZ A 1 A_Raise
		Loop
	Fire:
		FRZZ A 0 A_GunFlash
		FRZZ A 2 A_FireCustomMissile("FreezerBall")
		FRZZ A 20 A_ReFire
		Goto Ready
	Flash:
		FRZZ C 0 A_Jump(128,2)
		FRZZ C 2 BRIGHT A_Light1
		Goto Flash+3
		FRZZ D 2 BRIGHT A_Light1
		FRZZ D 0 A_Light0
		Stop
	}
}

ACTOR FreezerBall : PlasmaBall
{
	+ICEDAMAGE
	Decal FreezerScorch
}
