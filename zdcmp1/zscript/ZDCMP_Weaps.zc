/*
 * Copyright (c) 2007-2024 BlueShadow, Ozymandias81, Tormentor667, Xaser
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

// Weapons w/Casings (fixed up by Xaser a bit)
Class PistolCasing : Actor
{
	Default
	{
		Height 8;
		Radius 6;
		Speed 4;
		Scale 0.45;
		BounceType "Doom";
		BounceFactor 0.4;
		Mass 200; // [Blue Shadow] increasing the mass makes it sink faster in a 'swimmable water'
		SeeSound "weapons/case1";
		PROJECTILE;
		-NOGRAVITY
		-NOBLOCKMAP
	}

	States
	{
	Spawn:
		CAS2 A 0 A_StartSound("NULL");
		CAS2 ABCD 2 A_JumpIf(waterlevel > 0, "Underwater");
		Goto Spawn+1;
	Underwater:
		CAS2 ABCD 2 A_ScaleVelocity(0.925); // [Blue Shadow] makes the it de-accelerate :)
		Loop;
	Death:
		CAS2 A 0 A_Jump(64,4);
		CAS2 A 0 A_Jump(85,4);
		CAS2 A 0 A_Jump(128,4);
		CAS2 A 350;
		CAS2 AAAAAAAAAA 1 A_FadeOut(0.1);
		Stop;
		CAS2 B 350;
		CAS2 BBBBBBBBBB 1 A_FadeOut(0.1);
		Stop;
		CAS2 C 350;
		CAS2 CCCCCCCCCC 1 A_FadeOut(0.1);
		Stop;
		CAS2 D 350;
		CAS2 DDDDDDDDDD 1 A_FadeOut(0.1);
		Stop;
	}
}

Class PisCaseSpawn : Actor
{
	Default
	{
		Speed 20;
		PROJECTILE;
		+NOCLIP
	}

	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("PistolCasing", 0, 0, 0, 4, 0, Random(2, 4), Random(85, 95), SXF_CLIENTSIDE);
		Stop;
	}
}

Class RifleCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("PistolCasing", 0, 0, 0, 4, 0, Random(2, 4), Random(-95, -85), SXF_CLIENTSIDE);
		Stop;
	}
}

Class ShotgunCasing : PistolCasing
{
	Default
	{
		Height 12;
		Radius 9;
		Scale 0.7;
		SeeSound "weapons/shell";
	}

	States
	{
	Spawn:
		D3S1 A 0 A_StartSound("NULL");
		D3S1 ACBHEGFD 1 A_JumpIf(waterlevel > 0, "Underwater");
		Goto Spawn+1;
	Underwater:
		D3S1 ACBHEGFD 1 A_ScaleVelocity(0.925);
		Loop;
	Death:
		D3S1 A 0 A_Jump(32,8);
		D3S1 A 0 A_Jump(37,8);
		D3S1 A 0 A_Jump(43,8);
		D3S1 A 0 A_Jump(51,8);
		D3S1 A 0 A_Jump(64,8);
		D3S1 A 0 A_Jump(85,8);
		D3S1 A 0 A_Jump(128,8);
		D3S1 A 350;
		D3S1 AAAAAAAAAA 1 A_FadeOut(0.1);
		Stop;
		D3S1 B 350;
		D3S1 BBBBBBBBBB 1 A_FadeOut(0.1);
		Stop;
		D3S1 C 350;
		D3S1 CCCCCCCCCC 1 A_FadeOut(0.1);
		Stop;
		D3S1 D 350;
		D3S1 DDDDDDDDDD 1 A_FadeOut(0.1);
		Stop;
		D3S1 E 350;
		D3S1 EEEEEEEEEE 1 A_FadeOut(0.1);
		Stop;
		D3S1 F 350;
		D3S1 FFFFFFFFFF 1 A_FadeOut(0.1);
		Stop;
		D3S1 G 350;
		D3S1 GGGGGGGGGG 1 A_FadeOut(0.1);
		Stop;
		D3S1 H 350;
		D3S1 HHHHHHHHHH 1 A_FadeOut(0.1);
		Stop;
	}
}

Class ShotCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("ShotgunCasing", 0, 0, 0, 4, 0, Random(2, 4), Random(-95, -85), SXF_CLIENTSIDE);
		Stop;
	}
}

Class SShotCaseSpawn : ShotCaseSpawn
{
	Default
	{
		Speed 66;
	}

	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("ShotgunCasing", 0, 0, 0, 4, 0, Random(2, 4), Random(-95, -85), SXF_CLIENTSIDE);
		Stop;
	}
}

Class NewPistol : Pistol replaces Pistol
{
	Default
	{
		AttackSound "weapons/pistol";
	}

	States
	{
	Fire:
	Hold:
		PISG A 2;
		PISG A 1 A_SpawnItemEx("PisCaseSpawn",20,0,29,0,0,0,0,SXF_CLIENTSIDE);
		PISG B 0 A_GunFlash();
		PISG B 6 A_FireBullets(5.6, 0, 1, 5, "NewPuff");
		PISG C 4;
		PISG B 5 A_Refire();
		Goto Ready;
	}
}

Class NewShotgun : Shotgun replaces Shotgun
{
	Default
	{
		AttackSound "weapons/shotgf";
	}

	States
	{
	Fire:
	Hold:
		SHTG A 3;
		SHTG A 0 A_GunFlash();
		SHTG A 7 A_FireBullets(5.6, 0, 7, 5, "NewPuff");
		SHTG BC 5;
		SHTG D 1;
		SHTG D 3 A_SpawnItemEx("ShotCaseSpawn", 20, -4, 24, 0, 0, 0, 0, SXF_CLIENTSIDE);
		SHTG CB 5;
		SHTG A 3;
		SHTG A 7 A_Refire();
		Goto Ready;
	}
}

Class NewSuperShotgun : SuperShotgun replaces SuperShotgun
{
	Default
	{
		AttackSound "weapons/sshotf";
	}

	States
	{
	Fire:
		SHT2 A 3;
		SHT2 A 0 A_GunFlash();
		SHT2 A 7 A_FireBullets(11.2, 7.1, 20, 5, "NewPuff");
		SHT2 B 7 A_CheckReload();
		SHT2 C 7;
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", 20, -2, 30, 0, 0, 0, 180, SXF_CLIENTSIDE);
		SHT2 D 0 A_SpawnItemEx("SShotCaseSpawn", 20, 2, 30, 0, 0, 0, 180, SXF_CLIENTSIDE);
		SHT2 D 7 A_StartSound("weapons/sshoto");
		SHT2 E 7;
		SHT2 F 7 A_StartSound("weapons/sshotl");
		SHT2 G 7;
		SHT2 H 0 A_Refire();
		SHT2 H 7 A_StartSound("weapons/sshotc");
		Goto Ready;
	}
}

Class NewChaingun : Chaingun replaces Chaingun
{
	Default
	{
		AttackSound "weapons/chngun";
	}

	States
	{
	Fire:
	Hold:
		CHGG A 0 A_GunFlash();
		CHGG A 0 A_SpawnItemEx("PisCaseSpawn", 20, 6, 20, 0, 0, 0, 0, SXF_CLIENTSIDE);
		CHGG A 4 A_FireBullets(5.6, 0, 1, 5, "NewPuff");
		CHGG A 0 A_JumpIfNoAmmo(3);
		CHGG B 0 A_SpawnItemEx("PisCaseSpawn", 20, 6, 20, 0, 0, 0, 0, SXF_CLIENTSIDE);
		CHGG B 4 A_FireBullets(5.6, 0, 1, 5, "NewPuff");
		CHGG B 0 A_Refire();
		Goto Ready;
	Flash:
		CHGF A 4 BRIGHT A_Light1;
		CHGF A 0 BRIGHT A_JumpIfNoAmmo(2);
		CHGF B 4 BRIGHT A_Light2;
		TNT1 B 0 BRIGHT A_Light0;
		Stop;
	}
}

Class NewPuff : BulletPuff //replaces BulletPuff
{
	Default
	{
		Alpha 1.0;
	}

	States
	{
	Spawn:
	Melee:
		PUFF A 2;
		PUFF A 0 A_Jump(128,2);
		PUFF A 2 A_SpawnItemEx("Puff2", flags: SXF_CLIENTSIDE);
		PUFF BCD 3;
		Stop;
	}
}

Class Puff2 : Actor
{
	Default
	{
		Mass 1;
		Scale 0.5;
		PROJECTILE;
		+BRIGHT
		+NOCLIP
	}

	States
	{
	Spawn:
		PUFF E 1;
		PUFF E 1 A_StartSound("puff/ric");
		PUFF FGH 2;
		Stop;
	}
}

Class BlankPuff : Actor
{
	Default
	{
		+NOCLIP
	}

	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	}
}

Class RocketNew : Rocket Replaces Rocket
{
	Default
	{
		-ROCKETTRAIL
	}

	States
	{
	Spawn:
		MISL A 1 Bright A_SpawnItemEx("RefRocketTrail", -16, frandom(-0.1,0.1), frandom(3.0,4.0), 0, frandom(-0.4,0.4), frandom(0.0,0.5));
		Loop;
	}
}

Class RefRocketTrail : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Speed 0;
		Damage 0;
		Scale 0.30;
		RenderStyle "STYLE_Translucent";
		Alpha 1.0;
		+NOGRAVITY
	}

	States
	{
	Spawn:
		PDBA V 0;
		PDBA V 0 A_Jump(255,1,2,3,4);
		PDBA VWXYZ 1
		{
			A_FadeOut(frandom(0.04,0.05));
			A_SetScale(Scale.X + frandom(0.013,0.016), Scale.Y + frandom(0.013,0.016));
		}
		Wait;
	}
}