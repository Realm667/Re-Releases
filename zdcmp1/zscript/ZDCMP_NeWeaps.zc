//===========================================================================
//
// Nuke launcher
//
//===========================================================================

Class NukeLauncher : RocketLauncher
{
	Default
	{
		Weapon.AmmoType "NukeAmmo";
		Weapon.AmmoUse 1;
	}

	States
	{
	Fire:
	Hold:
		MISG B 8 A_GunFlash();
		MISG B 12 A_FireProjectile("NukeShot");
		MISG B 0 A_ReFire();
		Goto Ready;
	}
}

Class NukeShot : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		+ROCKETTRAIL
		//Decal Scorch
	}

	States
	{
	Spawn:
		NUKS A 4;
		Loop;
	Death:
		MISL B 0 BRIGHT A_SetTranslucent(1,3);
		MISL B 4 BRIGHT A_Mushroom();
		MISL C 6 BRIGHT;
		MISL D 4 BRIGHT;
		Stop;
	}
}

Class NukeAmmo : Ammo
{
	Default
	{
		Inventory.PickupMessage "$PK_NUKA";
		Inventory.Amount 2;
		Inventory.MaxAmount 20;
		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 40;
		Inventory.Icon "NUKEA0";
	}

	States
	{
	Spawn:
		NUKE A -1;
		Stop;
	}
}

//===========================================================================
//
// Freezer
//
//===========================================================================

Class Freezer : Weapon
{
	Default
	{
		Weapon.AmmoType "Cell";
		Weapon.AmmoGive 40;
		Weapon.AmmoUse 1;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "$PK_FREZ";
		Inventory.Icon "FREZA0";
	}

	States
	{
	Spawn:
		FREZ A -1;
		Loop;
	Ready:
		FRZZ A 1 A_WeaponReady();
		Loop;
	Deselect:
		FRZZ A 1 A_Lower();
		Loop;
	Select:
		FRZZ A 1 A_Raise();
		Loop;
	Fire:
		FRZZ A 0 A_GunFlash();
		FRZZ A 2 A_FireProjectile("FreezerBall");
		FRZZ A 20 A_ReFire();
		Goto Ready;
	Flash:
		FRZZ C 0 A_Jump(128,2);
		FRZZ C 2 BRIGHT A_Light1();
		Goto Flash+3;
		FRZZ D 2 BRIGHT A_Light1();
		FRZZ D 0 A_Light0();
		Stop;
	}
}

Class FreezerBall : PlasmaBall
{
	Default
	{
		+ICEDAMAGE
		//Decal FreezerScorch
	}
}