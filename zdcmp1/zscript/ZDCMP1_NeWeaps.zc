//===========================================================================
//
// Nuke launcher
//
//===========================================================================

Class NukeLauncher : RocketLauncher replaces RocketLauncher
{
	Default
	{
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 2;
		Weapon.AmmoGive1 2;
		Weapon.AmmoGive2 2;
		Weapon.AmmoType1 "RocketAmmo";
		Weapon.AmmoType2 "NukeAmmo";
	}

	States
	{
	AltFire:
		MISG B 8 A_GunFlash();
		MISG B 12 A_FireProjectile("NukeShot");
		MISG B 0 A_ReFire();
		Goto Ready;
	}
}

Class NukeShot : Actor	//improved by RichardDS90/Salahmander and Ozymandias81
{
	int user_theta;

	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		DamageType "Fire";
		Scale 0.87;
		Projectile;
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		+ROCKETTRAIL
	}

	States
	{
	Spawn:
		NUKS A 4;
		Loop;
	Death:
		NUKS A 0 A_SpawnProjectile("NukeShock",random(96,112),0,user_theta,CMF_AIMOFFSET);
		NUKS A 0 A_SpawnProjectile("NukeShock",random(96,112),0,user_theta,CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH,64);
		NUKS A 0 A_SpawnProjectile("NukeShock",random(96,112),0,user_theta,CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH,-64);
		NUKS A 0 A_SpawnProjectile("NukeShock",random(96,112),0,user_theta,CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH,random(-64,64));
		"####" A 0 {user_theta=user_theta+20;}
		"####" A 0 A_JumpIf(user_theta==360,"Explode");
		Loop;
	Explode:
		TNT1 A 0 { A_Mushroom(); A_SetRenderStyle(0.75,STYLE_Add); }
		NUKE ABC 2 Bright {scale *=1.0; Radius_Quake(10,10,0,16,0);}
		TNT1 A 0 A_SpawnItemEx("MultiExplo", Random(-20, 20), Random(-20, 20), Random(-20, 20) );
		NUKE D 2 Bright { A_Explode(192,128); A_SpawnItemEx("GeneralExplosion_ShockwaveOrange", Flags: SXF_CLIENTSIDE | SXF_TRANSFERSCALE); A_SpawnItemEx("GeneralExplosion_ShockwaveRed", Flags: SXF_CLIENTSIDE | SXF_TRANSFERSCALE);}
		TNT1 A 0 A_SpawnItemEx("MultiExplo", Random(-20, 20), Random(-20, 20), Random(-20, 20) );
		NUKE EFGHIJKLMNOPQRSTUVWX 2 Bright;
		Stop;
	}
}

Class MultiExplo : Rocket
{
	Default
	{
		RenderStyle "Add";
	}

	States
	{
	Spawn:
		MISL B 0 BRIGHT A_SetRenderStyle(0.75,STYLE_Add);
		MISL B 8 BRIGHT A_Explode(128,128);
		MISL C 6 BRIGHT;
		MISL D 4 BRIGHT;
		Stop;
	}
}

Class NukeShock : MultiExplo
{
	States
	{
	Spawn:
		MISL B 0 BRIGHT A_SetRenderStyle(0.75,STYLE_Add);
		TNT1 A 0 A_SpawnItemEx("DarkSmoke1");
		MISL B 6 BRIGHT;
		TNT1 A 0 A_SpawnItemEx("DarkSmoke1");
		MISL C 4 BRIGHT;
		TNT1 A 0 A_SpawnItemEx("DarkSmoke1");
		MISL D 2 BRIGHT;
		Stop;
	}
}

Class GeneralExplosion_ShockwaveOrange: Actor //Talon1024 from BoA explosion effects
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.75;
		+BRIGHT
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+ROLLSPRITE
	}
	
	States
	{
	Spawn:
		SHWV A 0;
	Setup:
		"####" "#" 0 {
			Roll = FRandom[Boom](0.0, 360.0);
			Scale *= .5;
		}
	Expand:
		"####" "#" 3 {
			Scale *= 1.1875;
			A_FadeOut(0.09375, FTF_REMOVE);
		}
		Loop;
	}
}

Class GeneralExplosion_ShockwaveRed : GeneralExplosion_ShockwaveOrange //Talon1024 from BoA explosion effects
{
	Default
	{
		+FLATSPRITE
	}
	
	States
	{
	Spawn:
		SHWV B 0;
		Goto Setup;
	}
}

Class NukeAmmo : Ammo
{
	Default
	{
		Inventory.PickupMessage "$PK_NUKA";
		Inventory.Amount 2;
		Inventory.MaxAmount 20;
		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 40;
		Inventory.Icon "NUKSB0";
	}

	States
	{
	Spawn:
		NUKS B -1;
		Stop;
	}
}

//===========================================================================
//
// Freezer
//
//===========================================================================
Class Freezer : Weapon
{
	Default
	{
		Weapon.AmmoType "FreezeAmmo";
		Weapon.AmmoGive 40;
		Weapon.AmmoUse 1;
		Inventory.PickupSound "freezer/pick";
		Inventory.PickupMessage "$PK_FREZ";
	}
	
	States
	{
	Spawn:
		FROS A -1;
		Loop;
	Ready:
		FRST ABCDEFGHIJ 1 A_WeaponReady();
		Loop;
	Deselect:
		FRST JJIIHHGGFFEEDDCCBBAA 1 A_Lower();
		FRST A 0 A_StopSound(CHAN_BODY);
		Wait;
	Select:
		FRST AABBCCDDEEFFGGHHIIJJ 1 A_Raise();
		Goto Ready;
	Fire:
		FRSF AB 1 A_StartSound ("freezer/fire",CHAN_WEAPON);
		TNT1 A 0 A_SetBlend("Cyan",0.095,5);
		FRSF C 1 A_FireProjectile("FreezerBall");
		FRSF D 1;
		TNT1 A 0 A_Refire();
		Goto Ready;
	Flash:
		TNT1 A 4 BRIGHT A_Light1();
		Goto LightDone;
		TNT1 A 4 BRIGHT A_Light1();
		Goto LightDone;
	}
}

Class FreezeAmmo : Ammo
{
	Default
	{
		Ammo.BackpackAmount 20;
		Ammo.BackpackMaxAmount 600;
		Inventory.Amount 20;
		Inventory.MaxAmount 300;
		Inventory.PickupMessage "$FROSTA";
		Inventory.Icon "FROPC0";
	}
	
	States
	{
	Spawn:
		FROP DDAABBCC 4;
		Loop;
	}
}

Class FreezerBall : PlasmaBall
{
	Default
	{
		SeeSound "freezer/iceshot";
		DeathSound "freezer/icehit";
		Obituary "$OB_FROST";
		+ICEDAMAGE
	}
	
	States
	{
	Spawn:
		FROM C 0;
		FROM C 0 A_SpawnItemEx("FrostTrail");
		FROM C 1 BRIGHT;
		FROM D 0 A_SpawnItemEx("FrostTrail");
		FROM D 1 BRIGHT;
		Loop;
	Death:
		FROM E 0;
		FROM E 0 A_Explode(8, 32);
		FROM EFGHI 3 BRIGHT;
		Stop;
	}
}

Class FrostTrail : Actor
{
	Default
	{
		RenderStyle "STYLE_Translucent";
		Alpha 0.75;
		Scale 0.7;
		+DONTSPLASH
		+NOBLOCKMAP
		+NOCLIP
		+NOGRAVITY
		+NOTELEPORT
	}
	
	States
	{
	Spawn:
		FROM A 0;
		FROM A 1 A_SetTranslucent(0.70, 1);
		FROM B 1 A_SetTranslucent(0.65, 1);
		FROM A 1 A_SetTranslucent(0.60, 1);
		FROM B 1 A_SetTranslucent(0.55, 1);
		FROM A 1 A_SetTranslucent(0.50, 1);
		FROM B 1 A_SetTranslucent(0.45, 1);
		FROM A 1 A_SetTranslucent(0.40, 1);
		FROM B 1 A_SetTranslucent(0.35, 1);
		FROM A 1 A_SetTranslucent(0.20, 1);
		FROM B 1 A_SetTranslucent(0.15, 1);
		FROM A 1 A_SetTranslucent(0.10, 1);
		FROM B 1 A_SetTranslucent(0.05, 1);
		Stop;
	}
}