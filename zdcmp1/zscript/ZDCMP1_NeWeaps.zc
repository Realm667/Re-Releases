//===========================================================================
//
// Nuke launcher
//
//===========================================================================

Class NukeLauncher : RocketLauncher replaces RocketLauncher
{
	Default
	{
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 5; //no more use of NukeAmmo - ozy81
		Weapon.AmmoGive1 2;
		Weapon.AmmoGive2 2;
		Weapon.AmmoType1 "RocketAmmo";
		Weapon.AmmoType2 "RocketAmmo";
	}

	States
	{
	AltFire:
	Hold:
		MISG B 8 A_GunFlash();
		MISG B 12 A_FireProjectile("NukeShot");
		MISG B 0 A_ReFire();
		Goto Ready;
	}
}

Class NukeShot : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		+ROCKETTRAIL
	}

	States
	{
	Spawn:
		NUKS A 4;
		Loop;
	Death:
		MISL B 0 BRIGHT A_SetTranslucent(1,3);
		MISL B 4 BRIGHT A_Mushroom();
		MISL C 6 BRIGHT;
		MISL D 4 BRIGHT;
		Stop;
	}
}

//===========================================================================
//
// Freezer
//
//===========================================================================
Class Freezer : Weapon
{
	Default
	{
		Weapon.AmmoType "Cell";
		Weapon.AmmoGive 40;
		Weapon.AmmoUse 1;
		Inventory.PickupSound "frostgun/Pick";
		Inventory.PickupMessage "$PK_FREZ";
		Inventory.Icon "FREZA0";
	}
	
	States
	{
	Spawn:
		FROS A -1;
		Loop;
	Ready:
		FRST ABCDEFGHIJ 1 A_WeaponReady();
		Loop;
	Deselect:
		FRST JJIIHHGGFFEEDDCCBBAA 1 A_Lower();
		FRST A 0 A_StopSound(CHAN_BODY);
		Wait;
	Select:
		FRST AABBCCDDEEFFGGHHIIJJ 1 A_Raise();
		Goto Ready;
	Fire:
		FRSF AB 1 A_StartSound ("frostgun/Fire",CHAN_WEAPON);
		TNT1 A 0 A_SetBlend("Cyan",0.095,5);
		TNT1 A 0 A_Quake(random(1,5),random(1,5),0,64);
		FRSF C 1 A_FireProjectile("FreezerBall");
		FRSF D 1;
		TNT1 A 0 A_Refire();
		//FRSF EFGHIJ 1;
		//FRSR AAAABBBBCCCCDDDDEEEEFFFFGGGGHHIIJJKKLL 1;
		//FRST A 5;
		Goto Ready;
	Flash:
		TNT1 A 4 BRIGHT A_Light1();
		Goto LightDone;
		TNT1 A 4 BRIGHT A_Light1();
		Goto LightDone;
	}
}

Class FreezerBall : PlasmaBall
{
	Default
	{
		+ICEDAMAGE
	}
}