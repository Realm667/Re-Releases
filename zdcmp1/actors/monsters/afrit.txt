ACTOR Afrit 3120
{
	Health 800
	Speed 6
	Radius 24
	Height 72
	PainChance 50
	Mass 500
	Meleedamage 10
	SeeSound "baron/sight"
	PainSound "baron/pain"
	DeathSound "baron/death"
	ActiveSound "baron/active"
	MeleeSound "baron/melee"
	Missiletype "AfritBall"
	MissileHeight 44
	REACTIONTIME 8
	MONSTER
	+NORADIUSDMG
	+FLOORCLIP
	+NOGRAVITY
	+FLOATBOB
	+FLOAT
	Obituary "%o was scorched by an Afrit"
	HitObituary "%o found the Afrit too hot to handle."
	States
	{
	Spawn:
		FRIT ABCD 4 Bright A_Look
		Loop
	See:
		FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0)
		FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180)
		FRIT AABBCCDD 2 Bright A_Chase
		Goto See +2
	Melee:
		FRIT ST 8 Bright A_FaceTarget
		FRIT U 6 Bright A_ComboAttack
		Goto See +2
	Missile:
		FRIT E 0 A_Jump(128,4)
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_ComboAttack
		Goto See +2
		FRIT EF 6 Bright A_FaceTarget
		FRIT G 6 Bright A_CustomMissile("Comet", 44, 0, 0)
		Goto See +2
	Pain:
		FRIT HH 4 Bright A_Pain
		Goto See +2
	Death:
		FRIT I 0 Bright A_NoGravity
		FRIT I 6 Bright A_Scream
		FRIT J 5 Bright A_NoBlocking
		FRIT KLMNOPQR 4 Bright
		Stop
	}
}

ACTOR Comet
{
    Radius 6
    Height 8
    Speed 10
    Damage 5
    ExplosionDamage 40
    ExplosionRadius 80
    Scale .55
    PROJECTILE
    SeeSound "CometLaunch"
    DeathSound "CometExplosion"
    MissileType CometTail
    MissileHeight 0
    States
    {
    Spawn:
        COMT AAAABBBBCCCC 1 Bright A_MissileAttack
        Loop
    Death:
        COMT D 3 Bright A_CustomMissile("CometDeathGlow", 0, 0, 0)
        COMT E 3 Bright A_Explode
        COMT E 0 A_CustomMissile("CometDeath", 0, 0, 0)
        Stop
    }
}

ACTOR CometTail
{
    PROJECTILE
    RENDERSTYLE ADD
	+BRIGHT
    +NOCLIP
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1
        Stop
    }
}

ACTOR CometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.7
    States
    {
    Spawn:
        COMT FGHI 3 Bright
        Stop
    }
}

ACTOR CometDeathGlow : CometTail
{
    Scale 2.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3
        Stop
    }
}

ACTOR OrbitCometTail : CometTail
{
    Scale .5
}

ACTOR OrbitComet : Comet
{
	Speed 5
	Scale .28
	+NOCLIP
	SeeSound ""
	MissileType OrbitCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COMT AABBCC 1 Bright A_MissileAttack
		COMT A 0 A_CustomMissile("OrbitComet", 0, 0, 75)
		COMT A 0 A_MissileAttack
		Stop
	}
}

ACTOR OrbitComet2 : OrbitComet
{
	MissileType OrbitCometTail
	MissileHeight 0
	States
	{
	Spawn:
		COMT AABBCC 1 Bright A_MissileAttack
		COMT A 0 A_CustomMissile("OrbitComet2", 0, 0, -75)
		COMT A 0 A_MissileAttack
		Stop
	}
}

ACTOR AfritBall
{
	Radius 6
	Height 8
	Speed 15
	Damage 8
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.8
	+BRIGHT
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		FRTM AB 5
		Loop
	Death:
		FRTM CDE 6
		Stop
	}
}