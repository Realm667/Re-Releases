// [XA] Am I the only one bugged by the fact that the actors are called "thunder"?
//  Thunder is the sound. Lightning is the thing. :P

actor Thunder_GeneratorWhite : SwitchableDecoration 3199
{
	+NoBlockMap +NoGravity +NoInteraction +NoClip -Solid +CLIENTSIDEONLY
	Renderstyle Add
	Height 1
	Radius 1
	Alpha 0.2
	Scale 4.0
	states
	{
	Spawn:
	Active:
		TNT1 A 0 
		TNT1 A 0 A_PlaySoundEx("thunder/hit", "Voice", 0, 2)                
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Thunder_Spark", 0, 0, 0, random(1,10), 0, random(1,10), random(0,359), 129)
		THND AAAA 1 Bright A_FadeIn(0.2)
		THND AAAA 2 Bright A_FadeOut(0.2)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1  Bright A_SpawnItemEx("Thunder_Smoke", 0, 0, 0, random(0,3), 0, random(2,3), random(0,359), 128)
		Goto Inactive
	Inactive:
		TNT1 A 1
		Loop
	}
}

actor Thunder_GeneratorTan : Thunder_GeneratorWhite 3198
{
	translation "80:111=48:79"
}

actor Thunder_GeneratorGreen : Thunder_GeneratorWhite 3197
{
	translation "80:111=112:127"
}

actor Thunder_GeneratorRed : Thunder_GeneratorWhite 3196
{
	translation "80:111=171:191"
}

actor Thunder_GeneratorBlue : Thunder_GeneratorWhite 3195
{
	translation "80:111=192:207"
}

actor Thunder_GeneratorPink : Thunder_GeneratorWhite 3194
{
	translation "80:111=16:47"
}

Actor Thunder_Spark
{
	Speed 3
	Height 2
	Radius 1
	Mass 0
	+Missile +LowGravity
	RenderStyle Add
	Scale 0.4
	States
	{
	Spawn:
		TNT1 A 0
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK ABCDEFGHHH 1 Bright A_FadeOut(0.1)
		Stop
	Death:
		SPRK ABCDEFGHHH 1 Bright A_FadeOut(0.1)
		Stop		
	}
}

Actor Thunder_Smoke
{
	Height 2
	Radius 1
	+Missile +NoGravity +NoInteraction +NoBlockmap
	RenderStyle Translucent
	Alpha .5
	Scale .65
	States
	{
	Spawn:
		SMK1 IHGFEEDDCCBBBBAAAAAA 2 A_FadeOut(0.05) 
		Stop
	}
}

// [XA] Smaller spawner for the boss entrance.

ACTOR BossLightning : Thunder_GeneratorBlue {
	Scale 2.0
}