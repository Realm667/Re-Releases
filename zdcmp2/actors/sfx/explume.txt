// Arg 0 -- number of vertical explosions to spawn
ACTOR XAExplosionPlume : SwitchableDecoration 11209
{	
	Radius 1
	Mass 1
	+NOINTERACTION
	States
	{
	Spawn:
	Inactive:
		TNT1 A 0 A_SetArg(4,args[0])
		TNT1 A -1
		Stop	
	Active:
		TNT1 A 8 A_SpawnItemEx("XAExplosionEffect",0,0,192*(args[0]-args[4]),0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_CountDownArg(4, "Inactive")
		Loop
	}
}

ACTOR XAExplosionEffect
{
	Scale 1.5
	Radius 1
	Mass 1
	Speed 00
	Projectile
	-ACTIVATEPCROSS
	-ACTIVATEIMPACT
	+THRUACTORS
	Renderstyle Add
	States
	{
	Spawn:
		EXP1 A 0 Bright
		EXP1 A 3 Bright A_PlaySound("world/explosionplume")
		EXP1 BCDEF 3 Bright
		EXP1 GH 3 Bright A_FadeOut(0.25)
		Stop
	Death:
		TNT1 A 0
		Stop
	}
}