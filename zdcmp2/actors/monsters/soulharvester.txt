// Soul Harvester -- by Vader

ACTOR SoulHarvester 10563
{
	//$Category Monsters
	Tag "Soul Harvester"
    Health 100
    Radius 20
    Height 56
    Speed 8
    PainChance 160
    MONSTER 
    +FLOORCLIP
    SeeSound "harvester/sight"
    PainSound "imp/pain"
    DeathSound "harvester/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o couldn't evade a soul harvester."
    MissileType SoulHarvesterBall
    MeleeDamage 3
    States
    {
    Spawn:
        SLHV AB 10 A_Look
        Loop
    See:
        SLHV AABBCCDD 3 A_Chase
        Loop
    Melee:
    Missile:
        SLHV EFG 4 A_FaceTarget
        SLHV HIJK 5 Bright A_FaceTarget
        SLHV L 8 Bright A_MissileAttack
        SLHV M 10
        Goto See
    Pain:
    	SLHV N 3
    	SLHV N 3 A_Pain
    	Goto See
    Death:
        SLHV O 7 A_Scream
        SLHV PQR 7 
        SLHV S 6 A_SpawnItemEx("SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
        SLHV T 5 A_Fall
        SLHV UV 5
        SLHV W -1
        Stop
    XDeath:
        SLHV X 6 A_Xscream
        SLHV Y 6 A_SpawnItemEx("SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
        SLHV Z 6
        SLHV [ 6 A_Fall
        SLHV ] 6
        SLHW A -1
        Stop
    Raise:
        SLHV WVUTSRQPO 5
        Goto See
	Burn:
		BURN A 3 Bright A_PlaySound("misc/burn", CHAN_AUTO)
		BURN B 3 Bright A_Scream
		BURN C 3 Bright A_PlaySound("misc/flame", CHAN_AUTO)
		BURN D 3 Bright A_NoBlocking
		BURN EFGHIJKLMNOP 3 Bright 
        BURN Q 0 A_SpawnItemEx("SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
		BURN Q 3 Bright A_PlaySound("misc/bodyhit", CHAN_VOICE)
		BURN RSTU 3 Bright 		
		BURN V -1
		Stop
    }
}
ACTOR SoulHarvester2 : SoulHarvester
{
	DropItem "Soul"
}

ACTOR SoulHarvesterBall
{
    Radius 8
    Height 6
    Scale 0.4
    Speed 9
    Damage 2
    +SEEKERMISSILE
    PROJECTILE
    Renderstyle ADD
    SeeSound "harvester/scream"
    DeathSound "imp/shotx"
	Decal DoomImpScorch
    States
    {
    Spawn:
    	SHBA A  0 Bright A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
        SHBA AB 2 Bright A_SeekerMissile (10,20)
        Loop
    Death:
        SHBA CDEFGHIJKL 4 Bright
        Stop
    }
}

ACTOR SoulHarvesterGhost
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.7
    +NOCLIP
    SeeSound "harvester/ghost"
    States
    {
    Spawn:
    Death:
        SHGH A 0
        SHGH A 0 A_PlaySound("Harvester/Ghost")
        SHGH ABCDEFG 4 Bright
        Stop
    }
}

ACTOR SoulBallTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.5
    +NOCLIP
    States
    {
    Spawn:
    Death:
    	SBTR H 6 
        SBTR ABCDEFG 4 Bright
        Stop
    }
}

