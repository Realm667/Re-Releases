ACTOR Crab
{
	//$Category Stronghold_Critters
	Health 1
	Radius 8
	Height 4
	Speed 1
	Scale 0.7
	Mass 10
	MONSTER
	+FLOORCLIP
	+FRIENDLY
	-COUNTKILL
	-ACTIVATEMCROSS
	-SOLID
	States
	{
	Spawn:
		CRAB AAAABBBBCCCCDDDD 1 A_Wander
		Loop
	Death:
		CRAB EFGHIJ 4
		Stop
	}
}

ACTOR Babycrab : Crab
{
	//$Category Stronghold_Critters
	Radius 4
	Height 2
	Speed .5
	Scale 0.4
	Mass 5
}

ACTOR FireFly
{
	//$Category Stronghold_Critters
	Health 2
	Radius 1
	Height 1
	Speed 0.5
	Mass 5
	Scale 0.25
	+NOGRAVITY
	+NOTARGET
	+FLOATBOB
	+SPAWNFLOAT
	+NOLIFTDROP
	+DONTOVERLAP
	+CANNOTPUSH
	+RANDOMIZE
	+PUSHABLE
	+FRIENDLY
	States
	 {
	Spawn:
		FFLY ABABABABABABABABABABABA 2 A_Wander
		FFLY BBB 2 Bright A_Wander
		Loop
	Death:
		"####" A 2 A_Scream
		"####" B 2 A_Fall
		Stop
	}
}

ACTOR DragonFly : FireFly
{
	//$Category Stronghold_Critters
	Speed 10
	States
	{
	Spawn:
		DRAG AB 2 A_Wander
		Loop
	}
}

ACTOR StrangeLight_Red
{
	//$Category Stronghold_Critters
	Radius 1
	Height 1
	Speed 1
	Mass 5
	Renderstyle Add
	Scale 0.2
	+CANPUSHWALLS
	+CANUSEWALLS
	+CANPASS
	+ISMONSTER
	+NOGRAVITY
	+NOTARGET
	+FLOATBOB
	+SPAWNFLOAT
	+NOLIFTDROP
	+DONTOVERLAP
	+CANNOTPUSH
	+RANDOMIZE
	-PUSHABLE
	+FRIENDLY
	+ALLOWCLIENTSPAWN
	States
	{
	Spawn:
		STLI A 2 A_Wander
		Loop
	}
}

ACTOR StrangeLight_Blue : StrangeLight_Red
{
	//$Category Stronghold_Critters
	States
	{
	Spawn:
		STLI B 2 A_Wander
		Loop
	}
}

ACTOR StrangeLight_White : StrangeLight_Red
{
	//$Category Stronghold_Critters
	States
	{
	Spawn:
		STLI C 2 A_Wander
		Loop
	}
}

ACTOR StrangeLight_Green : StrangeLight_Red
{
	//$Category Stronghold_Critters
	States
	{
	Spawn:
		STLI D 2 A_Wander
		Loop
	}
}

ACTOR Creep2
{
	//$Category Stronghold_Critters
	health 1
	radius 2
	height 2
	scale 0.1
	speed 2
	maxstepheight 4
	maxdropoffheight 4
	bloodcolor red
	+SHOOTABLE
	+RANDOMIZE
	+FRIENDLY
	States
	{
	Spawn:
		CRIZ AAABBB 1 A_Wander
		CRIZ A 0 A_Jump(20,"Spawn2")
		Loop
	Spawn2:
		CRIZ A 5 A_Jump(20,"Spawn")
		Loop
	Death:
		CRIZ D 3 A_Playsound("Creep2/die")
		CRIZ E 3 A_UnsetShootable
		CRIZ F -1
		Stop
	}
}

ACTOR Spiderbug_Red : Creep2
{
	//$Category Stronghold_Critters
	scale 0.2
	bloodcolor green
	States
	{
	Spawn:
		CR3P AAAABBBBCCCC 1 A_Wander
		CR3P A 0 A_Jump(20,"Jump")
		Loop
	Jump:
		CR3P A 1 ThrustThingZ(0,20,0,0)
		Goto Spawn
	Death:
		CR3P D 3 A_Playsound("Creep/die")
		CR3P EF 3 A_UnsetShootable
		CR3P G -1
		Stop
	}
}

ACTOR Spiderbug_Brown : Spiderbug_Red
{
	//$Category Stronghold_Critters
	translation "176:191=64:79", "32:47=64:79"
}