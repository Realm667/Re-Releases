////////////////////////////////////////////////////////
/////////////////////////// FIST ///////////////////////
////////////////////////////////////////////////////////
actor StrongholdFist : Fist replaces Fist
{
	AttackSound "*fist"
	states
	{
	Deselect:
		PUNG A 0 A_Lower
		PUNG A 1 A_Lower
		loop
	Select:
		PUNG A 0 A_Raise
		PUNG A 1 A_Raise
		loop
	Fire:
		PKFS LBCD 1
		PKFS E 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		PKFS E 2 A_CustomPunch(2*random(1,10),1)
		PKFS E 0 A_Jump(256,2)
		PKFS E 2 A_CustomPunch(34*random(1,10),1,0,"ElectricalPuff")
		PKFS FGHI 2
		PKFS JKL 1
		PUNG A 5 A_ReFire
		Goto Ready
	}
}

actor ElectricalPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.5
	VSpeed 0.25
	Scale 0.5
	Mass 5
	SeeSound "weapons/plasmax"
	AttackSound "weapons/plasmax"
	States
	{
	Spawn:
		PLSE ABCDE 4 bright
		stop
	}
}

////////////////////////////////////////////////////////
////////////////// CHAINSAW ////////////////////////////
////////////////////////////////////////////////////////
actor StrongholdChainsaw : Chainsaw replaces Chainsaw
{
	Decal BulletChip
	+GHOST
	Weapon.DefaultKickback
	states
	{
	Deselect:
		SAWG C 0 A_Lower
		SAWG C 1 A_Lower
		loop
	Select:
		SAWG C 0 A_Raise
		SAWG C 1 A_Raise
		loop
	Fire:
		SAWG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SAWG AB 2 A_Saw
		SAWG B 0 A_ReFire
		goto Ready
	SuperFire:
		SAWG ABAB 1 A_Saw("weapons/sawfull","weapons/sawhit",6)
		SAWG B 0 A_ReFire
		goto Ready
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PISTOL ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdPistol : Pistol replaces Pistol
{
	Weapon.AmmoUse2 8
	Weapon.AmmoType2 "Clip"
	attacksound "weapons/pistol"
	decal bulletchip
	states
	{
	Ready:
		PISG A 1 A_JumpIfInventory("PCount",1,1)
		Goto Reload
		PISG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		PISG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		PISG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		PISG A 0 A_Lower
		PISG A 1 A_Lower
		loop
	Select:
		PISG A 0 A_Raise
		PISG A 1 A_Raise
		loop
	Fire:
		PISG A 4 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PISG B 0 A_FireBullets (5.6, 0, 1, 5, "NewPuff")
		PISF A 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		goto Ready
	SuperFire:
		PISG A 0 A_GunFlash("SuperFlash")
		PISG B 3 A_FireBullets(4, 2.5, -1, 12, "ElectricalPuff")
		PISG CAB 3 A_ReFire
		Goto Ready
	AltFire:
		PISG A 4
		PISG A 0 A_GunFlash("SuperFlash")
		PISG B 4 A_FireBullets(10, 7, 8, 12, "ElectricalPuff")
		PISG CA 4
		PISG B 4 A_ReFire
		Goto Ready
	Reload:
		PISG D 5 A_PlaySound("weapons/clipou")
		PISG EFGHIJK 4
		PISG L 4 A_Playsound("weapons/clipin")
		PISG MNO 4
		PISG P 4 A_Playsound("weapons/clipre")
		PISG QR 4
		PISG S 4 A_GiveInventory("PCount")
		Goto Ready
	SuperFlash:
		PISF A 3 Bright A_Light1
		Goto LightDone
	}
}

ACTOR PCount : Ammo{}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SHOTGUN //////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdShotgun : Shotgun replaces Shotgun
{
	Weapon.AmmoUse2 1
	Weapon.AmmoType2 "Shell"
	obituary "$WP_SHOT"
	attacksound "weapons/shotgf"
	decal bulletchip
	states
	{
	Ready:
		SHTG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		SHTG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		SHTG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		SHTG A 0 A_Lower
		SHTG A 1 A_Lower
		loop
	Select:
		SHTG A 0 A_Raise
		SHTG A 1 A_Raise
		loop
	Fire:
		SHTG A 3
		SHTG E 0 A_GunFlash
		SHTG E 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SHTG E 2 A_FireBullets (4, 3, 7, 5, "NewPuff")
		SHTG F 3
		SHTG G 2
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		goto Ready
	SuperFire:
		SHTG E 2 A_FireBullets (3, 2.5, 10, 12, "ElectricalPuff")
		SHTG FG 2
		SHTG B 3
		SHTG CDC 2
		SHTG B 3
		SHTG A 1 A_ReFire
		goto Ready
	AltFire:
		SHTG A 3
		SHTG E 0 A_GunFlash
		SHTG E 2 A_FireBullets (12, 10, 8, 9, "RepPuff")
		SHTG FGBCDCB 3
		SHTG A 1 A_ReFire
		goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SUPER SHOTGUN ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdSuperShotgun : SuperShotgun replaces SuperShotgun
{
	weapon.ammotype2 "Shell"
	weapon.ammouse2 2
	attacksound "weapons/sshotf"
	decal bulletchip
	states
	{
	Ready:
		SHT2 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		SHT2 A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		SHT2 A 1 A_WeaponReady
		Goto Ready
	Deselect:
		SHT2 A 0 A_Lower
		SHT2 A 1 A_Lower
		loop
	Select:
		SHT2 A 0 A_Raise
		SHT2 A 1 A_Raise
		loop
	Fire:
		SHT2 M 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		SHT2 M 2 Bright A_FireBullets (11.2,7.1,20,5,"NewPuff")
		SHT2 N 3 Bright
		SHT2 O 2 Bright
		SHT2 B 3
		SHT2 C 4
		SHT2 D 4 A_CheckReload
		SHT2 C 3
		SHT2 E 3 A_PlaySound ("weapons/sshoto",CHAN_WEAPON)
		SHT2 F 3
		SHT2 GH 4
		SHT2 I 3 A_PlaySound ("weapons/sshotl",CHAN_WEAPON)
		SHT2 JK 3
		SHT2 L 4
		SHT2 E 0 A_PlaySound ("weapons/sshotc",CHAN_WEAPON)
		SHT2 E 6 A_ReFire
		SHT2 A 5 A_ReFire
		goto Ready
	SuperFire:
		SHT2 A 2
		SHT2 A 0 A_GunFlash
		SHT2 M 0 A_FireBullets(14.6,9.4,20,12,"ElectricalPuff")
		SHT2 MNO 2 Bright
		SHT2 A 2
		SHT2 A 0 A_GunFlash
		SHT2 M 0 A_FireBullets(14.6,9.4,20,12,"ElectricalPuff")
	SuperFireFinish:
		SHT2 MNO 2 Bright
		SHT2 BC 2
		SHT2 D 2 A_CheckReload
		SHT2 C 2
		SHT2 E 2 A_PlayWeaponSound ("weapons/sshoto")
		SHT2 FGH 2
		SHT2 I 2 A_PlayWeaponSound ("weapons/sshotl")
		SHT2 JKL 2
		SHT2 E 0 A_PlayWeaponSound ("weapons/sshotc")
		SHT2 EA 2 A_ReFire
		goto Ready
	AltFire:
		SHT2 A 2
		SHT2 A 0 A_GunFlash
		SHT2 MM 0 A_FireBullets(14.6,9.4,10,3,"RepPuff")
		SHT2 MNO 2 Bright
		SHT2 A 2
		SHT2 A 0 A_GunFlash
		SHT2 MM 0 A_FireBullets(14.6,9.4,10,3,"RepPuff")
		Goto SuperFireFinish
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 3 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// AUTO SHOTGUN /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor AutoShotgun : Weapon
{
	//$Category Stronghold_Weapons
	+WEAPON.AMMO_CHECKBOTH
	obituary "$WP_AUTO"
	Inventory.PickupMessage "$IP_AUTO"
	Tag "$TAG_AUTO"
	weapon.selectionorder 700
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoType2 "ASGClip"
	Weapon.AmmoGive1 8
	Weapon.AmmoGive2 20
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	AttackSound "weapons/asgfir"
	decal bulletchip
	states
	{
	Spawn:
		WASG A -1
		stop
	Ready:
		ASGG A 0 A_JumpIfInventory("ASGClip",0,2)
		ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
		ASGG A 1 A_WeaponReady
		loop
	Deselect:
		ASGG A 0 A_Lower
		ASGG A 1 A_Lower
		loop
	Select:
		ASGG A 0 A_Raise
		ASGG A 1 A_Raise
		loop
	Fire:
		ASGG A 0 A_JumpIfInventory("ASGClip",1,1)
		Goto Reload
		ASGG A 1
		ASGG L 0 A_GunFlash
		ASGG A 0 A_TakeInventory("ASGClip",1)
		ASGG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		ASGG L 2 A_FireBullets(5,4,7,5,"NewPuff",0)
		ASGG M 3
		ASGG N 2
		ASGG A 4 A_PlaySound("weapons/asgld1",CHAN_AUTO)
		ASGG A 4 A_JumpIfInventory("PowerWeaponLevel2",1,1)
		ASGG A 0 A_ReFire
		goto Ready
	SuperFire:
		ASGG L 1 A_FireBullets(8.6,5.1,7,5,"ElectricalPuff",0)
		ASGG MN 1
		ASGG A 1 A_PlaySound("weapons/asgld1",CHAN_AUTO)
		ASGG A 0 A_ReFire
		goto Ready
	Reload:
		ASGG A 0 A_JumpIfInventory("Shell",1,2)
		ASGG A 2 A_PlaySound("Weapons/asgout",CHAN_WEAPON)
		Goto Ready
		ASGG BCDEF 3
		ASGG G 3 A_PlaySound("Weapons/asgout",CHAN_WEAPON)
		ASGG HIJ 3
		ASGG K 15
		ASGG JIH 3
	ReloadLoop:
		ASGG G 0 A_TakeInventory("Shell",1)
		ASGG G 0 A_GiveInventory("ASGClip",1)
		ASGG G 0 A_JumpIfInventory("ASGClip",20,2)
		ASGG G 0 A_JumpIfInventory("Shell",1,"ReloadLoop")
		ASGG G 3 A_PlaySound("Weapons/asgin",CHAN_WEAPON)
		ASGG FEDCB 3
		ASGG A 8 A_PlaySound("weapons/asgld1",CHAN_AUTO)
		goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	}
}

ACTOR ASGClip: Ammo
{
	Inventory.MaxAmount 20
	+Inventory.IgnoreSkill
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// CHAINGUN /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdChaingun : Chaingun replaces Chaingun
{
	Weapon.AmmoUse2 1
	Weapon.AmmoType2 "Clip"
	decal bulletchip
	states
	{
	Ready:
		CHGG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		CHGG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		CHGG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		CHGG A 0 A_StopSound(CHAN_WEAPON)
		CHGG A 0 A_Lower
		CHGG A 1 A_Lower
		goto Deselect+1
	Select:
		CHGG A 0 A_Raise
		CHGG A 1 A_Raise
		loop
	Fire:
		CHGG C 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
	Hold:
		CHGG C 0 A_PlaySound("weapons/chnglp",CHAN_WEAPON,1,1)
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(4,2.3,1,5,"NewPuff")
		CHGG BCD 1
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(4,2.3,1,5,"NewPuff")
		CHGG BCD 1
		CHGG A 0 A_Refire
	WindDown:
		CHGG A 0 A_StopSound(CHAN_WEAPON)
		CHGG A 0 A_PlaySound("weapons/chngdn",5)
		CHGG ABBCCCDDD 1 A_Weaponready
		Goto Ready
	SuperFire:
		CHGG A 0 A_GunFlash
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff")
		CHGG BCD 1
		CHGG A 0 A_GunFlash
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff",0)
		CHGG BCD 1
		CHGG A 0 A_Refire("SuperHold")
		Goto WindDown
	SuperHold:
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(4,2.3,1,5,"ElectricalPuff")
		CHGG C 1
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(4,2.3,1,5,"ElectricalPuff",0)
		CHGG C 1
		CHGG A 0 A_Refire("SuperHold")
		Goto WindDown+1
	AltFire:
		CHGG A 0 A_GunFlash
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(7.5,3.0,-1,16,"RepPuff")
		CHGG BCD 1
		CHGG A 0 A_GunFlash
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(7.5,3.0,-1,16,"RepPuff")
		CHGG BCD 1
		CHGG A 0 A_Refire("SuperHold")
		Goto WindDown
	AltHold:
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(7.5,3.0,-1,16,"RepPuff")
		CHGG C 1
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 0 A_PlaySound("weapons/chngun",CHAN_WEAPON)
		CHGG A 1 A_FireBullets(7.5,3.0,-1,16,"RepPuff")
		CHGG C 1
		CHGG A 0 A_Refire("SuperHold")
		Goto WindDown+1
	Flash:
		TNT1 A 0 A_Jump(128,"Flash2")
		CHGF A 1 BRIGHT A_Light2
		CHGF B 1 BRIGHT A_Light1
		Goto LightDone
	Flash2:
		CHGF C 1 BRIGHT A_Light1
		Goto LightDone
	SuperFlash:
		CHGF A 1 BRIGHT A_Light2
		CHGF D 1 BRIGHT A_Light1
		Goto LightDone
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// MINE LAYER ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR LandMineLayer : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_MINE"
	Weapon.AmmoType "MineAmmo"
	Weapon.AmmoGive 5
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "MineAmmo"
	Weapon.AmmoUse2 1
	Tag "$TAG_MINE"
	States
	{
	Spawn:
		MINE A -1
		Loop
	Ready:
		LAYR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("MineAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		LAYR A 0 A_Lower
		LAYR A 1 A_Lower
		Loop
	Select:
		LAYR A 0 A_Raise
		LAYR A 1 A_Raise
		Loop
	Fire:
		LAYR B 8 A_PlaySound("weapons/minebeep")
		LAYD ABCDE 1
		TNT1 A 5 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		TNT1 A 9 A_FireCustomMissile("Landmine3")
		TNT1 A 0 A_CheckReload
		LAYU ABCDE 1
		LAYR A 4
		Goto Ready
	AltFire:
		LAYR B 8 A_PlaySound("weapons/minebeep")
		LAYD ABCDE 1
		TNT1 A 10
		LAYT A 1 A_PlaySound("weapons/minethrow")
		LAYT BCDEFGH 1
		LAYR B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperAltFire")
		LAYT I 1 A_FireCustomMissile("Landmine2",0,1,0,8)
	AltFireFinish:
		TNT1 A 0 A_CheckReload
		LAYT JKLMNOPQR 1
		TNT1 A 10
		LAYU ABCDE 1
		LAYR A 6
		Goto Ready
	SuperFire:
		TNT1 A 10
		TNT1 A 19 A_FireCustomMissile("PowerLandmine3")
		TNT1 A 0 A_CheckReload
		LAYU ABCDE 1
		LAYR A 6
		Goto Ready
	SuperAltFire:
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",0,1,0,8)
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",-7,0,0,8)
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",7,0,0,8)
		Goto AltFireFinish
	}
}

ACTOR Landmine
{
	Radius 8 //was 5
	Height 5
	Mass 1000000
	Health 20
	Damage 10
	BounceSound "weapons/tink"
	+NOBLOOD
	+MISSILE
	+THRUGHOST
	States
	{
	Spawn:
		MINE A 5
		MINE B 5 Light("LandMineSpawnB")
		Loop
	Death:
		MISL B 0 A_Explode(384,160,0)
		MISL B 8 LIGHT("Rocket_X1")  A_PlaySound("weapons/mineexpl")
		MISL C 6 LIGHT("Rocket_X2")
		MISL D 4 LIGHT("Rocket_X3")
		Stop
	}
}

ACTOR Landmine2 : Landmine
{
	Speed 15
	BounceType Doom
	BounceSound "weapons/tink"
	+THRUGHOST
	States
	{
	Death:
		MINE A 0 A_SpawnItemEx("Landmine")
		Stop
	}
}

ACTOR Landmine3 : Landmine2
{
	Speed 2
}

ACTOR PowerLandmineA : Landmine
{
	Damage 20
	States
	{
	Death:
		MISL B 0 A_Explode(768,240,0)
		Goto Super::Death+1
	}
}

ACTOR PowerLandmineB : PowerLandmineA
{
	States
	{
	Death:
		MISL B 0 A_Explode(256,160,0)
		Goto Landmine::Death+1
	}
}

ACTOR PowerLandmine2 : PowerLandmineA
{
	Speed 15
	BounceType Doom
	BounceSound "weapons/tink"
	+THRUGHOST
	States
	{
	Death:
		MINE A 0 A_SpawnItem("PowerLandmineB")
		Stop
	}
}

ACTOR PowerLandmine3 : PowerLandmine2
{
	Speed 2
	States
	{
	Death:
		MINE A 0 A_SpawnItem("PowerLandmineA")
		Stop
	}
}

ACTOR MineAmmo : Ammo
{
	Inventory.Amount 2
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 40
	Inventory.Icon MINEA0
}

ACTOR Mines : WeaponGiver
{
	//$Category Stronghold_Weapons
	DropItem LandMineLayer
	Weapon.AmmoGive 2
	Inventory.PickupMessage "$IP_MIN2"
	States
	{
	Spawn:
		MINE B -1
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// ROCKET LAUNCHER //////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StrongholdRLauncher : RocketLauncher replaces RocketLauncher
{
	Weapon.AmmoUse2 1
	Weapon.AmmoType2 "RocketAmmo"
	States
	{
	Ready:
		MISG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		MISG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		MISG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		MISG A 0 A_Lower
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 0 A_Raise
		MISG A 1 A_Raise
		Loop
	Fire:
		MISF A 4 Bright A_GunFlash
		MISF B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		MISF B 5 Bright A_FireCustomMissile("Rocket2")
		MISF CD 5 Bright
		MISG A 0 A_Refire
		Goto Ready
	SuperFire:
		MISF B 2 Bright A_FireCustomMissile("PowerRocket2")
		MISF CD 4 Bright
		MISG A 0 A_Refire
		Goto Ready
	AltFire:
		MISF A 4 Bright A_GunFlash
		MISF B 2 Bright A_FireCustomMissile("PowerRocket2",fRandom(-0.5,.5),1,0,0,0,fRandom(-0.6,.6))
		MISF A 4 Bright A_GunFlash
		MISF B 2 Bright A_FireCustomMissile("PowerRocket2",fRandom(-1,1),1,0,0,0,fRandom(-0.8,.8))
		MISF A 4 Bright A_GunFlash
		MISF B 2 Bright A_FireCustomMissile("PowerRocket2",fRandom(-1.5,1.5),1,0,0,0,fRandom(-1,1))
		MISF CD 4 Bright
		MISG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 5 A_Light2
		TNT1 A 5 A_Light1
		Goto LightDone
	}
}

actor Rocket2 : Rocket replaces Rocket
{
	Decal Scorch
	states
	{
	Spawn:
		MISL A -1 BRIGHT LIGHT("ROCKET")
		Stop
	Death:
		MISL B 0 A_Explode
		MISL B 8 bright LIGHT("ROCKET_X1") A_SetTranslucent (0.67,1)
		MISL C 6 bright LIGHT("ROCKET_X2")
		MISL D 4 bright LIGHT("ROCKET_X3")
		stop
	}
}

actor PowerRocket2 : Rocket2
{
	speed 30
	damage 35
	scale 0.5
	translation "128:151=[192,192,192]:[64,64,64]","48:79=[255,255,255]:[0,0,0]","112:127=[255,255,255]:[0,0,0]"
	+THRUGHOST
	+SEEKERMISSILE
	states
	{
	Spawn:
		MISL A 1 bright LIGHT("ROCKETp") A_SeekerMissile(2,10)
		loop
	Death:
		MISL B 0 A_Explode
		MISL B 8 bright LIGHT("ROCKET_X1p") A_SetTranslucent (0.67,1)
		MISL C 6 bright LIGHT("ROCKET_X2p")
		MISL D 4 bright LIGHT("ROCKET_X3p")
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// HOMING ROCKET LAUNCHER ///////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR HRL : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_HOME"
	Obituary "WP_HOME"
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 4
	Weapon.AmmoUse 2
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoUse2 2
	Tag "$TAG_HOME"
	States
	{
	Spawn:
		WHRL A -1
		Loop
	Ready:
		HRLG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
		HRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
		HRLG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		HRLG A 0 A_Lower
		HRLG A 1 A_Lower
		Loop
	Select:
		HRLG A 0 A_Raise
		HRLG A 1 A_Raise
		Loop
	Fire:
		HRLG A 4
		HRLG C 5 Bright A_GunFlash
		HRLG D 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		HRLG D 0 A_FireCustomMissile("HomRocket1",-4,0)
		HRLG D 6 Bright A_FireCustomMissile("HomRocket1",4)
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_Refire
		Goto Ready
	SuperFire:
		HRLG D 0 A_FireCustomMissile ("PowerHomRocket1",-4,0,0,4.5)
		HRLG D 6 Bright A_FireCustomMissile ("PowerHomRocket1",4,1,0,4.5)
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_Refire
		Goto Ready
	AltFire:
		HRLG A 4
		HRLG C 5 Bright A_GunFlash
		HRLG DDDD 4 Bright A_FireCustomMissile ("PowerHomRocket1",fRandom(-6,6),1,0,4.5,0,fRandom(-1.50,1.50))
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_Refire
		Goto Ready
	Flash:
		TNT1 A 5 A_Light1
		TNT1 A 6 A_Light2
		TNT1 A 5 A_Light1
		Goto LightDone
	}
}

ACTOR HomRocket1 : PowerRocket2
{
	Radius 8
	Height 11
	Damage 15
	Scale 1
	Translation "0:0=0:0"
	-RANDOMIZE
	SeeSound "weapons/hrlfir"
	DeathSound "weapons/hrlexp"
	States
	{
	Spawn:
		HMIS A 4 Bright Light("ROCKETp") A_SeekerMissile (10,25,SMF_LOOK)
		Loop
	Death:
		MISL B 0 A_Explode
		Goto Super::Death+1
	}
}

ACTOR PowerHomRocket1 : HomRocket1
{
	Radius 4
	Height 5.5
	Scale 0.5
	Speed 35
	Damage 8
	States
	{
	Spawn:
		MISL A 2 Bright Light("ROCKETp") A_SeekerMissile(25,35,SMF_LOOK)
		Loop
	Death:
		MISL B 0 A_Explode(64,96)
		Goto PowerRocket2::Death+1
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PLASMA RIFLE /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StrongholdPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
	States
	{
	Ready:
		PLSG ABC 3 Bright A_WeaponReady
		Loop
	Deselect:
		PLSG E 0 A_Lower
		PLSG E 1 A_Lower
		Loop
	Select:
		PLSG E 0 A_Raise
		PLSG E 1 A_Raise
		Loop
	Fire:
		PLSG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PLSG F 0 A_GunFlash
		PLSG F 1 Bright A_FireCustomMissile("StrongholdPlasmaBall")
		PLSG GH 1 Bright
		PLSG D 20 A_Refire
		Goto Ready
	SuperFire:
		PLSG F 1 A_JumpIfInventory("Cell",2,1)
		Goto Fire+3
		PLSG F 1 Bright A_GunFlash
		PLSG F 1 Bright A_FireCustomMissile("PowerPlasmaBall")
		PLSG GH 1 Bright
		PLSG D 20 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR StrongholdPlasmaBall : PlasmaBall
{
	States
	{
	Spawn:
		PLSS AB 6 Bright LIGHT("PLASMABALL")
		Loop
	Death:
		PLSE A 4 Bright LIGHT("PLASMA_X1")
		PLSE BC 4 Bright LIGHT("PLASMA_X2")
		PLSE D 4 Bright LIGHT("PLASMA_X3")
		PLSE E 4 Bright LIGHT("PLASMA_X4")
		Stop
	}
}

actor PowerPlasmaBall : StrongholdPlasmaBall
{
	Radius 22.75
	Height 14
	Damage 9
	Scale 0.8
	BounceType Doom
	Bouncecount 5
	States
	{
	Spawn:
		PLSE A 2 bright A_SpawnItemEx("ElectricalPuff",Random(1,-1),Random(1,-1),Random(1,-1),Random(1,-1),Random(1,-1),Random(1,-1),Random(0,360))
		loop
	Death:
		TNT1 A 0 A_Explode(40,80)
		Goto Super::Death
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// RAILGUN    ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR RailGun : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_RAIL"
	Obituary "$WP_RAIL"
	DamageType Normal
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 40
	Weapon.AmmoUse 5
	Decal RailScorch
	Tag "$TAG_RAIL"
	States
	{
	Spawn:
		WRAL A -1
		Loop
	Ready:
		RAIG A 1 A_WeaponReady
		Loop
	Ready:
		RAIG A 0 A_JumpIfInventory("RailgunScope",1,2)
		RAIG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
		RSCP A 1 Bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Loop
	Deselect:
		RAIG A 0 A_TakeInventory("RailgunScope",1)
		RAIG A 0 A_ZoomFactor(1)
		RAIG A 0 A_Lower
		RAIG A 1 A_Lower
		Goto Deselect+2
	Select:
		RAIG A 0 A_TakeInventory("RailgunScope",1)
		RAIG A 0 A_ZoomFactor(1)
		RAIG A 0 A_Raise
		RAIG A 1 A_Raise
		Goto Select+2
	Fire:
		RAIG A 0 A_JumpIfInventory("RailgunScope",1,"ZoomFire")
		RAIG A 0 A_JumpIfInventory("RailCharge",2,"Fire3")
		RAIG A 0 A_JumpIfInventory("RailCharge",1,"Fire2")
		RAIG B 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG B 6 Bright A_GiveInventory("RailCharge")
		RAIG B 0 A_Refire
		RAIG E 3 Bright A_GunFlash
		RAIG E 1 A_PlaySound("weapons/railf1",CHAN_WEAPON)
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG F 6 Bright A_RailAttack(100,0,1,"FF 00 00","FF FF FF")
		RAIG F 0 A_Jump(256,2)
		RAIG F 6 Bright A_RailAttack(200,0,1,"FF 00 00","FF FF FF")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	Fire2:
		RAIG C 0 A_JumpIfInventory("Cell",10,1)
		Goto Fire+3
		RAIG C 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG C 6 Bright A_GiveInventory("RailCharge")
		RAIG C 0 A_Refire
		RAIG G 3 Bright A_GunFlash
		RAIG G 1 A_PlaySound("weapons/railf2",CHAN_WEAPON)
		RAIG H 0 A_RailAttack(0,0,1,"FF FF 00","FF FF FF")
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG H 6 Bright A_RailAttack(200,0,1,"FF FF 00","FF 45 00")
		RAIG H 0 A_Jump(256,2)
		RAIG H 6 Bright A_RailAttack(400,0,1,"FF FF 00","FF 45 00")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	Fire3:
		RAIG C 0 A_JumpIfInventory("Cell",15,1)
		Goto Fire2
		RAIG D 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG D 6 Bright
		RAIG D 0 A_Refire
		RAIG I 3 Bright A_GunFlash
		RAIG I 1 A_PlaySound("weapons/railf3",CHAN_WEAPON)
		RAIG J 0 A_RailAttack(0,0,1,"00 00 FF","00 45 FF")
		RAIG J 0 A_RailAttack(0,0,1,"00 00 FF","FF FF FF")
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG J 6 Bright A_RailAttack(300,0,1,"00 00 FF","00 FF 00")
		RAIG J 0 A_Jump(256,2)
		RAIG J 6 Bright A_RailAttack(600,0,1,"00 00 FF","00 FF 00")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	ZoomFire:
		RSCP A 6 Bright A_PlaySound("weapons/railch",CHAN_WEAPON)
		RSCP A 6 Bright A_GiveInventory("RailCharge")
		RSCP A 0 A_Refire
		RSCP A 3 Bright A_GunFlash
		RSCP A 0 A_JumpIfInventory("RailCharge",2,"ZoomFire3")
		RSCP A 0 A_JumpIfInventory("RailCharge",1,"ZoomFire2")
	ZoomFire1:
		RSCP A 1 Bright A_PlaySound("weapons/railf1",CHAN_WEAPON)
		RSCP A 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RSCP A 10 Bright A_RailAttack(100,0,1,"FF 00 00","FF FF FF")
		RSCP A 0 A_Jump(256,2)
		RSCP A 10 Bright A_RailAttack(200,0,1,"FF 00 00","FF FF FF")
		RSCP A 15 Bright A_TakeInventory("RailCharge")
		RSCP A 0 A_CheckReload
		Goto Ready
	ZoomFire2:
		RSCP A 0 A_JumpIfInventory("Cell",10,1)
		Goto ZoomFire1
		RSCP A 1 A_PlaySound("weapons/railf2",CHAN_WEAPON)
		RSCP A 0 A_RailAttack(0,0,1,"FF FF 00","FF FF FF")
		RSCP A 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RSCP A 10 Bright A_RailAttack(200,0,1,"FF FF 00","FF 45 00")
		RSCP A 0 A_Jump(256,2)
		RSCP A 10 Bright A_RailAttack(400,0,1,"FF FF 00","FF 45 00")
		RSCP A 15 A_TakeInventory("RailCharge")
		RSCP A 0 A_CheckReload
		Goto Ready
	ZoomFire3:
		RSCP A 0 A_JumpIfInventory("Cell",15,1)
		Goto ZoomFire2
		RSCP A 1 A_PlaySound("weapons/railf3",CHAN_WEAPON)
		RSCP A 0 A_RailAttack(0,0,1,"00 00 FF","00 45 FF")
		RSCP A 0 A_RailAttack(0,0,1,"00 00 FF","FF FF FF")
		RSCP A 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RSCP A 10 Bright A_RailAttack(300,0,1,"00 00 FF","00 FF 00")
		RSCP A 0 A_Jump(256,2)
		RSCP A 10 Bright A_RailAttack(600,0,1,"00 00 FF","00 FF 00")
		RSCP A 15 A_TakeInventory("RailCharge")
		RSCP A 0 A_CheckReload
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 4 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR RailCharge : Ammo
{
	Inventory.MaxAmount 3
}
ACTOR RailgunScope : Inventory{}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// STUNNER RIFLE ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StunnerRifle : PlasmaRifle
{
	Tag "Stunner Rifle"
	//$Category Stronghold_Weapons
	Weapon.AmmoUse 5
	Inventory.PickupMessage "$IP_STUN"
	States
	{
	Spawn:
		STPP A -1
		Loop
	Ready:
		STPO ABC 3 Bright A_WeaponReady
		Loop
	Deselect:
		STPO E 0 A_Lower
		STPO E 1 A_Lower
		Loop
	Select:
		STPO E 0 A_Raise
		STPO E 1 A_Raise
		Loop
	Fire:
		STPO F 0 A_GunFlash
		STPO F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		STPO F 3 Bright A_FireCustomMissile("StunnerBall")
		STPO GH 3 Bright
		STPO E 4
		STPO D 6 A_ReFire
		Goto Ready
	SuperFire:
		STPO F 3 Bright A_FireCustomMissile("PowerStunnerBall")
		STPO GH 3 Bright
		STPO D 16
		STPO D 6 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

actor StunnerBall : StrongholdPlasmaBall
{
	height 8
	speed 20
	scale 0.45
	damagetype Stunner
	damage (random(1,4))
	decal StunnerScorch
	Alpha 1
	seesound "weapons/stunner/shot"
	deathsound "weapons/stunner/hit"
	translation "192:207=250:254"
	+RIPPER
	states
	{
	Spawn:
		STPR A 1 bright LIGHT("StunnerBallLight") A_SpawnItemEx("StunnerBallFX")
		loop
	Death:
		PLSE A 4 Bright LIGHT("StunnerBallDeathA")
		PLSE B 4 Bright LIGHT("StunnerBallDeathB")
		PLSE C 4 Bright LIGHT("StunnerBallDeathC")
		PLSE D 4 Bright LIGHT("StunnerBallDeathD")
		PLSE E 4 Bright LIGHT("StunnerBallDeathE")
		Stop
	}
}

actor StunnerBallFX
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	scale 0.45
	renderstyle Add
	radius 1
	height 2
	States
	{
	Spawn:
		STPR A 1 bright A_FadeOut
		Loop
	}
}

actor PowerStunnerBall : StunnerBall
{
	radius 6
	Decal ""
	states
	{
	Spawn:
		STPR A 0 A_BishopMissileWeave
		STPR A 0 A_Tracer2
		STPR A 1 bright LIGHT("StunnerBallLight") A_SpawnItemEx("StunnerBallFX")
		loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////  BFG9000   ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdBFG9000 : BFG9000 Replaces BFG9000
{
	Weapon.SisterWeapon "PowerBFG9000"
	States
	{
	Ready:
		BFGG ABCD 3 Bright A_WeaponReady
		loop
	Deselect:
		BFGG E 0 A_Lower
		BFGG E 1 A_Lower
		loop
	Select:
		BFGG E 0 A_Raise
		BFGG E 1 A_Raise
		loop
	Fire:
		BFGG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		BFGG F 5 Bright A_PlaySound ("weapons/bfgf",CHAN_WEAPON)
		BFGG GHIJ 5 Bright
		BFGG K 5 Bright A_GunFlash
		BFGG L 4 Bright A_FireCustomMissile("StrongholdBFGBall")
		BFGG M 3 Bright
		BFGG N 3 Bright
		BFGG E 20 Bright A_ReFire
		goto Ready
	Flash:
		TNT1 A 7 A_Light1
		TNT1 A 8 A_Light2
		TNT1 A 8 A_Light1
		Goto LightDone
	SuperFire:
		BFGG FHJ 1 Bright
		BFGG K 0 A_GunFlash("SuperFlash")
		BFGG L 1 Bright A_FireCustomMissile("PowerBFGBall")
		BFGG N 1 Bright
		BFGG E 7 Bright A_ReFire("SuperFire")
		goto Ready
	SuperFlash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	}
}

ACTOR StrongholdBFGBall : BFGBall replaces BFGBall
{
	Decal BFGLightning
	States
	{
	Spawn:
		BFS1 AB 4 Bright LIGHT("BFGBALL")
		Loop
	Death:
		BFE1 A 8 Bright LIGHT("BFGBALL_X1")
		BFE1 B 8 Bright LIGHT("BFGBALL_X2")
		BFE1 C 8 Bright LIGHT("BFGBALL_X3") A_BFGSpray
		BFE1 D 8 Bright LIGHT("BFGBALL_X1")
		BFE1 E 8 Bright LIGHT("BFGBALL_X4")
		BFE1 F 8 Bright LIGHT("BFGBALL_X5")
		Stop
	}
}

// Unlike it's unpowered counterpart, it really really sucks to use this at close
// range now. That's the price you pay for being tough. ;)
actor PowerBFG9000 : StrongholdBFG9000
{
	weapon.ammouse 5
	Weapon.SisterWeapon "StrongholdBFG9000"
	+POWERED_UP
}

// This is a weaker version of the BFG that gets rapidfired. Instead of tracers, it simply explodes.
//Note that it foils boss immunity to explosions, and it WILL hurt the shooter.
actor PowerBFGBall : StrongholdBFGBall
{
	SeeSound "weapons/bfg_fire"
	DeathSound "weapons/bfg_hum"
	Speed 35
	Damage 20
	Scale 0.6
	+EXTREMEDEATH
	+FORCERADIUSDMG
	Obituary "$WP_PBFG"
	//SuicideObituary "%k couldn't handle the overwhelming power."
	States
	{
	Death:
		BFE1 A 3 Bright LIGHT("BFGBALL_X1") A_Explode(128,196)
		BFE1 B 3 Bright LIGHT("BFGBALL_X2")
		BFE1 C 3 Bright LIGHT("BFGBALL_X3")
		BFE1 D 3 Bright LIGHT("BFGBALL_X1")
		BFE1 E 3 Bright LIGHT("BFGBALL_X4")
		BFE1 F 3 Bright LIGHT("BFGBALL_X5")
		Stop
	}
}
//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PYRO CANNON //////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR PyroCannon : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_PYROC"
	Obituary "$WP_PYROC"
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 48
	Weapon.AmmoUse 48
	Tag "$TAG_PYROC"
	+WEAPON.EXPLOSIVE
	+WEAPON.BFG
	+WEAPON.DONTBOB
	+WEAPON.STAFF2_KICKBACK
	States
	{
	Spawn:
		WPYR A -1
		Loop
	Ready:
		PYRG A 1 A_WeaponReady
		Loop
	Deselect:
		PYRG A 0 A_Lower
		PYRG A 1 A_Lower
		Loop
	Select:
		PYRG A 0 A_Raise
		PYRG A 1 A_Raise
		Loop
	Fire:
		PYRG A 5
		PYRG B 8 Bright A_GunFlash
		PYRG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PYRG C 10 Bright A_FireCustomMissile("PyroShot")
		PYRG DEFG 5 Bright
		PYRG A 5 A_Refire
		Goto Ready
	SuperFire:
		PYRG C 10 Bright A_FireCustomMissile("ClusterPyroShot")
		PYRG DEFG 5 Bright
		PYRG A 5 A_Refire
		Goto Ready
	Flash:
		TNT1 A 8 A_Light1
		TNT1 A 10 A_Light2
		TNT1 A 15 A_Light1
		Goto LightDone
	}
}

ACTOR Gas : Ammo
{
	//$Category Stronghold_Weapons
	Inventory.Amount 24
	Inventory.MaxAmount 240
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "$IP_GAMMO"
	Inventory.Icon AGASA0
	States
	{
	Spawn:
		AGAS A -1
		Loop
	}
}

ACTOR Fuel : Gas
{
	//$Category Stronghold_Weapons
	Inventory.Amount 96
	Inventory.PickupMessage "$IP_FUEL"
	States
	{
	Spawn:
		AGAS B -1
		Loop
	}
}

ACTOR PyroShot
{
	Radius 8
	Height 12
	Speed 30
	Damage 180
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	Scale 0.75
	+THRUGHOST
	SeeSound "weapons/hellfi"
	DeathSound "weapons/hellex"
	Decal BigScorch
	States
	{
	Spawn:
		PBAL ABA 4 Bright LIGHT("PyroSpawnAB")
		PBAL BA 4 Bright LIGHT("PyroSpawnAB") A_Explode
		goto Spawn+3
	Death:
		PYXP A 2 Bright LIGHT("PyroSpawnA")
		PYXP B 2 Bright LIGHT("PyroSpawnB")
		PYXP C 2 Bright LIGHT("PyroSpawnC") A_Explode(192,128)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,0,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,45,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,90,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,135,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,180,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,225,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,270,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,315,4)
		PYXP C 0 A_Mushroom("PyroFlare",16)
		PYXP D 2 Bright LIGHT("PyroDeathD")
		PYXP E 2 Bright LIGHT("PyroDeathE")
		PYXP F 2 Bright LIGHT("PyroDeathF")
		PYXP G 2 Bright LIGHT("PyroDeathG")
		PYXP H 2 Bright LIGHT("PyroDeathH")
		PYXP I 2 Bright LIGHT("PyroDeathI")
		PYXP J 2 Bright LIGHT("PyroDeathJ")
		PYXP K 2 Bright LIGHT("PyroDeathK")
		PYXP L 2 Bright LIGHT("PyroDeathL")
		PYXP M 2 Bright LIGHT("PyroDeathM")
		PYXP N 2 Bright LIGHT("PyroDeathN")
		PYXP O 2 Bright LIGHT("PyroDeathO")
		PYXP P 2 Bright LIGHT("PyroDeathP")
		PYXP Q 2 Bright LIGHT("PyroDeathQ")
		PYXP R 2 Bright LIGHT("PyroDeathR")
		PYXP S 2 Bright LIGHT("PyroDeathS")
		PYXP T 2 Bright LIGHT("PyroDeathT")
		PYXP U 2 Bright LIGHT("PyroDeathU")
		stop
	}
}

ACTOR PyroSpawner
{
	Radius 1
	Height 1
	Damage 10
	PROJECTILE
	Speed 15
	+RIPPER
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		TNT1 AAAAA 5 A_CustomMissile("PyroBoom",0,0,0,4)
		stop
	}
}

ACTOR PyroBoom
{
	Radius 0
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	Scale 0.67
	+THRUGHOST
	+FORCERADIUSDMG
	SeeSound "weapons/boom1"
	States
	{
	Spawn:
		PYXP AB 2 Bright
		PYXP C 2 Bright A_Explode(192,128)
		PYXP DEFGHIJKLMNOPQRSTU 2 Bright
		stop
	}
}

ACTOR PyroFlare
{
	Radius 8
	Height 11
	Speed 35
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	Decal DoomImpScorch
	ALPHA 0.95
	-NOGRAVITY
	+THRUGHOST
	DeathSound "weapons/bang1"
	States
	{
	Spawn:
		TNT1 A 1 A_SpawnItemEx("PyroFX")
		loop
	Death:
		FRFX HIJ 2 Bright
		FRFX J 0 A_Explode(64)
		FRFX J 0 A_SpawnItemEx("DropFire")
		FRFX KLMNO 2 Bright
		stop
	}
}

ACTOR PyroFX
{
	+CLIENTSIDEONLY
	Radius 0
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.85
	States
	{
	Spawn:
		TNT1 A 3
		PYFX ABCDE 3 Bright
		Stop
	}
}

ACTOR ClusterPyroShot : PyroShot
{
	States
	{
	Death:
		TNT1 A 0 A_Mushroom("PyroShot",4)
		TNT1 A 0 A_Mushroom("PyroFlare",32)
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// FLAMER ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Flamer : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_BBQ"
	Obituary "$WP_BBQ"
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 48
	Weapon.AmmoUse 1
	Tag "$TAG_BBQ"
	+WEAPON.EXPLOSIVE
	States
	{
	Spawn:
		WFLM A -1
		Loop
	Ready:
		FLMG BCD 3 A_WeaponReady
		Loop
	Deselect:
		FLMG A 0 A_Lower
		FLMG A 1 A_Lower
		Loop
	Select:
		FLMG A 0 A_Raise
		FLMG A 1 A_Raise
		Loop
	Fire:
		FLMG E 0 A_GunFlash
		FLMG E 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG H 2 Bright A_Refire
		Goto Ready
	SuperFire:
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 0 A_GunFlash
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 0 A_GunFlash
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 2 Bright A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR FTFire
{
	Radius 2
	Height 4
	Speed 16
	Damage 1
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.67
	Scale 0.67
	SeeSound "weapons/flamer"
	DeathSound "weapons/scorch"
	+THRUGHOST
	+DONTSPLASH
	Gravity .125
	States
	{
	Spawn:
		TNT1 A 2
		FRFX ABCD 2 Bright A_Explode(5,8,0)
		FRFX D 0 A_Gravity
		FRFX EFG 2 Bright A_Explode(5,16,0)
		FRFX HIJ 2 Bright A_Explode(5,32,0)
		FRFX KLM 2 Bright A_Explode(5,64,0)
		FRFX NO 2 Bright
		stop
	Death:
		FRFX HIJ 2 Bright A_Explode(5,32,0)
		FRFX J 0 A_CustomMissile("DropFire",0,0,0,4)
		FRFX KLM 2 Bright A_Explode(5,64,0)
		FRFX NO 2 Bright
		stop
	}
}

ACTOR DropFire
{
	Radius 8
	Height 40
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.90
	-NOGRAVITY
	Gravity .125
	+NOEXPLODEFLOOR
	+NODAMAGETHRUST
	+THRUGHOST
	+DONTSPLASH
	States
	{
	Spawn:
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("FTFireSpawn") A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("FTFireSpawn") A_Explode(5,16,0)
		FLME A 0 A_Jump(192,"Death")
		loop
	Death:
		TNT1 A 0
		stop
	}
}

ACTOR PowerFTFire : FTFire
{
	Damage 2
	Translation Ice
	DamageType Ice
	States
	{
	Spawn:
		TNT1 A 2
		STEM ABCD 2 Bright A_Explode(7,8,0)
		STEM D 0 A_Gravity
		STEM EFG 2 Bright A_Explode(8,16,0)
		STEM HIJ 2 Bright A_Explode(7,32,0)
		STEM KJI 2 Bright A_Explode(8,64,0)
		STEM HGFEDCBA 1 Bright
		stop
	Death:
		STEM J 1 Bright A_Explode(7,32,0)
		STEM I 1 Bright A_Explode(8,64,0)
		STEM HGECA 1 Bright
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// DEVASTATOR ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Devastator : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_DEVAST"
	Obituary "$WP_DEVAST"
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 60
	Weapon.AmmoUse 60
	Tag "$TAG_DEVAST"
	+WEAPON.EXPLOSIVE
	+WEAPON.BFG
	+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		WDEV A -1
		Loop
	Ready:
		DEVG A 1 A_WeaponReady
		Loop
	Deselect:
		DEVG A 0 A_Lower
		DEVG A 1 A_Lower
		Loop
	Select:
		DEVG A 0 A_Raise
		DEVG A 1 A_Raise
		Loop
	Fire:
		DEVG B 10 A_PlaySound("weapons/devchr",CHAN_WEAPON)
		DEVG CDEF 10 Bright
		DEVG A 0 A_Refire
	Winddown:
		DEVG J 4 Bright A_GunFlash
		DEVG J 1 Bright A_Playsound("weapons/devfir")
		DEVG B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperWinddown")
		DEVG K 10 Bright A_FireCustomMissile("DevastatorBall")
		DEVG L 7 Bright
		Goto Ready
	SuperWinddown:
		DEVG K 0 A_FireBullets(25,25,250,50,"DevPuff",0)
		DEVG K 0 A_FireCustomMissile ("DevastatorCannonProjectile",0,0)
		DEVG K 10 Bright A_FireCustomMissile("DevastatorCannonProjectile2")
		DEVG L 7 Bright
		Goto Ready
	Hold:
		DEVG G 0 A_PlaySound("weapons/devfch",CHAN_WEAPON)
		DEVG B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperHold")
		DEVG GHIGHI 4 Bright A_FireBullets(4,4,4,4,"DevPuff",0,256)
		DEVG I 0 A_Refire
		Goto Winddown
	SuperHold:
		DEVG GHIGHI 4 Bright A_FireBullets(4,4,4,4,"DevPuff",0,Random(256,512))
		DEVG I 0 A_Refire
		Goto Winddown
	Flash:
		TNT1 A 5 A_Light1
		TNT1 A 10 A_Light2
		TNT1 A 7 A_Light1
		Goto LightDone
	}
}

Actor DevPuff
{
	Radius 3
	Height 3
	RENDERSTYLE ADD
	ALPHA 0.75
	+ALWAYSPUFF
	+PUFFONACTORS
	+NOGRAVITY
	DeathSound "weapons/buzz"
	States
	{
	Spawn:
		DLIT JKLJKLJKL 3 Bright
		stop
	Crash:
		TNT1 A 1
		stop
	}
}

ACTOR DevastatorBall
{
	Radius 12
	Height 8
	Speed 30
	Damage 256
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.90
	+THRUGHOST
	DeathSound "weapons/devexp"
	Decal DevastatorLightning
	var int user_theta;
	States
	{
	Spawn:
		DBAL AB 4 Bright LIGHT("DevBallLight")
		DBAL A 4 Bright LIGHT("DevBallLight")
		DBAL AAA 0 A_CustomMissile("LiteShot",0,0,Random(0,360))
		DBAL B 4 Bright LIGHT("DevBallLight")
		DBAL BBB 0 A_CustomMissile("LiteShot",0,0,Random(0,360))
		Goto Spawn+2
	Death:
		DBAL C 5 Bright LIGHT("DevBallBoomA")
		DBAL D 0 A_CustomMissile("DevastatorBall2",0,0,user_theta,2)
		DBAL D 0 A_SetUserVar("user_theta",user_theta+18)
		DBAL D 0 A_JumpIf(user_theta>=360,1)
		Goto Death+1
		DBAL D 5 Bright LIGHT("DevBallBoomB") A_Explode(320,256)
		DBAL E 5 Bright LIGHT("DevBallBoomC") A_Explode(320,256)
		DBAL F 5 Bright LIGHT("DevBallBoomD") A_Explode(320,256)
		DBAL G 5 Bright LIGHT("DevBallBoomE") A_Explode(320,256)
		DBAL HI 3 Bright
		stop
	}
}

ACTOR DevastatorBall2
{
	Radius 8
	Height 8
	Speed 10
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.90
	Damagetype lightning
	Decal DoomImpScorch
	+THRUGHOST
	+FORCEXYBILLBOARD
	SeeSound "weapons/devbal"
	DeathSound "weapons/devex2"
	States
	{
	Spawn:
		DBA2 AB 4 Bright LIGHT("DevPuffLight")
		Loop
	Death:
		DBA2 C 4 Bright LIGHT("DevPuffLight") A_Explode(64,64,0)
		DBA2 DE 4 Bright LIGHT("DevPuffLight")
		Stop
	}
}

ACTOR LiteShot
{
	Radius 8
	Height 12
	Speed 32
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.85
	Damagetype lightning
	+THRUGHOST
	+RIPPER
	+FORCEXYBILLBOARD
	SeeSound "weapons/devlit"
	DeathSound "weapons/devzap"
	States
	{
	Spawn:
		DLIT AAABBBCCC 1 Bright LIGHT("DevLiteABCD") A_SpawnItemEx("LiteTrail")
		Loop
	Death:
		DLIT D 1 BRIGHT LIGHT("DevLiteABCD")
		DLIT E 1 BRIGHT LIGHT("DevLiteE")
		DLIT F 1 BRIGHT LIGHT("DevLiteF")
		DLIT G 1 BRIGHT LIGHT("DevLiteG")
		DLIT H 1 BRIGHT LIGHT("DevLiteH")
		DLIT I 1 BRIGHT LIGHT("DevLiteI")
		DLIT J 1 BRIGHT LIGHT("DevLiteJ")
		DLIT K 1 BRIGHT LIGHT("DevLiteK")
		DLIT L 1 BRIGHT LIGHT("DevLiteL")
		DLIT M 1 BRIGHT LIGHT("DevLiteM")
		DLIT N 1 BRIGHT LIGHT("DevLiteN")
		DLIT O 1 BRIGHT LIGHT("DevLiteO")
		Stop
	}
}

ACTOR LiteTrail
{
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.67
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		DLIT ABC 3 Bright
		stop
	}
}

actor DevastatorCannonProjectile
{
	Radius 30
	Height 40
	Speed 100
	Projectile
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	+EXTREMEDEATH
	+THRUGHOST
	+THRUACTORS
	RenderStyle Add
	Alpha 0.9
	Decal BFGScorch
	var int user_theta;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("DevastatorDec",(user_theta*momx)/-70.0,-(user_theta*momy)/-70.0,2+(user_theta*momz)/-70.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SetUserVar("user_theta",user_theta+1)
		TNT1 A 0 A_JumpIf(user_theta>=100,1)
		Loop
		TNT1 A 1 BRIGHT A_SetUserVar("user_theta",0)
	SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("DevastatorTrail",(user_theta*momx)/-70.0,-(user_theta*momy)/-70.0,2+(user_theta*momz)/-70.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SetUserVar("user_theta",user_theta+1)
		TNT1 A 0 A_JumpIf(user_theta>=100,1)
		Loop
		TNT1 A 1 BRIGHT A_SetUserVar("user_theta",0)
		Loop
	Death:
		DBAL DDDDDDDDDDEEEEEEEEEEFFFFFFFFFFGGGGGGGGGGHHHHHHIIIIII 1 BRIGHT A_SpawnItemEx("DevastatorEffect",Random(4,-4),Random(4,-4),Random(4,-4),Random(16,-16),Random(16,-16),Random(16,-16),Random(0,360))
		Stop
	}
}

actor DevastatorCannonProjectile2 : FastProjectile
{
	Radius 30
	Height 40
	Speed 400
	Damage (Random(400,800))
	Projectile
	+RIPPER
	+NOTIMEFREEZE
	+FORCERADIUSDMG
	+EXTREMEDEATH
	+THRUGHOST
	RenderStyle Add
	Alpha 0.9
	Decal BFGScorch
	States
	{
	Death:
		TNT1 A 0 A_Explode(256,160)
		TNT1 A 0 A_Explode(256,256)
		TNT1 A 0 A_Explode(288,512) //Yeah you don't want to be anywhere near this when this explodes
		DBAL DDDDDDDDDDEEEEEEEEEEFFFFFFFFFFGGGGGGGGGGHHHHHHIIIIII 1 BRIGHT A_SpawnItemEx("DevastatorEffect",Random(4,-4),Random(4,-4),Random(4,-4),Random(8,-8),Random(8,-8),Random(8,-8),Random(0,360))
		Stop
	}
}

ACTOR DevastatorTrail
{
	Renderstyle Add
	Projectile
	alpha 0.9
	Scale 3.0
	DamageType "MarineAlly"
	Damage 0
	+THRUACTORS
	+NOCLIP
	+FORCERADIUSDMG
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(16,48,0)
		TNT1 A 0 A_Explode(32,64,0)
		TNT1 A 0 A_Explode(64,80,0)
		TNT1 A 0 A_SpawnItemEx("DevastatorDec")
		TNT1 A 0 A_SpawnItemEx("DevastatorDec2")
		TNT1 A 1 A_SpawnItemEx("DevastatorEffect",Random(4,-4),Random(4,-4),Random(4,-4),Random(4,-4),Random(4,-4),Random(4,-4),Random(0,360))
		Stop
	}
}

ACTOR DevastatorDec
{
	Renderstyle Add
	alpha 0.9
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		DBAL AAAAAABBBBBB  1 Bright A_FadeOut(0.005)
		Loop
	}
}

Actor DevastatorDec2 : DevastatorDec
{
	Scale 3.0
	States
	{
	Spawn:
		DBAL BBBBBBAAAAAA 1 Bright A_FadeOut(0.015)
		Loop
	}
}

Actor DevastatorEffect : DevastatorDec2
{
	Translation "192:207=176:191"
	+RANDOMIZE
	+NOCLIP
	+NOGRAVITY
	States
	{
	Spawn:
		PLSE ABCDE 6 Bright A_FadeOut(0.15)
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// REPEATER /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Repeater : Weapon
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "$IP_REPEAT"
	Obituary "$WP_REPEAT"
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	+WEAPON.EXPLOSIVE
	AttackSound "weapons/repfir"
	Decal FireDemonScorch
	Tag "$TAG_REPEAT"
	States
	{
	Spawn:
		WREP A -1
		Loop
	Ready:
		REPG A 1 A_WeaponReady
		Loop
	Deselect:
		REPG A 0 A_Lower
		REPG A 1 A_Lower
		Loop
	Select:
		REPG A 0 A_Raise
		REPG A 1 A_Raise
		Loop
	Fire:
		REPG A 4 A_PlaySound("weapons/repidl")
		REPG BCD 4
		REPG ABCD 3
		REPG ABCD 2
	Hold:
		REPG E 0 A_Gunflash
		DEVG B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperHold")
		REPG E 2 Bright A_FireBullets(4,4,2,9,"RepPuff",1,10240)
		REPG F 2 Bright
		REPG G 2 Bright A_FireBullets(4,4,2,9,"RepPuff",1,10240)
		REPG H 2 Bright
		REPG A 0 A_Refire
		REPG A 2 A_PlaySound("weapons/repstp")
		REPG BCD 2
		REPG ABCD 3
		REPG ABCD 4
		REPG ABCD 5
		Goto Ready
	SuperHold:
		REPG E 1 Bright A_FireBullets(10,6,-1,20,RepPuff,1,10240)
		REPG F 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG G 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG H 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG E 1 Bright A_FireBullets(10,6,-1,20,RepPuff,1,10240)
		REPG F 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG G 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG H 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG E 1 Bright A_FireBullets(10,6,-1,20,RepPuff,1,10240)
		REPG F 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG G 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG H 1 Bright A_FireBullets(10,6,-1,20,RepPuff,0,10240)
		REPG A 0 A_Refire("SuperHold2")
		Goto Hold+6
    SuperHold2:
		REPG E 1 Bright A_FireBullets(20,10,2,10,RepPuff,1,10240)
		REPG F 1 Bright A_FireBullets(20,10,2,10,RepPuff,0,10240)
		REPG G 1 Bright A_FireBullets(20,10,2,10,RepPuff,0,10240)
		REPG H 1 Bright A_FireBullets(20,10,2,10,RepPuff,0,10240)
		REPG A 0 A_Refire("SuperHold2")
		Goto Hold+6
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

Actor RepPuff
{
	Radius 3
	Height 3
	RENDERSTYLE ADD
	ALPHA 0.75
	+ALWAYSPUFF
	+PUFFONACTORS
	+NOGRAVITY
	+NOBLOCKMAP
	+PUFFGETSOWNER
	DeathSound "weapons/buzz"
	Decal FireDemonScorch
	States
	{
	Spawn:
		BLAS A 1 Bright LIGHT("RepPuffSpawnG")
		BLAS A 2 Bright LIGHT("RepPuffSpawnG") A_Explode(32,88)
		BLAS B 3 Bright LIGHT("RepPuffSpawnF")
		BLAS C 3 Bright LIGHT("RepPuffSpawnE")
		BLAS D 3 Bright LIGHT("RepPuffSpawnD")
		BLAS E 3 Bright LIGHT("RepPuffSpawnC")
		BLAS F 3 Bright LIGHT("RepPuffSpawnB")
		BLAS G 3 Bright LIGHT("RepPuffSpawnA")
		stop
	}
}

/////////////////////
// NEW BULLET PUFF //
/////////////////////

ACTOR NewPuff : BulletPuff replaces BulletPuff
{
	+CLIENTSIDEONLY
	Alpha 1.0
	Mass 1
	States
	{
	Spawn:
	Melee:
	    PUFF A 2 LIGHT("BPUFF1")
	    PUFF A 0 A_Jump(128,2)
	    PUFF A 2 LIGHT("BPUFF1") A_SpawnItemEx("Puff2")
	    PUFF B 4 LIGHT("BPUFF2")
	    PUFF CD 4
	    Stop
	}
}

ACTOR Puff2
{
	+CLIENTSIDEONLY
	PROJECTILE
	+NOCLIP
	Mass 1
	States
	{
	Spawn:
		PUFF E 2
		PUFF E 2 A_PlaySound("puff/ric")
		PUFF FGH 4
		Stop
	}
}