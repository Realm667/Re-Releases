ACTOR DukeParkingMeter 164
{
	//$Category Stronghold_DukeNukem
	Radius 4
	Height 40
	Scale 0.6
	Mass 900000
	+SOLID
	States
	{
	Spawn:
		DNST C -1
		Stop
	}
}
/*
ACTOR DukeLampPost 165
{
	//$Category Stronghold_DukeNukem
	Radius 4
	Height 128
	+SOLID
	States
	{
	Spawn:
		DNST D -1
		Stop
	}
}
*/
ACTOR DukeBin 166
{
	//$Category Stronghold_DukeNukem
	Radius 16
	Height 40
	Scale 0.6
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NODAMAGE
	Mass 0x7fffffff
	PainChance 255
	States
	{
	Spawn:
		DNBR B -1
		Stop
	Pain:
		DNBR C 10
		Goto Spawn
	}
}

ACTOR DukeBox 167
{
	//$Category Stronghold_DukeNukem
	Radius 4
	Height 8
	Scale 0.6
	States
	{
	Spawn:
		DNBX A -1
		Stop
	}
}

ACTOR DukeMetalBarrel1 168
{
	//$Category Stronghold_DukeNukem
	Radius 16
	Height 40
	Mass 100
	Scale 0.7
	Health 50
	+SOLID
	+NOBLOOD
	States
	{
	Spawn:
		DNBR A -1
		Stop
	}
}

ACTOR FlameBreathX : FlameBreathOvermind
{
	Speed 15
	Scale 0.5
	damagetype Normal
	Damage 2
	-NOTIMEFREEZE
	States
	{
	Spawn:
		XPLO A 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_1") A_Explode(2,62,0)
		XPLO B 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_2") A_Explode(2,62,0)
		XPLO C 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_3") A_Explode(2,62,0)
		XPLO D 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_4") A_Explode(2,62,0)
		XPLO E 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_5") A_Explode(2,62,0)
		XPLO F 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_6") A_Explode(2,62,0)
		XPLO G 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_7") A_Explode(2,62,0)
		XPLO H 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_8") A_Explode(2,62,0)
		XPLO I 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_9") A_Explode(2,62,0)
		XPLO J 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_10") A_Explode(2,62,0)
		XPLO K 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_11") A_Explode(2,62,0)
		XPLO L 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_12") A_Explode(2,62,0)
		XPLO M 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_13") A_Explode(2,62,0)
		XPLO N 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_14") A_Explode(2,62,0)
		XPLO O 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_15")
		XPLO P 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_16")
		XPLO Q 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_17")
		XPLO R 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_18")
		XPLO S 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_19")
		XPLO T 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_20")
		XPLO U 2 BRIGHT LIGHT("NetherworldQueenFlameBreath_21")
		Stop
	}
}

ACTOR Queen
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 2500
	Mass 800
	Radius 40
	Height 140
	Scale 0.65
	Speed 6.66666667
	PainChance 32
	MONSTER
	+FLOORCLIP
	+NORADIUSDMG
	+FULLVOLACTIVE
	SeeSound "queen/sight"
	PainSound "queen/pain"
	DeathSound "queen/death"
	ActiveSound "queen/active"
	Obituary "%o met the alien Queen."
	Damage 5
	States
	{
	Spawn:
		QUEE AB 10 A_Look
		Loop
	See:
		QUEE A 1 A_Chase
		QUEE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE A 1 A_Chase
		QUEE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE B 1 A_Chase
		QUEE BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE B 1 A_Chase
		QUEE BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE C 1 A_Chase
		QUEE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE C 1 A_Chase
		QUEE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE D 1 A_Chase
		QUEE DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		QUEE D 1 A_Chase
		QUEE DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		QUEE S 0 A_PlaySound("queen/scream")
		QUEE STU 8 A_FaceTarget
		QUEE UUUUUUUUUUUUUUUUUUUU 2 Bright A_CustomMissile("FlameBreathX", random(42,46), 0, random(-8,8))
		Goto See
	Melee:
		QUEE E 3 A_FaceTarget
		QUEE E 3 A_PlaySound("queen/shock")
		QUEE F 3 BRIGHT LIGHT("NetherworldQueen_Shock_1") A_CustomMeleeAttack(random(1,8)*5)
		QUEE G 3 BRIGHT LIGHT("NetherworldQueen_Shock_2") A_CustomMeleeAttack(random(1,8)*5)
		QUEE F 0 A_FaceTarget
		QUEE F 3 BRIGHT LIGHT("NetherworldQueen_Shock_1") A_CustomMeleeAttack(random(1,8)*5)
		QUEE G 3 BRIGHT LIGHT("NetherworldQueen_Shock_2") A_CustomMeleeAttack(random(1,8)*5)
		QUEE F 0 A_FaceTarget
		QUEE F 3 BRIGHT LIGHT("NetherworldQueen_Shock_1") A_CustomMeleeAttack(random(1,8)*5)
		QUEE G 3 BRIGHT LIGHT("NetherworldQueen_Shock_2") A_CustomMeleeAttack(random(1,8)*5)
		QUEE F 0 A_FaceTarget
		Goto See
	Pain:
		QUEE S 3
		QUEE S 3 A_Pain
		Goto See
	Pain.Stunner:
		QUEE S 3 A_ChangeFlag("NoPain", 1)
		QUEE S 300 A_Pain
		QUEE S 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		QUEE H 6 ACS_NamedExecuteAlways("AwardPoints",0)
		QUEE I 6 A_Scream
		QUEE J 6 A_Fall
		QUEE KLMNOP 6
		QUEE Q 3000
		QUEE Q 2 A_FadeOut(0.1)
		Wait
	}
}

ACTOR Cycloid
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 32
	BloodType "DN3DBlood", "DN3DBlood"
	Health 4000
	Mass 10000
	Radius 40
	Height 140
	Scale 1.35
	Speed 20
	PainChance 32
	MONSTER
	+FLOORCLIP
	SeeSound "cycloid/sight"
	PainSound "cycloid/pain"
	DeathSound "cycloid/death"
	ActiveSound "cycloid/active"
	Obituary "%o tried to face the Cycloid Emperor."
	States
	{
	Spawn:
		CYCL AB 10 A_Look
		Loop
	See:
		CYCL A 0 A_PlaySound("battlelord/stomp")
		CYCL A 1 A_Chase
		CYCL AAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL B 1 A_Chase
		CYCL BBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL C 1 A_Chase
		CYCL CCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL D 1 A_Chase
		CYCL DDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL A 0 A_JumpIfCloser(1024,"See")
		Goto Charge
	Charge:
		CYCL A 0 A_PlaySound("battlelord/stomp")
		CYCL AA 0 A_Chase
		CYCL AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL BB 0 A_Chase
		CYCL BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL CC 0 A_Chase
		CYCL CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL DD 0 A_Chase
		CYCL DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYCL A 0 A_JumpIfCloser(1024,"See")
		Loop
	Missile:
		CYCL E 0 A_PlaySound("cycloid/attack")
		CYCL E 0 A_Jump(128, "Psi") //change to jump if closer+chance
		CYCL E 4 A_FaceTarget
		CYCL F 4 BRIGHT	A_CustomMissile("Dukerocket", 48, 12, random(-45,45))
		CYCL E 0 A_CPosRefire
		Goto Missile+2
	Psi:
		CYCL G 20 A_FaceTarget
		CYCL HHHHHHHHHH 4 A_CustomMissile("PsiBlast", 64, 0, random(-45,45))
		CYCL E 0 A_Jump(128,"See")
		Goto Missile+1
	Pain:
		CYCL I 1
		CYCL I 1 A_Pain
		CYCL A 0 A_JumpIfCloser(1024,"See")
		Goto Charge
	Pain.Stunner:
		CYCL K 3 A_ChangeFlag("NoPain", 1)
		CYCL K 300 A_Pain
		CYCL K 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		CYCL I 8 ACS_NamedExecuteAlways("AwardPoints",0)
		CYCL J 8 A_Scream
		CYCL K 8 A_Fall
		CYCL LMNOP 8
		CYCL Q 3000
		CYCL Q 2 A_FadeOut(0.1)
		Wait
	}
}

ACTOR Overlord
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 3000
	Mass 2000
	Radius 48
	Height 100
	Scale 1.2
	Speed 4.16666667
	PainChance 32
	MONSTER
	+FLOORCLIP
	SeeSound "fatty/sight"
	AttackSound "duke/rocket"
	PainSound "fatty/pain"
	DeathSound "fatty/death"
	ActiveSound "fatty/active"
	Obituary "%o was blown apart by an overlord."
	States
	{
	Spawn:
		FATY AB 10 A_Look
		Loop
	See:
		FATY A 0 A_PlaySound("battlelord/stomp")
		FATY A 1 A_Chase
		FATY AAAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY B 1 A_Chase
		FATY BBBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY C 1 A_Chase
		FATY CCCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY D 1 A_Chase
		FATY DDDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Charge:
		FATY A 0 A_PlaySound("battlelord/stomp")
		FATY A 0 A_Chase
		FATY AAAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY B 0 A_Chase
		FATY BBBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY C 0 A_Chase
		FATY CCCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		FATY D 0 A_Chase
		FATY DDDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		LORD E 0 A_JumpIfCloser(1024,1)
		Goto Charge
		FATY EF 15 A_FaceTarget
		FATY G 8 A_FaceTarget
		FATY G 7 A_FaceTarget
		FATY H 7 Bright	A_CustomMissile("DukerocketSlow",100)
		FATY G 0 A_CPosRefire
		Goto Missile+4
	Pain:
		FATY I 3
		FATY I 3 A_Pain
		Goto See
	Pain.Stunner:
		FATY E 3 A_ChangeFlag("NoPain", 1)
		FATY E 300 A_Pain
		FATY E 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		FATY J 6 ACS_NamedExecuteAlways("AwardPoints",0)
		FATY K 6 A_Scream
		FATY L 6 A_Fall
		FATY MNOPQ 6
		FATY R 3000
		FATY R 2 A_FadeOut(0.1)
		Wait
	}
}

ACTOR AlienDrone
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 300
	Height 56
	Scale 0.75
	Speed 5.33333333
	PainChance 60
	Gravity 0.75
	MONSTER
	+FLOORCLIP
	SeeSound "alien/sight"
	PainSound "alien/pain"
	DeathSound "alien/death"
	ActiveSound "alien/active"
	Obituary "%o was shrunken by a protector drone."
	HitObituary "%o was scratched by a protector drone."
	States
	{
	Spawn:
		DGRD A 10 A_Look
		Loop
	See:
		DGRD B 1 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 1 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 1 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 1 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 1 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 1 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 1 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 1 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Jump(128,1) //change to jump if closer
		Goto See
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD B 0 A_Chase
		DGRD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD C 0 A_Chase
		DGRD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD D 0 A_Chase
		DGRD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DGRD E 0 A_Chase
		DGRD EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		DGRD F 6 A_FaceTarget
		DGRD F 0 A_PlaySound("alien/attack")
		DGRD GH 6 A_CustomMeleeAttack(random(1,8)*4)
		Goto See
	Missile:
		DGRD I 0 A_Jump(200,"Jump")
		DGRD I 0 A_PlaySound("alien/missile")
		DGRD IIIJJJJ 5 Bright A_FaceTarget
		DGRD J 0 A_CustomMissile("AlienDroneRay",52)
		Goto See
	Jump:
		DGRD R 1 A_FaceTarget
		DGRD R 0 A_Recoil(-8)
		DGRD RRR 3 ThrustThingZ(0,35,0,0)
		DGRD SSSSSSSSS 1
	CheckforFloor:
		DGRD T 1 A_JumpIf(velz==0,"Land")
		Loop
	Land:
		DGRD T 0 A_PlaySound("alien/splode")
		DGRD T 0 Thing_Stop(0)
		Goto See
	Pain:
		DGRD L 3
		DGRD L 3 A_Pain
		Goto See
	Pain.Stunner:
		DGRD L 3 A_ChangeFlag("NoPain", 1)
		DGRD L 300 A_Pain
		DGRG L 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		DGRD L 6 ACS_NamedExecuteAlways("AwardPoints",0)
		DGRD M 6 A_Scream
		DGRD N 6 A_Fall
		DGRD OP 6
		DGRD Q 3000
		DGRD Q 2 A_FadeOut(0.1)
		Wait
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0,1)
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}

}

ACTOR AlienDroneRay : MorphProjectile
{
	//$Category Stronghold_DukeNukem
	Scale 0.5
	Radius 8
	Height 8
	Speed 25
	Damage 1
	Projectile
	DeathSound "alien/splode"
	MorphProjectile.PlayerClass "ShrunkenPlayer"
	MorphProjectile.Duration 350
	MorphProjectile.MorphStyle MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER|MRF_FAILNOTELEFRAG|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHFORCED
	MorphProjectile.MorphFlash "ShrinkFuzz"
	MorphProjectile.UnMorphFlash "ShrinkFuzz"
	States
	{
	Spawn:
		DGS1 ABCD 4 Bright
		Loop
	Death:
		DGE1 ABCD 4 Bright
		Stop
	}
}

ACTOR KamikazeSentry
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 100
	Mass 80
	Height 56
	Scale 0.7
	Speed 3.75
	FloatSpeed 1
	PainChance 255
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	+NOBLOOD
	SeeSound "sentry/sight"
	PainSound "sentry/pain"
	ActiveSound "sentry/active"
	DeathSound "duke/splode"
	Obituary "%o let a sentry drone get too close."
	Damage 10
	MeleeRange 96
	States
	{
	Spawn:
		SENT A 10 A_Look
		Loop
	See:
		SENT A 1 A_Chase
		SENT AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SENT A 0 A_PlaySound("sentry/jet")
		Loop
	Missile:
		SENT A 0 A_PlaySound("sentry/charge") //unlike the lost soul, the sentry stops charging after a while so he doesnt get stuck
		SENT A 6 A_FaceTarget
		SENT A 6 A_SkullAttack
		SENT A 6 A_FaceTarget
		SENT A 38 A_SkullAttack
		Goto See
	Melee:
		SENT A 3 A_PlaySound("sentry/arm")
		TNT1 A 0 A_Die
	Pain:
		SENT A 3
		SENT A 3 A_Pain
		Goto See
	Pain.Stunner:
		SENT A 3 A_ChangeFlag("NoPain", 1)
		SENT A 300 A_Pain
		SENT A 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		TNT1 A 0 A_Explode(32,128,XF_NOTMISSILE)
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Scrap1",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Scrap2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_Fall
		XPLO A 0 A_Scream
		XPLO A 0 A_SetScale(1)
		XPLO ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
		Stop
	}
}

ACTOR Commander
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 400
	GibHealth -40
	Mass 200
	Height 56
	Scale 0.7
	Speed 4
	PainChance 64
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	SeeSound "commander/sight"
	PainSound "commander/pain"
	DeathSound "commander/death"
	ActiveSound "commander/active"
	Obituary "%o sucked down a commander's rocket."
	States
	{
	Spawn:
		COMM A 10 A_Look
		Loop
	See:
		COMM AAAABBBBCCCC 1 A_Chase
		Loop
	Missile:
		COMM I 0 A_PlaySound("commander/attack")
		COMM JJJJJJJJJJ 6 A_FaceTarget
		COMM J 4 BRIGHT A_CustomMissile("DukerocketTrailer",16)
		COMM J 8
		COMM J 0 A_Jump(150,1)
		Goto See
		COMM I 0 A_SpidRefire
		Goto Missile+7
	Pain:
		COMM K 3
		COMM K 3 A_Pain
		Goto See
	Pain.Stunner:
		COMM K 3 A_ChangeFlag("NoPain", 1)
		COMM K 300 A_Pain
		COMM K 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		COMM K 8 A_Scream
		COMM LM 8
		COMM N -1
		Stop
	Crash:
		COMM O 8 A_Fall
		COMM PQ 8
		COMM R -1
		Stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0,1)
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}
}

ACTOR Dukerocket
{
	Scale 0.6
	Radius 8
	Height 8
	Speed 25
	Damage 17
	Projectile
	SeeSound "duke/rocket"
	DeathSound "duke/splode"
	States
	{
	Spawn:
		DROK A -1
		Loop
	Death:
		XPLO A 0 A_Explode(128,128,0)
		XPLO A 0 A_SetScale(1)
		XPLO ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
		Stop
	}
}

actor DukerocketTrailer : Dukerocket
{
	SeeSound ""
	States
	{
	Spawn:
		DROK A 0
		DROK A 1 A_SpawnItemEx("Dukerockettrail", 0, 0, 3, 0, 0, 1)
		Loop
	Death:
		Goto Super::Death+1
	}
}

actor DukerocketSlow : Dukerocket
{
	SeeSound ""
	Scale 0.5
	Speed 18
}

ACTOR Dukerockettrail
{
	+CLIENTSIDEONLY
	Scale 0.5
	Radius 12
	Height 8
	Speed 1
	Damage 0
	RenderStyle Translucent
	PROJECTILE
	States
	{
	Spawn:
		TRAI XABCD 2
		Stop
	}
}

ACTOR BattleLord
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 64
	BloodType "DN3DBlood", "DN3DBlood"
	Health 1000
	Mass 2000
	Radius 20
	Height 72
	Scale 0.75
	Speed 3.33333333
	PainChance 32
	MONSTER
	+FLOORCLIP
	+BOSS
	+FULLVOLACTIVE
	SeeSound "battlelord/sight"
	AttackSound "lizard/attack"
	PainSound "battlelord/pain"
	DeathSound "battlelord/death"
	ActiveSound "battlelord/active"
	Obituary "%o was stomped on by a BattleLord."
	States
	{
	Spawn:
		LORD AB 10 A_Look
		Loop
	See:
		LORD A 1 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD A 1 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 1 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 1 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 1 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 1 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 1 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 1 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		LORD E 2 A_Jump(90,"Mortar")
	Chaingun:
		LORD E 3 A_FaceTarget
		LORD F 2 Bright	A_CPosAttack
		LORD E 3 A_FaceTarget
		LORD F 2 Bright	A_CPosAttack
		LORD E 3 A_FaceTarget
		LORD F 2 Bright A_CPosAttack
		LORD E 0 A_SpidRefire
		Goto Missile+1
	Mortar:
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_SpidRefire
		LORD E 0 A_Jump(256, "See","Missile")
		Goto See
	Pain:
		LORD I 3
		LORD I 3 A_Pain
		Goto See
	Pain.Stunner:
		LORD I 3 A_ChangeFlag("NoPain", 1)
		LORD I 300 A_Pain
		LORD I 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		LORD I 14 A_Scream
		LORD J 14 A_Fall
		LORD KLM 14
		LORD N 3000
		LORD N 2 A_FadeOut(0.1)
		Wait
	}
}

ACTOR BattleLordPal : BattleLord{}

ACTOR SuperBattleLord : BattleLord
{
	//$Category Stronghold_DukeNukem
	Scale 1.4
	Speed 6
	Radius 69
	Height 130
	Health 4000
	Mass 6000
	MinMissileChance 48
	Obituary "%o was perforated by the battlelord's super-chaingun."
	States
	{
	See:
		LORD G 0 A_PlaySound("battlelord/stomp")
		LORD A 1 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD A 1 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 1 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 1 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 1 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 1 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 1 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 1 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Charge:
		LORD G 0 A_PlaySound("battlelord/stomp")
		LORD A 0 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD A 0 A_Chase
		LORD AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 0 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD B 0 A_Chase
		LORD BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 0 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD C 0 A_Chase
		LORD CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 0 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LORD D 0 A_Chase
		LORD DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		LORD E 0 A_JumpIfCloser(1024, 1)
		Goto Charge
		LORD E 0 A_Jump(90, "Mortar")
		Goto Chaingun
	Mortar:
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_SpidRefire
		LORD E 0 A_Jump(256, "See","Missile")
		Goto See
	}
}

Actor Mortar
{
	Scale 0.6
	Radius 8
	Height 8
	Speed 16
	Damage 20
	PROJECTILE
	-NOGRAVITY
	BounceType Doom
	BounceSound "duke/bounce"
	States
	{
	Spawn:
		LGRE ABCD 2 A_SpawnItemEx("MortarTrail")
		Loop
	Death:
		XPLO A 0 A_SpawnItemEx("MortarTrap")
		Stop
	}
}


Actor MortarTrap
{
	Scale 0.6
	Radius 16
	Height 12
	Mass 1000000
	Health 10
	Damage 20
	+NOBLOOD
	+SHOOTABLE
	+MISSILE
	DeathSound "duke/splode"
	States
	{
	Spawn:
		LGRE ABCD 2
		Loop
	Melee:
	Death:
		XPLO A 0 A_Scream
		XPLO A 0 A_SetScale(1)
		XPLO ABCDEFGHIJKLMNOPQRSTU 2 BRIGHT
		Stop
	}
}

ACTOR MortarTrail
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOBLOCKMAP
	Scale 0.5
	States
	{
	Spawn:
		LGRE ABCDABCD 2 A_FadeOut(0.125)
		Stop
	}
}

ACTOR OctaBrain
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 120
	GibHealth -30
	Height 56
	Scale 0.7
	Speed 10
	Speed 3.33333333
	FloatSpeed 1.33333333
	Mass 400
	PainChance 200
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	SeeSound "octabrain/sight"
	AttackSound "octabrain/attack"
	PainSound "octabrain/pain"
	DeathSound "octabrain/death"
	ActiveSound "octabrain/active"
	Obituary "%o was blasted by an octabrain's psionic storm."
	States
	{
	Spawn:
		OCTA AB 10 A_Look
		Loop
	See:
		OCTA A 1 A_Chase
		OCTA AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		OCTA A 1 A_Chase
		OCTA AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		OCTA B 1 A_Chase
		OCTA BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		OCTA B 1 A_Chase
		OCTA BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		OCTA C 1 A_Chase
		OCTA CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		OCTA C 1 A_Chase
		OCTA CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		OCTA B 5
		OCTA C 5 A_FaceTarget
		OCTA D 20 A_FaceTarget
		OCTA D 5 BRIGHT A_CustomMissile("PsiBlast")
		OCTA C 5
		Goto See
	Pain:
		OCTA E 3
		OCTA E 3 A_Pain
		Goto See
	Pain.Stunner:
		OCTA E 3 A_ChangeFlag("NoPain", 1)
		OCTA E 300 A_Pain
		OCTA E 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		OCTA E 8 ACS_NamedExecuteAlways("AwardPoints",0)
		OCTA F 8 A_Scream
		OCTA G 8 A_Fall
		OCTA HI 8
		OCTA J 3000
		OCTA J 2 A_FadeOut(0.1)
		Wait
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0,1)
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}
}

ACTOR PsiBlast
{
	Scale 0.7
	Radius 12
	Height 8
	RenderStyle Translucent
	Alpha 0.85
	Speed 20
	Damage 10
	Projectile
	SeeSound "octabrain/blast"
	States
	{
	Spawn:
		OPSI ABCDEFGHIJKLMNOPQRST 2 Bright
		Stop
	Death:
		OPSI T 0
		Stop
	}
}

ACTOR LizardCommando
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 100
	GibHealth -25
	Height 56
	Scale 0.7
	Speed 4.8
	PainChance 32
	Gravity 0.6
	MONSTER
	+FLOORCLIP
	SeeSound "lizard/sight"
	AttackSound "lizard/attack"
	PainSound "lizard/pain"
	DeathSound "lizard/death"
	ActiveSound "lizard/roam"
	Obituary "%o was rattled by an enforcer."
	DropItem "LizardAmmoDrop" 64
	States
	{
	Spawn:
		LIZD AB 10 A_Look
		Loop
	See:
		LIZD AAAAA 0 A_Chase
		LIZD A 1 A_Chase
		LIZD AAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LIZD B 1 A_Chase
		LIZD BBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LIZD CCCCC 0 A_Chase
		LIZD C 1 A_Chase
		LIZD CCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LIZD D 1 A_Chase
		LIZD DDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		LIZD I 0 A_JumpIfCloser(512,1)
		Goto Jump
		LIZD I 0 A_Jump(256,"Chaingun","Jump","Spit")
	Chaingun:
		LIZD I 2 A_FaceTarget
		LIZD J 2 BRIGHT A_CPosAttack
		LIZD I 0 A_SpidRefire
		Loop
	Spit:
		LIZD EEEEFFFFGGGG 3 A_FaceTarget
		LIZD E 0 A_CustomMissile("LizardPhlegm",24,0,-20,2,3)
		LIZD E 0 A_CustomMissile("LizardPhlegm",24,0,20,2,3)
		LIZD E 8 A_CustomMissile("LizardPhlegm",24,0,0,2,3)
		Goto See
	Jump:
		LIZD Q 1 A_FaceTarget
		LIZD Q 0 A_Recoil(-8)
		LIZD QQQ 3 ThrustThingZ(0,35,0,0)
		LIZD RRRRRRRRR 1
	CheckforFloor:
		LIZD S 1 A_JumpIf(momz==0,"Land")
		Loop
	Land:
		LIZD Q 0 A_PlaySound("alien/splode")
		LIZD Q 0 Thing_Stop(0)
		Goto See
	Pain:
		LIZD K 3
		LIZD K 3 A_Pain
		LIZD E 0 A_Jump(25, 1) //dump chance
		Goto See
		LIZD EF 10
		LIZD G 15
		LIZD H 15 A_CustomMissile("ADump", 4,0,0)
		LIZD G 10
		Goto See
	Pain.Stunner:
		LIZD K 3 A_ChangeFlag("NoPain", 1)
		LIZD K 300 A_Pain
		LIZD K 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		LIZD K 6 ACS_NamedExecuteAlways("AwardPoints",0)
		LIZD L 6 A_Scream
		LIZD M 6 A_Fall
		LIZD NO 6
		LIZD P 3000
		LIZD PPPPPPPPPPPP 2 A_FadeOut(0.1)
		Stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		TNT1 A 0 A_PlaySound("duke/splat")
    	TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    	TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}

}

ACTOR LizardAmmoDrop : RandomSpawner
{
	//$Category Stronghold_DukeNukem
	DropItem Chaingun 255 1
	DropItem ClipBox 255 4
}

ACTOR LizardPhlegm
{
	Scale 0.7
	Radius 13
	Height 8
	Speed 20
	VSpeed 20
	Damage 10
	PROJECTILE
	-NOGRAVITY
	Gravity 0.6
	DamageType PoisonCloud
	PoisonDamage 50
	SeeSound "lizard/spit"
	States
	{
	Spawn:
		SPIT A 3 A_SpawnItemEx("LizardPhlegmTrail")
		Loop
	}
}

ACTOR LizardPhlegmTrail
{
	+CLIENTSIDEONLY
	Scale 0.35
	Radius 13
	Height 8
	+LOWGRAVITY
	States
	{
	Spawn:
		SPIT Z 2
		SPIT A 6
		Stop
	}
}

ACTOR ADump
{
	Scale 0.4
	Radius 13
	Height 8
	+TOUCHY
	States
	{
	Spawn:
		SHIT A -1
		Loop
	Death:
		SHIT A 1 A_PlaySound("lizard/shit")
		Stop
	}
}

ACTOR PigCop
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 100
	GibHealth -25
	Height 56
	Scale 0.72
	PainChance 32
	MONSTER
	+FLOORCLIP
	SeeSound "pig/sight"
	AttackSound "pig/attack"
	PainSound "pig/pain"
	DeathSound "pig/death"
	ActiveSound "pig/active"
	Obituary "%o was filled with lead by a pigcop."
	DropItem "PigCopAmmoDrop" 128
	States
	{
	Spawn:
		LARD AB 10 A_Look
		Loop
	See:
		LARD A 1 A_Chase
		LARD AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD A 1 A_Chase
		LARD AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD B 1 A_Chase
		LARD BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD B 1 A_Chase
		LARD BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD C 1 A_Chase
		LARD CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD C 1 A_Chase
		LARD CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD D 1 A_Chase
		LARD DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		LARD D 1 A_Chase
		LARD DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		LARD F 0 A_Jump(64,"DuckShoot")
	Shoot:
		LARD F 12 A_FaceTarget
		LARD G 10 Bright A_CustomBulletAttack(22.5,0,5,random(1,5)*3,"BulletPuff",0,CBAF_NORANDOM)
		LARD E 10 A_PlaySound("pig/cock")
		LARD EFF 7
		LARD E 0 A_Jump(128,"See")
		Goto Shoot+1
	DuckShoot: //heh
		LARD H 12 A_FaceTarget
		LARD I 22
		LARD J 10 Bright A_CustomBulletAttack(22.5,0,5,random(1,5)*3,"BulletPuff",0,CBAF_NORANDOM)
		LARD J 10 Bright A_PlaySound("pig/cock")
		LARD E 0 A_Jump(128,"See")
		Goto DuckShoot+1
	Pain:
		LARD K 3
		LARD K 3 A_Pain
		Goto See
	Pain.Stunner:
		LARD K 3 A_ChangeFlag("NoPain", 1)
		LARD K 300 A_Pain
		LARD K 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		LARD K 6 ACS_NamedExecuteAlways("AwardPoints",0)
		LARD L 6 A_Scream
		LARD N 6 A_Fall
		LARD O 6
		LARD P 6
		LARD Q 3000
		LARD Q 2 A_FadeOut(0.1)
		Wait
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}
}

ACTOR PigCopAmmoDrop : RandomSpawner
{
	//$Category Stronghold_DukeNukem
	DropItem Shotgun 255 1
	DropItem Shell 255 4
}

ACTOR DukeFodder
{
	//$Category Stronghold_DukeNukem
	painchance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood"
	Health 30
	GibHealth -15
	Height 56
	Scale 0.7
	Speed 3
	PainChance 200
	MONSTER
	+FLOORCLIP
	SeeSound "lizard/sight"
	PainSound "lizard/pain"
	DeathSound "lizard/death"
	ActiveSound "lizard/active"
	Obituary "%o was taken down by an assault trooper."
	DropItem Clip 128
	States
	{
	Spawn:
		DFOD AB 10 A_Look
		Loop
	See:
		DFOD G 0 A_ChangeFlag("NOGRAVITY",0)
		DFOD G 0 A_ChangeFlag("NOPAIN",0)
		DFOD G 0 A_ChangeFlag("FLOAT",0)
		DFOD A 1 A_Chase
		DFOD AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DFOD B 1 A_Chase
		DFOD BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DFOD C 1 A_Chase
		DFOD CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DFOD D 1 A_Chase
		DFOD DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Goto See+3
	Missile:
		DFOD F 0 A_JumpIfCloser(1024,2)
		DFOD F 0 A_Jump(224,1)
		Goto SeeJetpackRising
		DFOD F 0 A_Jump(128, "DuckMissile")
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		Goto See+3
	SeeJetpackRising:
		DFOD G 0 A_ChangeFlag("NOGRAVITY",1)
		DFOD G 0 A_ChangeFlag("NOPAIN",1)
		DFOD G 0 A_JumpIf(z-floorz>=128,"SeeJetpack")
		DFOD G 0 A_JumpIf(ceilingz-z<=56,"SeeJetpack")
		DFOD G 0 ThrustThingZ(0,3,0,0)
		DFOD G 2 A_Chase("","",0)
		Goto SeeJetpackRising+2
	SeeJetpack:
		DFOD G 0 A_ChangeFlag("FLOAT",1)
		DFOD G 1
		DFOD G 1 A_Chase("","JetpackAttack",0)
		Goto SeeJetpack+1
	JetpackAttack:
		DFOD G 0 A_Jump(64,1)
		Goto SeeJetpack+1
		DFOD G 0 A_FaceTarget
		DFOD G 0 A_CustomMissile("FodderLaser",26)
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD H 0 A_Chase("","",0)
		DFOD HHHH 1 Bright A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD H 0 A_Chase("","",0)
		DFOD HHHH 1 Bright A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		Loop
	DuckMissile:
		DFOD Q 10 A_FaceTarget
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_CustomMissile("FodderLaser",26)
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_CustomMissile("FodderLaser",26)
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_CustomMissile("FodderLaser",26)
		Goto See+3
	Pain:
		DFOD I 3
		DFOD I 3 A_Pain
		Goto See+3
	Pain.Stunner:
		DFOD I 3 A_ChangeFlag("NoPain", 1)
		DFOD I 300 A_Pain
		DFOD I 0 A_ChangeFlag("NoPain", 0)
		goto See+3
	Death:
		DFOD I 6 ACS_NamedExecuteAlways("AwardPoints",0)
		DFOD J 6 A_Scream
		DFOD K 6 A_Fall
		DFOD L 6
		DFOD M 3000
		DFOD M 2 A_FadeOut(0.1)
		Wait
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		TNT1 A 0 A_PlaySound("duke/splat")
    	TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    	TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		Stop
	}
}

ACTOR FodderLaser
{
	Scale 0.4
	Radius 8
	Height 8
	Speed 25
	Damage 2
	Projectile
	SeeSound "lizard/laser"
	DeathSound "duke/hit"
	States
	{
	Spawn:
		DLAS A 1 BRIGHT A_SpawnItemEx("FodderLaserTrail")
		Loop
	}
}

ACTOR FodderLaserTrail
{
	Scale 0.4
	Radius 8
	Height 8
	+Nogravity
	States
	{
	Spawn:
		DLAS A 4 Bright
		Stop
	}
}

ACTOR TeleFodder : DukeFodder
{
	//$Category Stronghold_DukeNukem
	Translation "112:127=[255,75,25]:[68,12,4]"
	Obituary "%o didn't see the assault captain coming."
	Health 60
	GibHealth -15
	States
	{
	SeeInvisible:
		TNT1 A 1
		TNT1 AAAA 0 A_Chase("GoVisible","",CHF_NOPLAYACTIVE)
		TNT1 A 1
		TNT1 A 1 A_Jump(10, "GoVisibleSee")
		Loop
	Missile:
		DFOD F 0 A_Jump(90, "GoInvisible")
		DFOD F 0 A_Jump(128, "DuckMissile")
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_CustomMissile("FodderLaser",26)
		Goto See+3
	GoInvisible:
		DFOD S 20 A_UnSetShootable
		DFOD T 4
		DFOD TTTTTTTTT 1 A_FadeOut(0.1)
		TNT1 A 0 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_UnSetSolid
		Goto SeeInvisible
	GoVisible:
		TNT1 A 6 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		TNT1 A 0 A_SetTranslucent(1.0,0)
		Goto Missile+2
	GoVisibleSee:
		TNT1 A 6 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		TNT1 A 0 A_SetTranslucent(1.0,0)
		Goto See
	}
}

actor TeleportFuzz
{
	PROJECTILE
	XScale 0.4
	YScale 0.5
	RenderStyle Translucent
	Alpha 0.756
	+ClientSideOnly
	States
	{
	Spawn:
		DFOG ABCDE 1
		Stop
	}
}

actor ShrinkFuzz : TeleportFuzz
{
	Translation "80:111=112:127"
	RenderStyle Add
	SeeSound "duke/shrink"
}

// Duke babes - added by Jimmy91 :D

ACTOR BabeSpawner : RandomSpawner 20088
{
	//$Category Stronghold_DukeNukem
	DropItem Babe1 255 1
	DropItem Babe2 255 1
	DropItem Babe3 255 1
	DropItem Babe4 255 1
}

ACTOR Babe1
{
	//$Category Stronghold_DukeNukem
	+SOLID
	+SHOOTABLE
	BloodType "DN3DBlood", "DN3DBlood"
	Health 250
	Height 64
	Radius 24
	Mass 9999999
	Scale 0.5
	DeathSound "duke/babedie"
	States
	{
	Spawn:
		BABE A -1
		Loop
	Death:
		"####" "#" 0 A_PlaySound("duke/splat")
		"####" "#" 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		"####" "#" 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		"####" "#" 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		"####" "#" 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		"####" "#" 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		"####" "##########" 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
		"####" "#" -1 A_Scream
		Stop
  }
}

ACTOR Babe2 : Babe1
{
	//$Category Stronghold_DukeNukem
	Height 48
	States
	{
	Spawn:
		BABE B -1
		Loop
	Death:
		BABE F 0
		Goto Super::Death
	}
}

ACTOR Babe3 : Babe1
{
	//$Category Stronghold_DukeNukem
	States
	{
	Spawn:
		BABE D -1
		Loop
	Death:
		BABE H 0
		Goto Super::Death
	}
}

ACTOR Babe4 : Babe1
{
	//$Category Stronghold_DukeNukem
	States
	{
	Spawn:
		BABE I -1
		Loop
	Death:
		BABE J 0
		Goto Super::Death
	}
}

ACTOR Babe5 : Babe1
{
	//$Category Stronghold_DukeNukem
	States
	{
	Spawn:
		BABE C -1
		Loop
	Death:
		BABE G 0
		Goto Super::Death
	}
}

// Duke gibs added by Jimmy91

actor DN3DBlood : Blood
{
	Scale 0.45
	+CORPSE
	States
	{
	Spawn:
		BLUD A 0
		BLUD A 0 A_Jump(128,2)
		BLUD A 0 A_SpawnItemEx("DN3DBloodSplat",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
		DNBD A -1
		Stop
	Crash:
		DNBD BCDEFGH 3
		Stop
	}
}

actor DN3DBloodSplat
{
	Radius 1
	Height 1
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		DNBS ABCD 2 Bright
		Stop
	}
}

actor Gibbage1
{
	PROJECTILE
	Radius 2
	Height 2
	Speed 9
	VSpeed 24
	Mass 5
	Scale 0.65
	Gravity 0.5
	Decal BloodSplat
	-NOGRAVITY
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+RIPPER
	+CORPSE
	+ClientSideOnly
	States
	{
	Spawn:
		GIB1 ABCD 6
		Loop
	Death:
		DNBD BCDEFGH 3
		Stop
	}
}

actor Gibbage2 : Gibbage1
{
	States
	{
	Spawn:
		GIB2 ABCD 6
		Loop
	}
}

actor Gibbage3 : Gibbage1
{
	States
	{
	Spawn:
		GIB3 ABCD 6
		Loop
	}
}

actor Gibbage4 : Gibbage1
{
	States
	{
	Spawn:
		GIB4 ABCD 6
		Loop
	}
}

actor Gibbage5 : Gibbage1
{
	States
	{
	Spawn:
		GIB5 ABCD 6
		Loop
	}
}

actor Gibbage6 : Gibbage1
{
	States
	{
	Spawn:
		DNBD A -1
		Loop
	}
}

actor Scrap1 : Gibbage1
{
	Decal ""
	Scale 0.5
	States
	{
	Spawn:
		SCRP ABCD 6
		Loop
	Death:
		SCRP A 0
		Stop
	}
}

actor Scrap2 : Scrap1
{
	States
	{
	Spawn:
		SCRP EFGHIJKLM 6
		Loop
	}
}