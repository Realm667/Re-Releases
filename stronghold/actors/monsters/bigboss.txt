ACTOR STBoss
{
	//$Category Stronghold_Monsters
	damagefactor "corefriendly",0
	damagefactor "bossfactor",0.05
	Health 1000000
	Radius 48
	Height 128
	Scale 2.0
	Mass 4000
	Speed 6
	PainChance 10
	MinMissileChance 255
	MeleeRange 96
	Monster
	+FLOORCLIP
	+NORADIUSDMG
	+BOSS
	+MISSILEMORE
	+MISSILEEVENMORE
	+LOOKALLAROUND
	+NOBLOCKMONST
	+NOTARGET
	+NOINFIGHTING
	PainSound "baron/pain"
	ActiveSound "baron/active"
	Translation "16:47=32:47","48:79=32:47","112:127=160:167"
	Obituary "%o couldn't evade the wrath of Mephistopheles."
	States
	{
	Spawn:
		BOSS AB 10 A_Look
		Loop
	Heal:
		BOSS H 8
	See:
		BOSS A 0 A_Jump(5,"Necromancy")
		BOSS A 0 A_JumpIfCloser(1024,"SeeClose")
		BOSS A 0 A_ChangeFlag("FRIGHTENED",0)
		BOSS AAAAAABBBBBBCCCCCCDDDDDD 1 A_VileChase
		Loop
	SeeClose:
		BOSS A 0 A_ChangeFlag("FRIGHTENED",1)
		BOSS AAAAAABBBBBBCCCCCCDDDDDD 1 A_VileChase
		Goto See
	Necromancy:
		BOSS E 0 A_Jump(45,"SpiritAttack")
		BOSS E 0 A_Jump(15,"RaiseArmy")
		BOSS E 0 A_Jump(5,"Haunting")
		Loop
	Melee:
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_CustomMissile("STBossAttack8")
		Goto See
	Missile:
		BOSS E 0 A_Jump(30,"FireNova")
		BOSS E 0 A_Jump(20,"ConvergeAttack")
		BOSS E 0 A_Jump(20,"FireVolley")
		BOSS E 0 A_Jump(20,"FireBall")
		BOSS E 0 A_Jump(15,"SpiritAttack")
		BOSS E 0 A_Jump(5,"RaiseArmy")
		BOSS E 0 A_Jump(1,"Haunting")
		Loop
	ConvergeAttack:
		BOSS EF 8 A_FaceTarget
		BOSS G 0 A_CustomMissile("STBossAttack1",64,0,23)
		BOSS G 0 A_CustomMissile("STBossAttack1",64,0,-23)
		BOSS G 8 A_CustomMissile("STBossAttack1",64,0,0)
		Goto See
	FireNova:
		BOSS EF 8 A_FaceTarget
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,30,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,60,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,90,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,120,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,150,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,180,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,210,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,240,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,270,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,300,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,330,2,4)
		BOSS G 0 A_CustomMissile("STBossAttack2",0,0,360,2,4)
		Goto See
	FireVolley:
		BOSS EF 8 A_FaceTarget
		BOSS GGGGGGGG 2 A_CustomMissile("STBossAttack3")
		Goto See
	FireBall:
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_CustomMissile("STBossAttack7")
		Goto See
	SpiritAttack:
		BOSS H 8 A_FaceTarget
		BOSS AAAA 0 A_CustomMissile("STBossAttack4")
		BOSS A 0 A_AlertMonsters
		Goto See
	RaiseArmy:
		BOSS H 8 A_FaceTarget
		BOSS GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 0 A_CustomMissile("STBossAttack5",0,0,random(0,360))
		Goto See
	Haunting:
		BOSS H 8 A_FaceTarget
		BOSS AAAAAAAAAAAAAAAA 0 A_CustomMissile("STBossAttack6")
		BOSS A 0 A_AlertMonsters
		Goto See
	Pain:
		BOSS H 4
		BOSS H 4 A_Pain
		Goto See
	Death:
		BOSS I 8 Thing_Destroy(0,1)
		BOSS J 8 A_Scream
		BOSS K 8 A_GiveToTarget("AwardItem",800)
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O -1 A_BossDeath
		Stop
	}
}

ACTOR STBossAttack1
{
	+SEEKERMISSILE
	Speed 20
	Damage 4
	Radius 12
	Height 12
	FastSpeed 35
	Projectile
	RenderStyle Add
	Alpha 1
	Scale 0.6
	Translation "112:127=160:167"
	+RANDOMIZE
	DamageType "fire"
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	states
	{
	Spawn:
		STFR A 0 A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 0 A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 0 A_SeekerMissile(0,4)
		STFR A 0 A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 0 A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		TNT1 A 0 A_SeekerMissile(0,4)
		loop
	Death:
		BAL7 C 6 Bright
		TNT1 A 0 A_SpawnItem("KDBossSparkGen",0,10,0)
		TNT1 A 0 A_SpawnItem("KDBossFlameGen",0,10,0)
		TNT1 A 0 A_SpawnItem("KDBossSmokeGen",0,10,0)
		stop
	}
}

ACTOR STBossAttack2
{
	Speed 20
	Damage 1
	Radius 24
	Height 24
	FastSpeed 35
	Projectile
	+RIPPER
	+RANDOMIZE
	+STRIFEDAMAGE
	RenderStyle Add
	Alpha 1
	Scale 4.0
	DamageType "fire"
	SeeSound "vile/firestrt"
	DeathSound "vile/firecrkl"
	states
	{
	Spawn:
		TNT1 A 0 A_Explode(random(0,random(0,1)),128,0)
		TNT1 A 0 A_SpawnItemEx("KDBossSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 1 A_SpawnItemEx("KDBossFlame",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 0 A_SpawnItemEx("KDBossSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 1 A_SpawnItemEx("KDBossBiggerFlame",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		LOOP
	Death:
		TNT1 AA 0 A_SpawnItem("KDBossFlameGen",0,0,0)
		TNT1 AA 0 A_SpawnItem("KDBossSparkGen",0,0,0)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("KDBossBiggerSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		FIRE CCBBCCBBCCBDCCDDCCDDEEDDEEDDEEFFEEFFEEFFGGHHGGHHGGHH 1 bright A_SpawnItemEx("KDBossBiggerFlame",Random(-60,60),Random(-60,60),Random(0,30),VelX,VelY,0.1 * Random(-2,160),0,128 | 8,0)
		stop
	}
}

ACTOR STBossAttack3
{
	Speed 20
	Damage 4
	Radius 18
	Height 18
	FastSpeed 35
	Projectile
	RenderStyle Add
	Alpha 1
	Scale 0.5
	Translation "112:127=160:167"
	+RANDOMIZE
	DamageType "fire"
	SeeSound "vile/firestrt"
	DeathSound "imp/shotx"
	states
	{
	Spawn:
		STFR A 0 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		STFR A 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		loop
	Death:
		BAL7 C 0
		BAL7 C 0 A_SpawnItem("KDBossFlameGen",0,0,0)
		BAL7 C 0 A_SpawnItem("KDBossSmokeGen",0,0,0)
		BAL7 C 0 A_SpawnItem("KDBossSparkGen",0,0,0)
		BAL7 C 6 bright A_Explode(32,256,0)
		BAL7 DE 6 bright
		stop
	}
}

ACTOR STBossAttack4
{
	Translation Ice
	RenderStyle Add
	Alpha 1.0
	+LOOKALLAROUND
	+SHOOTABLE
	+NOPAIN
	+SPECTRAL
	+SEEKERMISSILE
	+ISMONSTER
	+NONSHOOTABLE
	Radius 16
	Height 56
	Health 1000000
	Speed 20
	ReactionTime 2
	states
	{
	Spawn:
		SKUL A 0 BRIGHT A_JumpIfInventory("Clip",1,"Looking")
		SKUL A 0 BRIGHT A_GiveInventory("Clip",10)
	Looking:
		SKUL A 0 BRIGHT A_TakeInventory("Clip",1)
		SKUL A 0 BRIGHT A_JumpIfInventory("Clip",1,1)
		Goto Death
		SKUL A 0 BRIGHT A_Wander
		SKUL A 2 BRIGHT A_Look
		SKUL A 0 BRIGHT A_Wander
		SKUL A 1 BRIGHT A_Look
		SKUL A 0 BRIGHT A_Wander
		SKUL A 2 BRIGHT A_Look
		SKUL A 0 BRIGHT A_Wander
		SKUL A 1 BRIGHT A_Look
		SKUL B 0 BRIGHT A_Wander
		SKUL B 2 BRIGHT A_Look
		SKUL B 0 BRIGHT A_Wander
		SKUL B 1 BRIGHT A_Look
		SKUL B 0 BRIGHT A_Wander
		SKUL B 2 BRIGHT A_Look
		SKUL B 0 BRIGHT A_Wander
		SKUL B 1 BRIGHT A_Look
		loop
	See:
		SKUL A 0 BRIGHT A_TakeInventory("Clip",1)
		SKUL A 0 BRIGHT A_JumpIfInventory("Clip",1,1)
		Goto Death
		SKUL A 0 BRIGHT A_Wander
		SKUL A 2 BRIGHT A_VileChase
		SKUL A 0 BRIGHT A_Wander
		SKUL A 1 BRIGHT A_VileChase
		SKUL A 0 BRIGHT A_Wander
		SKUL A 2 BRIGHT A_VileChase
		SKUL A 0 BRIGHT A_Wander
		SKUL A 1 BRIGHT A_VileChase
		SKUL B 0 BRIGHT A_Wander
		SKUL B 2 BRIGHT A_VileChase
		SKUL B 0 BRIGHT A_Wander
		SKUL B 1 BRIGHT A_VileChase
		SKUL B 0 BRIGHT A_Wander
		SKUL B 2 BRIGHT A_VileChase
		SKUL B 0 BRIGHT A_Wander
		SKUL B 1 BRIGHT A_VileChase
		loop
	Melee:
		SKUL A 0 BRIGHT A_TakeInventory("Clip",1)
		SKUL A 0 BRIGHT A_JumpIfInventory("Clip",1,1)
		Goto Death
		SKUL CD 5 BRIGHT A_CustomMeleeAttack(random(0,2))
		SKUL A 5 BRIGHT A_Recoil(-30)
		SKUL A 0 BRIGHT A_Stop
		goto See
	Heal:
	Death:
		SKUL AABBAABBAA 3 BRIGHT A_FadeOut(0.1)
		stop
	}
}

ACTOR STBossAttack5
{
	Projectile
	+RIPPER -DROPOFF
	+BLOODLESSIMPACT
	Damage 0
	Speed 20
	FastSpeed 14
	states
	{
	Spawn:
		TNT1 A 5 A_SpawnItemEx("STBossSkeleton",0,0,0,0,0,0,0,0,230)
		loop
	Death:
		TNT1 A 0
		stop
	}
}

ACTOR STBossAttack6 : STBossAttack4
{
	states
	{
	Spawn:
		SKUL A 0 A_JumpIfInventory("Clip",1,"Looking")
		SKUL A 0 A_GiveInventory("Clip",60)
		goto Looking
	}
}

ACTOR STBossAttack7
{
	Speed 20
	Damage 4
	Radius 18
	Height 18
	FastSpeed 35
	Projectile
	RenderStyle Add
	Alpha 1
	Scale 0.75
	+RANDOMIZE
	DamageType "fire"
	SeeSound "vile/firestrt"
	DeathSound "imp/shotx"
	states
	{
	Spawn:
		STFR B 1 bright A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(-6,6),0.1 * Random(-6,6),0.1 * Random(-2,17),0,128,0)
		loop
	Death:
		MISL B 6 bright A_Explode(32,256,0)
		MISL G 0 A_SpawnItem("KDBossFlameGen",0,0,0)
		MISL G 0 A_SpawnItem("KDBossSmokeGen",0,0,0)
		MISL G 0 A_SpawnItem("KDBossSparkGen",0,0,0)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,30,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,60,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,90,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,120,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,150,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,180,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,210,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,240,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,270,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,300,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,330,2,4)
		MISL G 0 A_CustomMissile("STBossAttack2",0,0,360,2,4)
		MISL CD 6 bright
		stop
	}
}

ACTOR STBossAttack8
{
	Speed 0
	Damage 1000
	Radius 24
	Height 24
	FastSpeed 35
	Projectile
	+RANDOMIZE
	+STRIFEDAMAGE
	RenderStyle Add
	Alpha 1
	Scale 4.0
	DamageType "fire"
	SeeSound "vile/firestrt"
	DeathSound "vile/firecrkl"

	ReactionTime 20

	states
	{
	Spawn:
		TNT1 A 0 A_Explode(random(0,random(0,1)),128,0)
		TNT1 A 0 A_SpawnItemEx("KDBossSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 1 A_SpawnItemEx("KDBossFlame",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 0 A_SpawnItemEx("KDBossSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 1 A_SpawnItemEx("KDBossBiggerFlame",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		TNT1 A 0 A_CountDown
		Loop
	Death:
		TNT1 AA 0 A_SpawnItem("KDBossFlameGen",0,0,0)
		TNT1 AA 0 A_SpawnItem("KDBossSparkGen",0,0,0)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("KDBossBiggerSmoke",Random(-40,40),Random(-40,40),Random(0,20),VelX,VelY,0.1 * Random(-2,80),0,128 | 8,0)
		FIRE A 0 A_PlaySound("vile/firecrkl")
		FIRE CBCBCDCDCDEDEDEFEFEFGHGHGH 2 bright A_Explode(random(0,random(0,100)),128,0)
		stop
	}
}
ACTOR STBossSkeleton
{
	BloodType "KDBossSmallerSmoke"

	obituary "%o got whalloped by a skeleton."
	health 20
	scale 0.75
	radius 15
	height 42
	mass 20
	speed 15
	painchance 255
	seesound ""
	painsound ""
	deathsound "skeleton/death"
	activesound ""
	Translation "48:79=80:111","16:47=80:111","176:191=80:111","160:167=80:103",
		"128:151=88:103","232:235=104:105","236:239=110:111","13:15=107:111"
	DamageFactor "Fire",0
	MONSTER
	+FLOORCLIP +NOPAIN +STRIFEDAMAGE
	states
	{
	Spawn:
		SKEL Q 0 A_JumpIfInventory("Clip",1,"Beginning")
		SKEL Q 0 A_GiveInventory("Clip",1)
		Goto Raise
	Beginning:
		SKEL AB 10 A_Look
		loop
	See:
		SKEL Q 0 A_ChangeFlag("NOPAIN",0)
		SKEL AABBCCDDEEFF 2 A_Chase
		loop
	Melee:
		SKEL G 0 A_FaceTarget
		SKEL G 6 A_SkelWhoosh
		SKEL H 6 A_FaceTarget
		SKEL I 0 A_PlaySound("skeleton/melee")
		SKEL I 6 A_CustomMeleeAttack(random(1,random(1,3)))
		goto See
	Pain:
		SKEL L 5
		SKEL L 5 A_Pain
		goto See
	Death:
		SKEL LM 7
		SKEL N 7 A_Scream
		SKEL O 7 A_NoBlocking
		SKEL PPPPPPP 1 A_SpawnItemEx("KDBossSmallerSmoke",Random(-7,7),Random(-7,7),0,0,0,0.01 * Random(1,125),0,128,0)
		SKEL Q -1
		stop
	Raise:
		SKEL QPONML 5
		goto See
	}
}

ACTOR STFireEffect {
	Scale 4.0
	RenderStyle Add
	states {
	Spawn:
		FIRE ABABCDCDEFEFGHGH 2 bright
		stop
	}
}

// ============================== SFX Actors by KeksDose
// =====================================================
// By adjusting the reactiontime,you can adjust how
// many particles are spawned in the generator actors.

Actor KDBossSparkGen
{
	+NoInteraction

	ReactionTime 39

	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("KDBossSpark",0,0,0,0.1 * Random(0,125),0.1 * Random(0,125),0.1 * Random(0,140),Random(0,359),128,96)
		TNT1 A 0 A_CountDown
		Loop
	}
}

Actor KDBossSmokeGen : KDBossSparkGen
{
	ReactionTime 40

	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("KDBossSmoke",0,0,0,0.1 * Random(0,12),0.1 * Random(0,12),0.1 * Random(0,25),Random(0,359),128,0)
		TNT1 A 0 A_CountDown
		Loop
	}
}

Actor KDBossFlameGen : KDBossSmokeGen
{
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("KDBossFlame",0,0,0,0.1 * Random(0,20),0.1 * Random(0,20),0.1 * Random(0,25),Random(0,359),128,0)
		TNT1 A 0 A_CountDown
		Loop
	}
}

Actor KDBossSpark
{
	+Missile
	+NoBlockmap

	Radius 4
	Height 8
	Gravity 0.4

	RenderStyle Add
	Scale 0.15

	States
	{
	Spawn:
		PAO1 A 40 Bright
		PAO1 A 1 Bright A_FadeOut(0.05)
		Wait
	}
}

Actor KDBossSmoke
{
	+NoInteraction

	RenderStyle Translucent
	Scale 0.8
	Alpha 0.15

	States
	{
	Spawn:
		TNT1 AA 0 A_Jump(255,"IdleA","IdleB","IdleC","IdleD")
	IdleA:
		SMOK A 1 A_FadeOut(0.0007)
		Loop
	IdleB:
		SMOK B 1 A_FadeOut(0.0007)
		Loop
	IdleC:
		SMOK C 1 A_FadeOut(0.0007)
		Loop
	IdleD:
		SMOK D 1 A_FadeOut(0.0007)
		Loop
	}
}

Actor KDBossBiggerSmoke : KDBossSmoke { Scale 1.4 }
Actor KDBossSmallerSmoke : KDBossSmoke { Scale 0.5 }

Actor KDBossFlame
{
	+NoInteraction

	RenderStyle Add
	Scale 0.6
	Alpha 0.4

	States
	{
	Spawn:
		TNT1 AA 0 A_Jump(255,"IdleA","IdleB","IdleC","IdleD")
	IdleA:
		FRED A 1 A_FadeOut(0.012)
		Loop
	IdleB:
		FRED B 1 A_FadeOut(0.012)
		Loop
	IdleC:
		FRED C 1 A_FadeOut(0.012)
		Loop
	IdleD:
		FRED D 1 A_FadeOut(0.012)
		Loop
	}
}

Actor KDBossBiggerFlame : KDBossFlame { Scale 1.2 }
