ACTOR ArchonOfHell : StrongholdBaronOfHell 30104
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 2000
	PainChance 20
	Obituary "%o was no match for an archon of hell."
	HitObituary "%o was ripped apart by an archon of hell."
	BloodColor "33 FF 00"
	+NOTARGET
	+MISSILEMORE
	+BOSS
	+DONTHARMCLASS
	SeeSound "monster/ar2sit"
	DeathSound "monster/ar2dth"
	States
	{
	Spawn:
		BOS4 AB 10 A_Look
		Loop
	Missile:
		BOS4 B 0 A_Jump(128,15)
		BOS4 EF 8 Bright A_FaceTarget
		BOS4 G 0 A_CustomMissile ("BaronBall",28)
		BOS4 G 0 A_CustomMissile ("BaronBall",28,0,-4)
		BOS4 G 3 Bright A_CustomMissile ("BaronBall",28,0,4)
		BOS4 H 3 Bright
		BOS4 H 0 A_Jump(64,8)
		BOS4 IJ 8 Bright A_FaceTarget
		BOS4 K 0 A_CustomMissile ("BaronBall",28)
		BOS4 K 0 A_CustomMissile ("BaronBall",28,0,-4)
		BOS4 K 3 Bright A_CustomMissile ("BaronBall",28,0,4)
		BOS4 L 3 Bright
		BOS4 L 0 A_Jump(64,1)
		Goto See
		BOS4 MN 8 Bright A_FaceTarget
		BOS4 O 0 A_Jump(128,3)
		BOS4 O 4 Bright A_CustomMissile ("ArchonComet",28)
		BOS4 P 4 Bright
		Goto See
		BOS4 O 0 A_CustomMissile ("BaronBall",28,0,-6)
		BOS4 O 0 A_CustomMissile ("BaronBall",28,0,-4)
		BOS4 O 0 A_CustomMissile ("BaronBall",28,0,-2)
		BOS4 O 0 A_CustomMissile ("BaronBall",28,0,2)
		BOS4 O 0 A_CustomMissile ("BaronBall",28,0,4)
		BOS4 O 3 Bright A_CustomMissile ("BaronBall",28,0,6)
		BOS4 P 3 Bright
		Goto See
	Melee:
		BOS4 EF 8 Bright A_FaceTarget
		BOS4 G 3 Bright A_CustomMeleeAttack(random(1,8)*10,"baron/melee")
		BOS4 H 3 Bright
		BOS4 H 0 A_Jump (128,1)
		Goto See
		BOS4 IJ 8 Bright A_FaceTarget
		BOS4 K 3 Bright A_CustomMeleeAttack(random(1,8)*10,"baron/melee")
		BOS4 L 3 Bright
		BOS4 L 0 A_Jump (64,1)
		Goto See
		BOS4 MN 8 Bright A_FaceTarget
		BOS4 O 3 Bright A_CustomMeleeAttack(random(1,8)*20,"baron/melee")
		BOS4 P 3 Bright
		Goto See
	Pain:
		BOS4 Q 2
		BOS4 Q 2 A_Pain
		Goto See
	Pain.Stunner:
		BOS4 Q 2 A_ChangeFlag("NoPain",1)
		BOS4 Q 300 A_Pain
		BOS4 Q 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem",12)
		BOS4 R 6 Bright
		BOS4 S 6 Bright A_Scream
		BOS4 T 6 Bright
		BOS4 U 6 Bright A_NoBlocking
		BOS4 VW 6 Bright
		BOS4 X 6 Bright A_SpawnItemEx("ArchonSoul",0,0,86,0,0,3)
		BOS4 YZ 6 Bright
		BOZ4 Z 6 Bright A_Fall
		Stop
	Raise:
		Stop
	}
}

ACTOR ArchonComet
{
	Radius 8
	Height 12
	Speed 25
	Damage 20
	PROJECTILE
	SeeSound "weapons/firbfi"
	DeathSound "weapons/hellex"
	DamageType Fire
	States
	{
	Spawn:
		ARCB AAAABBBBCCCC 1 Bright A_SpawnItemEx("ArchonCometTrail")
		loop
	Death:
		ARCB J 3 Bright A_SetTranslucent(0.67,1)
		ARCB K 3 Bright A_Explode(128,128,0)
		ARCB LMN 3 Bright
		stop
	}
}

ACTOR ArchonSoul
{
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Speed 3
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.80
	States
	{
	Spawn:
		BFX1 ABCD 6 BRIGHT
		Stop
	}
}

ACTOR ArchonCometTrail
{
	+CLIENTSIDEONLY
	Radius 3
	Height 3
	Scale 0.75
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.67
	States
	{
	Spawn:
		TNT1 A 3
		ARCB DEFGHI 3 BRIGHT
		Stop
	}
}