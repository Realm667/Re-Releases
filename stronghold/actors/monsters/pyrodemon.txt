ACTOR PyroDemon 30105
{ 
//$Category Stronghold_Monsters
painchance "Stunner", 128

spawnid 246
Health 2000
Radius 24 
Height 64 
Mass 1000 
Speed 8 
PainChance 20 
MONSTER
MeleeDamage 10
OBITUARY "%o was burnt to a crisp by a pyro demon."
HITOBITUARY "%o was melted by a pyro demon."
Bloodcolor "75 25 00"
+NoTarget
+FloorClip 
+MissileMore
+NoRadiusDMG
+Boss
+DontHurtSpecies
SEESOUND "pyro/see" 
PAINSOUND "pyro/pain" 
DEATHSOUND "pyro/death"
ACTIVESOUND "pyro/idle"
states 
   { 
   Spawn: 
       PYRO AB 10 A_Look 
       Loop 
   See:
       PYRO AA 3 A_Chase
       PYRO A 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO BB 3 A_Chase
       PYRO B 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO CC 3 A_Chase
       PYRO C 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO DD 3 A_Chase
       PYRO D 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       Loop 
   Missile:
       PYRO B 0 A_Jump(32,29)
       PYRO B 0 A_Jump(32,20)
       PYRO EF 8 BRIGHT A_FaceTarget 
       PYRO G 0 BRIGHT A_CustomMissile ("FireShot",28,0,0,0)
       PYRO G 0 BRIGHT A_CustomMissile ("FireShot",28,0,-4,0)
       PYRO G 3 BRIGHT A_CustomMissile ("FireShot",28,0,4,0)
       PYRO H 3 BRIGHT
       PYRO H 0 A_Jump(32,8)
       PYRO IJ 8 BRIGHT A_FaceTarget 
       PYRO K 0 BRIGHT A_CustomMissile ("FireShot",28,0,0,0)
       PYRO K 0 BRIGHT A_CustomMissile ("FireShot",28,0,-4,0)
       PYRO K 3 BRIGHT A_CustomMissile ("FireShot",28,0,4,0)
       PYRO L 3 BRIGHT
       PYRO L 0 A_Jump(32,1)
       Goto See
       PYRO MN 8 BRIGHT A_FaceTarget
       PYRO O 4 BRIGHT A_CustomMissile ("PyroBlast",28,0,0,0)
       PYRO P 4 BRIGHT
       Goto See
       PYRO MN 8 BRIGHT A_FaceTarget
       PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,0,0)
       PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,90,0)
       PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,180,0)
       PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,270,0)
       PYRO P 4 BRIGHT
       Goto See
	PYRO O 1 A_FaceTarget
        PYRO O 0 A_CustomMissile ("EscapeExplosion",28,10,0,0)
        PYRO O 0 A_Mushroom ("Pyroball",16)
       PYRO O 3 
       PYRO P 3 BRIGHT
    PYRO A 0 A_UnSetShootable
    PYRO A 1 A_PlaySound("pyro/tele")
    PYRO A 1 A_SetTranslucent(0.90)
    PYRO A 1 A_SetTranslucent(0.80)
    PYRO A 1 A_SetTranslucent(0.70)
    PYRO A 1 A_SetTranslucent(0.60)
    PYRO A 1 A_SetTranslucent(0.50)
    PYRO A 1 A_SetTranslucent(0.40)
    PYRO A 1 A_SetTranslucent(0.30)
    PYRO A 1 A_SetTranslucent(0.20)
    PYRO A 1 A_SetTranslucent(0.10)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
    TNT1 A 0 A_Jump(128,25)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
    PYRO A 1 A_PlaySound("pyro/tele")
    PYRO A 1 A_SetTranslucent(0.10)
    PYRO A 1 A_SetTranslucent(0.20)
    PYRO A 1 A_SetTranslucent(0.30)
    PYRO A 1 A_SetTranslucent(0.40)
    PYRO A 1 A_SetTranslucent(0.50)
    PYRO A 1 A_SetTranslucent(0.60)
    PYRO A 1 A_SetTranslucent(0.70)
    PYRO A 1 A_SetTranslucent(0.80)
    PYRO A 1 A_SetTranslucent(0.90)
    PYRO A 1 A_SetTranslucent(1.0)
    PYRO A 0 A_CustomMissile ("EscapeExplosion",28,10,0,0)
    PYRO O 0 A_Mushroom ("Pyroball",16)
    PYRO A 0 A_SetShootable
       Goto See
   Pain: 
       PYRO Q 2 
       PYRO Q 2 A_Pain 
       Goto See 
   Pain.Stunner:
       PYRO Q 2 A_ChangeFlag("NoPain", 1)
       PYRO Q 300 A_Pain
       PYRO Q 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 32)
       PYRO R 20 BRIGHT A_Scream
       PYRO S 20 BRIGHT
       PYRO T 20 BRIGHT 
       PYRO U 20 BRIGHT A_NoBlocking 
       PYRO VW 6 BRIGHT 
       PYRO X 6 BRIGHT
       PYRO Y 3 BRIGHT 
       PYRO Z 3000
       PYRO ZZZZZZZZZZZZ 2 A_FadeOut(0.1)
       Stop
     } 
}

ACTOR FireShot
{
   Radius 3
   Height 3
   Speed 15
   Damage 3
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.67
   +FIREDAMAGE
   SeeSound "pyro/flame"
   DeathSound "pyro/flamepoof"
   States
   {
   Spawn:
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      Loop
   Death:
      FSHT A 0 Bright A_Explode
      FSHT B 0 Bright
      FSHT B 3 Bright
      FSHT C 3 Bright
      FSHT D 3 Bright
      FSHT E 3 Bright
      Stop
   }
}

actor FireTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1 Bright
        Stop
    }
}

ACTOR EscapeExplosion 
{
    SeeSound "pyro/explosion"
    Radius 8
    Height 56
    Speed 0
    ExplosionDamage 50
    ExplosionRadius 192
    States
    {
    Spawn:
        NKXP B 3 BRIGHT
        NKXP C 3 BRIGHT
        NKXP D 3 BRIGHT A_Explode
        NKXP EFGHIJKLMNOPQRSTUV 3 BRIGHT
        Stop
    }
}

ACTOR PyroBall
{
   Radius 10
   Height 8
   Speed 15
   SEESOUND "pyro/bomb"
   DEATHSOUND "pyro/bombhit"
   PROJECTILE  
   RENDERSTYLE Add
   Alpha 0.7
   -NoGravity
   +DoomBounce
   +LowGravity
   +FireDamage
   +SeekerMissile
   states
   {
   Spawn:
      RBAL A 0 BRIGHT
      RBAL A 0 BRIGHT A_AlertMonsters
      RBAL AAAABBBB 1 BRIGHT 
      Goto Spawn +2
   Death:
      RBAL DEFGHI 4 BRIGHT 
      Stop
   }
}

ACTOR FirebombExplosion1
{
   Radius 11
   Height 8
   Speed 18
   Damage 6
   +NOBLOCKMAP 
   +DROPOFF 
   +MISSILE 
   +ACTIVATEIMPACT 
   +ACTIVATEPCROSS 
   ExplosionDamage 128
   ExplosionRadius 144
   Obituary "%o was killed by a Firebomb explosion."
   States
   {
   Spawn:
      FRME A 1 Bright
      Loop
   Death:
      FRME A 0
      FRME A 0 A_PlaySound("pyro/blastexplosion")
      FRME A 3 Bright A_Explode
      FRME B 1 Bright A_Mushroom("Pyroball",8)
      FRME B 2 Bright A_CustomMissile("FirebombExplosion2",25,0,180)
      FRME CDEFGHIJKLMNOPQRS 3 Bright
      Stop
   }
}

ACTOR FirebombExplosion2 : FirebombExplosion1
{
   ExplosionRadius 136
   States
   {
   Death:
      FRME A 0
      FRME A 0 A_PlaySound("pyro/blastexplosion")
      FRME A 3 Bright A_Explode
      FRME B 1 Bright
      FRME B 2 Bright A_CustomMissile("FirebombExplosion3",20,0,180)
      FRME CDEFGHIJKLMNOPQRS 3 Bright
      Stop
   }
}

ACTOR FirebombExplosion3 : FirebombExplosion1
{
   Speed 14
   Damage 5
   ExplosionRadius 128
   States
   {
   Death:
      FRME A 0
      FRME A 0 A_PlaySound("pyro/blastexplosion")
      FRME A 3 Bright A_Explode
      FRME BCDEFGHIJKLMNOPQRS 3 Bright
      Stop
   }
}

ACTOR PyroBlast
{
   PROJECTILE
   +SeekerMissile
   +NoGravity
   RENDERSTYLE Add
   Alpha 0.4
   SEESOUND "pyro/blast"
   DEATHSOUND "pyro/flamepoof"
   Speed 17
   Height 10
   Radius 10
   states
   {
   Spawn:
    SBLL ABCDEFGHIJ 1 BRIGHT A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
    SBLL A 0 BRIGHT A_SeekerMissile(0,2)
    Goto Spawn+1
   Death:
    SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,45)
    SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,135)
    SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,225)
    SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,315)
    SBLL LMNOPQRSTUVW 1 BRIGHT
    Stop
   }
}

ACTOR WalkFire
{
     +NOCLIP
     +MISSILE
     +DROPOFF
     +RANDOMIZE
     Scale 1.5
     Speed 1
     RENDERSTYLE ADD
     ALPHA 0.67
     States
     {
     Spawn:
          TNT1 A 8
          FRTF A 3 Bright
	    FRTF A 0 
	    FRTF B 3 Bright
	    FRTF B 0 
	    FRTF C 3 Bright
	    FRTF C 0 
          FRTF C 0 
          FRTF C 0 A_Jump(191,4)
          FRTF C 0 A_CustomMissile("WalkFire", 0, 24, 0)
          FRTF C 0 A_CustomMissile("WalkFire", 0, 0, 0)
          FRTF C 0 A_CustomMissile("WalkFire", 0, -24, 0)
          FRTF DEFGHIJKLMNO 3 Bright
          Stop
     }
}


ACTOR RainCometNull
{
  Radius 0
  Height 0
  Speed 8
  PROJECTILE
  +FloorClip
  +Ripper
  +DoomBounce
  +CeilingHugger
  +DontHurtSpecies
  states
  {
  Spawn:
    ZAP5 A 0 A_Jump(5,8)
    ZAP5 AAAAAA 1 A_Wander
    ZAP5 A 8 A_CustomMissile("FallingComet",-24,random(0,128),random(0,360),2,random(1,89))
    Loop
  Death:
    ZAP5 A 0
    Stop
  }
}



ACTOR FallingComet
{
    Radius 6
    Height 8
    Speed 5
    Damage 5
    ExplosionDamage 40
    ExplosionRadius 80
    Scale .55
    PROJECTILE
    SEESOUND "pyro/comet"
    DEATHSOUND "pyro/cometdeath"
    -NoGravity
    -Float
    +FloorClip
    +DontHurtSpecies 
    +SpawnCeiling
    states
    {
    Spawn:
        COMT AAAABBBBCCCC 1 BRIGHT A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
        Loop
    Death:
        COMT D 3 BRIGHT A_SpawnItemEx("CometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
        COMT E 3 BRIGHT A_Explode(20,128,0)
        COMT E 0 A_SpawnItemEx("CometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
        Stop
    }
}

actor CometDeathGlow : FireTail
{
    Scale 2.0
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3 Bright
        Stop
    }
}

actor CometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.7
    States
    {
    Spawn:
        COMT FGHI 3 Bright
        Stop
    }
}
