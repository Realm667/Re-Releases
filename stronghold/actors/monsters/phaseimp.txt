ACTOR PhaseImp : StrongholdDoomImp 24105
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 150
	Mass 120
	PainChance 192
	SeeSound "neodarkimp/sight"
	PainSound "neodarkimp/pain"
	DeathSound "neodarkimp/death"
	ActiveSound "neodarkimp/active"
	Obituary "%o was incinerated by a phase imp."
	HitObituary "%o was scratched by a phase imp."
	var int user_loop;
	RenderStyle Translucent
	States
	{
	Spawn:
		DIMP AB 10 A_Look
		Loop
	See2:
		TNT1 A 0 A_SetUserVar("user_loop",0)
		TNT1 AAA 0 A_Chase("","")
		TNT1 A 1 A_SetUserVar("user_loop",user_loop+1)
		TNT1 A 0 A_JumpIf(user_loop>=32,1)
		Goto See2+1
	See3:
		TNT1 AAA 0 A_Chase
		TNT1 A 1
		Loop
	Melee:
		DIMP E 0 A_SetTranslucent(1)
		DIMP EF 8 A_FaceTarget
		DIMP G 6 A_CustomComboAttack("DarkImpBall",32,random(1,8)*4,"neodarkimp/melee")
		Goto See
	Missile:
		DIMP E 0 A_Jump(192,"Melee")
		DIMP E 0 A_PlaySound("neodarkimp/teleport")
		DIMP E 8 Bright A_FaceTarget
		DIMP E 0 A_FaceTarget
		DIMP EEEEEEEEE 1 Bright A_FadeOut(0.1,0)
		Goto See2
	Death:
		DIMP A 0 A_GiveToTarget("AwardItem",5)
		DIMP I 0 A_SetTranslucent(1)
		Goto Super::Death+1
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem",7)
		DIMP N 0 A_SetTranslucent(1)
		Goto Super::XDeath+1
	}
}

ACTOR DarkImpBall : DoomImpBall
{
	Damage 4
	+SEEKERMISSILE
	SeeSound "neodarkimp/attack"
	DeathSound "neodarkimp/shotx"
	Alpha 0.75
	Decal DarkImpBallScorch
	States
	{
	Spawn:
		DIMB AB 4 Bright LIGHT("DarkImpBall") A_SeekerMissile(20,0)
		Loop
	Death:
		DIMB C 6 Bright LIGHT("DarkImpBall_X1")
		DIMB D 6 Bright LIGHT("DarkImpBall_X2")
		DIMB E 6 Bright LIGHT("DarkImpBall_X3")
		Stop
	}
}