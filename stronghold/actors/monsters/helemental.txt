actor Helemental 30130
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	obituary "%o was electrocuted by a Hades elemental."
	hitobituary "%o got chomped by a Hades elemental."
	health 1600
	radius 34
	height 64
	mass 800
	speed 3.33333333
	painchance 32
	seesound "monster/helsit"
	painsound "monster/helpai"
	deathsound "monster/heldth"
	activesound "monster/helact"
	MONSTER
	+FLOAT
	+NOGRAVITY
	+BOSS
	+MISSILEMORE
	+NOTARGET
	+DONTHARMCLASS
	+NORADIUSDMG
	RenderStyle Translucent
	states
	{
	Spawn:
		HELE A 10 A_Look
		loop
	See:
		HELE A 0 A_SetShootable
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 1 A_Chase
		HELE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELE A 0 A_Jump(72,1)
		goto See+1
		HELE A 0 A_UnSetshootable
		HELE A 1 A_PlaySound("monster/heltel")
		HELE AAAAAAAAA 1 A_FadeOut(.1,0)
		TNT1 A 0 A_Jump(128,75)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 A 0 A_Jump(128,75)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase("","")
		HELE A 1 A_PlaySound("monster/heltel")
		HELE AAAAAAAAAA 1 A_FadeIn
		HELE A 0 A_SetShootable
		Goto See
	Missile:
		HELE A 0 A_Jump(256,"Missile1","Missile2","Missile3")
	Missile1:
		HELE EF 5 Bright A_Facetarget
		HELE G 0 A_CustomMissile("HadesBall",24,0,-10)
		HELE G 0 A_CustomMissile("HadesBall",24,0,-5)
		HELE G 0 A_CustomMissile("HadesBall",24)
		HELE G 0 A_CustomMissile("HadesBall",24,0,5)
		HELE G 5 Bright A_CustomMissile("HadesBall",24,0,10)
		goto See+1
	Missile2:
		HELE EF 5 Bright A_Facetarget
		HELE G 0 A_CustomMissile("HadesBolt",32,0,-16,0,0)
		HELE G 0 A_CustomMissile("HadesBolt",32,0,0,0,0)
		HELE G 5 Bright A_CustomMissile("HadesBolt",32,0,16,0,0)
		goto See+1
	Missile3:
		HELE BC 5 Bright A_Facetarget
		HELE D 0 A_PainAttack("HS",45,PAF_NOSKULLATTACK)
		HELE D 5 Bright A_PainAttack("HS",-45,PAF_NOSKULLATTACK)
		goto See+1
	Melee:
		HELE ABD 5 Bright A_Facetarget
		HELE C 5 Bright A_CustomMeleeAttack(random(1,8)*10,"caco/melee")
		goto See+1
	Pain:
		HELE H 3
		HELE H 9 A_Pain
		goto See
	Pain.Stunner:
		HELE H 3 A_ChangeFlag("NoPain",1)
		HELE H 300 A_Pain
		HELE H 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem",51)
		HELE I 5 Bright A_Scream
		HELE JKL 5 Bright
		HELE M 5 Bright A_NoBlocking
		HELE M 0 A_SpawnItemEx("HS",36,36)
		HELE M 0 A_SpawnItemEx("HS",-36,36)
		HELE M 0 A_SpawnItemEx("HS",-36,-36)
		HELE M 0 A_SpawnItemEx("HS",36,-36)
		HELE NOP 5 Bright
		HELE QRSTU 5
		HELE V 3000 A_SetFloorClip
		HELE V 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR HadesBall : CacodemonBall
{
	Damage 8
	Speed 15
	Alpha 0.80
	DamageType lightning
	+FORCEXYBILLBOARD
	SeeSound "Monster/hadtel"
	DeathSound "Monster/hadsit"
	states
	{
	Spawn:
		HEFX AB 4 bright
		loop
	Death:
		HEFX CDEEFGH 3 bright
		stop
	}
}

ACTOR HadesBolt : CacodemonBall
{
	Damage 5
	Speed 5
	Radius 8
	Height 8
	Damagetype lightning
	SeeSound ""
	DeathSound "weapons/gntidl"
	YScale 4.0
	XScale 2.0
	ReactionTime 35
	+FLOORHUGGER
	BounceType Hexen
	+RIPPER
	-NOGRAVITY
	States
	{
	Spawn:
		LFX2 F 1 Bright A_Explode(16,64,0)
		LFX2 F 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
		LFX2 F 0 ThrustThing(random(0,255),1,0,0)
		LFX2 G 1 Bright A_Explode(16,64,0)
		LFX2 G 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
		LFX2 H 1 Bright A_Explode(16,64,0)
		LFX2 H 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
		LFX2 I 1 Bright A_Explode(16,64,0)
		LFX2 I 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
		LFX2 J 1 Bright A_Explode(16,64,0)
		LFX2 J 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
		LFX2 J 0 A_CountDown
		loop
	Death:
		LFX2 FGHIJ 2 Bright A_Explode(32,64,0)
		stop
	}
}

ACTOR HadesBolt2 : CacodemonBall
{
	Damage 0
	Speed 184
	DamageType lightning
	+RIPPER
	SeeSound "Monster/hadtel"
	DeathSound "Monster/hadsit"
	states
	{
	Spawn:
		TNT1 A 1 A_Explode(16,64,0)
		loop
	Death:
		TNT1 A 1
		stop
	}
}

//Monster-Hades Sphere
ACTOR HS 30116
{
	//$Category Stronghold_Monsters
	Health 35
	Radius 16
	Height 32
	Mass 3000
	Speed 5
	Obituary "%o was blown away by a Hades Sphere."
	MONSTER
	+LOOKALLAROUND
	+DONTGIB
	+NOTARGET
	+NOGRAVITY
	+FLOAT
	+DONTFALL
	+DONTHARMCLASS
	MeleeThreshold 196
	+FORCEXYBILLBOARD
	SeeSound "monster/hadsit"
	States
	{
	Spawn:
		TNT1 A 1 A_Look
		Loop
	See:
		HADE EFGH 4 BRIGHT
		HADE A 2 BRIGHT A_PlaySound("monster/hadwlk")
		HADE AABBBBCCCCDDDD 1 BRIGHT A_Chase
		HADE D 0 A_Jump(32,1)
		Goto See+4
		HADE H 0 A_UnSetSolid
		HADE H 0 A_UnSetShootable
		HADE H 5 BRIGHT A_PlaySound("monster/hadtel")
		HADE GFEIJKL 4 BRIGHT
	Teleporting:
		TNT1 AAAAAAAAAAAAAAAA 1 A_Chase
		TNT1 A 0 A_Jump(96,1)
		Loop
		HADE L 3 BRIGHT A_PlaySound("monster/hadtel")
		HADE KJIEFG 4 BRIGHT
		HADE H 0 A_SetShootable
		HADE H 0 A_SetSolid
		Goto See+4
	Melee:
		HADE HGFE 4 Bright A_SetShootable
		HADE E 0 A_Die
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem",12)
		HADE M 0 A_NoBlocking
		HADE M 0 A_SetTranslucent(.9,1)
		HADE M 0 A_PlaySound("monster/hadexp")
		HADE M 4 BRIGHT LIGHT("HadesBoom_1") A_Explode(112,112,XF_NOTMISSILE)
		HADE N 5 BRIGHT LIGHT("HadesBoom_2")
		HADE O 5 BRIGHT LIGHT("HadesBoom_3")
		HADE P 5 BRIGHT LIGHT("HadesBoom_4")
		HADE Q 5 BRIGHT LIGHT("HadesBoom_5")
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem",12)
		HADE N 0 A_NoBlocking
		HADE H 5 BRIGHT A_PlaySound("monster/haddth")
		HADE GFERSTUVWX 4 BRIGHT
		Stop
	}
}