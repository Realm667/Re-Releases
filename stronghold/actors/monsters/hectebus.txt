Actor Hectebus 22250 
{
  SpawnID 223
  Health 1500
  Radius 56
  Height 80
  Scale 1.25
  Speed 12
  PainChance 100
  PainChance "Stunner", 128
  BloodColor "73 00 00"
  Mass 1500
  SeeSound "Hectebus/Sight"
  PainSound "Hectebus/Pain"
  DeathSound "Hectebus/Death"
  ActiveSound "Hectebus/Active"
  Monster
  +FloorClip
  +MissileMore
  Obituary "%o was cremated by a Hectebus."
  States
  {
  Spawn:
    HECT AAAAAAAAAABBBBBBBBBB 1 A_Look
    Loop
  See:
    HECT ABC 6 A_Chase
    HECT D 0 A_PlaySoundEx("Hectebus/Step", "SoundSlot7", 0)
    HECT DEF 6 A_Chase
    HECT A 0 A_PlaySoundEx("Hectebus/Step", "SoundSlot7", 0)
    Loop
  Missile:
    HECT G 0 A_PlaySoundEx("Hectebus/Attack", "SoundSlot6", 0)
    HECT GGGG 4 A_FaceTarget
    HECT G 0 A_Jump(64, "Missile2")
    HECT U 4 Bright A_CustomMissile("Hecteball", 35, 41, 0)
    HECT IGG 4 A_FaceTarget
    HECT K 0 A_SpidReFire
    HECT V 4 Bright A_CustomMissile("Hecteball", 35, -41, 0)
    HECT IGG 4 A_FaceTarget
    HECT K 0 A_Jump(96, 2)
    HECT K 0 A_SpidReFire
    Goto Missile+6
    HECT K 0
    Goto See
  Missile2:
    HECT H 0 Bright A_CustomMissile("Hecteball", 35, 41, Random(-4, 2))
    HECT H 4 Bright A_CustomMissile("Hecteball", 35, -41, Random(-2, 4))
    HECT IGG 4 A_FaceTarget
    HECT H 0 Bright A_CustomMissile("Hecteball", 35, 41, Random(-4, 2))
    HECT H 4 Bright A_CustomMissile("Hecteball", 35, -41, Random(-2, 4))
    HECT IGG 4 A_FaceTarget
    HECT H 0 Bright A_CustomMissile("Hecteball", 35, 41, Random(-4, 2))
    HECT H 4 Bright A_CustomMissile("Hecteball", 35, -41, Random(-2, 4))
    HECT IGG 4 A_FaceTarget
    HECT K 0 A_Jump(200, 2)
    HECT K 0 A_SpidReFire
    Goto Missile2
    HECT K 0
    Goto See
  Pain:
    HECT J 8 A_Pain
    Goto See
  Pain.Stunner:
    HECT J 2 A_ChangeFlag("NoPain", 1)
    HECT J 300 A_Pain
    HECT J 0 A_ChangeFlag("NoPain", 0)
    Goto See
  Death:
    HECT K 0 A_GiveToTarget("AwardItem", 13)
    HECT K 0 A_Die
    HECT K 6 A_Scream
    HECT L 6
    HECT M 6
    HECT N 6 A_Fall
    HECT O 6
    HECT P 6
    HECT Q 6
    HECT R 6
    HECT S 6
    HECT T 3000
    HECT T 2 A_FadeOut(0.1) 
    Goto Death+12
  }
}

Actor Hecteball //Hectebus Fireball
{
  Radius 10
  Height 8
  Speed 20
  Damage 16
  Projectile
  RenderStyle Add
  Alpha 0.97
  SeeSound "Imp/Attack"
  DeathSound "Imp/ShotX"
  Decal Scorch
  States
  {
  Spawn:
    HECF AB 4 Bright
    Loop
  Death:
    HECF CDE 4 Bright
    Stop
  }
}
