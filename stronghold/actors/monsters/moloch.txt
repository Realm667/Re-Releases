//Moloch is a vicious cybernetic monster.	It has a devastating array of attacks: a lightning attack,a grenade attack,a nailgun,and a wraith-spitting attack.
//This monster is able to chain its attacks together in order to produce devastating combos. It will always chain each attack together randomly,so you may never know what attack may come next in the chain.

ACTOR Moloch 6666
{
	damagefactor "corefriendly",0
	damagefactor "bossfactor",0.05
	health 6500
	radius 40
	height 120
	mass 8000
	speed 4
	scale 0.75
	painchance 15
	damage 20
	Obituary "%o was sacrificed to Moloch."
	HitObituary "%o got shredded by Moloch's claws."
	seesound "moloch/sight"
	painsound "moloch/pain"
	deathsound "moloch/death"
	activesound "moloch/active"
	bloodcolor orange
	MONSTER
	+BOSS
	+QUICKTORETALIATE
	+FLOORCLIP
	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTMORPH
	+NOICEDEATH
	+FULLVOLACTIVE
	+DONTHARMCLASS
	var int user_theta;
	states
	{
	Spawn:
		MOLO AB 10 A_Look
		Loop
	See:
		MOLO A 1 A_Chase
		MOLO AAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO A 1 A_Chase
		MOLO AAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO B 1 A_Chase
		MOLO BBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO B 1 A_Chase
		MOLO BBBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO B 0 A_PlaySound("moloch/step1")
		MOLO C 1 A_Chase
		MOLO CCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO C 1 A_Chase
		MOLO CCCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO D 1 A_Chase
		MOLO DDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO D 1 A_Chase
		MOLO DDDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		MOLO D 0 A_PlaySound("moloch/step2")
		TNT1 A 0 A_JumpIfCloser(65,"Melee")
		Loop
	Melee:
		MOLO E 10 A_FaceTarget
		MOLO F 10 A_CustomMeleeAttack(40,"moloch/nailhitbleed","moloch/miss",extreme,1)
		Goto See
	Missile:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Jump (256,"Missile1","Missile2","Missile3","Missile4")
	Missile1:
		MOLO H 0 A_PlaySound("moloch/attack")
		MOLO HH 14 A_FaceTarget
		MOLO I 1 Bright A_CustomMissile ("MolochThunderbolt",65)
		MOLO I 10 Bright A_FaceTarget
		MOLO I 0 A_Jump (100,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Missile2:
		MOLO F 0 A_PlaySound("moloch/attack")
		MOLO F 12 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,-1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,-1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,-1,0)
		MOLO F 2 A_FaceTarget
		MOLO E 6 A_CustomMissile("HellGrenade",60,0,1,0)
		MOLO F 2 A_FaceTarget
		MOLO F 0 A_Jump (100,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Missile3:
		MOLO F 0 A_PlaySound ("moloch/attack")
		MOLO F 20 A_FaceTarget
		MOLO E 1 A_FaceTarget
		MOLO G 1 Bright A_CustomMissile ("MolochNail",55,random(-10,10),0,0)
		MOLO G 0 A_PlaySound("moloch/nail")
		MOLO E 1 A_FaceTarget
		MOLO G 1 Bright A_CustomMissile ("MolochNail",55,random(-10,10),0,0)
		MOLO G 0 A_PlaySound("moloch/nail")
		MOLO G 0 A_Jump (10,"Missile1","Missile2","Missile3","Missile4","See")
		MOLO G 1 Bright A_SpidRefire
		Goto Missile3+5
	Missile4:
		MOLO H 0 A_PlaySound("moloch/attack")
		MOLO HH 14 A_FaceTarget
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,1,30,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,-1,330,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,4,15,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,-4,345,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,1,30,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_CustomMissile ("MolochWraith",65,-1,330,0)
		MOLO I 5 Bright A_FaceTarget
		MOLO I 0 A_Jump (100,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Pain:
		MOLO H 0 A_SetUserVar("user_theta",0)
		MOLO H 0 A_CustomMissile("MolochScorch",0,0,user_theta)
		MOLO H 0 A_SetUserVar("user_theta",user_theta+30)
		MOLO H 0 A_JumpIf(user_theta>=360,1)
		Goto Pain+1
		MOLO H 0 A_Pain
		MOLO H 10 Radius_Quake(15,15,0,40,0)
		goto See
	Death:
		MOLO J 14 A_ScreamAndUnblock
		MOLO K 14 A_GiveToTarget("AwardItem",240)
		MOLO L 14 A_SetUserVar("user_theta",0)
		MOLO MNONMNONMNO 8
		MOLO PQ 7
		MOLO R 0 A_CustomMissile ("MolochDeathFire")
		MOLO R 0 Radius_Quake(40,60,0,40,0)
		MOLO R 0 A_PlaySound ("moloch/thud")
	Quake:
		MOLO H 0 A_CustomMissile("MolochQuake",0,-48,user_theta)
		MOLO H 0 A_SetUserVar("user_theta",user_theta+10)
		MOLO H 0 A_JumpIf(user_theta>=360,1)
		Loop
		MOLO R -1
		stop
	}
}

//Moloch's grenade attack.	These grenades spawn explosive embers upon death.
ACTOR HellGrenade
{
	Radius 10
	Height 10
	Speed 30
	Damage 20
	Damagetype "Fire"
	Scale 1.2
	PROJECTILE
	+EXTREMEDEATH
	-NOGRAVITY
	BounceType Doom
	Seesound "moloch/grenade"
	DeathSound "moloch/grenexp"
	States
	{
	Spawn:
		HGRN AB 3 Bright A_SpawnItemEX("HellSmoke",0,0,0,0,0,0,0,2)
		loop
	Death:
		MSP1 G 0 A_SetTranslucent (0.75,1)
		MSP1 G 2 Bright A_Explode(128,128,0)
		MSP1 G 0 A_Mushroom("HellGrenadeEmber",10)
		MSP1 HIJKLMNOP 2 Bright A_Mushroom("DoomImpBall",5)
		stop
	}
}

ACTOR HellGrenadeEmber
{
	Radius 5
	Height 5
	Speed 25
	Damage 5
	PROJECTILE
	RENDERSTYLE Add
	Alpha 0.90
	Damagetype "Fire"
	+THRUGHOST
	DeathSound "moloch/emberexp"
	States
	{
	Spawn:
		BAL3 AB 2 Bright A_SpawnItemEX("RedSmoke",0,0,0,0,0,0,0,2)
		loop
	Death:
		BAL3 CDE 4 Bright A_Explode(128,128,0)
		stop
	}
}

ACTOR HellSmoke
{
	+CLIENTSIDEONLY
	Radius 0
	Height 1
	PROJECTILE
	Scale 0.75
	RENDERSTYLE Translucent
	ALPHA 0.40
	States
	{
	Spawn:
		TNT1 A 3
		SMK2 ABCDE 1 Bright
		Stop
	}
}

ACTOR RedSmoke : HellSmoke
{
	Scale 1
	RENDERSTYLE Add
	ALPHA 0.85
	States
	{
	Spawn:
		TNT1 A 3
		FBL1 CDEFG 1 Bright
		Stop
	}
}

//Moloch's counterattack.	It shoots 12 of these whenever it's stunned.	An effective way of retaliating against groups of smaller targets.
actor MolochScorch
{
	radius 10
	height 14
	damage 15
	speed 30
	DamageType "Fire"
	RenderStyle Add
	alpha 0.75
	Scale 0.90
	seesound "moloch/scorch"
	deathsound "moloch/scorchhit"
	PROJECTILE
	+DROPOFF
	+FLOORHUGGER
	+EXTREMEDEATH
	states
	{
	Spawn:
		HBFR ABC 1 A_CStaffMissileSlither
		Loop
	Death:
		HBFR DEFGH 5 bright
		stop
	}
}

actor MolochDeathFire
{
	radius 4
	height 4
	renderstyle add
	alpha 0.90
	Scale 2.0
	seesound "moloch/deathfire"
	PROJECTILE
	+NOCLIP
	+DONTSPLASH
	states
	{
	Spawn:
		XPL2 ABCDEFGHIJK 3 bright A_Explode
		stop
	}
}

//Moloch's lightning attack.	It can tear through enemies and shoot lightning bolts.
ACTOR MolochThunderbolt : FastProjectile
{
	+CANNOTPUSH
	+SPAWNSOUNDSOURCE
	Radius 18
	Height 12
	Speed 50
	Damage 40
	RENDERSTYLE Add
	Alpha 0.90
	Scale 1.3
	Damagetype lightning
	+THRUGHOST
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+RIPPER
	+STRIFEDAMAGE
	SeeSound "moloch/thunder"
	Deathsound "moloch/thundercrash"
	States
	{
	Spawn:
		ZAP7 ABCDE 1 Bright A_SpawnItemEx("ThunderboltTrail")
		ZAP7 E 0 A_CustomMissile("ThunderFlash",0,0,45)
		ZAP7 E 0 A_CustomMissile("ThunderFlash",0,0,135)
		ZAP7 E 0 A_CustomMissile("ThunderFlash",0,0,225)
		ZAP7 E 0 A_CustomMissile("ThunderFlash",0,0,315)
		loop
	Death:
		ZAP7 FGHIJK 4 Bright A_Explode
		stop
	}
}

Actor ThunderboltTrail
{
	+NODAMAGETHRUST
	+CANNOTPUSH
	Scale 1.1
	Damage 1
	Projectile
	RenderStyle Add
	Alpha 0.5
	States
	{
	Spawn:
		ZAP7 AABBCCDDEE 2 Bright A_FadeOut(0.04)
		Loop
	}
}

actor ThunderFlash
{
	radius 6
	height 8
	damage 2
	speed 35
	Renderstyle Add
	Alpha 0.90
	seesound "moloch/flash"
	deathsound "moloch/flashhit"
	PROJECTILE
	+BLOODLESSIMPACT
	states
	{
	Spawn:
		ZAP6 ABC 2 bright
		loop
	Death:
		ZAP1 FEDCBA 1 bright A_Explode (10,20)
		stop
	}
}

//Moloch's earthquake attack.	This is performed when Moloch dies.	It can do incredible amounts of damage and can kill groups of weak enemies and severely damage strong ones.
ACTOR MolochQuake
{
	Speed 8
	Damage 15
	Radius 12
	Height 16
	RenderStyle Translucent
	Alpha 0.1
	PROJECTILE
	+DROPOFF
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	+FloorHugger
	+RIPPER
	SeeSound "moloch/thud"
	States
	{
	Spawn:
		IDGA CCAABBCCC 10
	Death:
		IDGA C 1 A_Explode
		Stop
	}
}

//Moloch's wraith attack.
ACTOR MolochWraith
{
	Speed 15
	Radius 10
	Height 6
	Damage 3
	ReactionTime 20
	Projectile
	+RIPPER
	+SEEKERMISSILE
	+SKYEXPLODE
	+NOEXPLODEFLOOR
	+EXTREMEDEATH
	+SEEKERMISSILE
	RenderStyle Add
	Alpha 0.9
	SeeSound "moloch/wraith"
	DeathSound "moloch/wraithdie"
	States
	{
	Spawn:
		SPRD AB 2 A_Tracer2
		SPRD A 0 A_Countdown
		SPRD B 0 A_Explode (5,15,0)
		Loop
	Death:
		SPRD DEFGHIJ 4 A_Explode (5,15,0)
		Stop
	}
}

//Moloch's nailgun attack.	Moloch can fire flesh-ripping nails in rapid succesion.	They can bounce off walls and floors.
actor MolochNail
{
	radius 2
	height 2
	damage (random(1,4)*6)
	speed 90
	Scale 0.65
	Bouncecount 3
	wallbouncefactor 2
	PROJECTILE
	+RIPPER
	BounceType Hexen
	+SPAWNSOUNDSOURCE
	+EXTREMEDEATH
	+BLOODSPLATTER
	states
	{
	Spawn:
		BLAD A -1 bright
		loop
	XDeath:
		SMK2 A 2 A_PlaySound("moloch/nailhitbleed")
		SMK2 BCDE 2
		Stop
	Crash:
	Death:
		FX57 A 0 A_PlaySound("moloch/nailhit")
		FX57 ABCDEFGHIJ 2 bright A_SetTranslucent (0.85,1)
		Stop
	}
}