ACTOR StrongholdScriptedMarine : ScriptedMarine
{
	Speed 2
	States
	{
	Spawn:
		PLAY A 4 A_MarineLook
		PLAY A 4 A_MarineNoise
		Loop
	Idle:
		"####" A 4 A_MarineLook
		"####" A 4 A_MarineNoise
		"####" A 4 A_MarineLook
		"####" B 4 A_MarineNoise
		"####" B 4 A_MarineLook
		"####" B 4 A_MarineNoise
		Loop
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee.Fist:
		"####" E 4 A_FaceTarget
		"####" E 4 A_M_Punch(1)
		"####" A 9
		"####" A 0 A_M_Refire(1)
		Loop
		"####" A 5 A_FaceTarget
		Goto See
	Melee.Berserk:
		"####" E 4 A_FaceTarget
		"####" E 4 A_M_Punch(10)
		"####" A 9
		"####" A 0 A_M_Refire(1)
		Loop
		"####" A 5 A_FaceTarget
		Goto See
	Melee.Chainsaw:
		"####" E 4 A_MarineNoise
		"####" E 4 A_M_Saw
		"####" E 0 A_M_SawRefire
		Goto Melee.Chainsaw+1
		"####" A 0
		Goto See
	Missile:
	Missile.None:
		"####" E 12 A_FaceTarget
		Goto Idle
		"####" F 6 BRIGHT
		Loop
	Missile.Pistol:
		"####" E 4 A_FaceTarget
		"####" F 6 BRIGHT A_M_FirePistol(1)
		"####" A 4 A_FaceTarget
		"####" A 0 A_M_Refire
		"####" A 5
		Goto See
	FireLoop.Pistol:
		"####" F 6 BRIGHT A_M_FirePistol(0)
		"####" A 4 A_FaceTarget
		"####" A 0 A_M_Refire
		Goto FireLoop.Pistol
		"####" A 5
		Goto See
	Missile.Shotgun:
		"####" E 3 A_M_CheckAttack
		"####" F 7 BRIGHT A_M_FireShotgun
		Goto See
	Missile.SSG:
		"####" E 3 A_M_CheckAttack
		"####" F 7 BRIGHT A_M_FireShotgun2
		Goto See
	Missile.Chaingun:
		"####" E 4 A_FaceTarget
		"####" FF 4 BRIGHT A_M_FireCGun(1)
		"####" FF 4 BRIGHT A_M_FireCGun(0)
		"####" A 0 A_M_Refire
		Goto Missile.Chaingun+3
		"####" A 0
		Goto See
	Missile.Rocket:
		"####" E 8
		"####" F 6 BRIGHT A_M_FireMissile
		"####" E 6
		"####" A 0 A_M_Refire
		Loop
		"####" A 0
		Goto See
	Missile.Plasma:
		"####" E 2 A_FaceTarget
		"####" E 0 A_FaceTarget
		"####" F 3 BRIGHT A_M_FirePlasma
		"####" A 0 A_M_Refire
		Goto Missile.Plasma+1
		"####" A 0
		Goto See
	Missile.Railgun:
		"####" E 4 A_M_CheckAttack
		"####" F 6 BRIGHT A_M_FireRailgun
		Goto See
	Missile.BFG:
		"####" E 5 A_M_BFGSound
		"####" EEEEE 5 A_FaceTarget
		"####" F 6 BRIGHT A_M_FireBFG
		"####" A 4 A_FaceTarget
		"####" A 0 A_M_Refire
		Loop
		"####" A 0
		Goto See
	SkipAttack:
		"####" A 1
		Goto See
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		Goto Idle
	Death:
		"####" H 10
		"####" I 10 A_Scream
		"####" J 10 A_NoBlocking
		"####" KLM 10
		"####" N -1
		Stop
	XDeath:
		"####" O 5
		"####" P 5 A_XScream
		"####" Q 5 A_NoBlocking
		"####" RSTUV 5
		"####" W -1
		Stop
	Raise:
		"####" MLKJIH 5
		Goto See
	}
}

ACTOR StrongholdMarineFist : StrongholdScriptedMarine replaces MarineFist
{
	States
	{
	Melee:
		Goto Super::Melee.Fist
	Missile:
		Stop
	}
}

ACTOR StrongholdMarineBerserk : StrongholdScriptedMarine replaces MarineBerserk
{
	States
	{
	Melee:
		Goto Super::Melee.Berserk
	Missile:
		Stop
	}
}

ACTOR StrongholdMarineChainsaw : StrongholdScriptedMarine replaces MarineChainsaw
{
	States
	{
	Melee:
		Goto Super::Melee.Chainsaw
	Missile:
		Stop
	}
}

ACTOR StrongholdMarinePistol : StrongholdScriptedMarine replaces MarinePistol
{
	States
	{
	Missile:
		Goto Super::Missile.Pistol
	}
}

ACTOR StrongholdMarineShotgun : StrongholdScriptedMarine replaces MarineShotgun
{
	States
	{
	Missile:
		Goto Super::Missile.Shotgun
	}
}

ACTOR StrongholdMarineSSG : StrongholdScriptedMarine replaces MarineSSG
{
	States
	{
	Missile:
		Goto Super::Missile.SSG
	}
}

ACTOR StrongholdMarineChaingun : StrongholdScriptedMarine replaces MarineChaingun
{
	States
	{
	Missile:
		Goto Super::Missile.Chaingun
	}
}

ACTOR StrongholdMarineRocket : StrongholdMarineFist replaces MarineRocket
{
	States
	{
	Missile:
		Goto Super::Missile.Rocket
	}
}

ACTOR StrongholdMarinePlasma : StrongholdScriptedMarine replaces MarinePlasma
{
	States
	{
	Missile:
		Goto Super::Missile.Plasma
	}
}

ACTOR StrongholdMarineRailgun : StrongholdScriptedMarine replaces MarineRailgun
{
	States
	{
	Missile:
		Goto Super::Missile.Railgun
	}
}

ACTOR StrongholdMarineBFG : StrongholdScriptedMarine replaces MarineBFG
{
	States
	{
	Missile:
		Goto Super::Missile.BFG
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARINE REINFORCEMENTS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor MarineItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_MARINE"
	Inventory.MaxAmount 10
	Tag "$TAG_MARINE"
	Inventory.Icon MARIA0
	+INVENTORY.INVBAR
	+GHOST
	Scale 0.75
	States
	{
	Spawn:
		MARP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 1 A_SpawnItemEx("TeleportFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarineShotgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineChaingun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(128,6)
		TNT1 A 0 A_Jump(24,4)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarineSSG",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineAutoShotgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineFlamer",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(48,2)
		TNT1 A 0 A_SpawnItemEx("STMarineRocket",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineHRL",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(48,4)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarinePlasma",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineRailgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("STMarineRepeater",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor SuperMarineItem : MarineItem
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_MARIN2"
	Inventory.MaxAmount 3
	Tag "$TAG_MARIN2"
	Inventory.Icon SMRIA0
	States
	{
	Spawn:
		SMAR A -1 BRIGHT
		Stop
	Use:
		TNT1 A 1 A_SpawnItemEx("TeleportFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(96,2)
		TNT1 A 0 A_SpawnItemEx("STMarineBFG",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Stop
		TNT1 A 0 A_Jump(96,2)
		TNT1 A 0 A_SpawnItemEx("STMarinePyroCannon",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Stop
		TNT1 A 0 A_SpawnItemEx("STMarineDevastator",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR STScriptedMarine : StrongholdScriptedMarine
{
	SeeSound "marine/sight"
	ActiveSound "marine/active"
	DeathSound "marine/death"
	PainSound "marine/pain"
	Health 300
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+GHOST
	+NOTIMEFREEZE
	+NODROPOFF
	DamageFactor "MarineAlly",0
	DamageType "MarineAlly"
	States
	{
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Raise:
		Stop
	}
}

Actor ShotgunmarineTimer : Powerup { Powerup.Duration 27 }
Actor SuperMarineTimer : Powerup { Powerup.Duration 52 }

actor STMarineShotgun : STScriptedMarine
{
	//$Category Stronghold_Marines
	Obituary "$OB_STMAS"
	AttackSound "weapons/shotgf"
	States
	{
	Spawn:
		PLA3 A 4 A_Look
		Loop
	Missile:
		"####" E 0 A_JumpIfInventory("ShotgunmarineTimer",1,"See")
		"####" E 3 A_FaceTarget
		"####" F 7 BRIGHT A_CustomBulletAttack(4, 3, 7, 5, "MarineImmunePuff")
		"####" F 0 A_GiveInventory("ShotgunmarineTimer")
		Goto See
	}
}

actor STMarineSSG : STScriptedMarine
{
	//$Category Stronghold_Marines
	Obituary "$OB_STSSG"
	AttackSound "weapons/sshotf"
	States
	{
	Spawn:
		PLA9 A 4 A_Look
		Loop
	Missile:
		"####" E 3 A_FaceTarget
		"####" F 7 BRIGHT A_CustomBulletAttack(11.25, 7.125, 20, 5, "MarineImmunePuff")
		"####" E 14
		"####" E 14 A_PlaySound ("weapons/sshoto")
		"####" E 13 A_PlaySound ("weapons/sshotl")
		"####" E 11 A_PlaySound ("weapons/sshotc")
		Goto See
	}
}

Actor STMarineAutoShotgun : STScriptedMarine
{
	AttackSound "weapons/asgfir"
	States
	{
	Spawn:
		PASG A 4 A_Look
		Loop
	Missile:
		"####" E 4 A_FaceTarget
		"####" F 5 BRIGHT A_CustomBulletAttack(5,4,7,5,"MarineImmunePuff")
		"####" E 6
		"####" A 0 A_PlaySound("weapons/asgld1")
		"####" E 4 A_CPosRefire
		"####" F 5 BRIGHT A_CustomBulletAttack(5,4,7,5,"MarineImmunePuff")
		"####" E 6
		"####" A 0 A_PlaySound("weapons/asgld1")
		"####" E 4 A_CPosRefire
		"####" F 5 BRIGHT A_CustomBulletAttack(5,4,7,5,"MarineImmunePuff")
		"####" E 6
		"####" A 0 A_PlaySound("weapons/asgld1")
		"####" E 4 A_CPosRefire
		"####" F 5 BRIGHT A_CustomBulletAttack(5,4,7,5,"MarineImmunePuff")
		"####" E 6
		"####" A 0 A_PlaySound("weapons/asgld1")
		Goto See
	}
}

actor STMarineChaingun : STScriptedMarine
{
	//$Category Stronghold_Marines
	Obituary "$OB_STCHM"
	AttackSound "weapons/chngun"
	States
	{
	Spawn:
		PLA4 A 4 A_Look
		Loop
	Missile:
		"####" E 2 A_FaceTarget
		"####" FF 4 BRIGHT A_CustomBulletAttack(0, 0, 1, 5, "MarineImmunePuff")
		"####" FF 4 BRIGHT A_CustomBulletAttack(4.2, 3, 1, 5, "MarineImmunePuff")
		"####" A 0 A_CPosRefire
		Goto Missile+3
	}
}

actor STMarineRocket : STScriptedMarine
{
	//$Category Stronghold_Marines
	Obituary "$OB_STRKM"
	States
	{
	Spawn:
		PLA5 A 4 A_Look
		Loop
	Missile:
		"####" E 8 A_FaceTarget
		"####" F 12 BRIGHT A_CustomMissile("ZMarineRocket")
		"####" E 8 A_SpidRefire
		Goto Missile+1
	}
}

actor ZMarineRocket : Rocket2
{
	DamageType "MarineAlly"
	States
	{
	Death:
		TNT1 A 0 A_Explode(128, 128, 0)
		Goto Super::Death+1
		Stop
	}
}

Actor STMarineHRL : STScriptedMarine
{
	States
	{
	Spawn:
		PHRL A 4 A_Look
		Loop
	Missile:
		"####" E 8 A_FaceTarget
		"####" F 12 BRIGHT A_CustomMissile("MarineHomRocket1")
		"####" E 8 A_SpidRefire
		Goto Missile+1
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("","",CHF_FASTCHASE)
		Goto See
	}
}

Actor MarineHomRocket1 : HomRocket1
{
	Damage 20
	-THRUGHOST
	DamageType "MarineAlly"
	States
	{
	Spawn:
		HMIS A 8 BRIGHT Light("ROCKETp")
		HMIS A 4 BRIGHT Light("ROCKETp") A_SeekerMissile(2,5)
		Wait
	Death:
		MISL B 0 A_Explode(128,128,0)
		Goto PowerRocket2::Death+1
	}
}

actor STMarinePlasma : STScriptedMarine
{
	//$Category Stronghold_Marines
	Obituary "$OB_STPLM"
	States
	{
	Spawn:
		PLA6 A 4 A_Look
		Loop
	Missile:
		"####" E 5 A_FaceTarget
		"####" F 2 BRIGHT A_CustomMissile("MarinePlasmaBall")
		"####" E 1 A_SpidRefire
		Goto Missile+1
	}
}

ACTOR MarinePlasmaBall : StrongholdPlasmaBall
{
	DamageType "MarineAlly"
}

Actor STMarineRailgun : STScriptedMarine
{
	Decal RailScorch
	States
	{
	Spawn:
		PRAI A 4 A_Look
		Loop
	Missile:
		"####" A 0 A_PlaySound("weapons/railch")
		"####" A 0 A_Jump(256, "Red", "Yellow", "Blue")
	Red:
		"####" EE 6 A_FaceTarget
		"####" A 0 A_StopSound(5)
		"####" F 4 BRIGHT A_CustomRailgun(100, 0, "Red", "white", 0, 1, 0, "MarineImmunePuff")
		"####" E 23
		Goto See
	Yellow:
		"####" EEEE 6 A_FaceTarget
		"####" A 0 A_StopSound(5)
		"####" G 4 BRIGHT A_CustomRailgun(200, 0, "Yellow", "white", 0, 1, 0, "MarineImmunePuff")
		"####" E 23
		Goto See
	Blue:
		"####" EEEEEE 6 A_FaceTarget
		"####" H 4 BRIGHT A_CustomRailgun(300, 0, "Blue", "white", 0, 1, 0, "MarineImmunePuff")
		"####" E 23
		Goto See
	}
}

Actor STMarineBFG : STScriptedMarine
{
	+MISSILEMORE
	States
	{
	Spawn:
		PB10 A 4 A_Look
		Loop
	Missile:
		"####" A 0 A_JumpIfInventory("SuperMarineTimer",1,"See")
		"####" A 0 A_ChangeFlag("NOPAIN",1)
		"####" A 0 A_PlaySound("weapons/bfgf")
		"####" EEEEE 6 A_FaceTarget
		"####" E 1
      	"####" F 10 A_CustomMissile("MarineBFGBall")
      	"####" E 33
		"####" A 0 A_GiveInventory("SuperMarineTimer")
		"####" A 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("","",CHF_FASTCHASE)
		Goto See
	}
}

Actor MarineBFGBall : StrongholdBFGBall
{
	DamageType "MarineAlly"
	States
	{
	Death:
		BFE1 A 8 BRIGHT LIGHT("BFGBALL_X1") A_Explode(128,256,0)
		BFE1 B 8 BRIGHT LIGHT("BFGBALL_X2")
		BFE1 C 8 BRIGHT LIGHT("BFGBALL_X3")
		BFE1 D 8 BRIGHT LIGHT("BFGBALL_X1")
		BFE1 E 8 BRIGHT LIGHT("BFGBALL_X4")
		BFE1 F 8 BRIGHT LIGHT("BFGBALL_X5")
		Stop
	}
}

Actor STMarinePyroCannon : STScriptedMarine
{
	States
	{
	Spawn:
		PPYR A 4 A_Look
		Loop
	Missile:
		"####" A 0 A_JumpIfInventory("SuperMarineTimer",1,"See")
		"####" A 0 A_ChangeFlag("NOPAIN",1)
		"####" EE 6 A_FaceTarget
		"####" E 1
		"####" F 15 A_CustomMissile("MarinePyroShot")
      	"####" E 20
		"####" A 0 A_GiveInventory("SuperMarineTimer")
		"####" A 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	}
}

Actor MarinePyroShot : PyroShot
{
	Damage 100
	DamageType Fire
	DamageType "MarineAlly"
	-THRUGHOST
	States
	{
	Spawn:
		PBAL ABA 4 BRIGHT LIGHT("PyroSpawnAB")
		PBAL BA 4 BRIGHT LIGHT("PyroSpawnAB") A_Explode
		goto Spawn+3
   Death:
		PYXP A 2 BRIGHT LIGHT("PyroSpawnA")
		PYXP B 2 BRIGHT LIGHT("PyroSpawnB")
		PYXP C 2 BRIGHT LIGHT("PyroSpawnC") A_Explode(128,256,0)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,0,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,45,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,90,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,135,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,180,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,225,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,270,4)
		PYXP C 0 A_CustomMissile("MarinePyroSpawner",0,0,315,4)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,0,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.75,4.75,18.5,0,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,17.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.4,4.4,18.5,45,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,6.3,6.3,18.5,45,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,72.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,90,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.75,4.75,18.5,90,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,107.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.4,4.4,18.5,135,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,6.3,6.3,18.5,135,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,162.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,180,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.75,4.75,18.5,180,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,197.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.4,4.4,18.5,225,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,6.3,6.3,18.5,225,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,252.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,270,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.75,4.75,18.5,270,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,287.5,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,4.4,4.4,18.5,315,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,6.3,6.3,18.5,315,SXF_ABSOLUTEANGLE)
		PYXP C 0 A_SpawnItemEx("MarinePyroFlare",0,0,0,3.7,3.7,18.5,342.5,SXF_ABSOLUTEANGLE)
		Goto Super::Death+12
	}
}

Actor MarinePyroSpawner : PyroSpawner
{
	Damage 0
	+PAINLESS
	States
	{
	Spawn:
		TNT1 AAAAA 5 A_CustomMissile("MarinePyroBoom",0,0,0,4)
		Stop
	}
}

Actor MarinePyroBoom : PyroBoom
{
	DamageType Fire
	States
	{
	Spawn:
		PYXP ABC 2 BRIGHT
		Goto Super::Spawn+3
	}
}

Actor MarinePyroFlare : PyroFlare
{
	Damage 7
	DamageType Fire
	DamageType "MarineAlly"
	States
	{
	Death:
		FRFX HIJ 2 BRIGHT
		FRFX J 0 A_Explode(48,64,0)
		FRFX J 0 A_SpawnItemEx("MarineDropFire")
		FRFX KLMNO 2 BRIGHT
		Stop
	}
}

Actor STMarineFlamer : STScriptedMarine
{
	MaxTargetRange 380
	States
	{
	Spawn:
		PFLM A 4 A_Look
		Loop
	Missile:
		"####" A 0 A_JumpIfCloser(560,1)
		Goto See
		"####" E 10 A_FaceTarget
	  	"####" FFFFFFFFF 1 A_CustomMissile("MarineFTFire",32,0,random(-4,4),0)
		"####" A 0 A_CPosRefire
		"####" A 0 A_JumpIfCloser(560,1)
		Goto See
		"####" A 0
		Goto Missile+2
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("","",CHF_FASTCHASE)
		Goto See
	}
}

Actor MarineFTFire : FTFire
{
	DamageType Fire
	DamageType "MarineAlly"
	-THRUGHOST
	States
	{
	Spawn:
		TNT1 A 2 BRIGHT
		FRFX ABCD 2 BRIGHT A_Explode(2,8,0)
		FRFX D 0 A_Gravity
		FRFX EFG 2 BRIGHT A_Explode(2,16,0)
		FRFX HIJ 2 BRIGHT A_Explode(2,32,0)
		FRFX KLM 2 BRIGHT A_Explode(2,64,0)
		FRFX NO 2 BRIGHT
		Stop
	Death:
		FRFX HIJ 2 BRIGHT A_Explode(2,32,0)
		FRFX J 0 A_CustomMissile("MarineDropFire",0,0,0,4)
		FRFX KLM 2 BRIGHT A_Explode(2,64,0)
		FRFX NO 2 BRIGHT
		Stop
	}
}

Actor MarineDropFire : DropFire
{
	DamageType Fire
	DamageType "MarineAlly"
	-THRUGHOST
	+PAINLESS
	States
	{
	Spawn:
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME BCDEFGHIJKLMN 2 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,32,0)
		FLME BCDEFGHIJKLMN 2 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,32,0)
		FLME BCDEFGHIJKLMN 2 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,32,0)
		FLME BCDEFGHIJKLMN 2 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Playsound("weapons/onfire")
		FLME A 1 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,32,0)
		FLME BCDEFGHIJKLMN 2 BRIGHT LIGHT("FTFireSpawn") A_Explode(2,16,0)
		FLME A 0 A_Jump(192,"Death")
		Loop
	}
}

Actor STMarineDevastator : STScriptedMarine
{
	States
	{
	Spawn:
		PDEV A 4 A_Look
		Loop
	Missile:
		"####" A 0 A_JumpIfInventory("SuperMarineTimer",1,"See")
		"####" A 0 A_ChangeFlag("NOPAIN",1)
		"####" A 0 A_PlaySound("weapons/devchr")
		"####" EEEEEEEEE 6 A_FaceTarget
		"####" E 1 A_PlaySound("weapons/devfir")
	  	"####" F 10 A_CustomMissile("MarineDevastatorBall")
	  	"####" E 7
		"####" A 0 A_GiveInventory("SuperMarineTimer",1)
		"####" A 0 A_ChangeFlag("NOPAIN",0)
		Goto See
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("","",CHF_FASTCHASE)
		Goto See
	}
}

Actor MarineDevastatorBall : DevastatorBall
{
	Damage 150
	-THRUGHOST
	DamageType "MarineAlly"
	States
	{
	Spawn:
		DBAL AB 4 BRIGHT LIGHT("DevBallLight")
		DBAL A 4 BRIGHT LIGHT("DevBallLight") A_Explode(64,128,0)
		DBAL A 0 A_Jump (160,3)
		DBAL AAA 0 A_CustomMissile("MarineLiteShot",0,0,Random(0,360))
		DBAL B 4 BRIGHT LIGHT("DevBallLight") A_Explode(64,128,0)
		DBAL B 0 A_Jump (160,3)
		DBAL BBB 0 A_CustomMissile("MarineLiteShot",0,0,Random(0,360))
		Goto Spawn+2
	Death:
		DBAL C 5 BRIGHT LIGHT("DevBallBoomA") A_SetUserVar("user_theta",18)
		DBAL D 5 BRIGHT LIGHT("DevBallBoomB") A_Explode(128,256,0)
		DBAL D 0 A_CustomMissile("MarineDevastatorBall2",0,0,user_theta,6)
		DBAL D 0 A_SetUserVar("user_theta",user_theta+18)
		DBAL D 0 A_JumpIf(user_theta>=360,1)
		Goto Death+2
		DBAL E 5 BRIGHT LIGHT("DevBallBoomC")
		DBAL F 5 BRIGHT LIGHT("DevBallBoomD")
		DBAL G 5 BRIGHT LIGHT("DevBallBoomE")
		DBAL HI 3 BRIGHT
		Stop
	}
}

Actor MarineDevastatorBall2 : DevastatorBall2
{
	Damage 6
	DamageType lightning
	DamageType "MarineAlly"
	-THRUGHOST
	States
	{
	Death:
		DBA2 C 4 BRIGHT LIGHT("DevPuffLight") A_Explode(48,64,0)
		DBA2 DE 4 BRIGHT LIGHT("DevPuffLight")
		Stop
	}
}

Actor MarineLiteShot : LiteShot
{
	Damage 6
	DamageType lightning
	DamageType "MarineAlly"
	-THRUGHOST
}

Actor STMarineRepeater : STScriptedMarine
{
	AttackSound "weapons/repfir"
	States
	{
	Spawn:
		PREP A 4 A_Look
		Loop
	Missile:
		"####" E 2 A_FaceTarget
		"####" FF 4 BRIGHT A_CustomBulletAttack(0,0,2,5,"MarineImmuneRepPuff")
		"####" FF 4 BRIGHT A_CustomBulletAttack(4,4,2,5,"MarineImmuneRepPuff")
		"####" A 0 A_CPosRefire
		Goto Missile+3
	Pain:
		"####" G 4
		"####" G 4 A_Pain
		"####" A 0 A_Chase("","",CHF_FASTCHASE)
		Goto See
	}
}

ACTOR MarineImmunePuff : NewPuff { DamageType "MarineAlly" }

Actor MarineImmuneRepPuff : RepPuff
{
	DamageType "MarineAlly"
	States
	{
	Spawn:
		BLAS A 1 BRIGHT LIGHT("RepPuffSpawnG")
		BLAS A 2 BRIGHT LIGHT("RepPuffSpawnG") A_Explode(32,88,0)
		Goto Super::Spawn+2
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SENTRY ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor Sentry
{
	//$Category Stronghold_Marines
	Mass 9999999999
	DamageFactor "overmindfactor", 10
	Radius 16
	Health 300
	MaxStepHeight 512
	MaxDropoffHeight 512
	+SHOOTABLE
	+NOBLOOD
	+FRIENDLY
	+LOOKALLAROUND
	+MISSILEMORE
	+MISSILEEVENMORE
	+NORADIUSDMG
	States
 	{
 	Spawn:
 		DGUN A 4 a_look
 		Loop
 	See:
 		DGUN A 4 A_chase
 		Loop
 	Missile:
 		DGUN A 4 A_facetarget
 		DGUN B 4 A_custommissile("SentryPlasma",16,-10)
 		DGUN C 4 A_custommissile("SentryPlasma",16,10)
 		DGUN A 7 A_CPosRefire //was A 4
 		Loop
 	Death:
 		TNT1 A 1
 		Stop
 	}
}

actor SentryPlasma
{
	Projectile
	Radius 1
	Height 2
	Speed 20
	Damage 3
	Scale 0.3
	DamageType "bossfactor"
	+DONTBLAST
	+THRUGHOST
	Translation "192:207=208:223","240:246=223:223"
	SeeSound "weapons/repfir"
	DeathSound "weapons/railf1"
	States
 	{
 	Spawn:
   		PLSS AB 4 BRIGHT LIGHT("SentryPlasma")
   		Loop
	Death:
		PLSE A 4 BRIGHT LIGHT("SentryPlasma1")
		PLSE BC 4 BRIGHT LIGHT("SentryPlasma2")
		PLSE D 4 BRIGHT LIGHT("SentryPlasma3")
		PLSE E 4 BRIGHT LIGHT("SentryPlasma4")
		Stop
 	}
}

actor SentryItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_SENTRY"
	Inventory.MaxAmount 5
	Inventory.Icon SENIA0
	Tag "$TAG_SENTRY"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		SENP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 A_SpawnItemEx("Sentry",64,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("ItemFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
  		TNT1 A 0 A_PlaySound("DSSTPLAC",CHAN_AUTO,0.7)
  		Stop
	}
}

Actor SuperSentry : Sentry
{
	+NODAMAGE
	-SHOOTABLE
	+NOTARGET
	States
 	{
	Missile:
 		DGUN A 2 A_facetarget
		DGUN A 2 A_facetarget
 		DGUN B 4 A_custommissile("SuperSentryPlasma",16,-10)
 		DGUN C 4 A_custommissile("SuperSentryPlasma",16,10)
 		DGUN A 4 A_CPosRefire //was A 4
 		Goto Missile+1
 	}
}

Actor SuperSentryPlasma : SentryPlasma
{
	Damage 5
	+NOEXTREMEDEATH
	+PAINLESS
}

Actor SuperSentryItem : SentryItem
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_SENTR2"
	Inventory.MaxAmount 2
	Inventory.Icon SSEIA0
	Tag "$TAG_SENTR2"
	States
	{
	Spawn:
		SSEN A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 A_SpawnItemEx("SuperSentry",64,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Goto Super::Use+1
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARREL SpawnER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor BarrelItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_BARREL"
	Inventory.MaxAmount 10
	Inventory.Icon BARIA0
	Tag "$TAG_BARREL"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		BARP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 1 A_SpawnItemEx("ItemFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("ExplosiveBarrelTimer",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
		Stop
	}
}

actor ExplosiveBarrelTimer
{
	Obituary "$OB_BARTIM"
	Health 20
	Radius 10
	Height 34
	deathsound "world/barrelx"
	MONSTER
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+DONTGIB
	+NOICEDEATH
	+FRIENDLY
	+NORADIUSDMG
	+INVULNERABLE
	States
	{
	Spawn:
		BAR1 A 35
		BAR1 A 0 A_ChangeFlag("INVULNERABLE",0)
		BAR1 AB 6 A_Look
		Goto Spawn+2
	See:
		BAR1 AB 2 A_Chase
		Loop
	Melee:
		BAR1 ABABABABABABABABABABABABABABABABAB 6
		BAR1 A 0 A_Die
	Death:
		BEXP A 5 BRIGHT
		BEXP B 5 BRIGHT A_Scream
		BEXP C 5 BRIGHT
		BEXP D 10 BRIGHT A_Explode
		BEXP E 10 BRIGHT
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// COINS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor CoinItem : CustomInventory
{
	//$Category Stronghold_Items
	Gravity 0.6
	Inventory.PickupMessage "$IP_COIN"
	Inventory.MaxAmount 100000
	Inventory.Icon SACKA0
	Tag "$TAG_COIN"
	+INVENTORY.KEEPDEPLETED
	States
	{
	Spawn:
		COIN A 1500
		COIN A 2 A_FadeOut
		Wait
	Use:
		//TODO: Make it so it drops 10 or so coins
		TNT1 A 0 A_SpawnItemEx("CoinItem",64,0,16)
		Stop
	}
}

// Item that monsters give player when they die. An ACS script gives the player the equal amount in CoinItem.
actor AwardItem : Inventory
{
	+INVENTORY.UNDROPPABLE
	Inventory.MaxAmount 1000000
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// LIFES ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor LifeItem : CustomInventory
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "IP_LIFEIT"
	Inventory.MaxAmount 9
	Inventory.Icon HERIA0
	Tag "$TAG_LIFEIT"
	+INVENTORY.INVBAR
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		HERP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 ACS_ExecuteWithResult(897) // Allows the script to Fail
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// JAILTRACKER //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Player is given this when they're in jail
actor JailTracker : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
}

// Player is given one of these per each player in jail
actor JailTrackerAllPlayers : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
	Inventory.MaxAmount 10000
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// THRUSTDUMMY //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Used for scripts that thrust player to detect when he's not where he should be
actor ThrustDummy : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SUPER SHIELD ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SuperShield : BasicArmorPickup
{
	//$Category Stronghold_Items
	armor.savepercent 100
	armor.saveamount 300
	Inventory.Icon "ARMXA0"
	Inventory.PickupMessage "$IP_SUPAS"
	States
	{
	Spawn:
		ARMX AB 3 BRIGHT
		Loop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// PORTABLE MEDICINE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PMedKit : HealthPickup
{
	//$Category Stronghold_Items
	Health 25
	Inventory.MaxAmount 10
	Inventory.Icon "PMH1A0"
	Inventory.PickupMessage "$IP_PMED"
	Tag "$TAG_PMED"
	+COUNTITEM
	States
	{
	Spawn:
		PM01 AABC 2
		Loop
	}
}

actor PSuperMedKit : PMedKit
{
	//$Category Stronghold_Items
	Health 100
	Inventory.Icon "PMH2A0"
	Inventory.PickupMessage "$IP_SMED"
	Tag "$TAG_SMED"
	States
	{
	Spawn:
		PM02 A -1
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// POWER UPS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// TIME FREEZER //////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
actor TimeFreezerItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_TIMEF"
	Inventory.Icon "FRZIA0"
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Powerup.Color "00 33 FF" 0.10
	Powerup.Type "TimeFreezer"
	Powerup.Duration -30
	Tag "$TAG_TIMEF"
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		FRZP A -1 BRIGHT
		Stop
	}
}

actor TimeFreezerPickup : TimeFreezerItem
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_TIMEP"
	Inventory.MaxAmount 10
	Inventory.Icon "TIMEZ0"
	States
	{
	Spawn:
		TIME ABCD 4 BRIGHT LIGHT("TimeSphere")
		Loop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
// QUAD Damage ////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerQuadDamage : PowerDamage
{
	DamageFactor "normal", 4
	Inventory.Icon "DOOMA0"
}

actor QuadDamageItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_QUAD"
	Powerup.Color RedMap
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Inventory.Icon "DAMIA0"
	Powerup.Type QuadDamage
	Powerup.Duration -30
	Tag "$TAG_QUAD"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		DAMP A -1 BRIGHT
		Stop
	}
}

actor QuadDamagePowerup : QuadDamageItem
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_QUADP"
	Inventory.Icon "DOOMA0"
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		DOOM ABCB 4 BRIGHT LIGHT("DoomSphere")
		Loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// INVULNERABILITY ///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor InvulnerabilityItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_INVU"
	Inventory.Icon "INVIA0"
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Powerup.Color InverseMap
	Powerup.Type "Invulnerable"
	Powerup.Duration -30
	Tag "$TAG_INVU"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		INVP A -1 BRIGHT
		Stop
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX ARMOR+ //////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MaxArmorBonus2 : BasicArmorBonus
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_MAXA"
	Armor.Savepercent 33.33333
	Armor.Saveamount 5
	Armor.Maxsaveamount 200
	Armor.MaxBonus 5
	Armor.MaxBonusMax 50
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		BON4 ABCDCB 6 BRIGHT
		Loop
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX HEALTH+ /////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MaxHealthBonus2 : UpgradeStamina
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_MAXH"
	Inventory.Amount 5
	Inventory.MaxAmount 300
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		BON3 ABCDCB 6 BRIGHT
		Loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// REGENERATION //////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RegenerationItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_REGE"
	Inventory.Icon "RESIA0"
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Powerup.Color "FF 00 AE" 0.10
	Powerup.Type "Regeneration"
	Powerup.Duration -30
	Powerup.Strength 10
	Tag "$TAG_REGE"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		RESP A -1 BRIGHT
		Stop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARRIER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor WallItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_BARK"
	Inventory.pickupsound "misc/p_pkup"
	Inventory.MaxAmount 3
	Inventory.Icon WALIA0
	Tag "$TAG_BARK"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		WALP A -1 BRIGHT
		Stop
	Use:
  		TNT1 A 0 A_SpawnItemEx("WallSpawner",64,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

actor WallSpawner
{
	+ISMONSTER
	+NOBLOCKMAP
	Radius 4
	Height 4
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_SpawnItemEx("WallPlaced",60, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",50, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",40, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",30, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",20, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",10, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",0,  32, 0, 0, 0, 0, 90,  SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-10,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-20,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-30,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-40,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-50,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-60,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		Stop
	}
}

actor WallBolt
{
	PROJECTILE
	+NOCLIP
	Speed 1
	RenderStyle Add
	Scale 0.33
	States
	{
	Spawn:
		WLXP A 1 A_FadeOut(0.025)
		Loop
	}
}

actor WallBolt2 : WallBolt
{
	States
	{
	Spawn:
		WLXP B 1 A_FadeOut(0.025)
		Loop
	}
}

actor WallPlaced
{
	Health 200
	Mass 0x7fffffff
	Radius 8
	Height 128
	Scale 0.5
	+FRIENDLY
	+DONTBLAST
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NORADIUSDMG
	+ISMONSTER
	DeathSound "WallItem/Explode"
	var int user_timer;
	States
 	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(user_timer>=4200,3)
		TNT1 A 0 A_SpawnItemEx("RedSparkleRandom",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,0,192)
		TNT1 A 1 A_SetUserVar("user_timer",user_timer+1)
  		Loop
		TNT1 A 1 A_Die
 	Death:
 		TNT1 A 0 A_Scream
		TNT1 A 0 A_KillSiblings
		TNT1 AAAA 0 A_CustomMissile("WallBolt",random(10,50),0,random(0,360),2,random(-15,15))
		TNT1 AAAA 0 A_CustomMissile("WallBolt2",random(10,50),0,random(0,360),2,random(-15,15))
 		Stop
  	}
}

actor RedSparkle
{
	+CLIENTSIDEONLY
	Height 2
	Radius 1
	+MISSILE
	+NOINTERACTION
	RenderStyle Add
	Alpha 0.9
	Translation "80:96=175:191"
	States
	{
	Spawn:
		PTCL ABCABCABCABCABCABC 2 BRIGHT
	Death:
		PTCL ABC 2 BRIGHT A_FadeOut
		Loop
	}
}

actor RedSparkleRandom : RandomSpawner
{
	DropItem "RedSparkleTiny"
	DropItem "RedSparkleSmall"
	DropItem "RedSparkleMedium"
}

actor RedSparkleTiny   : RedSparkle { Scale 0.125 }
actor RedSparkleSmall  : RedSparkle { Scale 0.2 }
actor RedSparkleMedium : RedSparkle { Scale 0.4 }

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RANDOM WEAPON ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RandomWeaponItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_RANW"
	Inventory.MaxAmount 5
	Inventory.Icon WEPIA0
	Tag "$TAG_RANW"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		WEPP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 A_Jump(64, 21)
		TNT1 A 0 A_Jump(64, 19)
		TNT1 A 0 A_Jump(64, 17)
		TNT1 A 0 A_Jump(64, 15)
		TNT1 A 0 A_Jump(64, 13)
		TNT1 A 0 A_Jump(64, 11)
		TNT1 A 0 A_Jump(64, 9)
		TNT1 A 0 A_Jump(64, 7)
		TNT1 A 0 A_Jump(64, 5)
		TNT1 A 0 A_Jump(64, 3)
		TNT1 A 0 A_Jump(64, 1)
		TNT1 A 0 A_GiveInventory("StrongholdBFG9000", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Pyrocannon", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Devastator", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Repeater", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Flamer", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Railgun", 4)
		Stop
		TNT1 A 0 A_GiveInventory("StunnerRifle", 4)
		Stop
		TNT1 A 0 A_GiveInventory("StrongholdPlasmaRifle", 4)
		Stop
		TNT1 A 0 A_GiveInventory("StrongholdRLauncher", 4)
		Stop
		TNT1 A 0 A_GiveInventory("HRL", 4)
		Stop
		TNT1 A 0 A_GiveInventory("LandMineLayer", 4)
		Stop
		TNT1 A 0 A_GiveInventory("Autoshotgun", 4)
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// AUTO DRONE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR AutoDroneItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_AUDR"
	Inventory.MaxAmount 2
	Inventory.Icon DRNIA0
	Tag "$TAG_AUDR"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		DRNP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimerRight",1,3)
		TNT1 A 0 A_GiveInventory("ForceCubeTimerRight",1)
		TNT1 A 0 A_SpawnItemEx("ForceCube1Right",0,0,0,0,0,0,0,33)
		Stop
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimerLeft",1,3)
		TNT1 A 0 A_GiveInventory("ForceCubeTimerLeft",1)
		TNT1 A 0 A_SpawnItemEx("ForceCube1Left",0,0,0,0,0,0,0,33)
		Stop
		TNT1 A 0
		Loop
	}
}

ACTOR ForceCube1Right
{
	Radius 0
	Height 0
	+NOTIMEFREEZE
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ForceCubeGiverRight",0,0,0,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube1Left : ForceCube1Right
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ForceCubeGiverLeft",0,0,0,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube2Right : CustomInventory
{
	+AUTOACTIVATE
	+NOTIMEFREEZE
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("RightCube",28,16,36,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube2Left : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("LeftCube",28,-16,36,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCubeTimerLeft : CustomInventory
{
	+NOTIMEFREEZE
	+INVENTORY.INTERHUBSTRIP
	States
	{
	Use:
		TNT1 A 0
		Stop
	}
}

ACTOR ForceCubeTimerRight : ForceCubeTimerLeft{}

ACTOR ForceCubeTimer2 : Inventory
{
	+NOTIMEFREEZE
	Inventory.MaxAmount 4200
}

ACTOR ForceCubeGiverRight
{
	Health 1000000000
	Radius 0
	Height 0
	Speed 0
	+NONSHOOTABLE
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOTIMEFREEZE
	MinMissileChance 0
	States
	{
	Spawn:
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 A 1 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerRight",1,2)
		TNT1 A 0 A_Jump(256,"Death")
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",4200,"Death")
		TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
		TNT1 A 1 A_GiveToTarget("ForceCube2Right",1)
		Loop
	Death:
		TNT1 A 0 A_TakeFromTarget("ForceCubeTimerRight",1)
		Stop
	}
}

ACTOR ForceCubeGiverLeft : ForceCubeGiverRight
{
	States
	{
	Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerLeft",1,2)
		TNT1 A 0 A_Jump(256,"Death")
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",4200,"Death")
		TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
		TNT1 A 1 A_GiveToTarget("ForceCube2Left",1)
		Loop
	Death:
		TNT1 A 0 A_TakeFromTarget("ForceCubeTimerLeft",1)
		Stop
	}
}

ACTOR RightCube
{
	Radius 0
	Height 0
	Speed 0
	Scale 0.1
	+FRIENDLY
	+NOGRAVITY
	+NOTIMEFREEZE
	+MISSILEMORE
	+MISSILEEVENMORE
	MinMissileChance 0
	States
	{
	Spawn:
		DRON AA 1 A_LookEx(2,0,800,0,135,"See")
		Stop
	See:
		DRON A 0 A_Jump(240,3) // the higher the number, the less frequent it attacks
		DRON A 1 A_CustomMissile("AutoDroneBall",8,0,0,0)
		TNT1 A 0 A_PlaySound("ForceCube/Fire")
		DRON A 1
		Stop
	}
}

ACTOR LeftCube : RightCube
{
	States
	{
	See:
		DRON A 0 A_Jump(240,3) // the higher the number, the less frequent it attacks
		DRON A 1 A_CustomMissile("AutoDroneBall",8,0,0,0)
		TNT1 A 0 A_PlaySound("ForceCube/Fire")
		DRON A 1
		Stop
	}
}

ACTOR AutoDroneBall
{
	Radius 8
	Height 8
	Speed 20
	Damage 48
	PROJECTILE
	RenderStyle ADD
	Alpha 0.90
	Scale 0.2
	DamageType "bossfactor"
	+NOTIMEFREEZE
	+THRUGHOST
	SeeSound "weapons/devbal"
	DeathSound "weapons/devex2"
	States
	{
	Spawn:
		DBA2 AB 4 BRIGHT
		Loop
	Death:
		DBA2 CDE 4 BRIGHT
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SMARTBOMB ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SmartBombItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_SBOMB"
	Inventory.Icon SMAIA0
	Tag "$TAG_SBOMB"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		SMAP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 1 A_SpawnItemEx("SmartBombGrenade", 0, 0, 32, 0, 10, 6, 90)
		Stop
	}
}

actor SmartBombGrenade
{
	PROJECTILE
	-NOGRAVITY
	+HEXENBOUNCE
	+BOUNCEONACTORS
	+CANBOUNCEWATER
	+NOEXPLODEFLOOR
	+SLIDESONWALLS
	-NOTELEPORT
	BounceFactor 0.6
	DamageType "corefriendly"
	Obituary "$OB_SBOMB"
	Speed 6
	Radius 2
	Height 2
	States
	{
	Spawn:
		UVGR ABCD 1
		UVGR ABCD 2
		UVGR ABCD 4
		UVGR A 16 A_Stop
		UVGR A 0 A_PlaySound("UVCharge",CHAN_VOICE,1,0,ATTN_NONE)
		UVGR A 0 A_SpawnItemEx("smallestlightemmit")
		UVGR A 0 A_SpawnItemEx("smallerlightemmit")
		UVGR E 170 BRIGHT A_SpawnItemEx("smalllightemmit")
		UVGR E 0 A_SpawnItemEx("Blindinglightemmit")
		UVGR E 0 A_SpawnItemEx("MoreBlindinglightemmit")
		UVGR E 0 A_SpawnItemEx("MostBlindinglightemmit")
		UVGR E 0 A_CustomMissile("LightRailer", 0, 0, 0)
		UVGR E 0 Radius_Quake(9,17,0,64,0)
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit1")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit2")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit3")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit4")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit5")
		TNT1 A 15
		Stop
	}
}

actor smalllightemmit
{
	+CLIENTSIDEONLY
	Radius 0
	Height 0
	RenderStyle ADD
	Alpha 0.75
	Scale 0.5
	States
	{
	Spawn:
		WHFL A 170 BRIGHT
		WHFL A 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

actor smallerlightemmit : smalllightemmit
{
	Scale 0.35
}

actor smallestlightemmit : smalllightemmit
{
	Scale 0.2
}

actor Blindinglightemmit : smalllightemmit
{
	Alpha 0.5
	Scale 1.0
	States
	{
	Spawn:
		WHFL A 50 BRIGHT
		WHFL A 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

actor moreBlindinglightemmit : Blindinglightemmit
{
	Scale 0.75
}

actor mostBlindinglightemmit : Blindinglightemmit
{
	Scale 0.5
}

actor Lightrailer
{
	+MISSILEMORE
	+LOOKALLAROUND
	Obituary "$OB_SBOM2"
	ReactionTime 25
	MinMissileChance 0
	DamageType "corefriendly"
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A 0 A_Changeflag("FRIENDLY", 1)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 0)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 1)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 0)
		TNT1 A 1 A_Look
		Stop
	See:
		TNT1 A 1 A_Facetarget
		TNT1 A 1 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_Countdown
		goto Spawn
	}
}

actor RingLightemmit1
{
	+CLIENTSIDEONLY
	Radius 0
	Height 0
	RenderStyle ADD
	Alpha 0.5
	Scale 5.0
	States
	{
	Spawn:
		WHFL B 2 BRIGHT
		Stop
	}
}

actor ringlightemmit2 : ringlightemmit1
{
	Alpha 0.4
	Scale 6.0
}

actor ringlightemmit3 : ringlightemmit1
{
	Alpha 0.3
	Scale 7.0
}

actor ringlightemmit4 : ringlightemmit1
{
	Alpha 0.2
	Scale 8.0
}

actor ringlightemmit5 : ringlightemmit1
{
	Alpha 0.1
	Scale 9.0
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// VAMPIRISM / DRAIN /////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor DrainItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_VAMP"
	Inventory.Icon "VAMIA0"
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Powerup.Color "AA 00 00" 0.20
	Powerup.Type "Drain"
	Powerup.Duration -45
	Tag "$TAG_VAMP"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		VAMP A -1 BRIGHT
		Stop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////
// PROTECTION /////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerQuarterDamage : PowerProtection
{
	DamageFactor "normal", 0.25
	Inventory.Icon "MEGAA0"
}

actor ProtectionItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_PROT"
	Inventory.Icon "PROIA0"
	Inventory.MaxAmount 3
	Inventory.UseSound "pickups/slowmo"
	Powerup.Color "FF AA 00" 0.10
	Powerup.Type QuarterDamage
	Powerup.Duration -45
	Tag "$TAG_PROT"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		PROP A -1 BRIGHT
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// MORPHPowerup /////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MorphPlayerItem : PowerupGiver
{
	//$Category Stronghold_Items
	Scale 0.75
	Inventory.PickupMessage "$IP_MORPH"
	Inventory.UseSound "pickups/slowmo"
	Inventory.MaxAmount 2
	Inventory.Icon "MORIA0"
	Powerup.Type MorphPlayer
	Powerup.Duration -45
	Tag "$TAG_MORPH"
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		MORP A -1 BRIGHT
		Stop
	}
}

ACTOR PowerMorphPlayer : PowerMorph
{
	PowerMorph.PlayerClass "MorphedPlayer"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_WHENINVULNERABLE|MRF_NEWTIDBEHAVIOUR|MRF_LOSEACTUALWEAPON)
}

ACTOR MorphPlayerItem2 : MorphPlayerItem
{
	Powerup.Type MorphPlayer2
}

ACTOR PowerMorphPlayer2 : PowerMorphPlayer
{
	PowerMorph.PlayerClass "MorphedPlayer2"
}

/////////////////////////////////////////////////////////////////////////////////////////////////
// I WIN BUTTON - Powerup ///////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerOSPDamage : PowerDamage
{
	DamageFactor "normal", 4
}
actor PowerOSPSpeed : PowerSpeed
{
	Speed 2.0
}

actor OSPSpeed : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Duration -10
	Powerup.Type "OSPSpeed"
	States
	{
	Spawn:
		MEGA A -1
		Loop
	}
}
actor OSPInv : OSPSpeed
{
	Powerup.Type "Invulnerable"
	Powerup.Color GoldMap
}
actor OSPQuad : OSPSpeed
{
	Powerup.Type "OSPDamage"
	Powerup.Color "FF 00 00",0.35
}
actor OSPPower : OSPSpeed
{
	Powerup.Type "WeaponLevel2"
}

actor OSPHealth : Health
{
	Inventory.Amount 100
	Inventory.MaxAmount 100
	States
	{
	Spawn:
		SOUL A -1
		Loop
	}
}

actor IWinButtonItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.Icon IWBIA0
	Inventory.PickupMessage "$IP_IWIN"
	+INVENTORY.INVBAR
	Tag "$TAG_IWIN"
	States
	{
	Spawn:
		IWBP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("OSPInv")
		TNT1 A 0 A_GiveInventory("OSPQuad")
		TNT1 A 0 A_GiveInventory("OSPSpeed")
		TNT1 A 0 A_GiveInventory("OSPPower")
		TNT1 A 0 A_GiveInventory("OSPHealth")
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// Powerup	///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor WeaponPowerItem : CustomInventory
{
	//$Category Stronghold_Items
	Inventory.PickupMessage "$IP_WEAPOW"
	Inventory.pickupsound "pickup"
	Inventory.MaxAmount 3
	Inventory.Icon POWIA0
	tag "$TAG_WEAPOW"
	+INVENTORY.INVBAR
	Scale 0.75
	States
	{
	Spawn:
		POWP A -1 BRIGHT
		Stop
	Use:
		TNT1 A 1 A_GiveInventory("WeaponPowerupGiver")
		Stop
	}
}

actor WeaponPowerupGiver : PowerupGiver
{
	Powerup.Color "80 00 ff",0.1
	Powerup.Duration -60
	Powerup.Type WeaponLevel2
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 0
}

actor StrongholdSoulsphere : Soulsphere replaces Soulsphere
{
	-INVENTORY.AUTOACTIVATE
	-INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		SOUL ABCDCB 6 BRIGHT LIGHT("SoulSphere")
		Loop
	}
}

actor StrongholdMegasphere : Megasphere replaces Megasphere
{
	-INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		MEGA ABCD 6 BRIGHT LIGHT("MegaSphere")
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfHealthLower(200,"Win")
		Fail
	Win:
		Goto Super::Pickup
	}
}

actor InvulnPowerup: CustomInventory replaces InvulnerabilitySphere
{
	Inventory.PickupMessage "$IP_INVUP"
	+COUNTITEM
	+INVENTORY.BIGPOWERUP
	Inventory.PickupSound "misc/p_pkup"
	States
	{
	Spawn:
		PINV ABCD 6 BRIGHT LIGHT("INVULN")
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PowerInvulnerable", 1,2)
		TNT1 A 0 A_GiveInventory("InvulnerabilitySphere")
		Stop
		TNT1 A 0
		Fail
	}
}

actor InvisPowerup : CustomInventory replaces BlurSphere
{
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$IP_PARINS"
	+COUNTITEM
	+INVENTORY.BIGPOWERUP
	States
	{
	Spawn:
		PINS A 6 BRIGHT LIGHT("BLURSPHERE1") LIGHT("BLURSPHERE2")
		PINS B 6 BRIGHT LIGHT("BLURSPHERE1") LIGHT("BLURSPHERE3")
		PINS C 6 BRIGHT LIGHT("BLURSPHERE1") LIGHT("BLURSPHERE4")
		PINS D 6 BRIGHT LIGHT("BLURSPHERE1") LIGHT("BLURSPHERE5")
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PowerInvisibility", 1, 2)
		TNT1 A 0 A_GiveInventory("BlurSphere")
		Stop
		TNT1 A 0
		Fail
	}
}

actor LightAmp: CustomInventory replaces Infrared
{
    +COUNTITEM
	Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupMessage "$IP_VISOR"
    States
    {
	Spawn:
		PVIS A 6
		PVIS B 6 BRIGHT
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PowerLightAmp", 1,2)
		TNT1 A 0 A_GiveInventory("Infrared")
		Stop
		TNT1 A 0
		Fail
    }
}

actor MegaMap : CustomInventory replaces AllMap
{
	+COUNTITEM
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$IP_MAPSC"
	States
	{
	Spawn:
		PMAP ABCDCB 6 BRIGHT
		Loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PowerScanner",1,3)
		TNT1 A 0 A_GiveInventory("Allmap")
		TNT1 A 0 A_GiveInventory("DoomScanner")
		Stop
		TNT1 A 0
		Fail
	}
}

actor DoomScanner : PowerupGiver
{
	Powerup.Duration 0x7FFFFFFF
	Powerup.Type "Scanner"
	+INVENTORY.AUTOACTIVATE
}

actor AmmoBackpack : CustomInventory replaces Backpack
{
	Inventory.PickupSound "ammo/backpack"
	Inventory.PickupMessage "$IP_BAMMO"
	+COUNTITEM
	States
	{
	Spawn:
		BPAK A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("BackpackTimer", 1,3)
		TNT1 A 0 A_GiveInventory("BackpackTimer")
		TNT1 A 0 A_GiveInventory("Backpack")
		Stop
	PickupFail:
		TNT1 A 0
		Fail
	}
}

actor BackpackTimer : Powerup
{
	Powerup.Duration -30
}

///////////////////////////////////
// ALTERED RED KEY - DUKE SECRET //
// and other secret keys //////////
///////////////////////////////////

actor RedCardDN : RedCard replaces RedCard
{
	//$Category Stronghold
	-INVENTORY.INTERHUBSTRIP
}

actor AccessDiscBlue : Key
{
	//$Category Stronghold
	Inventory.PickupMessage "$IP_BDISC"
	Inventory.Icon "TNT1A0"
	-INVENTORY.INTERHUBSTRIP
	States
	{
	Spawn:
		DDSC A -1
		Loop
	}
}

actor AccessDiscGreen : AccessDiscBlue
{
	//$Category Stronghold
	Inventory.PickupMessage "$IP_GDISC"
	States
	{
	Spawn:
		DDSC B -1
		Loop
	}
}

//Don't know what this item is
ACTOR WhyZandronumWhy //TELL US THE REASON WHY
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 ACS_Execute(1,0)
		Stop
	}
}