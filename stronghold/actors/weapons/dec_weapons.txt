////////////////////////////////////////////////////////
/////////////////////////// FIST ///////////////////////
////////////////////////////////////////////////////////
actor StrongholdFist : Fist replaces Fist
{
	states
	{
	Deselect:
		PUNG A 0 A_Lower
		PUNG A 1 A_Lower
		loop
	Select:
		PUNG A 0 A_Raise
		PUNG A 1 A_Raise
		loop
	Fire:
		PKFS LBCD 1
		PKFS E 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		PKFS E 2 A_Punch
		PKFS E 0 A_Jump(256,2)
		PKFS E 2 A_CustomPunch(20,0,0,"ElectricalPuff")
		PKFS FGHI 2
		PKFS JKL 1
		PUNG A 5 A_ReFire
		Goto Ready
	}
}

actor ElectricalPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.5
	VSpeed 0.25
	Scale 0.5
	Mass 5
	SeeSound "weapons/plasmax"
	AttackSound "weapons/plasmax"
	States
	{
	Spawn:
		PLSE ABCDE 4 bright
		stop
	}
}

////////////////////////////////////////////////////////
////////////////// CHAINSAW ////////////////////////////
////////////////////////////////////////////////////////
actor StrongholdChainsaw : Chainsaw replaces Chainsaw
{
	Decal BulletChip
	+GHOST
	Weapon.DefaultKickback
	states
	{
	Deselect:
		SAWG C 0 A_Lower
		SAWG C 1 A_Lower
		loop
	Select:
		SAWG C 0 A_Raise
		SAWG C 1 A_Raise
		loop
	Fire:
		SAWG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SAWG AABB 2 A_Saw
		SAWG B 0 A_ReFire
		goto Ready
	SuperFire:
		SAWG AABBAABBAABB 1 A_Saw
		SAWG CCDDCCDDCCDD 1 A_Saw
		SAWG B 0 A_ReFire
		goto Ready
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PISTOL ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdPistol : Pistol replaces Pistol
{
	attacksound "weapons/pistol"
	decal bulletchip
	states
	{
	Ready:
		PISG A 1 A_JumpIfInventory("PCount",1,1)
		Goto Reload
		PISG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		PISG A 0 A_Lower
		PISG A 1 A_Lower
		loop
	Select:
		PISG A 0 A_Raise
		PISG A 1 A_Raise
		loop
	Fire:
		PISG A 4 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PISG B 0 A_FireBullets (5.6, 0, 1, 5, "BulletPuff")
		PISF A 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		goto Ready
	SuperFire:
		PISG A 2
		PISG B 0 A_FireBullets (5.6, 0, 1, 5, "BulletPuff")
		PISF A 3 A_GunFlash("SuperFlash")
		PISG C 2
		PISG B 5 A_ReFire
		Goto Ready
	Reload:
		PISG D 5 A_PlaySound("weapons/clipou")
		PISG EFGHIJK 4
		PISG L 4 A_Playsound("weapons/clipin")
		PISG MNO 4
		PISG P 4 A_Playsound("weapons/clipre")
		PISG QR 4
		PISG S 4 A_GiveInventory("PCount")
		Goto Ready
	SuperFlash:
		PISF A 3 Bright A_Light1
		Goto LightDone
	}
}

ACTOR PCount : Ammo{}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SHOTGUN //////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdShotgun : Shotgun replaces Shotgun
{
	obituary "%o got pummeled by %k's shotgun."
	attacksound "weapons/shotgf"
	decal bulletchip
	states
	{
	Deselect:
		SHTG A 0 A_Lower
		SHTG A 1 A_Lower
		loop
	Select:
		SHTG A 0 A_Raise
		SHTG A 1 A_Raise
		loop
	Fire:
		SHTG A 3
		SHTG E 0 A_GunFlash
		SHTG E 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SHTG E 2 A_FireBullets (4, 3, 7, 5, "BulletPuff")
		SHTG F 3
		SHTG G 2
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		goto Ready
	SuperFire:
		SHTG E 2 A_FireBullets (1.5, 0.5, 10, 5, "BulletPuff")
		SHTG F 3
		SHTG G 2
		SHTG BCDCB 2
		SHTG A 1 A_ReFire
		goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SUPER SHOTGUN ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdSuperShotgun : SuperShotgun replaces SuperShotgun
{
	attacksound "weapons/sshotf"
	decal bulletchip
	states
	{
	Deselect:
		SHT2 A 0 A_Lower
		SHT2 A 1 A_Lower
		loop
	Select:
		SHT2 A 0 A_Raise
		SHT2 A 1 A_Raise
		loop
	Fire:
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		SHT2 M 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		SHT2 M 2 Bright A_FireBullets (11.2,7.1,20,5,"BulletPuff")
		SHT2 N 3 Bright
		SHT2 O 2 Bright
		SHT2 B 3
		SHT2 C 4
		SHT2 D 4 A_CheckReload
		SHT2 C 3
		SHT2 E 3 A_PlaySound ("weapons/sshoto",CHAN_WEAPON)
		SHT2 F 3
		SHT2 GH 4
		SHT2 I 3 A_PlaySound ("weapons/sshotl",CHAN_WEAPON)
		SHT2 JK 3
		SHT2 L 4
		SHT2 E 0 A_PlaySound ("weapons/sshotc",CHAN_WEAPON)
		SHT2 E 6 A_ReFire
		SHT2 A 5 A_ReFire
		goto Ready
	SuperFire:
		SHT2 M 0 A_FireCustomMissile ("SSGRipper",0,1,0,0)
		SHT2 M 0 A_PlaySound ("weapons/sshotf",CHAN_WEAPON)
		SHT2 MMMMMMMMMMMMMMMMMMM 0 A_FireCustomMissile ("SSGRipper",random(-11.2,11.2),0,0,random(-7.1,7.1))
		SHT2 M 2 Bright
		Goto Fire+4
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 3 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR SSGRipper
{
	Radius 13
	Height 8
	Speed 500
	Damage 5
	Projectile
	+RIPPER
	+NOEXTREMEDEATH
	+INVISIBLE
	Obituary "%o was splattered by %k's super shotgun."
	States
	{
	Death:
		TNT1 A 1 A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,160,128)
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// AUTO SHOTGUN /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor AutoShotgun : Weapon 5118
{
	//$Category Stronghold_Weapons
	+WEAPON.AMMO_CHECKBOTH
	obituary "%o got blasted away by %k's autoshotgun."
	Inventory.PickupMessage "You got the automatic shotgun!"
	Tag "AutoShotgun"
	weapon.selectionorder 700
	Weapon.AmmoType1 "Shell"
	Weapon.AmmoType2 "ASGClip"
	//Weapon.AmmoGive1 20
	Weapon.AmmoGive2 20
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	AttackSound "weapons/asgfir"
	decal bulletchip
	states
	{
	Spawn:
		WASG A -1
		stop
	Ready:
		ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
	Deselect:
		ASGG A 0 A_Lower
		ASGG A 1 A_Lower
		loop
	Select:
		ASGG A 0 A_Raise
		ASGG A 1 A_Raise
		loop
	Fire:
		ASGG A 0 A_JumpIfInventory("ASGClip",1,1)
		Goto Reload
		ASGG A 1
		ASGG L 0 A_GunFlash
		ASGG A 0 A_TakeInventory("ASGClip",1)
		ASGG L 2 A_FireBullets(5,4,7,5,"BulletPuff",0)
		ASGG M 3
		ASGG N 2
		ASGG A 4 A_PlaySound("weapons/asgld1",CHAN_AUTO)
		ASGG A 4 A_JumpIfInventory("PowerWeaponLevel2",1,1)
		ASGG A 0 A_ReFire
		goto Ready
	Reload:
		ASGG A 0 A_JumpIfInventory("Shell",1,2)
		ASGG A 2 A_PlaySound("Weapons/asgout",CHAN_WEAPON)
		Goto Ready
		ASGG BCDEF 3
		ASGG G 3 A_PlaySound("Weapons/asgout",CHAN_WEAPON)
		ASGG HIJ 3
		ASGG K 15
		ASGG JIH 3
	ReloadLoop:
		ASGG G 0 A_TakeInventory("Shell",1)
		ASGG G 0 A_GiveInventory("ASGClip",1)
		ASGG G 0 A_JumpIfInventory("ASGClip",20,2)
		ASGG G 0 A_JumpIfInventory("Shell",1,"ReloadLoop")
		ASGG G 3 A_PlaySound("Weapons/asgin",CHAN_WEAPON)
		ASGG FEDCB 3
		ASGG A 8 A_PlaySound("weapons/asgld1",CHAN_AUTO)
		goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	}
}

ACTOR ASGClip: Ammo
{
	Inventory.MaxAmount 20
	+Inventory.IgnoreSkill
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// CHAINGUN /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdChaingun : Chaingun replaces Chaingun
{
	decal bulletchip
	states
	{
	Deselect:
		CHGG A 0 A_StopSound(5)
		CHGG A 0 A_Lower
		CHGG A 1 A_Lower
		goto Deselect+1
	Select:
		CHGG A 0 A_Raise
		CHGG A 1 A_Raise
		loop
	Fire:
		CHGG C 0 A_PlaySound("weapons/chnglp",5,1,1)
		CHGG C 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
	Hold:
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(4,2.3,1,5,"BulletPuff")
		CHGG BCD 1
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(4,2.3,1,5,"BulletPuff")
		CHGG BCD 1
		CHGG A 0 A_Refire
	WindDown:
		CHGG A 0 A_StopSound(5)
		CHGG A 0 A_PlaySound("weapons/chngdn",6)
		CHGG ABBCCCDDD 1 A_Weaponready
		Goto Ready
	SuperFire:
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(1,1.1,1,5,"BulletPuff")
		CHGG BCD 1
		CHGG A 0 A_GunFlash
		CHGG A 1 A_FireBullets(3,2.1,1,5,"BulletPuff")
		CHGG BCD 1
		CHGG A 0 A_Refire("SuperHold")
		Goto WindDown
	SuperHold:
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 1 A_FireBullets(6,3.1,1,5,"BulletPuff")
		CHGG C 1
		CHGG A 0 A_GunFlash("SuperFlash")
		CHGG A 1 A_FireBullets(6,3.1,1,5,"BulletPuff")
		CHGG C 1
		CHGG A 0 A_Refire("SuperHold")
	WindDown:
		CHGG A 0 A_StopSound(5)
		CHGG A 0 A_PlaySound("weapons/chngdn",6)
		CHGG ABBCCCDDD 1
		Goto Ready
	Flash:
		TNT1 A 0 A_Jump(128,"Flash2")
		CHGF A 1 BRIGHT A_Light2
		CHGF B 1 BRIGHT A_Light1
		Goto LightDone
	Flash2:
		CHGF C 1 BRIGHT A_Light1
		Goto LightDone
	SuperFlash:
		CHGF A 1 BRIGHT A_Light2
		CHGF D 1 BRIGHT A_Light1
		Goto LightDone
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// MINE LAYER ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR LandMineLayer : Weapon 25084
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got some land mines!"
	Weapon.AmmoType "MineAmmo"
	Weapon.AmmoGive 5
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "MineAmmo"
	Weapon.AmmoUse2 1
	Tag "Land Mines"
	States
	{
	Spawn:
		MINE A -1
		Loop
	Ready:
		LAYR A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("MineAmmo",1,3)
		TNT1 A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
		LAYR A 0 A_Lower
		LAYR A 1 A_Lower
		Loop
	Select:
		LAYR A 0 A_Raise
		LAYR A 1 A_Raise
		Loop
	Fire:
		LAYR B 8 A_PlaySound("weapons/minebeep")
		LAYD ABCDE 1
		TNT1 A 5 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		TNT1 A 9 A_FireCustomMissile("Landmine3")
		TNT1 A 0 A_CheckReload
		LAYU ABCDE 1
		LAYR A 4
		Goto Ready
	AltFire:
		LAYR B 8 A_PlaySound("weapons/minebeep")
		LAYD ABCDE 1
		TNT1 A 10
		LAYT A 1 A_PlaySound("weapons/minethrow")
		LAYT BCDEFGH 1
		LAYR B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperAltFire")
		LAYT I 1 A_FireCustomMissile("Landmine2",0,1,0,8)
	AltFireFinish:
		TNT1 A 0 A_CheckReload
		LAYT JKLMNOPQR 1
		TNT1 A 10
		LAYU ABCDE 1
		LAYR A 6
		Goto Ready
	SuperFire:
		TNT1 A 10
		TNT1 A 19 A_FireCustomMissile("PowerLandmine3")
		TNT1 A 0 A_CheckReload
		LAYU ABCDE 1
		LAYR A 6
		Goto Ready
	SuperAltFire:
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",0,1,0,8)
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",-7,0,0,8)
		LAYT I 1 A_FireCustomMissile("PowerLandmine2",7,0,0,8)
		Goto AltFireFinish
	}
}

ACTOR Landmine
{
	Radius 8 //was 5
	Height 5
	Mass 1000000
	Health 20
	Damage 10
	BounceSound "weapons/tink"
	+NOBLOOD
	+MISSILE
	+THRUGHOST
	States
	{
	Spawn:
		MINE AB 5
		Loop
	Death:
		MISL B 0 A_PlaySound("weapons/mineexpl")
		MISL B 8 A_Explode(384,160,0)
		MISL C 6
		MISL D 4
		Stop
	}
}

ACTOR Landmine2 : Landmine
{
	Speed 15
	BounceType Doom
	BounceSound "weapons/tink"
	+THRUGHOST
	States
	{
	Death:
		MINE A 0 A_SpawnItemEx("Landmine")
		Stop
	}
}

ACTOR Landmine3 : Landmine2
{
	Speed 2
}

ACTOR PowerLandmineA : Landmine
{
	Damage 20
	States
	{
	Death:
		MISL B 0 A_PlaySound("weapons/mineexpl")
		MISL B 8 A_Explode(768,240,0)
		MISL C 6
		MISL D 4
		Stop
	}
}

ACTOR PowerLandmineB : PowerLandmineA
{
	States
	{
	Death:
		MISL B 0 A_PlaySound("weapons/mineexpl")
		MISL B 8 A_Explode(256,160,0)
		MISL C 6
		MISL D 4
		Stop
	}
}

ACTOR PowerLandmine2 : PowerLandmineA
{
	Speed 15
	BounceType Doom
	BounceSound "weapons/tink"
	+THRUGHOST
	States
	{
	Death:
		MINE A 0 A_SpawnItem("PowerLandmineB")
		Stop
	}
}

ACTOR PowerLandmine3 : PowerLandmine2
{
	Speed 2
	States
	{
	Death:
		MINE A 0 A_SpawnItem("PowerLandmineA")
		Stop
	}
}

ACTOR MineAmmo : Ammo
{
	Inventory.Amount 2
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 40
	Inventory.Icon MINEA0
}

ACTOR Mines : WeaponGiver 25085
{
	//$Category Stronghold_Weapons
	DropItem LandMineLayer
	Inventory.Amount 2
	Inventory.PickupMessage "Picked up some landmines."
	States
	{
	Spawn:
		MINE B -1
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// ROCKET LAUNCHER //////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StrongholdRLauncher : RocketLauncher replaces RocketLauncher
{
	States
	{
	Deselect:
		MISG A 0 A_Lower
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 0 A_Raise
		MISG A 1 A_Raise
		Loop
	Fire:
		MISF A 4 Bright A_GunFlash
		MISF B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		MISF B 5 Bright A_FireCustomMissile("Rocket2")
		MISF CD 5 Bright
		MISG A 0 A_Refire
		Goto Ready
	SuperFire:
		MISF B 5 Bright A_FireCustomMissile("PowerRocket2",0,1,-1,-1)
		MISF B 5 Bright A_FireCustomMissile("PowerRocket2",0,0,1,-1)
		MISF B 5 Bright A_FireCustomMissile("PowerRocket2",0,0,-1,1)
		MISF B 2 Bright A_FireCustomMissile("PowerRocket2",0,0,1,1)
		MISF CD 5 Bright
		MISG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 5 A_Light2
		TNT1 A 5 A_Light1
		Goto LightDone
	}
}

actor Rocket2 : Rocket replaces Rocket
{
	Decal Scorch
	states
	{
	Spawn:
		MISL A -1 BRIGHT LIGHT("ROCKET")
		Stop
	Death:
		MISL B 0 A_SetTranslucent (0.67,1)
		MISL B 8 bright LIGHT("ROCKET_X1") A_Explode
		MISL C 6 bright LIGHT("ROCKET_X2")
		MISL D 4 bright LIGHT("ROCKET_X3")
		stop
	}
}

actor PowerRocket2 : Rocket2
{
	speed 30
	damage 35
	scale 0.5
	translation "128:151=[192,192,192]:[64,64,64]","48:79=[255,255,255]:[0,0,0]","112:127=[255,255,255]:[0,0,0]"
	+THRUGHOST
	+SeekerMissile
	states
	{
	Spawn:
		MISL A 0 A_SeekerMissile(10,2)
		MISL A 0 A_BishopMissileWeave
		MISL A 1 bright A_BishopMissileWeave
		loop
	Death:
		MISL B 0 A_SetTranslucent (0.67,1)
		MISL B 8 bright A_Explode(96,128)
		MISL C 6 bright
		MISL D 4 bright
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// HOMING ROCKET LAUNCHER ///////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR HRL : Weapon 20103
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the homing rocket launcher!"
	Obituary "%o couldn't dodge %k's homing rockets."
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoGive 4
	Weapon.AmmoUse 2
	Tag "Homing Rocket Launcher"
	States
	{
	Spawn:
		WHRL A -1
		Loop
	Ready:
		HRLG A 1 A_WeaponReady
		Loop
	Deselect:
		HRLG A 0 A_Lower
		HRLG A 1 A_Lower
		Loop
	Select:
		HRLG A 0 A_Raise
		HRLG A 1 A_Raise
		Loop
	Fire:
		HRLG A 4
		HRLG C 5 Bright A_GunFlash
		HRLG D 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		HRLG D 0 A_FireCustomMissile("HomRocket1",-4,0)
		HRLG D 6 Bright A_FireCustomMissile("HomRocket1",4)
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_Refire
		Goto Ready
	SuperFire:
		HRLG D 0 A_JumpIfInventory("RocketAmmo",4,1)
		Goto Fire+4
		HRLG D 0 A_FireCustomMissile("PowerHomRocket1",4,1,0,4.5)
		HRLG D 0 A_FireCustomMissile("PowerHomRocket1",-4,0,0,4.5)
		HRLG D 0 A_FireCustomMissile("PowerHomRocket1",3.7,0,3)
		HRLG D 0 A_FireCustomMissile("PowerHomRocket1",-4.3,0,3)
		HRLG D 0 A_FireCustomMissile("PowerHomRocket1",4.3,1,-3)
		HRLG D 6 Bright A_FireCustomMissile("PowerHomRocket1",-3.7,1,-3)
		HRLG E 5 Bright
		HRLG F 4 Bright
		HRLG B 3 A_Refire
		Goto Ready
	Flash:
		TNT1 A 5 A_Light1
		TNT1 A 6 A_Light2
		TNT1 A 5 A_Light1
		Goto LightDone
	}
}

ACTOR HomRocket1 : Rocket2
{
	Radius 8
	Height 11
	Speed 30
	Damage 15
	+SeekerMissile
	-RANDOMIZE
	+THRUGHOST
	SeeSound "weapons/hrlfir"
	DeathSound "weapons/hrlexp"
	States
	{
	Spawn:
		HMIS A 8 Bright
		HMIS A 4 Bright A_SeekerMissile(10,25)
		Wait
	}
}

ACTOR PowerHomRocket1 : HomRocket1
{
	Radius 4
	Height 5.5
	Scale 0.5
	Speed 35
	Damage 8
	States
	{
	Spawn:
		MISL A 1 Bright A_SeekerMissile (30,25)
		Loop
	Death:
		MISL B 0 A_SetTranslucent (0.67,1)
		MISL B 8 Bright A_Explode(64,96)
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// RAILGUN    ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR RailGun : Weapon 20102
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the railgun!"
	Obituary "%o was railed by %k."
	DamageType Normal
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 40
	Weapon.AmmoUse 5
	Decal RailScorch
	Tag "Railgun"
	States
	{
	Spawn:
		WRAL A -1
		Loop
	Ready:
		RAIG A 1 A_WeaponReady
		Loop
	Deselect:
		RAIG A 0 A_Lower
		RAIG A 1 A_Lower
		Loop
	Select:
		RAIG A 0 A_Raise
		RAIG A 1 A_Raise
		Loop
	Fire:
		RAIG A 0 A_JumpIfInventory("RailCharge",2,"Fire3")
		RAIG A 0 A_JumpIfInventory("RailCharge",1,"Fire2")
		RAIG B 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG B 6 Bright A_GiveInventory("RailCharge")
		RAIG B 0 A_Refire
		RAIG E 3 Bright A_GunFlash
		RAIG E 1 A_PlaySound("weapons/railf1",CHAN_WEAPON)
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG F 6 Bright A_RailAttack(100,0,1,"FF 00 00","FF FF FF")
		RAIG F 0 A_Jump(256,2)
		RAIG F 6 Bright A_RailAttack(200,0,1,"FF 00 00","FF FF FF")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	Fire2:
		RAIG C 0 A_JumpIfInventory("Cell",10,1)
		Goto Fire+2
		RAIG C 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG C 6 Bright A_GiveInventory("RailCharge")
		RAIG C 0 A_Refire
		RAIG G 3 Bright A_GunFlash
		RAIG G 1 A_PlaySound("weapons/railf2",CHAN_WEAPON)
		RAIG H 0 A_RailAttack(0,0,1,"FF FF 00","FF FF FF")
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG H 6 Bright A_RailAttack(200,0,1,"FF FF 00","FF 45 00")
		RAIG H 0 A_Jump(256,2)
		RAIG H 6 Bright A_RailAttack(400,0,1,"FF FF 00","FF 45 00")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	Fire3:
		RAIG C 0 A_JumpIfInventory("Cell",15,1)
		Goto Fire2
		RAIG D 6 A_PlaySound("weapons/railch",CHAN_WEAPON)
		RAIG D 6 Bright
		RAIG D 0 A_Refire
		RAIG I 3 Bright A_GunFlash
		RAIG I 1 A_PlaySound("weapons/railf3",CHAN_WEAPON)
		RAIG J 0 A_RailAttack(0,0,1,"00 00 FF","00 45 FF")
		RAIG J 0 A_RailAttack(0,0,1,"00 00 FF","FF FF FF")
		RAIG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,3)
		RAIG J 4 Bright A_RailAttack(300,0,1,"00 00 FF","00 FF 00")
		RAIG J 0 A_Jump(256,2)
		RAIG J 4 Bright A_RailAttack(600,0,1,"00 00 FF","00 FF 00")
		RAIG A 4
		RAIG K 15 A_TakeInventory("RailCharge")
		RAIG K 0 A_CheckReload
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 4 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR RailCharge : Ammo
{
	Inventory.MaxAmount 3
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// STUNNER RIFLE ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StunnerRifle : PlasmaRifle 23099
{
	Tag "Stunner Rifle"
	//$Category Stronghold_Weapons
	Weapon.AmmoUse 5
	Inventory.PickupMessage "You got the stunner rifle!"
	States
	{
	Spawn:
		STPP A -1
		Loop
	Ready:
		STPO ABC 3 Bright A_WeaponReady
		Loop
	Deselect:
		STPO E 0 A_Lower
		STPO E 1 A_Lower
		Loop
	Select:
		STPO E 0 A_Raise
		STPO E 1 A_Raise
		Loop
	Fire:
		STPO F 0 A_GunFlash
		STPO F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		STPO F 3 Bright A_FireCustomMissile("StunnerBall")
		STPO GH 3 Bright
		STPO E 4
		STPO D 6 A_ReFire
		Goto Ready
	SuperFire:
		STPO F 3 Bright A_FireCustomMissile("PowerStunnerBall")
		STPO GH 3 Bright
		STPO D 16
		STPO D 6 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

actor StunnerBall : PlasmaBall
{
	height 8
	speed 20
	scale 0.45
	damagetype Stunner
	damage 1
	decal StunnerScorch
	Alpha 1
	seesound "weapons/stunner/shot"
	deathsound "weapons/stunner/hit"
	translation "192:207=250:254"
	+RIPPER
	states
	{
	Spawn:
		STPR A 1 bright A_SpawnItemEx("StunnerBallFX")
		loop
	}
}

actor StunnerBallFX
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	scale 0.45
	renderstyle Add
	radius 1
	height 2
	States
	{
	Spawn:
		STPR A 1 bright A_FadeOut
		Loop
	}
}

actor PowerStunnerBall : StunnerBall
{
	radius 6
	Decal ""
	states
	{
	Spawn:
		STPR A 0 A_BishopMissileWeave
		STPR A 0 A_Tracer2
		STPR A 0 A_SpawnItemEx("StunnerBallFX")
		STPR A 1 Bright
		loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PLASMA RIFLE /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StrongholdPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
	States
	{
	Ready:
		PLSG ABC 3 Bright A_WeaponReady
		Loop
	Deselect:
		PLSG E 0 A_Lower
		PLSG E 1 A_Lower
		Loop
	Select:
		PLSG E 0 A_Raise
		PLSG E 1 A_Raise
		Loop
	Fire:
		PLSG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PLSG F 0 A_GunFlash
		PLSG F 1 Bright A_FireCustomMissile("PlasmaBall")
		PLSG GH 1 Bright
		PLSG D 20 A_Refire
		Goto Ready
	SuperFire:
		PLSG F 1 A_JumpIfInventory("Cell",2,1)
		Goto Fire+3
		PLSG F 1 Bright A_GunFlash
		PLSG F 1 Bright A_FireCustomMissile("PowerPlasmaBall")
		PLSG GH 1 Bright
		PLSG D 20 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

actor PowerPlasmaBall : PlasmaBall
{
	Radius 22.75
	Height 14
	Damage 9
	Scale 0.8
	BounceType Doom
	Bouncecount 5
	States
	{
	Spawn:
		PLSE A 6 bright
		loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////  BFG9000   ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor StrongholdBFG9000 : BFG9000 Replaces BFG9000
{
	Weapon.SisterWeapon "PowerBFG9000"
	States
	{
	Ready:
		BFGG ABCD 3 Bright A_WeaponReady
		loop
	Deselect:
		BFGG E 0 A_Lower
		BFGG E 1 A_Lower
		loop
	Select:
		BFGG E 0 A_Raise
		BFGG E 1 A_Raise
		loop
	Fire:
		BFGG F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		BFGG F 5 Bright A_PlaySound ("weapons/bfgf",CHAN_WEAPON)
		BFGG GHI 5 Bright
		BFGG J 4 Bright
		BFGG K 7 Bright A_GunFlash
		BFGG L 8 Bright A_FireCustomMissile("BFGBall")
		BFGG M 8 Bright
		BFGG N 7 Bright
		BFGG E 20 Bright A_ReFire
		goto Ready
	Flash:
		TNT1 A 7 A_Light1
		TNT1 A 8 A_Light2
		TNT1 A 8 A_Light1
		Goto LightDone
	SuperFire:
		BFGG FHJ 1 Bright
		BFGG K 0 A_GunFlash("SuperFlash")
		BFGG L 1 Bright A_FireCustomMissile("PowerBFGBall")
		BFGG N 1 Bright
		BFGG E 7 Bright A_ReFire("SuperFire")
		goto Ready
	SuperFlash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	}
}

// Unlike it's unpowered counterpart, it really really sucks to use this at close
// range now. That's the price you pay for being tough. ;)
actor PowerBFG9000 : StrongholdBFG9000
{
	weapon.ammouse 5
	Weapon.SisterWeapon "StrongholdBFG9000"
	+POWERED_UP
}

// This is a weaker version of the BFG that gets rapidfired. Instead of tracers, it simply explodes.
//Note that it foils boss immunity to explosions, and it WILL hurt the shooter.
actor PowerBFGBall : BFGBall
{
	SeeSound "weapons/bfg_fire"
	DeathSound "weapons/bfg_hum"
	Speed 35
	Damage 20
	Scale 0.6
	+EXTREMEDEATH
	+FORCERADIUSDMG
	Obituary "$OB_MPBFG_BOOM" // "%o was splintered by %k's BFG."
	//SuicideObituary "%k couldn't handle the overwhelming power."
	States
	{
	Death:
		BFE1 A 3 bright A_Explode(128,196)
		BFE1 BCDEF 3 bright
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PYRO CANNON //////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR PyroCannon : Weapon 20107
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the pyro cannon prototype!"
	Obituary "%o was incinerated by %k's pyro cannon."
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 48
	Weapon.AmmoUse 48
	Tag "Pyro Cannon"
	+WEAPON.EXPLOSIVE
	+WEAPON.BFG
	+WEAPON.DONTBOB
	+WEAPON.STAFF2_KICKBACK
	States
	{
	Spawn:
		WPYR A -1
		Loop
	Ready:
		PYRG A 1 A_WeaponReady
		Loop
	Deselect:
		PYRG A 0 A_Lower
		PYRG A 1 A_Lower
		Loop
	Select:
		PYRG A 0 A_Raise
		PYRG A 1 A_Raise
		Loop
	Fire:
		PYRG A 5
		PYRG B 8 Bright A_GunFlash
		PYRG A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		PYRG C 10 Bright A_FireCustomMissile("PyroShot")
		PYRG DEFG 5 Bright
		PYRG A 5 A_Refire
		Goto Ready
	SuperFire:
		PYRG C 0 A_TakeInventory("Gas",48)
		PYRG CCC 3 Bright A_FireCustomMissile("PyroShot",0,0)
		PYRG DEFG 5 Bright
		PYRG A 5 A_Refire
		Goto Ready
	Flash:
		TNT1 A 8 A_Light1
		TNT1 A 10 A_Light2
		TNT1 A 15 A_Light1
		Goto LightDone
	}
}

ACTOR Gas : Ammo 22106
{
	//$Category Stronghold_Weapons
	Inventory.Amount 24
	Inventory.MaxAmount 240
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "Picked up a gas canister."
	Inventory.Icon AGASA0
	States
	{
	Spawn:
		AGAS A -1
		Loop
	}
}

ACTOR Fuel : Gas 22107
{
	//$Category Stronghold_Weapons
	Inventory.Amount 96
	Inventory.PickupMessage "Picked up a gas pack."
	States
	{
	Spawn:
		AGAS B -1
		Loop
	}
}

ACTOR PyroShot
{
	Radius 8
	Height 12
	Speed 30
	Damage 180
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	Scale 0.75
	+THRUGHOST
	SeeSound "weapons/hellfi"
	DeathSound "weapons/hellex"
	Decal BigScorch
	States
	{
	Spawn:
		PBAL ABA 4 Bright
		PBAL BA 4 Bright A_Explode
		goto Spawn+3
	Death:
		PYXP AB 2 Bright
		PYXP C 2 Bright A_Explode(192,128)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,0,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,45,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,90,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,135,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,180,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,225,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,270,4)
		PYXP C 0 A_CustomMissile("PyroSpawner",0,0,315,4)
		PYXP C 0 A_Mushroom("PyroFlare",16)
		PYXP DEFGHIJKLMNOPQRSTU 2 Bright
		stop
	}
}

ACTOR PyroSpawner
{
   Radius 1
   Height 1
   Damage 10
   PROJECTILE
   Speed 15
   +RIPPER
   +BLOODLESSIMPACT
   States
   {
   Spawn:
      TNT1 AAAAA 5 A_CustomMissile("PyroBoom",0,0,0,4)
      stop
   }
}

ACTOR PyroBoom
{
   Radius 0
   Height 1
   PROJECTILE
   RENDERSTYLE ADD
   DamageType Fire
   ALPHA 0.95
   Scale 0.67
   +THRUGHOST
   SeeSound "weapons/boom1"
   States
   {
   Spawn:
      PYXP AB 2 Bright
      PYXP C 2 Bright A_Explode(192,128)
      PYXP DEFGHIJKLMNOPQRSTU 2 Bright
      stop
   }
}

ACTOR PyroFlare
{
	Radius 8
	Height 11
	Speed 35
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	Decal DoomImpScorch
	ALPHA 0.95
	-NOGRAVITY
	+THRUGHOST
	DeathSound "weapons/bang1"
	States
	{
	Spawn:
		TNT1 A 1 A_SpawnItemEx("PyroFX")
		loop
	Death:
		FRFX HIJ 2 Bright
		FRFX J 0 A_Explode(64)
		FRFX J 0 A_SpawnItemEx("DropFire")
		FRFX KLMNO 2 Bright
		stop
	}
}

ACTOR PyroFX
{
	+CLIENTSIDEONLY
	Radius 0
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.85
	States
	{
	Spawn:
		TNT1 A 3
		PYFX ABCDE 3 Bright
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// FLAMER ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Flamer : Weapon 20105
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the flamethrower! Time for a BBQ!"
	Obituary "%o was BBQed by %k's flamethrower."
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 48
	Weapon.AmmoUse 1
	Tag "Flamethrower"
	+WEAPON.EXPLOSIVE
	States
	{
	Spawn:
		WFLM A -1
		Loop
	Ready:
		FLMG BCD 3 A_WeaponReady
		Loop
	Deselect:
		FLMG A 0 A_Lower
		FLMG A 1 A_Lower
		Loop
	Select:
		FLMG A 0 A_Raise
		FLMG A 1 A_Raise
		Loop
	Fire:
		FLMG E 0 A_GunFlash
		FLMG E 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG H 2 Bright A_Refire
		Goto Ready
	SuperFire:
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 0 A_GunFlash
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 0 A_GunFlash
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
		FLMG A 1 Bright A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
		FLMG A 2 Bright A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR FTFire
{
	Radius 2
	Height 4
	Speed 16
	Damage 1
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.67
	Scale 0.67
	SeeSound "weapons/flamer"
	DeathSound "weapons/scorch"
	+THRUGHOST
	+DONTSPLASH
	Gravity .125
	States
	{
	Spawn:
		TNT1 A 2
		FRFX ABCD 2 Bright A_Explode(5,8,0)
		FRFX D 0 A_Gravity
		FRFX EFG 2 Bright A_Explode(5,16,0)
		FRFX HIJ 2 Bright A_Explode(5,32,0)
		FRFX KLM 2 Bright A_Explode(5,64,0)
		FRFX NO 2 Bright
		stop
	Death:
		FRFX HIJ 2 Bright A_Explode(5,32,0)
		FRFX J 0 A_CustomMissile("DropFire",0,0,0,4)
		FRFX KLM 2 Bright A_Explode(5,64,0)
		FRFX NO 2 Bright
		stop
	}
}

ACTOR DropFire
{
	Radius 8
	Height 40
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.90
	-NOGRAVITY
	Gravity .125
	+NOEXPLODEFLOOR
	+NODAMAGETHRUST
	+THRUGHOST
	+DONTSPLASH
	States
	{
	Spawn:
		FLME A 1 Bright A_Playsound("weapons/onfire")
		FLME A 1 Bright A_Explode(5,16,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("weapons/onfire")
		FLME A 1 Bright A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("weapons/onfire")
		FLME A 1 Bright A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("weapons/onfire")
		FLME A 1 Bright A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 1 Bright A_Playsound("weapons/onfire")
		FLME A 1 Bright A_Explode(5,32,0)
		FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
		FLME A 0 A_Jump(192,1)
		loop
		TNT1 A 0
		stop
	}
}

ACTOR PowerFTFire : FTFire
{
	Damage 2
	Translation Ice
	DamageType Ice
	States
	{
	Spawn:
		TNT1 A 2
		STEM ABCD 2 Bright A_Explode(7,8,0)
		STEM D 0 A_Gravity
		STEM EFG 2 Bright A_Explode(8,16,0)
		STEM HIJ 2 Bright A_Explode(7,32,0)
		STEM KJI 2 Bright A_Explode(8,64,0)
		STEM HGFEDCBA 1 Bright
		stop
	Death:
		STEM J 1 Bright A_Explode(7,32,0)
		STEM I 1 Bright A_Explode(8,64,0)
		STEM HGECA 1 Bright
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// DEVASTATOR ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Devastator : Weapon 20101
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the devastator! Hell yeah!"
	Obituary "%o was desintergrated by %k's Devastator."
	Weapon.AmmoType "Cell"
	Weapon.AmmoGive 60
	Weapon.AmmoUse 60
	Tag "Devastator"
	+WEAPON.EXPLOSIVE
	+WEAPON.BFG
	+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		WDEV A -1
		Loop
	Ready:
		DEVG A 2 A_WeaponReady
		Loop
	Deselect:
		DEVG A 0 A_Lower
		DEVG A 1 A_Lower
		Loop
	Select:
		DEVG A 0 A_Raise
		DEVG A 1 A_Raise
		Loop
	Fire:
		DEVG B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperFire")
		DEVG B 10 A_PlaySound("weapons/devchr",CHAN_WEAPON)
		DEVG CDEF 10 Bright
		DEVG A 0 A_Refire
	Winddown:
		DEVG J 4 Bright A_GunFlash
		DEVG J 1 Bright A_Playsound("weapons/devfir")
		DEVG K 10 Bright A_FireCustomMissile ("DevastatorBall")
		DEVG L 7 Bright
		Goto Ready
	Hold:
		DEVG G 0 A_PlaySound("weapons/devfch",CHAN_WEAPON)
		DEVG GHIGHI 4 Bright A_FireBullets(4,4,2,6,"DevPuff",0,64)
		DEVG I 0 A_Refire
		Goto Winddown
	SuperFire:
		DEVG B 5 A_PlaySound("weapons/devfch",CHAN_WEAPON)
		DEVG CDEF 5 Bright
		DEVG A 0 A_Refire("SuperHold")
	SuperWinddown:
		DEVG J 2 Bright A_GunFlash("SuperFlash")
		DEVG J 1 Bright A_Playsound("weapons/devfir")
		DEVG K 5 Bright A_FireCustomMissile ("DevastatorBall")
		DEVG L 3 Bright
		Goto Ready
	SuperHold:
		DEVG G 0 A_PlaySound("weapons/devfch",CHAN_WEAPON)
		DEVG GHIGHI 2 Bright A_FireBullets(4,4,2,6,"DevPuff",0,64)
		DEVG I 0 A_Refire("SuperHold")
		Goto SuperWinddown
	Flash:
		TNT1 A 5 A_Light1
		TNT1 A 10 A_Light2
		TNT1 A 7 A_Light1
		Goto LightDone
	SuperFlash:
		TNT1 A 2 A_Light1
		TNT1 A 5 A_Light2
		TNT1 A 4 A_Light1
		Goto LightDone
	}
}

Actor DevPuff
{
	Radius 3
	Height 3
	RENDERSTYLE ADD
	ALPHA 0.75
	+ALWAYSPUFF
	+PUFFONACTORS
	+NOGRAVITY
	DeathSound "weapons/buzz"
	States
	{
	Spawn:
		DLIT JKLJKLJKL 3 Bright
		stop
	Crash:
		TNT1 A 1
		stop
	}
}

ACTOR DevastatorBall
{
	Radius 12
	Height 8
	Speed 30
	Damage 256
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.90
	+THRUGHOST
	DeathSound "weapons/devexp"
	Decal DevastatorLightning
	var int user_theta;
	States
	{
	Spawn:
		DBAL AB 4 Bright
		DBAL A 4 Bright A_Explode
		DBAL A 0 A_Jump (160,3)
		DBAL AAA 0 A_CustomMissile("LiteShot",0,0,Random(0,360))
		DBAL B 4 Bright A_Explode
		DBAL B 0 A_Jump (160,3)
		DBAL BBB 0 A_CustomMissile("LiteShot",0,0,Random(0,360))
		Goto Spawn+2
	Death:
		DBAL C 5 Bright A_SetUserVar("user_theta",18)
		DBAL D 5 Bright A_Explode(320,256)
		DBAL D 0 A_CustomMissile("DevastatorBall2",0,0,user_theta,2)
		DBAL D 0 A_SetUserVar("user_theta",user_theta+18)
		DBAL D 0 A_JumpIf(user_theta>=360,1)
		Goto Death+2
		DBAL EFG 5 Bright A_Explode(320,256)
		DBAL HI 3 Bright
		stop
	}
}

ACTOR DevastatorBall2
{
	Radius 8
	Height 8
	Speed 10
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.90
	Damagetype lightning
	Decal DoomImpScorch
	+THRUGHOST
	+FORCEXYBILLBOARD
	SeeSound "weapons/devbal"
	DeathSound "weapons/devex2"
	States
	{
	Spawn:
		DBA2 AB 4 Bright
		Loop
	Death:
		DBA2 C 1 Bright
		DBA2 C 3 Bright A_Explode(64,64,0)
		DBA2 DE 4 Bright
		Stop
	}
}

ACTOR LiteShot
{
	Radius 8
	Height 12
	Speed 32
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.85
	Damagetype lightning
	+THRUGHOST
	+RIPPER
	+FORCEXYBILLBOARD
	SeeSound "weapons/devlit"
	DeathSound "weapons/devzap"
	States
	{
	Spawn:
		DLIT AAABBBCCC 1 Bright A_SpawnItemEx("LiteTrail")
		Loop
	Death:
		DLIT DEFGHIJKLMNO 1 Bright
		Stop
	}
}

ACTOR LiteTrail
{
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.67
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		DLIT ABC 3 Bright
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// REPEATER /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Repeater : Weapon 20100
{
	//$Category Stronghold_Weapons
	Inventory.PickupMessage "You got the plasma repeater!"
	Obituary "%o was disintegrated by %k's repeater."
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	+WEAPON.EXPLOSIVE
	AttackSound "weapons/repfir"
	Decal FireDemonScorch
	Tag "Plasma Repeater"
	States
	{
	Spawn:
		WREP A -1
		Loop
	Ready:
		REPG A 1 A_WeaponReady
		Loop
	Deselect:
		REPG A 0 A_Lower
		REPG A 1 A_Lower
		Loop
	Select:
		REPG A 0 A_Raise
		REPG A 1 A_Raise
		Loop
	Fire:
		REPG A 4 A_PlaySound("weapons/repidl")
		REPG BCD 4
		REPG ABCD 3
		REPG ABCD 2
	Hold:
		REPG E 0 A_Gunflash
		DEVG B 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SuperHold")
		REPG E 2 Bright A_FireBullets(4,4,2,9,RepPuff,1,10240)
		REPG F 2 Bright
		REPG G 2 Bright A_FireBullets(4,4,2,9,RepPuff,1,10240)
		REPG H 2 Bright
		REPG A 0 A_Refire
		REPG A 2 A_PlaySound("weapons/repstp")
		REPG BCD 2
		REPG ABCD 3
		REPG ABCD 4
		REPG ABCD 5
		Goto Ready
	SuperHold:
		REPG E 1 Bright A_FireBullets(4,4,2,9,RepPuff,1,10240)
		REPG F 1 Bright
		REPG G 1 Bright A_FireBullets(4,4,2,9,RepPuff,1,10240)
		REPG H 1 Bright
		Goto Hold+6
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

Actor RepPuff
{
	Radius 3
	Height 3
	RENDERSTYLE ADD
	ALPHA 0.75
	+ALWAYSPUFF
	+PUFFONACTORS
	+NOGRAVITY
	+NOBLOCKMAP
	+PUFFGETSOWNER
	DeathSound "weapons/buzz"
	States
	{
	Spawn:
		BLAS A 1 Bright
		BLAS A 2 Bright A_Explode(32,88)
		BLAS BCDEFG 3 Bright
		stop
	}
}

/////////////////////
// NEW BULLET PUFF //
/////////////////////

ACTOR NewPuff : BulletPuff replaces BulletPuff
{
	Alpha 1.0
	Mass 1
	States
	{
	Spawn:
	Melee:
	    PUFF A 2
	    PUFF A 0 A_Jump(128,2)
	    PUFF A 2 A_SpawnItemEx("Puff2")
	    PUFF BCD 4
	    Stop
	}
}

ACTOR Puff2
{
	+CLIENTSIDEONLY
	PROJECTILE
	+NOCLIP
	Mass 1
	States
	{
	Spawn:
		PUFF E 2
		PUFF E 2 A_PlaySound("puff/ric")
		PUFF FGH 4
		Stop
	}
}